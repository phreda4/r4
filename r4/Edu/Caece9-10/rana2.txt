|-----------------------------------------------------------------------------
|--------------------------LA RANA AURORA 2-----------------------------------
|------------------------"El Poder Invencible"--------------------------------
|-----------------------Autor: Ezequiel Urrizaga------------------------------
|-------------------------Powered by Reda4 :P---------------------------------
|---------------------------Copyright 2010------------------------------------
|-----------------------------------------------------------------------------
^r4/lib/btn.txt
^r4/lib/sprite.txt
^inc/rana2.spr


#xEne 0.1
#yEne 0.9 
#tEne 0.2
#VxEne 0.02  |velocidad
#VyEne 0.01  |velocidad
#eneazar 200
#mover 0

#xRan 0.1
#yRan -0.9
#txRan 0.1
#tyRan 0.1
#vxRan #vyRan
#vidas 3

#xPan 0
#yPan 0
#tPan 1.1

#xPro 20.0
#yPro 20.0
#VyPro 0.05
#tpro 0.1
#yadisparo 0

#vyBom -0.1
#bombas 0 2 2 0 0 2 2 0 0 2 2 0 0 2 2 0 -1 | v, x, y, t
#baux bombas
#baux2 bombas
#tbom 0.1
#vtbom 0.05
#tbommax 0.3
#PUNTAJE1 0
#nivel 1
#continua 1




:calcpro |Calcular distancia
	xEne xPro - dup *.
	yEne yPro - dup *. +
	abs
;

:calcexp |xbom ybom
	xRan rot - dup *.
	yran rot - dup *. +
	abs
;

:posicionenemigo
	xEne yEne fpos
	tEne dup fdim
	'dib3 nsprite
;

|-----------------MOVIMIENTO--------------------------
:movimientoenemigo
	yene 
		0.9 =? ( -0.03 'yene +! )( 0.03 'yene +! )
	drop
	VxEne 'xEne +!
	xEne 
		1.0 >? ( vxene neg 'vxene ! )
		-1.0 <? ( vxene neg 'vxene ! )
	drop
;

:moverderecha
	xRan 0.87 <? ( 0.04 'vxran ! )( 0 'vxran ! ) drop 
	yran -0.9 =? ( 0.03 'yran +! )( -0.03 'yran +! ) drop | revisar
	txran 0 >? ( txran neg 'txran ! ) drop
;

:moverizquierda
	xRan -0.87 >? ( -0.04 'vxran ! )( 0 'vxran ! ) drop 
	yran -0.9 =? ( 0.03 'yran +! )( -0.03 'yran +! ) drop  | revisar
	txran 0 <? ( txran neg 'txran ! ) drop 
	
;

:detener
	0 'vxran !  -0.9 'yran ! 
;

:movimientousuario 
	mover 
		1 =? ( moverderecha )
		-1 =? ( moverizquierda )
		0 =? ( detener )
	drop
	xran yran fpos
	txran tyran fdim
	Vxran 'xran +!
	ypro -0.3 <? ( 'dib5 nsprite )( 'dib1 nsprite ) drop
;

:movimientoproyectil
	vypro 'ypro +!
	calcpro 0.004 <? (
				1 'PUNTAJE1 +!
				PUNTAJE1 3 =? ( 0 'continua ! ) drop
				vxene neg 'vxene !
				2.0 'ypro !
			) drop
	ypro 1.0 >? ( 0 'yadisparo ! ) drop
;

:explotar
	baux 'baux2 !
	4 'baux2 +! baux2 @
	4 'baux2 +! baux2 @
	4 'baux2 +!
	vtbom baux2 +! | x y t

	baux2 @ tbommax >? ( 
							0 baux !
						) drop
	calcexp 0.1 <? (   	
						-1 'vidas +! 
						0 'continua !
					) drop

;

:movimientobomba | baux baux2
	'bombas 'baux !
	( baux @ -1 <>? )(	1 =? (
								baux 'baux2 !
								8 'baux2 +!
								baux2 @ -0.85 <? ( 
													explotar 
												)(	
													vyBom baux2 +! 
												) drop
							) drop
						16 'baux +! 
					) drop
;


|--------------------DIBUJAR------------------------------
:mostrarNivel
	0.0 -0.3 fpos 0.8 0.1 fdim ink 1 font print ;

:dibujarpantalla
	xPan yPan fpos
	tPan dup fdim
	'dib2 nsprite
	0.9 0.9 vidas 
	
	( 0 >? )( 1 - rot rot 2dup fpos 0.07 dup fdim 'dib1 nsprite swap 0.1 - swap rot ) 3drop


;

:posicionproyectil
	xPro yPro fpos
	tPro dup fdim
	'dib6 nsprite
;


:disparar
	xRan txRan 0.01 0 >? ( drop - )( drop  + ) 'xPro !
	yRan 0.05 + 'yPro !
	1 'yadisparo !
;

:dibujabomba
	'bombas 'baux !
	( baux @ -1 <>? )(
						1 =? (
								4 'baux +! baux @
								4 'baux +! baux @
								dup	rot	swap
								fpos |GENERA LOS NUEVOS X E Y, ALMACENA POSICION
								4 'baux +!	baux @
					 			dup fdim | GENERA TAMAÑO
								-0.83 <? ( 'dib7 )( 'dib4 ) nsprite drop | BOMBA O EXPLOSION
					
							)( 12 'baux +! ) drop
						4 'baux +!
					) drop
;

:resetear
	0 'puntaje1 !  0 'continua ! 
	0.1 'xene ! 0.9 'yene !
	0.1 'xran ! -0.9 'yran !
	'bombas 'baux !
	( baux @ -1 <>? )( 0 baux ! 16 'baux +! drop ) drop
;

:inicializarjuego
	1 'continua ! 3 'vidas ! resetear
	0.02 'vxene ! 200 'eneazar !
	1 'nivel !
;
:subirnivel
	1 'nivel +! 0.01 vxene abs + 'vxene ! eneazar 2 / 'eneazar !
;

:dibujacaida
	xene yene fpos
	tene tene neg fdim
	'dib9 nsprite
	yene -0.9 >? ( -0.03 'yene +! )( resetear subirnivel ) drop

;

|-------------------------OTROS--------------------------------

:teclado
	[ mover 0? ( 1 'mover ! ) drop ; ] <ri>
	[ 0 'mover ! ; ] >ri<
	[ mover 0? ( -1 'mover ! ) drop ; ] <le> 
	[ 0 'mover ! ; ] >le<
	[ yadisparo 0? ( disparar ) drop ; ] <up>	
;



:generarbomba
	1.0 rand eneazar mod 0 =? (
								'bombas 'baux !
								( baux @ 1 =? )( 16 'baux +! drop )
 			 					0 =? (
										1 baux !	| NUEVA BOMBA
										4 'baux +!
										xEne baux !
										4 'baux +!
										yEne baux !
										4 'baux +!
										tbom baux !
										4 'baux +!
				   				) drop
							) 2drop
;




:mostrarNivel
	scr fonti Home verde
	nivel " Nivel: %d " print cr
;

:main
    show30 clrscr
   			dibujarpantalla
			continua 1? (
				teclado
				posicionenemigo
				movimientoenemigo
		 	  	movimientousuario
				posicionproyectil
				movimientoproyectil
				generarbomba
				movimientobomba
				dibujabomba
			)(
				'exit >esc<
				PUNTAJE1 3 <>? (
						vidas 0 >? (
								resetear
								1 'continua !
								)(
									0 0 fpos 0.5 0.5 fdim 'dib8 nsprite
						) drop
				)(
						dibujacaida
				) drop
			) drop
  	mostrarNivel

;

|----------------------MENU-----------------------------

:mostrarLeyenda
	0.0 -0.3 fpos 0.8 0.1 fdim ink 1 font printc ;

:prejuego ;

:menu
	show
		clrscr home
		"LA RANA AURORA 2:" $00ff00 mostrarLeyenda
		cr cr sp
		"''El Poder Invencible''" $00ff00 mostrarLeyenda
		cr cr sp
		0.5 0 fpos
		0.2 0.2 fdim 'dib1 nsprite

        	rojo  'main inicializarjuego "COMENZAR!" btnt
		cr cr sp
        	azul 'exit "SALIR..." btnt
		cmano
		'exit >esc<
		;

|-------------------------------------------------------


: menu ;
