||***Investigacion en ingenieria de software***||
||*********************************************||
||*******Profesor:Pablo H. Reda****************||
||*********************************************||
||*******Alumna: Antonela Di Scala*************||
||*********************************************||
||******Cursada: Primer cuatrimestre 2010******||
||*********************************************||

^r4/lib/btn.txt
^r4/lib/3dmat.txt
^inc/espacio.spr
^r4/lib/trace.txt
^r4/lib/sprite.txt
^r4/lib/fontt.txt

#dibujos
'dib1 'dib2 'dib10 'dib4
'dibb 'dibc 'dibd 'dibe
'dibf 'dib5 'dibb 'dibc
'dibd 'dibe 'dibf 'dib1

#dibu 'diba

| Coordenadas del auto
#xa 8 #ya 0 

#modo
#puntaje
#tiempo

#mapa
0 0 0 0 0 0 0 0 1 0 0 0 0 0 0 0 0 0
0 0 1 1 1 1 1 1 1 1 1 1 8 1 1 1 0 0
0 0 1 0 0 0 0 0 0 0 0 0 0 0 0 1 0 0
0 0 1 1 1 5 1 1 1 1 1 1 0 0 0 1 0 0
0 0 0 0 0 0 0 0 0 0 0 5 0 0 0 8 0 0
0 0 0 0 0 0 0 0 0 0 0 1 0 0 0 1 0 0
0 0 0 1 6 1 1 1 1 1 1 1 0 0 0 1 0 0
0 0 0 1 0 0 0 0 0 0 0 0 0 0 0 1 0 0
0 0 0 1 1 1 0 0 0 0 0 0 0 1 8 1 0 0
0 0 0 0 0 1 1 7 1 1 0 0 0 1 0 0 0 0
0 0 1 0 0 1 0 0 0 1 0 0 0 1 0 0 0 0
0 0 8 1 1 1 0 0 0 1 0 0 0 1 0 0 0 0
0 0 1 0 0 1 0 0 0 1 0 0 1 1 0 0 0 0
0 0 1 0 0 1 0 0 0 9 0 0 1 0 0 0 0 0
0 0 1 1 1 1 0 0 0 1 0 1 1 0 0 0 0 0
0 0 0 0 0 1 0 0 0 1 0 1 0 0 0 0 0 0
0 0 0 0 0 0 0 0 1 9 0 1 1 1 0 0 0 0
0 0 0 0 0 0 0 0 2 0 0 0 0 0 0 0 0 0

#mapa2
0 0 0 0 0 0 0 0 1 0 0 0 0 0 0 0 0 0
0 0 0 0 0 1 1 1 1 1 1 1 8 1 1 1 0 0
0 0 0 0 0 1 0 0 0 0 0 0 0 0 0 1 0 0
0 0 1 1 1 5 1 1 1 1 1 1 1 1 0 1 0 0
0 0 0 0 0 1 0 0 0 0 0 0 0 1 0 8 0 0
0 0 0 0 0 0 0 0 0 0 0 1 0 1 0 1 0 0
0 0 0 1 6 1 1 1 1 1 1 1 1 5 0 1 0 0
0 0 0 1 0 0 0 0 0 0 0 0 0 1 0 1 0 0
0 0 0 1 1 1 0 0 0 0 0 0 0 1 0 1 0 0
0 0 0 0 0 1 1 7 1 1 0 0 0 1 0 0 0 0
0 0 1 0 0 1 0 0 0 1 0 0 0 1 0 0 0 0
0 0 8 1 1 1 0 0 0 1 0 0 0 1 0 0 0 0
0 0 1 0 0 1 0 0 0 1 0 0 1 1 0 0 0 0
0 0 1 0 0 1 0 0 0 9 0 0 1 0 0 0 0 0
0 0 1 1 1 1 0 0 0 1 0 1 1 0 0 0 0 0
0 0 0 0 0 1 0 0 0 1 0 1 0 0 0 0 0 0
0 0 0 0 0 0 0 0 1 9 0 1 1 1 0 0 0 0
0 0 0 0 0 0 0 0 2 0 0 0 0 0 0 0 0 0

#mapas 'mapa 'mapa2 0
#mapaactual 0

| 18 x 18 x 4
#mapete )( 1300

| --- Mapa

:mapa? | -- 0 | dirMapa
	'mapas mapaactual 4 * + @ ;

:cargarmapa
	mapa? 1? ( 'mapete swap 325 move ; ) ;

:putmap | x y -- 
   18 * + 2 << 'mapete  + ! ;

:getmap | x y -- v  
   18 * +  2 << 'mapete  + @ ;

:drawmapa | 'mapa --
	>r
	mpush
	-9.0 -9.0 0 mtransi | coordenada x=0 y=0 del mapa z=0
	-9.0 ( 9.0 <? )(
		-9.0 ( 9.0 <? )(
			r@+
			$f and 2 << 'dibujos + @ 3dnsprite
			1.0 0.0 0.0 mtransi
			1.0 + ) drop
		-18.0 1.0 0.0 mtransi
		1.0 + ) drop
	mpop rdrop
    ;

:dibujaauto
	mpush
	xa 1.0 * 9.0 -
	ya 1.0 * 9.0 -
	0 mtransi
	'dib4 3dnsprite
	mpop
	;

| --- Movimientos

:movder
	xa 1+ ya  getmap
	0? ( drop ; ) drop
	1 'xa +!
	;
	
:movizq
	xa 1- ya  getmap
	0? ( drop ; ) drop
	-1 'xa +!
	;

:movabajo
	xa ya 1+ getmap
	0? ( drop ; ) drop
	1 'ya +!
	;

:movarr
	xa ya 1- getmap
	0? ( drop ; ) drop
	-1 'ya +!
	;

| --- Consumir Bichos

:sumabi
	10 'puntaje  +! ;

:restabi
	-10 'puntaje  +! ;

:borrabi
	1 xa ya putmap ;

:_consumebi
	xa ya getmap
	5 =? ( borrabi sumabi )
 	6 =? ( borrabi sumabi )
 	7 =? ( borrabi sumabi )
  	8 =? ( borrabi restabi )
  	9 =? ( borrabi sumabi )
	drop
	;

| --- Resetear valores

:reset
	8 'xa !
	0 'ya !
	20 'tiempo !
	cargarmapa
	;

:resetAll
	0 'puntaje !
	0 'mapaactual !
	reset
	; 

| --- Carteles

:_nivel
    0.0 0.85 fpos
	0.8 0.1 fdim
	naranja font-vard-12-bold home
	mapaactual 1+ "Nivel: %d" print 
	;

:_puntuacion
    0.0 0.85 fpos
	0.8 0.1 fdim
	rojo font-vard-12-bold home
    cr cr puntaje "Puntaje: %d" print cr cr cr
	;

:_tiempo
	0.5 0.85 fpos
	0.8 0.1 fdim
	violeta 2 font
	sp sp tiempo "Tiempo: %d" print cr cr cr
	;
:mostrarleyenda
	0.0 -0.1 fpos 
	0.8 0.1 fdim 
	ink 1 font printc ;

:mostrarlogo
	0.0 0.4 fpos 
	0.5 0.5 fdim nsprite ;


| -- Controla fin del laberinto

:_finallab
    xa ya getmap
    2 =? (
			puntaje 60 >=? ( 2 )( 3 )
			'modo !
			drop
			exit
		)
	drop
	;

:juego
	show
		clrscr
		[ 4 'modo ! exit ; ] >esc<
	    'movabajo <dn>
	    'movder <ri>
	    'movizq <le>
	    'movarr <up>

		1.0 3dmode
		xa 1.0 * 9.0 - neg
		ya 1.0 * 9.0 - neg
		4.0 mtransi

		'mapete drawmapa
		dibujaauto
		"Laberinto PP" $0000ff mostrarleyenda
         sp sp
		_consumebi
		_tiempo
		_puntuacion
		_nivel
		_finallab

		| --- Timer
		1 .segs .restart
		tiempo 1- 0 <=? ( drop 3 'modo ! exit ; ) 'tiempo !
	;

:nogano
	show
		cls
		0.0 0.2 fpos
		0.8 0.1 fdim
		| Cartel Verde
		$00ff00 ink 1 font home
		mapaactual 1+ "Nivel %d" printc cr
	 	2 .segs exit
	;

:gano
    show
		cls
		0.0 0.2 fpos
		0.8 0.1 fdim
		'dib4 mostrarLogo
		cr cr cr cr cr cr cr "Laberinto PP" $0000ff mostrarLeyenda
		 cr cr cr sp sp sp
		| Cartel Verde
		$00ff00 ink 1 font home
		 "Ganaste!!!!!" printc
		cr cr cr cr cr sp sp sp
		cr cr _puntuacion
	 	2 .segs exit
	;

:perdio
	show
		cls
		0.0 0.2 fpos
		0.8 0.1 fdim
		"Laberinto PP" $0000ff mostrarLeyenda
		cr cr cr cr cr sp sp sp
		| Cartel Rojo
		$ff0000 ink 1 font home
		"Perdiste!" printc cr cr cr
		2 .segs exit
	;

:prejuego
	0 'modo !
	show
		modo

		| -- Juego nuevo
		0? (
			drop
			resetAll
			juego ;
		)

		| -- Gano
		2 =? (
				drop
				| Proximo nivel
				mapaactual 1+ 'mapaactual !
				| Existe este nivel ?
				mapa?
				| No existe GANO el juego
				0? ( drop gano exit ; )
				drop
				| Existe Al prox. Nivel
				nogano reset juego ;
			)
			
		| -- Perdio
		3 =? (  drop perdio exit ; ) 
		
		| -- Salio del juego
		4 =? ( drop exit ; )
		
		drop
	;

:iniciargui
	'exit >esc<
	[ 0 'modo ! prejuego ; ] <enter>
	;

:mostrarlogo
	0.0 0.4 fpos 0.5 0.5 fdim nsprite ;



:principal
	show
		iniciargui
		clrscr home
		'dib4 mostrarLogo
		"Laberinto PP" $0000ff mostrarLeyenda
		cr cr cr cr cr sp sp sp
 		amarillo 'prejuego "Jugar" btnt
		sp sp sp sp sp sp sp sp sp sp
        violeta 'exit "Salir" btnt
        cmano
	;

: 33 principal ;