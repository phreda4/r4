|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
|| FX-specific words

| createStar	arguments: impulse		returns: --
| createDebri	arguments: x y impulse	returns: --
| createCloud	arguments: x y alpha	returns: --

| arguments: 'particle | returns: 'particle
:changeAlpha
	dup @imageIndex
	0? (
		drop
		dup @alpha
		150 <? (
			1 + over swap !alpha
		)(
			drop
			dup 1 !imageIndex
		)
	)(
		drop
		dup @alpha
		0 >? (
			1 - over swap !alpha
		)(
			drop
			dup 0 !alpha
			dup 0 !tl
		)
	)
	;

| arguments: 'particle | returns: 'particle
:cloudAction
	changeAlpha
	dup @alpha alpha
	'spr_cloud1 over @xy drawExt | 3dnsprite
	255 alpha
	;

| arguments: 'particle | returns: 'particle
:cloudAction2
	changeAlpha
	dup @alpha alpha
	'spr_cloud2 over @xy drawExt | 3dnsprite
	255 alpha
	;

| arguments: x y alpha | returns: --
::createCloud
	listfxCnt listfxSize >=? (
		4drop
		;
	)(
		drop
	)
	
	rot rot swap >r >r			| alpha
	
	rand abs 0.01 mod 0.01 + 0	| alpha hspeed vspeed
	swap >r >r					| alpha					| vspeed hspeed y x
	
	-1					| alpha
	swap				| tl alpha
	1					| flags
	'defaultHitAction	| hitAction
	rand +? (
		drop
		'cloudAction		| action
	)(
		drop
		'cloudAction2		| action
	)
	0					| vmask
	0					| hmask
	0					| gravityDir
	0					| gravity
	0					| friction
	r>					| vspeed
	r>					| hspeed
	0					| direction
	5.0					| scale
	r> r>				| y x
	'listfx
	'listfxCnt
	+list
	;



| arguments: 'particle | returns: 'particle
:starAction
	dup @alpha alpha
	'spr_star over @xy drawExt | 3dnsprite
	255 alpha
	dup 0.01 swap 12 + +!
	
	| 0.01 pinpon over 12 + +!
	| dup 0.4 pinpon 0.2 + swap 8 + !
	;

| arguments:  impulse | returns: --
::createStar
	>r
	listfxCnt listfxSize >=? (
		drop
		rdrop
		;
	)(
		drop
	)
	
	rand abs 1.0 mod	| speed(0--1.0)
	3 >> r> +			| speed(0.125--0.25)
	
	rand abs 1.0 mod	| speed dir(0--1.0)
	2 >> 0.125 +		| speed dir(0.625--0.875)
	calcSpeeds			| hspeed vspeed
	swap >r >r
	
	150					| tl
	200					| alpha
	1					| flags
	'defaultHitAction	| hitAction
	'starAction			| action
	0.5					| vmask
	0.5					| hmask
	0.75				| gravityDir
	0.01				| gravity
	0					| friction
	r>					| vspeed
	r>					| hspeed
	0.55				| direction
	rand 18 >> abs 0.1 +	| scale
	'player @xy 0.5 - swap
	'listfx
	'listfxCnt
	+list
	;

| arguments: 'particle | returns: 'particle
:debriAction
	dup @alpha alpha
	'spr_platform1 over @xy drawExt | 'particle
	255 alpha
	
	0.04 over 12 + +!
	;

| arguments: x y impulse | returns: --
::createDebri
	listfxCnt listfxSize >=? (
		4drop
		;
	)(
		drop
	)
	
	swap rot >r >r
	
	rand abs 1.0 mod	| speed(0--1.0)
	3 >> +				| speed(0.125--0.25)
	
	rand abs 1.0 mod	| speed dir(0--1.0)
	2 >> 0.625 +		| speed dir(0.625--0.875)
	calcSpeeds			| hspeed vspeed
	swap >r >r
	
	30					| tl
	255					| alpha
	0					| flags
	'defaultHitAction	| hitAction
	'debriAction		| action
	0.5					| vmask
	1.0					| hmask
	0.25				| gravityDir
	0.06				| gravity
	0					| friction
	r>					| vspeed
	r>					| hspeed
	0					| direction
	rand abs 1.0 mod 1 >>	| scale
	r> r>				| y x
	'listfx
	'listfxCnt
	+list
	;
