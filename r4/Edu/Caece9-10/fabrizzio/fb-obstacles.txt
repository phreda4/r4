|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
|| Obstacle-specific words

|||| Exported words:

| platformAction	arguments: 'particle			returns: newAddr
| CreatePlatform	arguments: x y					returns: --
| CreateFloor		arguments: x y cont interval	returns: --
| CreateWall		arguments: x y cont interval	returns: --
	
| arguments: 'particle | returns: 'particle
::platform1Action
	dup @alpha alpha
	'spr_platform1 over @xy DrawExt
	255 alpha | 'particle

	dup @x 'player @x - 		| 'particle x-p.x

	dup abs 1.5 <? (			| 'particle x-p.x abs(x-p.x)
		2drop 0
	)(
		4.7 >=? (				| 'particle x-p.x abs(x-p.x)
			2drop 0.25			| 'particle 0.25
		)(
			drop				| 'particle x-p.x
			1.5 -
			0.0815 *.
			-? (
				0.25 +
			)
		)
	)
	over 12 + !					| 'particle
;

| arguments: 'particle | returns: newAddr
::platform2Action
	dup @alpha alpha
	'spr_platform2 over @xy DrawExt
	255 alpha
	;

| arguments: x y | returns: --
::CreatePlatform
	listobCnt listobSize >=? (
		drop
		;
	)(
		drop
	)
	
	swap >r >r
	
	-1					| tl
	255					| alpha
	1					| flags
	'defaultHitAction	| hitAction
	'platform1Action	| action
	0.5					| vmask
	1.0					| hmask
	0.25				| gravityDir
	0					| gravity
	0					| friction
	0					| vspeed
	0					| hspeed
	0					| direction
	1.0					| scale
	r> r>				| x y
	'listob
	'listobCnt
	+list
	;

| arguments: x y cont interval | returns: --
::CreateFloor
	>r								| x y cont			| r:: interval
	( 0 >? )(
		dup pick3 + pick2			| x y cont cont+x y
		CreatePlatform				| x y cont
		r -							| x y cont-interval
	)
	3drop
	rdrop
	;

| arguments: x y cont interval | returns: --
::CreateWall
	>r								| x y cont			| r:: interval
	( 0 >? )(
		pick2 pick2 pick2 +			| x y cont x y+cont
		CreatePlatform				| x y cont
		r -							| x y cont-interval
	)
	3drop
	rdrop
	;
