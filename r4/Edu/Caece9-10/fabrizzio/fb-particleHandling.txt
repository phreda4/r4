|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
|| Particle Handling

|||| Words exported:
| +list				arguments: tl flags hitAction action Vmask Hmask
|								gravityDir gravity friction vspeed hspeed
|								direction scale y x 'list 'listCnt			returns: --
| GeneralUpdate		arguments: 'particle									returns: 'particle
| SimpleUpdate		arguments: 'particle									returns: 'particle
| delspr			arguments: 'listCnt 'list 'toErase						returns: 'newAct
| delList			arguments: 'listCnt										returns: --
| cycleLayer		arguments: 'listCnt 'list								returns: --

|||| Vars exported:

#:listob )( 65536
#:listobCnt 0
#:listfx )( 16384
#:listfxCnt 0
#:listen )( 8192
#:listenCnt 0

#:listobSize 1024
#:listfxSize 256
#:listenSize 128

#:player )( 64

|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
|| Particle Updating Words

| arguments: 'particle | returns: 'particle
::GeneralUpdate
	mpush
	dup
	>r >r					| -- | r = initialAdr initialAdr
	r@+ r@+ 0 mtransi		| -- | r = initialAdr 'scale
	r> r swap >r @flagFacing
	1? (
		drop
		r@+ dup neg swap 0 mscalei	| --	| r = initialAdr 'direction
	)(
		drop
		r@+ dup 0 mscalei			| --	| r = initialAdr 'direction
	)
	r@+						| direction		| r = initialAdr 'hspeed
	1? ( mrotzi )( drop )	| --
	r@+ r@+					| hspeed vspeed | r = initialAdr 'friction
	swap r@+				| vspeed hspeed friction | r = initialAdr 'gravity
	calcFriction			| vspeed newHspeed
	swap					| newHspeed vspeed
	r@+ r @	rdrop			| newHspeed vspeed gravity gravDir  | r = initialAdr
	addGravity				| newHspeed newVspeed
	over					| newHspeed newVspeed newHspeed
	r 16 + !				| newHspeed newVspeed newHspeed 'hspeed !
	0.8						| newHspeed newVspeed 0.8
	>? (
		drop 0.8
	)
	dup						| newHspeed newVspeed newVspeed
	r 20 + !				| newHspeed newVspeed newVspeed 'vspeed !
	r 4 + +!				| newHspeed newVspeed 'y +!
	r +!					| newHspeed 'x +!
	r> dup @action			| initialAdr 'action
	exec					| newAddr
	mpop
	;

| arguments: 'particle | returns: 'particle
::SimpleUpdate
	mpush
	dup						| addr addr
	>r >r					| --				| r = addr addr
	r@+ r@+ 0 mtransi		| --				| r = addr 'scale
	r@+ dup 0 mscalei		| --				| r = addr 'direction
	r@+						| direction			| r = addr 'hspeed
	1? ( mrotzi )( drop )	| --
	r@+ r@+	rdrop			| hspeed vspeed		| r = addr
	
	r 4 + +!				| hspeed vspeed 'y +!
	r +!					| hspeed 'x +!
	r> dup @action			| addr				| r = --
	exec					| newAddr
	mpop
	;


| arguments: 'particle | returns: newAddr
::defaultAction
	;

|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
|| Particle Removal/Adding Words

| arguments: 'listCnt 'list 'toErase | returns: --
::delspr
	dup					| 'listCnt 'list 'toErase 'toErase
	nodeBytes + rot		| 'listCnt 'toErase 'toErase+nodeBytes 'list
	pick3 @				| 'listCnt 'toErase 'toErase+nodeBytes 'list listCnt
	nodeBytes * +		| 'listCnt 'toErase 'toErase+nodeBytes 'last
	over - 2 >>			| 'listCnt 'toErase 'toErase+nodeBytes cnt_de_var(bytesHastaFin*0.25(porque hay 4 bytes por variable))
	move				| 'listCnt
	-1 swap +!			| -- | listCnt -= 1
	;
	
| arguments: 'listCnt | returns: --
::delList
	0 swap !
	;

| arguments: tl flags hitAction action Vmask Hmask gravityDir gravity friction vspeed hspeed direction scale y x 'list 'listCnt | returns: --
::+list
	dup @ 1 rot			| variablesVarias 'list listCnt 1 'listCnt
	+!					| variablesVarias 'list listCnt(original)		| listCnt += 1
	nodebytes * +		| variablesVarias 'last
	nodeBytes 2 >>		| variablesVarias act cnt(cantidad de variables que hay por particula)
	( 0 >? )(
		>r				| variablesVarias(15) act						| R: cnt
		!+				| variablesVarias(14) act(sig)
		r>				| variablesVarias(14) act(sig) cnt				| R: --
		1 -				| variablesVarias(14) act(sig) cnt-1
	)
	2drop				| --
	;

|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
|| Particle Cycling Words

| arguments: 'listCnt 'list | returns: --
::cycleLayer
	dup dup pick3 @				| 'listCnt 'list 'act 'list listCnt
	nodeBytes * +				| 'listCnt 'list 'act 'last
	
	( <? )(						| 'listCnt 'list 'act
		dup nodeBytes + 4 -		| 'listCnt 'list 'act 'tl
		dup @					| 'listCnt 'list 'act 'tl tl
		
		0 <? (
			2drop 1				| 'listCnt 'list 'act 1
		)(
			1 - dup				| 'listCnt 'list 'act 'tl tl-1 tl-1
			rot !				| 'listCnt 'list 'act tl-1 				| tl = tl-1
		)
		
		0 <=? (
			drop				| 'listCnt 'list 'act
			pick2 pick2 pick2	| 'listCnt 'list 'act 'listCnt 'list 'act
			delspr				| 'listCnt 'list 'act
		)(
			drop				| 'listCnt 'list 'act
			GeneralUpdate		| 'listCnt 'list 'newAct
			nodeBytes +			| 'listCnt 'list 'newAct+nodeBytes
		)
		
		over pick3 @			| 'listCnt 'list 'act 'list listCnt
		nodeBytes * +			| 'listCnt 'list 'act 'last
	)
	
	3drop
	;