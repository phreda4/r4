|---------------------
| Vertigo
| Leonardo Specioso
| Anio 2010
|---------------------

^inc/vertigo.spr
^r4/lib/btn.txt
^r4/lib/sprite.txt

#x #y
#xv #yv
#ax #ay
#xb 6.00 #yb -1.00

#xcam 0.0
#ycam 0.0
#zcam 10.5

#puntuacionactual 0.00

#pasto -5
#agua -10
#fuego -15

#nivel 0
#salir
#estadojuego

#mapaactual

#mapa1
6 3 2 2 5 5 2 2 2 2 2 2 2
2 2 2 5 5 5 2 2 2 2 2 2 2
2 2 2 2 5 5 2 2 2 2 2 2 2
2 2 2 2 5 5 2 2 2 2 2 2 2
2 2 2 2 5 5 5 5 5 2 3 3 2
2 2 2 2 5 5 5 5 5 2 3 3 2
2 2 2 2 2 2 2 5 5 2 2 2 2
2 2 2 2 6 6 2 5 5 2 2 2 2
2 2 3 3 6 6 2 5 5 2 2 2 2
2 2 3 3 2 2 2 5 5 2 2 2 2
2 2 2 2 2 2 2 5 5 2 2 2 2
2 2 2 2 2 2 2 4 4 2 2 2 2
2 2 2 2 2 2 2 4 4 2 2 2 2

#mapa2
6 6 6 6 5 5 2 2 2 2 2 2 2
6 6 6 6 5 5 2 2 2 2 2 2 2
6 6 6 6 5 5 2 2 2 2 2 2 2
6 6 6 6 5 5 2 2 2 2 2 2 2
6 6 6 6 5 5 2 2 2 2 2 2 2
2 2 2 2 5 5 5 5 5 2 2 2 2
2 2 2 2 5 5 5 5 5 2 2 2 2
2 2 2 2 2 2 3 5 5 6 6 6 2
2 2 6 6 6 2 3 5 5 6 6 6 2
2 2 6 6 6 2 3 5 5 6 6 6 2
2 2 6 6 6 2 3 5 5 6 6 2 2
2 2 6 6 6 2 3 4 4 2 2 2 2
2 2 2 2 2 2 3 4 4 2 2 2 2

#mapa3
6 3 2 2 5 5 5 5 5 5 2 2 2
2 2 2 5 2 2 6 6 5 5 2 2 2
2 2 2 2 2 5 5 5 5 5 2 2 2
2 6 2 2 2 5 5 5 5 5 2 2 2
2 2 2 2 2 5 5 2 2 2 2 2 2
2 2 3 2 2 5 5 2 2 2 2 3 2
2 2 5 5 5 5 5 2 2 2 2 3 2
2 2 5 5 5 5 5 3 6 6 2 3 2
2 2 5 5 2 2 2 2 6 6 2 2 2
2 2 5 5 2 2 2 2 6 6 2 2 2
3 3 5 5 2 2 2 2 2 2 2 2 2
3 3 5 5 5 5 5 4 4 2 2 2 2
3 3 5 5 5 5 5 4 4 2 2 2 2



#convierte 0 'dib2 'dib3 'dib4 'dib5 'dib6

#ciclo 'dib2 'dib3 'dib4 'dib5

:dibujamapa
	0? ( drop ; )
	1 =? ( drop msec 8 >> $c and 'ciclo + @ 3dnsprite ; )
	4 * 'convierte + @ 3dnsprite ;

:piso | --  ; dibuja piso segun mapaactual
	mapaactual >r
	mpush
	-6.0 6.0 0.0 mtransi
	13 ( 1? )( 1-
		13 ( 1? )( 1-
			r@+ dibujamapa
			1.0 0.0 0.0 mtransi
			) drop
		-13.0 -1.0 0.0 mtransi
		) drop
	rdrop
	mpop
	;

:resetjuego
	0 'nivel !

:resetapuntuacion
	0 'puntuacionactual  !
	;


:reseteatablero
	0.0 'xcam !
	0.0 'ycam !
	10.5 'zcam !
	;



:resetnivel
	0 'x ! 	0 'y !
	0 'xv ! 0 'yv !
	0 'ax ! 0 'ay !
	6.0 'xb ! -1.0 'yb !
	reseteatablero

;


:menu
   show clrscr
	    'exit >esc<
    	'mapa1 'mapaactual !
  		0 'nivel !
    	10 font home rojo cr
		"Vertigoo Espacial" printc cr cr
		7 font
		"El juego de la bolita" printc
		sp verde home 3 4 gotoxy [ 1 'estadojuego ! exit ; ] "Play" .btnt
		sp rojo  [ 0 'estadojuego ! exit ; ] "Salir" .btnt
    	cmano ;

:gana
	show clrscr
		piso
		10 font home rojo
		5 5 gotoxy puntuacionactual "TU PUNTAJE FUE: %d" print
		3 .segs  | sale a los 5 segundos
		resetnivel
  		resetjuego
		resetapuntuacion
		'mapa1 'mapaactual !
		exit
		;

:maparefresh
	0 =? ( 'mapa2 'mapaactual ! )
	1 =? ( 'mapa3 'mapaactual  ! )
	2 =? ( gana menu )
	drop
;

:sombra
	mpush
	yb 1.0 pinpon - xb 0.0 mtransi
	128 alpha
	'dib7 3dnsprite
	255 alpha
	mpop
	;

:celdaactual | -- c
	yb 7.5 + 16 >>
	-? ( 3 nip ; ) 13 >? ( 3 nip ; )
    xb neg 6.0 + 16 >>
	-? ( 3 nip ; ) 13 >? ( 3 nip ; )
	13 * +
	2 << 'mapaactual + @ |en esta linea con el fech obtengo
					 |el valor de la posicion en el mapa (1, 2, 4 etc)
	;

:textonivel
	10 font home rojo
	5 5 gotoxy
    nivel
	1 =? ( "Has ganado: 100 puntos por pasar el primer nivel" print cr
		    "preparate para el proximo desafio..." print cr	 )
    2 =? ( "Has ganado: 100 puntos por pasar el segundo nivel" print cr
		    "preparate para el proximo desafio..." print cr	 )
    3 =? ( "Has ganado: 100 puntos por pasar el tercer nivel" print cr
		    "preparate para el ultimo desafio..." print cr	 )
	5 .segs  | sale a los 5 segundos
	;

:gananivel
	nivel
	0 =? ( 100 'puntuacionactual +! )
    1 =? ( 250 'puntuacionactual +! )
    2 =? ( 500 'puntuacionactual +! )
	1 'nivel +!
	maparefresh
	resetnivel
    textonivel
	;

:bola
	sombra
	mpush
    yb xb 1.0  pinpon
	0.2 <? ( |umbral
		celdaactual
		2 =? ( pasto 'puntuacionactual  +! )
		6 =? ( agua 'puntuacionactual  +! )
		3 =? ( exit 2 'estadojuego ! )
		4 =? ( gananivel )
		drop )

	mtransi
    y 2 >> neg mrotyi
	x 2 >> neg mrotxi | para que la bola se vea siempre de frente
 	x 2 >> 'xb +!
	y 2 >> neg 'yb +!
	'dib1 3dnsprite
	mpop
	;


:teclado
	[ -10 'ay ! ; ] <ri>
	[ 0 'ay ! ; ] >ri<
	[ 10 'ay ! ; ] <le>
	[ 0 'ay ! ; ] >le<

	[ 10 'ax ! ; ] <up>
	[ 0 'ax ! ; ] >up<
	[ -10 'ax ! ; ] <dn>
	[ 0 'ax ! ; ] >dn<
	;

:main
	show60 clrscr
	    teclado
		'exit >esc<

		0.7 3dmode
		0.5 mrotx

        y 2 >> mroty
	    x 2 >> mrotx

		ay 'yv +! |se le suma la aceleracion a la velocidad
		yv 'y +!

        ax 'xv +! |se le suma la aceleracion a la velocidad
		xv 'x +!

		|pone los limites a los angulos de inclinacion en x y reseta la velocidad en x
		x
		0.350 >? (
			0.350 'x !
			0 'xv !
			)

        |pone los limites a los angulos de inclinacion en x y reseta la velocidad en x
		-0.350 <? (
			-0.350 'x !
			0 'xv !
			) drop

		|pone los limites a los angulos de inclinacion en y e reseta la velocidad en y
		y -0.20 <? (
			-0.20 'y !
			0 'yv !
			)

       |pone los limites a los angulos de inclinacion en y e reseta la velocidad en y
		0.20 >? (
			0.20 'y !
			0 'yv !
			) drop

        xcam ycam zcam mtrans
		piso
	    bola

	   |**** DEBUG
		16 font	home blanco
		|x "%f "   print
		|y "%f "	print cr
		nivel "Nivel:%d" print cr
		puntuacionactual "Puntuacion:%d " print cr
        |dup "%d " print
        |yb "yb:%f " print
		|xb "xb:%f" print cr
	    |celdaactual "%d" print cr
		;


:pierde
	show clrscr
		piso
		10 font home rojo
		0 7 gotoxy "TE CAISTE :S" print
		5 .segs  | sale a los 5 segundos
		resetnivel
        exit
		;


:principal
	0 'estadojuego !
	( menu estadojuego 1? )( drop
		resetjuego main
		estadojuego 2 =? ( pierde )( gana ) drop
		) drop
	;



: 33 principal ;
