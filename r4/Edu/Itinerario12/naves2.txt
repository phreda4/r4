| simple alien shot
| PHREDA 2011

^r4/lib/gui.txt
^r4/lib/sprite.txt
^r4/lib/bmr.txt
^r4/edu/itinerario12/simple.spr
^r4/edu/itinerario12/simple.bmr

#puntos 0
#vidas 3

#xn 0.0 #yn -0.9
#vxn 0.0 #vyn 0.0

:resetgame
	3 'vidas !
	0 'puntos !
	;

|---- generic list
#fxs 0 0
#aliens 0 0
#disparos 0 0

:listp.create | size 'fx --
	here over 4+ !
	swap 'here +!
:listp.clear | 'fx --
	dup 4+ @ swap ! ;

:listp!+ | 'vec 'fx -- adr
	dup >r @ !+
	32 r> +! ;

:listp.del1 | 'fx last now -- 'fx last now
	dup pick2 32 - 8 move
	-32 pick3 +!
	swap 32 - swap 32 - ;

:listp.draw | 'fx --
	dup @+ swap @
	( over <? )(
		dup @ exec 0? ( drop listp.del1 )
		32 + ) 3drop ;

:listp.map | vec 'fx --
	dup @+ swap @
	( over <? )(
		pick3 exec 0? ( drop listp.del1 )
		32 + ) 4drop ;

:listp.map0 | vec 'fx --
	dup @+ swap @
	( over <? )(
		pick3 exec 0? ( drop listp.del1 4drop 0 ; )
		32 + ) 4drop ;

:listp.deln | adr 'fx --
	dup @ -32 rot +!
	32 - 8 move ;

|---------
|alien 1
:alienp
 	dup 4+ >r
	r@+ dup abs 1.4 >? ( r 4+ dup @ neg swap !  ) drop
	r@+ |dup abs 1.0 >? ( r 4+ dup @ neg swap !  ) drop

	over sin 2 >> +
|	-0.8 <? ( rdrop 2drop 0 ; ) | llego abajo
	-0.8 <? ( r 4+ dup @ neg swap ! )
	1.5 >? ( r 4+ dup @ neg swap ! )

  	fpos
	r@+ over 4+ +!
	r@+ over 8 + +!
|	r @ 0? ( rdrop ; ) 1- r!+
	rdrop
	0.1 dup fdim
	'alien nsprite
|	xc w 2/ - yc h 2/ - 'alienb w h bmr.drawscale
	;

:nuevoalien
	200 -0.001 0.01 1.0 1.3
	'alienp 'aliens listp!+
	!+ !+ !+ !+ ! ;
|---------
:fragmento
 	dup 4+ >r
	r@+ r@+ fpos
	r@+ over 4+ +!
	r @ 0.0001 - r !
	r@+ over 8 + +!
	r @ 0? ( rdrop ; ) 1- dup r!+
	2* alpha
	rdrop
	0.1 dup fdim
|	xc w 2/ - yc h 2/ - 'coso1b w h bmr.drawscale
	'coso1 nsprite
	255 alpha
	;

:frags | x y n -- x y n
	rand 0.01 mod rand 0.01 mod pick4 pick4 swap
	'fragmento 'fxs listp!+
	!+ !+ !+ !+ 32 swap ! ;

|---------
| disparo

:crash | x y 'crash fx last now --
	>r
	pick4 r 4+ @ - abs 0.05 >? ( drop r> ; ) drop
	pick3 r 8 + @ - abs 0.05 >? ( drop r> ; ) drop
	pick4 pick4
	8 ( 1? )( 1- frags ) 3drop
	r>
	0
	1 'puntos +! ;

:fireshot | adr -- adr/adr 0 delete
 	dup 4+ >r
	r@+ |dup abs 1.0 >? ( r 4+ dup @ neg swap ! ) drop
	r@+ dup abs 1.0 >? ( rdrop 3drop 0 ; ) drop
	'crash 'aliens listp.map0 0? ( nip nip rdrop ; )
	fpos
	r@+ over 4+ +!
	r@+ over 8 + +!
	rdrop
	0.08 dup fdim
	'disparo nsprite
|	xc 16 - yc 16 - 'disparob 32 32 bmr.drawscale
	;

:disparo
	0.02 0 yn xn
	'fireshot 'disparos listp!+
	!+ !+ !+ ! ;

|---------
:dibujaymuevenave
	scr home blanco
	dup "%d " print
	puntos "%d" print

	0.05 dup fdim
	xn yn fpos
|	'nave nsprite
	xc w 2/ - yc h 2/ - 'naveb w h bmr.drawscale

	xn vxn +
	1.0 >? ( 1.0 nip )
	-1.0 <? ( -1.0 nip )
	'xn ! ;

:teclas
	rand 7 >> $ff and 7 <? ( nuevoalien ) drop

	[ -0.01 'vxn ! ; ] <le>
	[ 0.01 'vxn ! ; ] <ri>
	[ 0 'vxn ! ; ] dup >le< >ri<

	'disparo <spc>
	'exit >esc< ;

:main
	'aliens listp.clear
	'disparos listp.clear
	'fxs listp.clear
	show clrscr
		'disparos listp.draw
		'aliens listp.draw
		'fxs listp.draw
		dibujaymuevenave
		teclas ;

:inicio
	mark
	$ffff 'fxs listp.create
	$ffff 'aliens listp.create
	$ffff 'disparos listp.create
	;

: inicio 33 main ;

