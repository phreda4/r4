|//<!-- 2-player version of Slime Volleyball -->
|// Original code: Quin Pendragon, 1999.
| version r4 - PHREDA
|---------------------------------------------------
^r4/lib/gui.txt
^r4/lib/trace.txt

#p1Score 0	#p2Score 0

#p1X -0.8	#p1Y 0.9
#p2X  0.8	#p2Y 0.9
#p1XV 0     #p1YV 0
#p2XV 0     #p2YV 0

#ballX -0.8 #ballY 0.6
#ballVX 0   #ballVY 0

#JUMPVEL 	0.1
#SLIMEVEL 	0.02
#GRAVITY 	0.01
#PISO 		-0.9
#RADIOSLIME	0.08
#RADIOBALL	0.04

:inicio
	0 'p1Score ! 0 'p2Score !
	-0.8 'p1X !  PISO 'p1Y !
	 0.8 'p2X !  PISO 'p2Y !
	0 'p1XV !   0 'p1YV !
	0 'p2XV !   0 'p2YV !
	-0.8 'ballX ! 0.6 'ballY !
	0 'ballVX !	0 'ballVY !	;

|------------------------------
:ballhitx		ballvx neg 'ballvx ! ;
:ballhity		ballvy neg 'ballvy ! ;

:squared2d | x y  -- x y v
	ballx pick2 - dup *. bally pick2 - dup *. + ;

:calcball | x y  --
	squared2d | x y v
	RADIOSLIME RADIOBALL + dup *. <? (
		bally pick2 - 2/ dup 'bally +! 'ballvy +!
		ballx pick3 - 2/ dup 'ballx +! 'ballvx +!
		) 3drop ;

|------------------------------
:DrawBall
	ballvy 0.0016 -
	-0.18 <? ( -0.18 nip ) 0.18 >? ( 0.18 nip )
	dup 'bally +! 'ballvy !
	bally PISO <? (
		PISO 'bally !
		ballhity
		ballx 0.0 <? ( 'p1Score )( 'p2Score ) nip 1 swap +!
		) drop
	ballvx
	-0.1 <? ( -0.1 nip ) 0.1 >? ( 0.1 nip ) 'ballvx !
	ballx ballvx +
	-0.9 <? ( -0.9 nip ballhitx ) 0.9 >? ( 0.9 nip ballhitx ) 'ballx !
	bally -0.45 <? (
		ballx abs 0.05 <? (
			ballhitx
			) drop
		) drop
	ballx 0.0 <? ( p1x p1y )( p2x p2y ) calcball drop
 	ballX ballY fpos RADIOBALL dup fdim blanco gc.fcircle ;

|------------------------------
:juego
	show clrscr
	|----	jugador 1
		p1x p1xv + -0.9 <? ( -0.9 nip ) -0.1 >? ( -0.1 nip ) 'p1x !
		p1yv 0? ( drop )(
			p1y + GRAVITY neg 'p1yv +!
			PISO <? ( PISO nip  0 'p1yv ! ) 'p1y ! )
		p1x p1y fpos RADIOSLIME dup fdim rojo gc.fcircle
	|----	jugador 2
		p2x p2xv + 0.9 >? ( 0.9 nip ) 0.1 <? ( 0.1 nip ) 'p2x !
		p2yv 0? ( drop )(
			p2y + GRAVITY neg 'p2yv +!
			PISO <? ( PISO nip 0 'p2yv ! ) 'p2y ! )
		p2x p2y fpos RADIOSLIME dup fdim azul gc.fcircle
		DrawBall
|---- 	Dibujo fondo
		verde
		0 -0.95 fpos 2.0 0.05 fdim gc.fbox
		0 -0.7 fpos 0.03 0.25 fdim gc.fbox

|---- Carteles
		scr
		10 font
		5 0 gotoxy verde "Slimetlet" print
		1 0 gotoxy blanco p2Score p1Score "%d - %d" print
|		5 1 at azul bally ballx "%d.%d" print
	'exit >esc<
|---- player 2
	[ SLIMEVEL neg 'p1XV ! ; ] 30 ?key
	[ SLIMEVEL 'p1XV ! ; ] 32 ?key
	[ 0 'p1XV ! ; ] dup 30 ?ukey 32 ?ukey
	[  p1Y PISO <>? ( drop ; ) drop JUMPVEL 'p1YV ! ; ] 17 ?key
|---- player 1
	[ SLIMEVEL neg 'p2XV ! ; ] <le>
	[ SLIMEVEL 'p2XV ! ; ] <ri>
	[ 0 'p2XV ! ; ] dup >le< >ri<
	[  p2Y PISO <>? ( drop ; ) drop JUMPVEL 'p2YV ! ; ] <up>
		;


: 0 paper inicio juego ;
