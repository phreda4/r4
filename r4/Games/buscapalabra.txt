| Busca Palabras
| 2016
|---------------------
^r4/lib/gui.txt
^r4/lib/rfont.txt
^inc/rft/robotobold.rft

#tiempo
#cntpal
#lista
#elegida
#pantalla
#puntos

#palabras
"uno"
"dos"
"tres"
"cuatro"
"cinco"
"seis"
"siete"
"ocho"
"nueve"
"diez"
0

:generalista
	0 'cntpal !
	'palabras
	( dup c@ 1? )( drop
		dup , 1 'cntpal +!
		( c@+ 31 >? )( drop ) drop
		trim ) 2drop ;

:memoria
	mark
	here 'lista !
	generalista
	here 'pantalla ! ;

:nropal |4 -5
	4 * lista + @ ;

:ppal | x y p --
	nropal
	sizeprint cch
	rot >r
	neg 2/ rot + swap
	neg 2/ rot + swap
	over 1- over 1-
	atxy r@ ink@ >r blanco print
	atxy r> ink r> print
	;

:inipant
	pantalla >a
	0 ( cntpal <? )(
		sw 2/ rnd over 100 - mod + a!+
		sh 2/ rnd over 120 - mod + a!+
		dup a!+
		rand a!+
		1+ ) drop
	0 a!
	rnd cntpal mod abs 'elegida !
	;

:continua
	1 'puntos +!
	inipant ;

:inppal
	nropal
	sizeprint cch
	rot >r
	neg 2/ rot + swap
	neg 2/ rot + swap atxy
	r> print2gc drop
	'continua guiBtn
	;

:clickelegida
	elegida 16 * pantalla + >a
	a@+ a@+ a@ 
	inppal ;

:verpant
	pantalla >a
	( a@+ 1? )( a@+ a@+ a@+ ink ppal )
	drop ;

:movpant
	pantalla >a
	( a@+ 1? )( drop
		rnd sign a> 4 - +! drop
		rnd sign a> +! drop
		12 a+ )
	drop ;

:main
	0 'puntos !
	60 'tiempo !
	inipant
	show clrscr
		robotobold 0.11 %s rfont!
		home blanco
		0 0 gotoxy elegida nropal printc
		0 rows 1- gotoxy tiempo "%d" printc

		robotobold 0.09 %s rfont!
		verpant
		movpant
		clickelegida

		'exit >esc<
		cminiflecha
		1 .segs .restart
		-1 'tiempo +!
		tiempo 0? ( exit ) drop ;

: memoria main ;