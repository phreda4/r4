| Menu de opciones para Milagros
| PHREDA 2013
|------------------------------------

^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/input.txt
^r4/lib/rfont.txt
^r4/lib/ricons.txt

^inc/ric/fontawesomewebfont.ric
^inc/rft/robotobold.rft

#sstart #sexit #serror

#sndcal
#sndlab
#sndsal

:loads
	mark

	"media/milagros/start.ogg" sload 'sstart !
	"media/milagros/exit.ogg" sload 'sexit !
	"media/milagros/error.ogg" sload 'serror !

	"media/milagros/calculadora.ogg" sload 'sndcal !
	"media/milagros/laberinto.ogg" sload 'sndlab !
	"media/milagros/salir.ogg" sload 'sndsal !
	;


|-----------------------------------------------------
|	ComponeFrase
#frase  )( 1024
#frase> 'frase
#fraseA 0

:frase,
	frase> !+ 'frase> ! ;

:clearfrase
	'frase 'frase> ! ;

:hablando
	-1 sinfo 1? ( drop ; ) drop
	fraseA 0? ( drop ; )
	@+ 1? ( splay )( nip )
	'fraseA ! ;

:frase!
	0 frase, 'frase 'fraseA ! ;


:execal
	"r4/milagros/calculadora.txt" run ;

:exelab
	"r4/milagros/laberinto.txt" run ;

#opciones
'sndcal 'execal "Calculadora"
'sndlab 'exelab "Laberinto"
'sndsal 'exit "Salir"
0

#nopc 0
#maxopc 3

:showmenu
	0 'opciones ( @+ 1? )(
		drop 4+
		over nopc =? ( blanco robotobold 52 rfont! )( verde  robotobold 48 rfont! ) drop
		dup printc cr
		>>0 swap 1+ swap )
	3drop
	;

:saymenu
	clearfrase
	'opciones nopc
	( 1? )( 1- swap 8 + >>0 swap ) drop
	@ @ frase, frase! ;

:playmenu
	'opciones nopc
	( 1? )( 1- swap 8 + >>0 swap ) drop
	4+ @ exec ;

|-----------------------------------------------------
:inicio
	0 'nopc !
	saymenu
	show clrscr
		hablando
		robotobold 64 rfont!
		blanco "Menu Principal" printc cr cr
		showmenu
		[ sndcal frase, frase! ; ] <f1>
		[ nopc 1+ maxopc >=? ( 0 nip ) 'nopc ! saymenu ; ] dup <dn> <ri>
		[ nopc 1- -? ( maxopc 1- nip ) 'nopc ! saymenu ; ] dup <up> <le>
		[ playmenu ; ] <enter>
		'exit >esc<
		cminiflecha
		;

:swait
	-1 ( sinfo 0? ) drop ;

:main
	sstart splay
	inicio
	sexit splay
	swait ;

: loads main ;