| edit bones
| PHREDA 2017
|----------------
^r4/lib/gui.txt
^r4/lib/3dmat.txt
^r4/lib/vesprite.txt
^r4/lib/dlgfile.txt

^r4/rtv/testskin.vsp
^r4/lib/trace.txt

#model
#model>

#filename )( 256
#bbonetest )( $fff

#xcam 0 #ycam 0 #zcam 50.0

#xr 0 #yr 0

#nodob
#xd #yd


:model.reset
	0.0 0.0 0.0 1	| root node
	model !+ !+ !+ !+
	dup 'model> !
	0 swap ! ;

|------------------------------------------
:dumpmod | adr --
	( @+ 1? )(
		"%h " print
		@+ "%f " print
		@+ "%f " print
		@+ "%f " print
		cr allowchome
		) 2drop ;

:dumpbbc | adr --
	( @+ 1? )(
		"%h " print
		@+ "%f " print
		cr allowchome
		) 2drop ;


|------------------------------------------
:3dop project3d op ;
:3dline project3d line ;

:drawboxz | z --
	-0.5 -0.5 pick2 3dop
	0.5 -0.5 pick2 3dline
	0.5 0.5 pick2 3dline
	-0.5 0.5 pick2 3dline
	-0.5 -0.5 rot 3dline ;

:drawlinez | x1 x2 --
	2dup -0.5 3dop 0.5 3dline ;

| coord in screen
#cachepoint )( 1024
#cachepoint> 'cachepoint

#cachereal )( 1024
#cachereal> 'cachereal

#boneslevel )( 256

:xy>xxyy | xy -- x y
	dup 16 << 16 >> swap 16 >> ;

:xxyy>xy | x y -- xy
	16 << swap $ffff and or ;

:drawstick | level --
	1 =? ( rojo )( naranja )
	0 0 0 transform drop swap
	cachereal> !+ !+ 'cachereal> !
	0 0 0 project3d
	2dup 3 fbox
	2dup op
	xxyy>xy dup
	cachepoint> !+ 'cachepoint> !
	over 2 << 'boneslevel + !
	2 <? ( ; )
	dup 1- 2 << 'boneslevel + @ xy>xxyy
	verde line ;

:onpoint | x y -- p
	xymouse rot
	- abs 3 >? ( 3drop 0 ; ) drop
	- abs 3 >? ( drop 0 ; ) drop
	1 ;

:onpoints | -- nodo+1
	'cachepoint
	( cachepoint> <? )(
		@+ xy>xxyy
		onpoint 1? ( drop 'cachepoint - 2 >> ; ) drop
		) drop 0 ;

:drawcube |
	verde
	-0.5 drawboxz
	0.5 drawboxz
	-0.5 -0.5 drawlinez
	0.5 -0.5 drawlinez
	0.5 0.5 drawlinez
	-0.5 0.5 drawlinez ;

:drawbones | bones --
	'cachepoint 'cachepoint> !
	'cachereal 'cachereal> !
	>b
	0 ( b@+ 1? )( $ff and
		swap over - 1+ clamp0 nmpop
		mpush
		b@+ b@+ b@+ mtransi
		drawstick |drawcube
		) drop
	nmpop
	;

|------------

:addnodemove
	nodob 0? ( drop ; )
	1- 2 << 'cachepoint + @
	xy>xxyy op
	xymouse line ;

:hacelugar | nro --
    model> model - 4 >>
    16 'model> +!
    >=? ( ; )
    dup 4 << model + | src
    dup 16 + swap 	| dst src
    model> over - 2 >>
    move>
	;


|------------
:addnodeup
	nodob 0? ( drop ; )
	 xymouse zcam invproject3d 'yd ! 'xd !
	hacelugar
	dup 4 << model + >a
	1- 3 << 'cachereal +
	a> 16 - @ 1+ a!+
	@+ xd swap - a!+
	@ yd swap - a!+
	0 a!
	0 model> !
	;

:editaddnode
	gris "* CREATE NODE " fillp
	blink 1? ( rojo )( blanco ) drop
	print cr

	verde model dumpmod cr
	azul 'bbonetest dumpbbc cr

	[ onpoints 'nodob ! ; ]
	'addnodemove
	'addnodeup
	guiMap
	;

|------------
:searchfather | adr nivel -- father
	swap ( @+ $ff and pick2 >=? )( drop 20 - )
	drop
	model - 4 >>
	3 << 'cachereal +
	nip
	;


:movenodemove
	nodob 0? ( drop ; )
    xymouse zcam invproject3d 'yd ! 'xd !
	1- dup 4 << model + >a
    0? ( drop yd xd a> 4+ !+ ! ; )	| root!
    1- 4 << model +
    a@+ $ff and
	searchfather
	@+ xd swap - a!+
	@ yd swap - a!
	;

:editamovenode
	gris
	"* MOVE NODE " fillp
	blink 1? ( verde )( blanco ) drop
	print cr

	verde model dumpmod cr
	azul 'bbonetest dumpbbc cr

	[ onpoints 'nodob ! ; ]
	'movenodemove
	[ ; ]
	guiMap
	;

|--------------------- compile
| largo|nivel rxyz
| 24|8 10|10|10

:drawskin | level --
	0 0 0 project3d
	2dup
	pick4 1 =? ( amarillo )( verde ) drop
	2 fbox 2dup op
	swap
	2dup cachepoint> !+ !+ 'cachepoint> !
	pick2 3 << 'boneslevel + !+ !
	2 <? ( ; )
|	skinsprite
	1- 3 << 'boneslevel + 
	@+ swap @ azul line
	;

:bbone | largo lev ves tipo -- largo xx
	0? ( 2drop ; ) drop	| sin dibujo
	mpush
	1.0 pick3 7 >> 1.0 mscalei
	3dvesprite
	mpop
  	;
:a
	mpush
	pick2 9 >>
	0 over 0 mtransi	| medio de segmento
	1.0 swap 1.0 mscalei
	3dvesprite
	mpop
	;

:drawbbones | adr --
	>b
	0 ( b@+ 1? )( dup $ff and	| nivel largo nnivel
		rot over - 1+ clamp0 | anterior-actual+1
		nmpop
		mpush
		b@+ mrotzi
		b@+ b@+ swap bbone
		swap 8 >> 0 swap 0 mtransi
|		drawskin
		) drop
	nmpop
	;

#dx #dy

:convert2bb | bones --
	16 + >b
	'bbonetest
	0 $1 rot !+ !+	| root
	0 0 rot !+ !+
	0 'boneslevel 4+ !
	( b@+ 1? )(
		$ff and
		b@+ dup 'dx ! dup *.
		b@+ dup 'dy ! dup *. +
		b@+ dup *. +
		sqrt. 8 <<
		over >r
		or
		swap !+
		dx dy atan2
		dup r@ 2 << 'boneslevel + !
		r> 1- 2 << 'boneslevel + @ -
		swap !+

		1 swap !+
		'vsbone swap !+

		) swap !
	;

#skinnode

:addskin
	skinnode 0? ( drop ; )
	1- 4 << 'bbonetest + 12 +
	'vshead swap ! ;

:drawcursors
	skinnode 0? ( drop ; ) 1-
	2 << 'cachepoint + @ xy>xxyy
	blink 1? ( negro )( blanco ) drop
	4 box
	;

:editaskin
	gris
	"* SKIN MODE " fillp
	blink 1? ( verde )( blanco ) drop
	print cr

	[ onpoints 'skinnode ! ; ] [ ; ] guidnmove
	drawcursors
	;

|---------------------
:savebones
	"media/bon" dlgfilesave 0? ( drop ; )
	'filename strcpy
	'filename ".bon" =pos
	0? ( ".bon" pick2 strcat ) drop
 	'bbonetest
 	cachepoint> 'cachepoint - 3 << | bones count bytes!!
	'filename save
	;

:loadbones
	"media/bon" dlgfileload 0? ( drop ; )
	dup 'filename strcpy
	'bbonetest swap load 0 swap !
	 ;


|---------------------
:freelook
	xymouse
	sh 2/ - 7 << swap
	sw 2/ - neg 7 << swap
	neg mrotx
	mroty ;

:3dscreen
	|--- grafic
	omode
|	freelook
	xcam ycam zcam mtrans
	model drawbones

	|---- debug
    24.0 0 0 mtransi
	'bbonetest drawbbones
	;


#modonode 'editaddnode

:main
	33
	show clrscr fonti
|		dup "%d" print cr
		"F1:New F2:Edit F3:CONV F4:Skin F5:SAVE F6:LOAD" print cr
       	modonode exec

		3dscreen

		[ 'editaddnode 'modonode ! ; ] <f1>
		[ 'editamovenode 'modonode ! ; ] <f2>
		[ model convert2bb 'editaskin 'modonode ! ; ] <f3>
		[ addskin ; ] <f4>

		'savebones <f5>
		'loadbones <f6>

		'exit >esc<

		cminiflecha ;

:ini
	mark
	here dup 'model !
	$fff + 'here !
	model.reset
	;

: ini main ;