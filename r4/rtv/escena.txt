| escena
| PHREDA 2017
|------------------
^r4/lib/gui.txt
^r4/lib/part8.txt
^r4/lib/vesprite.txt

^r4/lib/penner.txt

^r4/rtv/fx1.vsp

#fx 0 0		| fx
#objs 0 0	| objetos
#tims 0 0	| tiempo

#xcam #ycam #zcam 3.0

| x y z rx ry rz scale dibujo
:drawsprite
	dup >b
	mpush
	b@+ b@+ b@+ mtransi
	b@+ mrotxi
	b@+ mrotyi
	b@+ mrotzi
	b@+ 1? ( dup dup mscalei )( drop )
	b@ 3dvesprite
	mpop ;

:sprite.move2d | x y spr --
	>a swap a!+ a! ;

:sprite.move | x y z spr --
	>a rot a!+ swap a!+ a! ;

:objs! | spr sca rz ry rx z y x --
	'objs p! >a
	a!+ a!+ a!+
	a!+ a!+ a!+
	a!+ a! ;

:objs.clear
	'objs p.clear
	'tims p.clear
	'fx p.clear
	;

:emem
	mark
	1024 'objs p.ini
	1024 'tims p.ini
	1024 'fx p.ini
	;

|---------- animadores
#deltat
#prevt

:time.ini
	msec 'prevt ! 0 'deltat ! ;

:time.next
	msec dup prevt - 'deltat ! 'prevt ! ;

|-----
:catmullRom | p0 p1 p2 p3 t -- v
	>r pick2 - 2/ 	| p0 p1 p2 v1
	>r rot over
	swap - 2/ r>	| p1 p2 v0 v1
	2dup +			| p1 p2 v0 v1 v0+v1
	pick3 2* - pick4 2* + 
	r@ dup dup *. *. *.	| p1 p2 v0 v1 A
	pick4 dup 2* + neg pick4 dup 2* + + pick3 2* - rot - 
	r@ dup *. *. +		| p1 p2 v0 A+B
	swap r> *. +
	nip + ;

|---- tiempo
:tiempo
	>a
	a@ deltat + dup a!+
	a@+ *. 1.0 min	| t
	a@+ over Ela_InOut
	*. a@+ + a@ !
	1.0 - 1? ( drop ; )
	;

:tiempo2
	>a
	a@ deltat + dup a!+
	a@+ *. 1.0 min	| t
	dup Ela_InOut
	a@+ over *. a@+ +	| t tc x1
	a@+ rot *. a@+ +	| t x1 y1
	swap a@ !+ !
	1.0 - 1? ( drop ; )
	;

|--- agrega a timeline
:+rot2d  | fn obj time --
	'tiempo 'tims p!+ >a
	0 a!+ 1000 *. 1.0 swap /. a!+
	20 +	| fn obj
	dup @			| fn obj obja
	rot over - a!+	| dB
	a!+				| A
	a! ;

:+scale  | fn obj time --
	'tiempo 'tims p!+ >a
	0 a!+ 1000 *. 1.0 swap /. a!+
	24 +	| escala
	dup @			| fn obj obja
	rot over - a!+	| dB
	a!+				| A
	a! ;

:+move2d | x y obj time --
	'tiempo2 'tims p!+ >a
	0 a!+ 1000 *. 1.0 swap /. a!+	| x y obj
	rot over @ swap over - a!+ a!+		| y obj
	swap over 4+ @ swap over - a!+ a!+	| obj
	a!
	;


|----------------------------------
:escena
	time.next
	'tims p.exec

	omode
|	freelook
|	msec 4 << mroty
	xcam ycam zcam mtrans
	'drawsprite 'objs p.mapv
	;

|---------------------------------- TEST
:dump
	dup >b
	b@+ b@+ "%d %d " print
	b@+ b@+ "%f %f " print
	b@+ b@+ "%f %f " print cr
	;

:DUMPS
	'dump 'tims p.mapv
	;

:sord
	'svg6 rand 0.5 mod 0.8 + rand 0 0 0.0 rand 1.0 mod rand 1.0 mod objs! ;

:randobj
	rand 'objs p.cnt mod abs 'objs p.nro ;

:randtime
	rand 2.0 mod 3.0 + ;


:movt
	rand 1.0 mod rand 1.0 mod
    randobj
	randtime
	+move2d ;

:scat
	rand 0.8 mod 1.0 +
	randobj
	randtime
	+scale ;

:rot2
	rand 2.0 mod
	randobj
	randtime
	+rot2d ;


:main
	33
	time.ini
	show clrscr
		fonti
		dup "%d " print cr
		DUMPS cr
|		'objs p.cnt "%d" print cr

		escena
		'sord <f1>

		'movt <f2>
		'scat <f3>
		'rot2 <f4>

		'exit >esc<
		;

: emem main ;