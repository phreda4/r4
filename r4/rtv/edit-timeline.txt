| edit timeline
| 2017 PHREDA
|-----------------------------------------
^r4/lib/gui.txt
^r4/lib/part16.txt

^r4/lib/fontj.txt
^r4/lib/btn.txt

^r4/lib/input.txt
^r4/lib/win.txt
^r4/lib/dlgfile.txt
^r4/lib/dlgcol.txt
^r4/lib/vesprite.txt

^r4/lib/boxprint.txt
^r4/lib/penner.txt

^r4/rtv/testskin.vsp
^r4/rtv/fx1.vsp
^r4/rtv/tool16.ico

^r4/lib/trace.txt

#bon1
#bon2

#xcam 0 #ycam 0 #zcam 50.0
#xr 0 #yr 0

#time>
#ltime 0

#timei 0
#timew 60	| calculado
#obji 0
#objw 18    | calculado


#fx 0 0		| fx
#objs 0 0	| objetos
#tims 0 0	| tiempo
#timeline 0 0

#strmem )( $ffff
#strmem> 'strmem

|---------- str mem
:strini
	'strmem 'strmem> ! ;

:,str | "" -- adr
	strmem> dup >r
	( swap c@+ 1? )( rot c!+ )
	rot c!+ 'strmem> !
	drop
	r> 'strmem - ;

:str. | adr -- radd
	'strmem + ;

|---------- animadores
#deltat
#prevt

:time.ini
	msec 'prevt ! 0 'deltat ! ;

:time.next
	msec dup prevt - 'deltat ! 'prevt ! ;


#cadapos

:generatebox
	dup 4+ >r
	mpush
	r@+ r@+ r@+ mtransi
	r@+ 1? ( mrotxi )( drop )
	r@+ 1? ( mrotyi )( drop )
	r@+ 1? ( mrotzi )( drop )
	r@+ 1? ( dup dup mscalei )( drop )
	rdrop

    cadapos >r
	-1.0 -1.0 0	project3d xy>d r!+
	-1.0 1.0 0	project3d xy>d r!+
	1.0 1.0 0 project3d xy>d r!+
	1.0 -1.0 0	project3d xy>d r!+
	r> 'cadapos !

	mpop ;

:generatepick
	omode
	xcam ycam zcam mtrans
	here 'cadapos !
	'generatebox 'objs p.mapv
	;

|------------ point in cuad
#xt #yt
#inside

|...x2-x1 * y-y1 - y2-y1 * x-x1
:cross | x1 y1 x2 y2 -- x2 y2 cr
	2swap 						| x2 y2 x1 y1
	pick3 pick2 - yt pick2 - *	| x2 y2 x1 y1 A
	pick3 rot - rot neg xt + * - ;

:top | x y -- x y x y
	0 'inside ! 2dup ;

:tline | x y x y -- x y 1..fail
	cross 0? ( ; )
	inside 0? ( swap 'inside ! ; )
	xor $80000000 and ;

:tinside | x y x y -- 1..fail
	2swap cross nip nip 0? ( ; )
	inside xor $80000000 and ;


:searchin | adr -- 1
	>r
	r@+ d>xy top
	r@+ d>xy tline drop
	r@+ d>xy tline 1? ( rdrop nip nip nip nip ; ) drop
	r> @ d>xy tline 1? ( nip nip nip nip ; ) drop
	tinside
	;

:pickobj
	xymouse 'yt ! 'xt !
	here >r
	'objs p.cnt ( 1? )(
		r searchin 0? ( drop rdrop 'objs p.cnt swap - ; )
		drop 16 r+ 1- ) drop rdrop -1 ;

:drawpick
	verde
	here >r
	'objs p.cnt ( 1? )( 1-
		r@+ d>xy 2dup op
		r@+	d>xy line
		r@+	d>xy line
		r@+	d>xy line line
		) drop rdrop ;



|--- bones
:loadbon | filename -- mem
	here dup rot
	load 0 swap !+ 'here !
	|... fix
	dup
	( @+ 1? )( drop
		'vsbone over 8 + ! | adr of vesprite is incorrect!
		12 + ) 2drop
	|...
	;

:bbone | largo lev ves tipo -- largo xx
	0? ( 2drop ; ) drop	| sin dibujo
	mpush
	1.0 pick3 7 >> 1.0 mscalei
	3dvesprite
	mpop
  	;

:drawbbones | adr --
	>r
	0 ( r@+ 1? )( dup $ff and	| nivel largo nnivel
		rot over - 1+ clamp0 | anterior-actual+1
		nmpop
		mpush
		r@+ mrotzi
		r@+ r@+ swap bbone
		swap 8 >> 0 swap 0 mtransi
		) drop
	rdrop
	nmpop
	;

|---Objs
| sprite
| text
| bones-skin
| bitmap
| fx

:tl.reset
	;

:tl.draw
	;

|--------------------------
| x y z rx ry rz scale dibujo
:drawsprite | adr -- adr
	>r
	mpush
	r@+ r@+ r@+ mtransi
	r@+ 1? ( mrotxi )( drop )
	r@+ 1? ( mrotyi )( drop )
	r@+ 1? ( mrotzi )( drop )
	r@+ 1? ( dup dup mscalei )( drop )
	r> @ 3dvesprite
	mpop ;

:objs! | spr sca rz ry rx z y x --
	'drawsprite 'objs p!+ >r
	r!+ r!+ r!+
	r!+ r!+ r!+
	r!+ r> ! ;

|----------------------------
:drawtext | adr -- adr
	>r
	mpush
	r@+ r@+ r@+ mtransi
	r@+ 1? ( mrotxi )( drop )
	r@+ 1? ( mrotyi )( drop )
	r@+ 1? ( mrotzi )( drop )
	r@+ 1? ( dup dup mscalei )( drop )
	r@+ dup 24 >> $f and swap $ffffff and
	setfont3d
	r@+ dup $ffffff and ink
	r> @ str.
	boxplacec
	swap 24 >> $ff and boxprint
	mpop ;

:text! | str tt tt sca rz ry rx z y x --
	'drawtext 'objs p!+ >r
	r!+ r!+ r!+
	r!+ r!+ r!+
	r!+
	r!+ r!+
	,str r> ! ;

|----------------------------
:objs.clear
	'objs p.clear
	'tims p.clear
	'fx p.clear
	;

:addspr

	;

| objeto texto
:addtex
	;

:addfig
	;


|---------------------------
:randrotz	rand 0 0 ;
:randrot	rand rand rand ;
:randxyz
	0.0
|	rand 25.0 mod
|	rand 25.0 mod
	xymouse zcam invproject3d
	swap
	;

:mousexyz
	0 xymouse zcam invproject3d swap ;


|---------------------------
:rand+obj
	'svg6
	rand 0.5 mod 1.8 +

	randrotz
	randxyz
	objs!
	generatepick
	;

:rand+txt
	"Hola_a_todos"	| texto
	$12 24 <<		| align
	rand $ffffff and or	 | color
	$7 24 <<			| font
	rand 1.0 mod 3.0 + $ffffff and
	or	| size

	0.0 | scale
	randrotz
	randxyz
	text!
	generatepick
	;


|---------------------------
#cursorobj -1

:dnobj
	pickobj 'cursorobj !
	;

:moveobj
	cursorobj -? ( drop ; )
	6 << 'objs 4+ @ + >r
	xymouse zcam invproject3d
	swap
	4 r+ r !
	4 r+ r !
	rdrop
	generatepick
	;

:drawcursor
	'dnobj 'moveobj [ ; ] guiMap
	cursorobj -? ( drop ; )
	verde
	4 << here + >r
	r@+ d>xy
	2dup 4 box
	2dup op
	r@+	d>xy line
	r@+	d>xy line
	r@+	d>xy line line
	rdrop ;

:freelook
	xymouse
	sh 2/ - 7 << swap
	sw 2/ - neg 7 << swap
	neg mrotx
	mroty ;


:3dscreen
	time.next
	'tims p.draw

	omode
|	freelook
	xcam ycam zcam mtrans
	drawcursor
	'objs p.draw
	;
:a
    -12.0 0 0 mtransi
	bon1 drawbbones

    24.0 0 0 mtransi
	bon2 drawbbones
	;

|---------------
#txttxt )( 256
#txtcolor $ff
#txtaa $c
#txtfont 7
#txtsize 32
#txtpadx 0
#txtpady 0

:addtxt
	'txttxt		| texto
	txtaa 24 <<		| align
|	txtcolor $ffffff and or	 | color
|	txtfont 24 <<			| font
|	txtsize $ffffff and
|	or	| size

|	"Hola_a_todos"	| texto
|	$12 24 <<		| align
	rand $ffffff and or	 | color
	$7 24 <<			| font
	rand 1.0 mod 3.0 + $ffffff and
	or	| size

	0.0 | scale
	0 0 0 	|	randrotz
	0 0 0	|	mousexyz
	text!
	generatepick exit ;

:edit-txt
	blanco
	"Texto:" print cr
	'txttxt 256 32 3 << 4 3 <<
	inputm
	cr cr cr cr cr cr cr2
    "Color:" print 'txtcolor inputhex cr cr
	blanco "Fuente:" print txtfont "%d" print cr cr2
|	cyan 'dfont "<" btnt sp 'ufont ">" btnt cr cr
	blanco "Tamaño:" print 'txtsize inputdec cr cr2
	blanco "Alineacion:" print cr cr2
|	"1" 1 bal sp "2" 2 bal sp "3" 3 bal cr cr
|	"4" 5 bal sp "5" 6 bal sp "6" 7 bal cr cr
|	"7" 9 bal sp "8" 10 bal sp "9" 11 bal cr cr
	blanco
	"Pading:" print cr cr2
	"x:" print 'txtpadx inputdec sp
	"y:" print 'txtpady inputdec cr cr2

	cr cr verde
    'addtxt " + " btnt
	;

|---------------
:addimg
	'svg6
	rand 0.5 mod 1.8 +
	0 0 0	|	randrotz
	0 0 0	|	randxyz
	objs!
	generatepick exit ;

:edit-img
	blanco
	"Figura: " print

	cr cr verde
    'addimg " + " btnt
	;

|---------------
#modoobjr 'edit-img 'edit-txt
#modoobjl 'i_img 'i_name 0
#modoobjv 0

:edit-obj
	show clrscr
		3dscreen

		fonti scr home
		gris 2 linesfill
		2 2 atxy
		rojo 'exit dup >esc< 'i_exit ibtn
		verde
        'modoobjv 'modoobjl icomode
        cr cr verde
        modoobjv 2 << 'modoobjr + @ exec
		cminiflecha ;

|------------------------------------------
:addkeyframe | obj time --
	'timeline p!+ ! 	|	!+ >r
    0 'timeline p.lastsort
	;

:addtime
	rand 8 >> $f and	| objeto
	rand 9 >> $ff and
	'timeline p!+ ! 	|	!+ >r
    0 'timeline p.lastsort
	;

|------------------------------------------
:afirs
	0 'time> ! ;
:aprev
	time> 1- 0 max 'time> ! ;
:anext
	time> 1+ ltime >? ( dup 'ltime ! ) 'time> ! ;
:alast
	ltime 'time> ! ;

:aplay
	time.ini
	show60 clrscr
		time.next
		'tims p.draw

		omode
	|	freelook
		xcam ycam zcam mtrans
		'objs p.draw

		'exit >esc<
	;

#toolplay 'icofi 'afirs  'icopr 'aprev 'icopl 'aplay 'icone 'anext 'icola 'alast 0

:dlgPlay
	sp cyan
	'toolplay icotool
	verde
	ltime time> " %d/%d " print
	;

#winPlay 0 0 16 180 50 "Play"

|-------------------------------------
:drawpoint
	dup
	@+
	timei <? ( 2drop ; ) timei - timew >? ( 2drop ; )
	swap @
	obji <? ( 2drop ; ) obji - objw >? ( 2drop ; )
	| x y
	3 << 32 + ty1 + swap
	3 << 32 + tx1 + swap
	3 fbox
	;

:timelineobjs
	tx1 ty1 48 + atxy
	'objs p.cnt
	0 ( objw <? )(
		dup obji +
		pick2 >=? ( 3drop ; )
		"%d" print cr
		1+ ) 2drop ;

:timelinecursor
	blanco
 	time> timei -
	3 << 32 + tx1 + 1-
	ty1 48 +
	over 2 +
	ty2 8 - caja
	;

:dlgTime
	fontj
	sp cyan
	'toolplay icotool
	negro
	ltime time> " %d/%d " print
	cr

	timelineobjs
	timelinecursor
	verde
	'drawpoint 'timeline p.mapv

	[ timei 1- clamp0 'timei ! ; ] <le>
	[ 1 'timei +! ; ] <ri>
	[ obji 1- clamp0 'obji ! ; ] <up>
	[ 1 'obji +! ; ] <dn>

	fonti
	;

#winTime 1 1 300 480 200 "Time"

|-------------------------------------
:dlgobj
	cursorobj -? ( drop ; )
	verde
	6 << 'objs 4+ @ +
	4+ >r
|	"x:" print 'modx 8 4 inputreal cr
	r@+ "x:%f " print

	r@+ "y:%f " print
	r@+ "z:%f " print cr
	r@+ "rx:%f " print
	r@+ "ry:%f " print
	r@+ "rz:%f " print cr
	r@+ "s:%f " print cr
	rdrop ;

#winObj 0 600 16 200 250 "Obj"

|-------------------------------------
:main
	sw 2 - dup 'wintime 12 + !
	32 - 3 >> 'timew !
	sh 204 - 'wintime 8 + !

	sw 204 - 'winobj 4+ !
	4
	show fonti clrscr
		3dscreen

		fonti scr home
		gris 1 linesfill
		blanco dup ":r%d EditTimeline " print
		verde
		"F2+Spr" print sp
		"F3+Tex" print sp
		"F4+Fig" print sp
|		azul 'dlgplay 'winplay guiWin
		azul 'dlgTime 'wintime guiWin
		azul 'dlgobj 'winobj guiWin

		cr

        'edit-obj <f1>
|       'addspr <f2>
        'rand+obj <f2>
|        'addtex <f3>
        'rand+txt <f3>
|        'addfig <f4>

		'addtime <f4>

		'exit >esc<
		cminiflecha ;

:ini
	rerand
	mark

	1024 'objs p.ini
	1024 'tims p.ini
	1024 'fx p.ini
	1024 'timeline p.ini

	"media/bon/test1.bon" loadbon 'bon1 !
	"media/bon/person.bon" loadbon 'bon2 !
	;

: ini main ;