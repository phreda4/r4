| simple mandelbrot viewer
| PHREDA 2010
|---------------------------------
^r4/lib/btn.txt

#xmax #ymax #xmin #ymin

:calc | p q cx cy -- p q cx cy xn yn r
	over dup *. over dup *. - pick4 + | xn
	pick2 pick2 *. 2* pick4 +		| xn yn
	over dup *. over dup *. +			| xn yn r
	;

:mandel | x y -- x y v
	over xmax xmin - sw */ xmin +	| x y p
	over ymax ymin - sh */ ymin +	| x y p q
	0 0 0 | cx cy it
	( 255 <? )( >r 	| x y p q cx cy
		calc		| x y p q cx cy xn yn r
		4.0 >? ( 4drop 3drop r> ; )
		drop rot drop rot drop
		r> 1+ )
	nip nip nip nip
	;

:scrman
	0 ( sh <? )(
		0 over setxy
		0 ( sw <? )(
			swap
			mandel dup 8 << or dup 8 << or px!+
			swap
			1+ ) drop
		1+ ) drop
	;

:main
	2.0 'xmax ! 2.0 'ymax !
	-3.0 'xmin ! -2.0 'ymin !
	scrman
	blanco scr home " Mandelbrot" print
	show
		'exit >esc<
		;

: main ;

