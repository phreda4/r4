| haikus.txt - 2011 phreda
| from http://forthsalon.appspot.com/
|--------------------------------
^r4/lib/gui.txt
^r4/lib/math.txt
^r4/lib/trace.txt

#x #y

|--- insert haiku HERE
|--- burn - BradN
:square dup *. ;
:len square swap square + sqrt ;
:spiral
	x - swap y -
	2dup len >r atan2 r> + 20.0 *. sin ;

:haiku | -- r g b
	msec $ffff and | time
	|0.2
	0.2 spiral
	0.7 0.3 spiral
	0.4 0.6 spiral *. *.
	dup x *.
	1.0 x - 1.0 y - + 0.25 *.
	;
|---------------------

:rgb>c | r g b -- c
	-? ( 0 nip )
	8 >> $ff and swap
	-? ( 0 nip )
	$ff00 and or swap
	-? ( 0 nip )
	8 << $ff0000 and or
	;

:frame
	0 0 setxy
	0 ( 256 <? )(
|		dup 1.0 256 */ 'y !
		dup 1.0 8 *>> 'y !
		0 over setxy
		0 ( 256 <? )(
|			dup 1.0 256 */ 'x !
			dup 1.0 8 *>> 'x !
			haiku rgb>c a!+
			1+ ) drop
		1+ ) drop
	;

:main
	show clrscr
		frame
		cmano
		'exit >esc< ;

: main ;
