| r4code.txt
| Interprete de R4
| PHREDA 2007
|---------------------------------
^r4/lib/print.txt
^r4/lib/trace.txt

^r4/system/mem-dic.txt

^r4/system/vstack.txt
^r4/system/r4token.txt
^r4/system/r4post.txt
^r4/system/r4print.txt

#:<<ip

#memvar
#freemem
#msg

|---- Comprobaciones
:dstack? | cnt --
	3 << neg dpila> +
	'dpila >=? ( drop ; ) drop
	"data stack empty" 'msg ! 0 nip ;

:rstack? | cnt --
	3 << neg rpila> +
	'rpila >=? ( drop ; ) drop
	"return stack empty" 'msg ! 0 nip ;

:progmem? | adr -- adr
	prog >=? ( prog> <=? ( ; ) )
	"fuera de codemem" 'msg ! 0 nip ;

:datamem? | adr -- adr 0/1
	memvar >=? ( freemem <=? ( ; ) )
	"fuera de datamem" 'msg ! 0 nip ;

|--- interprete
:i0		drop ;
:i:		drop ;
:i#		drop ;

:idec	tok>cte d.pushDec ;
:ihex	tok>cte d.pushHex ;
:ibin	tok>cte d.pushBin ;
:ifix	tok>cte d.pushFix ;
:istr	tok>str d.pushStr ;

|--------- ejecuta palabra
:iwor	8 >> |dup +code
		nro>tok swap r.pushExe ;

|-------- ejecuta palabra

:ivar	8 >> nro>mem @ d.pushVar ;
:idvar	8 >> nro>mem d.pushDVar ;
:idwor	8 >> nro>tok d.pushDCod ;

|--- retorna
:i;		drop r.cnt 0? ( nip ; ) 2drop r.pop  ;

|--- estructuras de control
:i)(
:i)		8 >> + ;
:i(     drop ;
:i[     8 >> over d.pushExe + ;
:i]     drop ;

|--ahora sin value desde aca
:iEXEC
	d.pop |0? ( drop ; )
	swap r.pushExe
	|	progmem?
	;

:i0?	@+ d.TOS 0? ( 2drop ; ) drop 8 >> + ;
:i+?	@+ d.TOS +? ( 2drop ; ) drop 8 >> + ;
:i-?	@+ d.TOS -? ( 2drop ; ) drop 8 >> + ;
:i1?	@+ d.TOS 1? ( 2drop ; ) drop 8 >> + ;
:i=?	@+ d.NOS d.pop =? ( 2drop ; ) drop 8 >> + ;
:i<?	@+ d.NOS d.pop <? ( 2drop ; ) drop 8 >> + ;
:i>?	@+ d.NOS d.pop >? ( 2drop ; ) drop 8 >> + ;
:i<=?	@+ d.NOS d.pop <=? ( 2drop ; ) drop 8 >> + ;
:i>=?   @+ d.NOS d.pop >=? ( 2drop ; ) drop 8 >> + ;
:i<>?   @+ d.NOS d.pop <>? ( 2drop ; ) drop 8 >> + ;
:iAND?  @+ d.NOS d.pop AND? ( 2drop ; ) drop 8 >> + ;
:iNAND? @+ d.NOS d.pop NAND? ( 2drop ; ) drop 8 >> + ;

|--- memoria
:i@     d.pop @ d.pushVar ;
:iC@	d.pop c@ d.pushVar ;
:iW@    d.pop w@ d.pushVar ;
:i!     d.pop2 ! ;
:iC!    d.pop2 c! ;
:iW!    d.pop2 w! ;
:i+!	d.pop2 +! ;
:iC+!	d.pop2 c+! ;
:iW+!	d.pop2 w+! ;
:i@+	d.pop @+ swap d.pushVar d.pushVar ;
:iC@+	d.pop c@+ swap d.pushVar d.pushVar ;
:iW@+	d.pop w@+ swap d.pushVar d.pushVar ;
:i!+	d.pop2 !+ d.pushVar ;
:iC!+	d.pop2 c!+ d.pushVar ;
:iW!+	d.pop2 w!+ d.pushVar ;

#rega

:i>A	d.pop 'rega ! ;
:iA>	rega d.pushVar ;
:iA@    rega @ d.pushVar ;
:iA!    d.pop rega ! ;
:iA+	d.pop 'rega +! ;
:iA@+	rega @+ d.pushVar 'rega ! ;
:iA!+   d.pop rega !+ 'rega ! ;

#regb

:i>B	d.pop 'regb ! ;
:iB>	regb d.pushVar ;
:iB@    regb @ d.pushVar ;
:iB!    d.pop regb ! ;
:iB+	d.pop 'regb +! ;
:iB@+	regb @+ d.pushVar 'regb ! ;
:iB!+   d.pop regb !+ 'regb ! ;

|---- pantalla
:iSW 		sw 2/ d.pushDec ;
:iSH  		sh 2/ d.pushDec ;
:iFRAMEV	framev d.pushVar ;
:iREDRAW    ; |redraw >xfb ;
:iUPDATE    ; |1 update drop ;

|---- dibujo
:iOP    	d.pop2 op ;
:iLINE  	d.pop2 line ;
:iCURVE 	d.pop4 curve ;
:iCURVE3 	d.pop6 curve3 ;
:iPLINE 	d.pop2 pline ;
:iPCURVE 	d.pop4 pcurve ;
:iPCURVE3 	d.pop6 pcurve3 ;
:iPOLI   	poli ;
:iFCOL   	d.pop2 fcol ;
:iFCEN   	d.pop2 fcen ;
:iFMAT   	d.pop2 fmat ;
:iSFILL  	sfill ;
:iLFILL  	lfill ;
:iRFILL  	rfill ;
:iTFILL  	d.pop tfill ;

:iINK    	d.pop ink ;
:iINKF   	ink@ d.pushVar ;
:iALPHA  	d.pop ALPHA ;
|----- sis
:iMSEC   	msec d.pushVar ;
:iTIME   	time rot d.pushVar swap d.pushVar d.pushVar ;
:iDATE   	date rot d.pushVar swap d.pushVar d.pushVar ;
:iEND    	drop 0 ;
:iRUNi   	vDROP ;
|-----mouse
|:iTPEN		tpen d.pushVar ;
:iXYMOUSE	xymouse swap d.pushVar d.pushVar ;
:iBMOUSE    bmouse d.pushVar ;
|-----teclado
:iSKEY  	d.pop key! ;
:iKEY		key d.pushVar ;
|-----joystick
:iCNTJOY    cntjoy d.pushVar ;
:iGETJOY    d.pop GETJOY d.pushVar ;
|--- carpetas
:iFFIRST	d.pop FFIRST d.pushVar ;
:iFNEXT		FNEXT d.pushVar ;
|--- archivos
:iLOAD      d.pop2 load d.pushVar ;  | 'from "filename" -- 'to
:iSAVE      d.pop3 save ;   |  'from cnt "filename" --
:iAPPEND      d.pop3 append ;   |  'from cnt "filename" --

:iMEM       freemem d.pushVar ; | mem

|--- mueve mem
:iMOVE      d.pop3 move ;
:iMOVE>     d.pop3 move> ;
:iFILL     d.pop3 fill ;
:iCMOVE     d.pop3 cmove ;
:iCMOVE>    d.pop3 cmove> ;
:iCFILL     d.pop3 cfill ;

|--- sonido
:iSLOAD     d.pop SLOAD d.pushVar ;  | "" -- n
:iSPLAY     d.pop SPLAY ;  | n/0 --
:iSINFO     d.pop SINFO d.pushVar ;  | "" -- n
:iSSET     d.pop4 SSET ;  | pan vol freq "" --

:iOPENURL	d.pop3 openurl d.pushVar ;

|---------- tabla de saltos
#tablai	| tabla de saltos de instrucciones
i0 i: i#
idec ihex ibin ifix istr
iwor ivar idwor idvar
i;
i(		i)(		i)		i[		i]		iEXEC
i0?		i+?		i-?		i1?		i=?		i<?		i>?		i<=?	i>=?	i<>?	iAND?	iNAND?
vDUP	vDROP	vOVER	vPICK2	vPICK3	vPICK4	vSWAP	vNIP	vROT
v2DUP	v2DROP	v3DROP	v4DROP	v2OVER	v2SWAP
v>R		vR>		vR@
vAND	vOR		vXOR	vNOT	v+		v-		v*		v/		v*/
v*>>	v/MOD	vMOD	vABS	vSQRT   vCLZ	v<</
vNEG	v1+		v4+		v1-		v2/		v2*		v<<		v>>	v0>>
i@		iC@		iW@		i!		iC!		iW!		i+!		iC+!	iW+!
i@+		i!+		iC@+	iC!+	iW@+	iW!+
i>A	iA> iA@ iA! iA+ iA@+ iA!+
i>B	iB> iB@ iB! iB+ iB@+ iB!+
iMOVE	iMOVE>	iFILL iCMOVE	iCMOVE> iCFILL
iMEM	iFFIRST	iFNEXT
iLOAD	iSAVE	iAPPEND
iUPDATE
iXYMOUSE iBMOUSE
iSKEY	iKEY
iCNTJOY	iGETJOY
iMSEC	iTIME	iDATE	iEND	iRUNi
iSW		iSH		|iCLS
iREDRAW	iFRAMEV
|iSETXY	iPX+!	iPX!+	iPX@ iXFB	i>XFB	iXFB> iPAPER
iINK	iINKF	iALPHA
iOP		iLINE	iCURVE	iCURVE3 iPLINE	iPCURVE	iPCURVE3 iPOLI
iFCOL	iFCEN	iFMAT	iSFILL	iLFILL	iRFILL	iTFILL
iSLOAD	iSPLAY	iSSET	iSINFO					|--- Sonido
iOPENURL

::dumpta
	'tablai memmap ;

::stepmacro | nro --
  	11 + 2 << 'tablai +  @ exec ;

::stepword | adr --
	dup 8 + @ 1 and? ( drop 12 + @ @ d.pushVar ; ) drop
	4+ @
	<<ip dup r.pushExe
	swap
	( over <>? )(
		@+ dup $ff and
		17 >? ( nip )
		2 << 'tablai + @ exec
		0? ( '<<ip ! 2drop ; )
		) 2drop
	'<<ip ! ;

::stepadr | adr --
	dup 8 + @ 1 and? ( drop 12 + @ d.pushVar ; ) drop
	4+ @ d.pushExe
	;


|-------------------------------
| Asigma memoria para ejecucion
|
:mem0 | nro --
	freemem !+ 'freemem ! ;
:mem1 | nro --
	freemem w!+ 'freemem ! ;
:mem2 | nro --
	freemem c!+ 'freemem ! ;
:mem3 | nro --
	'freemem +! ;

#gmem 'mem0

:gstr | adr --
	freemem
	( swap c@+ 1? )( rot c!+ ) rot
	c!+ 'freemem ! drop ;

:accpal | val instr --
	7 <? ( drop tok>cte gmem exec ; ) 	| nro
	7 =? ( drop tok>str	gstr ; )			| str
	12 <? ( drop 8 >>
			dup nro>var? 1? ( drop nro>mem )( drop nro>tok )
			mem0 ; )	| pal
	13 =? ( 2drop 'mem2 'gmem ! ; ) | (
	14 =? ( 2drop 'mem3 'gmem ! ; )	| )(
	16 =? ( 2drop 'mem1 'gmem ! ; ) | [
	2drop 'mem0 'gmem ! ;

|idec ihex ibin ifix istr	3 4 5 6 7
|iwor ivar idwor idvar      8 9 10 11
|i;							12
|i(	i)(	i) i[ i]			13 14 15 16 17

:addmem | inifree adr1token -- inifree adr1token
	dup @  | inicio tokens
	'mem0 'gmem !
	( @+ 1? )(
		dup $ff and
		3 <? ( 3drop ; ) | # o :
		17 >? ( 3drop ; ) | o > ]
		accpal
       )
	2drop
	;



:tokeninfolist | adr cnt --
	( 1? )( 1- swap | cnt adr
		<<ip =? ( blink 1? ( negro )( blanco ) drop ">" print )
		@+ tokenprint
		<<ip 4+ =? ( blink 1? ( negro )( blanco ) drop "<" print )
		sp lout? 1? ( cr ) drop
		swap ) 2drop ;

:esvar | adr --
	dup @ "#%w " violeta print
	12 + @
	0? ( "%h" print ; )
	@ "%d" amarillo print
	;

::printword | n --
|	cntwords >=? ( drop ; )
	5 << 'indicepal +
	indicepal> >=? ( drop ; )
	dup 8 + @ 1 and? ( drop esvar ; ) drop
	dup @ ":%w " rojo print
	dup 4+ @
	swap 16 + @
	tokeninfolist
	;

::printname | n --
	5 << 'indicepal +
	indicepal> >=? ( drop ; )
	dup 8 + @ 1 and? ( violeta "#" )( rojo ":" ) print drop
	@ "%w " print ;


|--- reserva memoria para dicc de memoria y memoria de vars
| 1do recorre variables,
|     asigna direccion real y valor en direccion

:c/var | nropal cntll -- nropal
	drop
	dup 5 << 'indicepal +	| nro dirw
	freemem over 4+ 		| nro dirw free tok
	addmem
	freemem pick2 =? ( 4 'freemem +! ) drop | si es variable sin nros
	8 + ! drop
	;

|---- vista de palabras
#viewexec )( 8192 | vista de ejecucion
#viewexec> 'viewexec
#viewstate )( 8192

:+word | nro --
	dup getllamadas 0? ( 2drop ; ) drop
	1 viewexec> 'viewexec - 'viewstate + ! | modo
	viewexec> !+ 'viewexec> !
	;

:+words | nro --
	dup getllamadas 0? ( 2drop ; ) drop
	1 viewexec> 'viewexec - 'viewstate + ! | modo
	viewexec> !+ 'viewexec> !
	;

:viewset | nro --
	2 << 'viewstate + 1 swap ! ;

|---------------------------
::vm.dumpcode
	'viewstate
	'viewexec ( viewexec> <? )(
		@+ rot @+
		0? ( drop swap cr printname )( drop swap cr printword )
		allowchome
		swap ) 2drop
	cr
	;

::startvm | --
	here dup 'memvar ! 'freemem !
   'c/var varmap
::resetvm | --
	d.ini	| inicia pilad
	r.ini	| inicia pilar
	<<boot '<<ip !

	'viewexec 'viewexec> !
	newwords ( cntwords <? )(
		dup +word
		1+ )
	+words
	;

::stepvm | --
	<<ip 0? ( drop ; )
:step..
	@+ dup $ff and
	17 >? ( nip )
	2 << 'tablai + @ exec
	'<<ip ! ;

::stepvmn | --
	<<ip 0? ( drop ; )
	dup @ $ff and 8 <>? ( drop step.. ; ) drop
	dup 4+ swap
	( over <>? )(
		@+ dup $ff and
		17 >? ( nip )
		2 << 'tablai + @ exec
		0? ( 2drop ; )
		) nip
	'<<ip ! ;

