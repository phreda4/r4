| Editor de bmr (BITMAPR)
| PHREDA 2010
|--------------------------------------------------
^r4/lib/gui.txt
^r4/lib/input.txt

^r4/lib/dlg.txt
^r4/lib/dlgfile.txt
^r4/lib/dlgcol.txt

^r4/lib/ricons.txt
^inc/ric/fontawesome.ric
^r4/lib/fonta.txt

^r4/lib/img.txt
^r4/lib/loadjpg.txt
^r4/lib/loadpng.txt
^r4/lib/loadbmp.txt

^r4/lib/bmr.txt
^r4/lib/trazo.txt

^r4/lib/trace.txt

#modo 0
#bmrm #bmrw #bmrh
#bmrww #bmrhh
#orden 0
#nrormt 0
#nomrmt )( 32

#color $ff

#zoom 2
#xi #yi | donde esta el pixel 0,0

#xv #yv	| aux para mover

:refreshbmr
	bmr.wh@
	dup 'bmrhh ! zoom << 'bmrh !
	dup 'bmrww ! zoom << 'bmrw !
	bmr.res@ 'bmrm !
	;

|------ floodfill
#cf
#cc

:filltoh | x y -- x y y2
	2dup
	( 1+ bmrhh <? )( | x y x yn
		2dup bmr@ cf <>? ( drop nip ; ) drop
		cc pick2 pick2 bmr!
		) nip ;

:fillto0 | x y -- x y y1
	2dup
	( 1- +? )( | x y x yn
		2dup bmr@ cf <>? ( drop nip ; ) drop
		cc pick2 pick2 bmr!
		) nip ;

:fillcolorline | x y --
	2dup bmr@ cf <>? ( 3drop ; ) drop
	cc pick2 pick2 bmr!
	fillto0 1+ rot rot | y1 x y
	filltoh 1- nip rot | x y2 y1
	( over <? )( | x y2 yn
		pick2 1+ bmrww <? ( over fillcolorline )( drop )
		pick2 1- -1 >? ( over fillcolorline )( drop )
		1+ ) 3drop ;


:bmrfill! | c x y --
	2dup whin 0? ( 4drop ; ) drop | no dibuja afuera
	yi - zoom >> -? ( 3drop ; )
	swap xi - zoom >> -? ( 3drop ; )
	swap 2dup bmr@ pick3 =? ( 4drop ; )
	'cf ! rot 'cc ! 
	fillcolorline ;

 
|*******************************
:ajusta
	bmr.wh@
	sh 2/ swap zoom << 2/ - 'yi !
	sw 2/ swap zoom << 2/ - 'xi !
    refreshbmr
	;

:drawbmr | --
	bmrm 1 and? ( bmrw bmrh xi yi drawalphagrid ) drop
	xi yi bmrw bmrh bmr.drawscalei
	blanco
	xi 1- yi 1- 2dup op
	over bmrw + 1+ over line
	over bmrw + 1+ over bmrh + 1+ line
	2dup bmrh + 1+ line line
	;

|-----------------------------------
:borrodib
	"Borro Dibujo ?" dlgsino
	1? ( bmr.clear ) drop
	;

|---- imagen
#bmp
#xa #ya #xb #yb
#wb #hb

:clearsel
	-1 'xa ! ;

:keybox | x y
    2dup hb + 'yb ! wb + 'xb ! 'ya ! 'xa ! ;

:modoseleccion
	[ xymouse keybox ; ]
	dup
	guiDnMove
	blink 1? ( blanco )( negro ) drop
	xa -? ( drop ; ) ya xb 1- yb 1- caja
	;

:getimagen
	xa -? ( drop ; )
	xb >? ( xb 'xa ! 'xb ! )( drop )
	ya yb >? ( yb 'ya ! 'yb ! )( drop )
	xb xa -	yb ya - bmr.wh!
	ajusta
	xa ( xb <? )(
		ya ( yb <? )(
		2dup bmp img.get $ffffff and pick2 xa - pick2 ya - bmr!
		1+ ) drop
	1+ ) drop
	exit
	;

:cargabmps
	bmr.wh@ 'hb ! 'wb !
	clearsel
	0 0 keybox
	show clrscr
		0 0 bmp img.draw
		modoseleccion
		blanco
		0 rows 3 - gotoxy
		dup "%d " print
		xa "x1:%d " print
		ya "y1:%d " print
		xb "x2:%d " print
		yb "y2:%d " print cr cr2
		rojo
		'getimagen <f1> "F1-GET" print |btnt no puede ser un botÃ³n porque al clickar cambia la seleccion..
		cyan
		'exit >esc<
		[ xa ya 1+ keybox ; ] <dn>
		[ xa ya 1- keybox ; ] <up>
		[ xa 1+ ya  keybox ; ] <ri>
		[ xa 1- ya keybox ; ] <le>
		cminiflecha ;

:loadimg | filename -- img
	".jpg" =pos 1? ( drop loadjpg ; ) drop
	".png" =pos 1? ( drop loadpng ; ) drop
	".bmp" =pos 1? ( drop loadbmp ; )
	2drop 0 ;

:cargabmp
	mark
	"media/bmp" dlgfileload 0? ( drop empty ; )
	loadimg 0? ( drop empty ; )
	'bmp !
	cargabmps
	empty
	;

|---- botones
:btnric | icon vec --
	ink@ >r btne blanco 2-gc drawric 2+gc r> ink ;

|------------------------------
:modofill
	[ color xymouse bmrfill! ; ] guiBtn ;

:toofill
	;

|---- dibujo en frame
:bmr@m | x y -- c
	2dup whin 0? ( 3drop ; ) drop | no obtiene
	yi - zoom >> swap xi - zoom >> swap
	bmr@ ;

:bmr!m32
	color rot rot
:bmr!m | c x y --
	2dup whin 0? ( 3drop ; ) drop | no dibuja afuera
	yi - zoom >> -? ( 3drop ; )
	swap xi - zoom >> -? ( 3drop ; )
	swap bmr! ;

:mododraw
	[ 'bmr!m32 vop! ; ] [ xymouse vline! ; ]  guiDnMove ;

:toodraw ;

|------EDIT
:pick.dn
	xymouse getpixel 'color !
	0 'modo ! ;

:modoedit
	'pick.dn 'pick.dn guiDnMove ;

:tooedit
	;

|--- modo vista
:vista.dn
	xymouse 'yv ! 'xv ! ;

:vista.move
	xymouse
	dup yv - 'yi +! 'yv !
	dup xv - 'xi +! 'xv ! ;

:bz+ zoom 1+ 6 >? ( 0 nip ) 'zoom ! ajusta refreshbmr ;
:bz- zoom 1- -? ( 6 nip ) 'zoom ! ajusta refreshbmr ;

:modovista
	'vista.dn 'vista.move guiDnMove ;

:toovista
	;

|------- modos
#modos 'mododraw 'modoedit 'modovista 'modofill
#tools 'toodraw 'tooedit 'toovista 'toofill

|----- botonera
#modogui 'mododraw
#modotoo 'toodraw

:setmodo
	dup 'modo !
	dup 2 <<
	dup 'modos + @ 'modogui !
	'tools + @ 'modotoo !
	;

:botonmodo | icon nro --
	modo =? ( azul )( negro )
	'setmodo btne
	modo =? ( blanco )( gris )
	drop
	8-gc drawric 8+gc gcdn ;

:modoactual
	drawbmr
	modogui exec ;

:bundo
:bredo
	;

|---- cambia resolucion
:changew | dw
	bmr.wh@ swap rot + swap
	bmr.wh!
	ajusta ;

:changeh | dh
	bmr.wh@ rot +
	bmr.wh!
	ajusta ;

:changea
	bmr.res@ 1 xor bmr.res!
	refreshbmr ;

:botonera
	verde
|	[ 1 changew ; ] "+" link [ -1 changew ; ] "-" link
|	[ 1 changeh ; ] "+" link [ -1 changeh ; ] "-" link

	[ 16 dup bmr.wh! ajusta ; ] "16" link
	[ 32 dup bmr.wh! ajusta ; ] "32" link
	[ 64 dup bmr.wh! ajusta ; ] "64" link
	[ 128 dup bmr.wh! ajusta ; ] "128" link

|	amarillo 'changea "ALPHA" sp link

	48 qdim
	24 sh 24 - pos
	rojo 'exit dup >esc< btne blanco 'i.off drawric

	24 48 pos
	'i.pencil 0 botonmodo
	'i.tint 1 botonmodo
	'i.move 2 botonmodo
	'i.magic 3 botonmodo

    24 qdim
	-10 -10 +pos
	verde oscuro
	'i.undo 'bundo btnric gc>>
	'i.repeat 'bredo btnric gcdn gc<<

	cyan oscuro
   	'i.zoom_out 'bz- btnric gc>>
	'i.zoom_in 'bz+ btnric gcdn gc<<

	cyan oscuro
	'i.picture 'cargabmp btnric gc>>
	rojo oscuro
	'i.trash 'bmr.clear btnric gcdn gc<<

	violeta oscuro
	'i.long_arrow_left 'bmr.scrl btnric gc>>
	'i.long_arrow_right 'bmr.scrr btnric gcdn gc<<
	'i.long_arrow_up 'bmr.scru btnric gc>>
	'i.long_arrow_down 'bmr.scrd btnric gcdn gc<<
   	'i.resize_vertical 'bmr.flipv btnric gc>>
	'i.resize_horizontal 'bmr.fliph btnric gcdn gc<<

	modotoo exec

	sw 26 - 48 pos
	'color btncol
	;

|-----------------------------
:main
	$aaaaaa paper
	4
	ajusta
	show fonti clrscr
		verde oscuro 2 linesfill
		blanco
		dup ":r%d BMR Editor " print
		bmr.wh@ swap " %dx%d " print
		modoactual
		botonera
		cminiflecha ;

:   mark
	"mem/notepad.bmr" bmr.load
	$fffff 'here +!
	refreshbmr
	main
	"mem/notepad.bmr" bmr.save
	;
