| Editor de nsprite
| PHREDA 2008
|--------------------------------------------------
^r4/lib/gui.txt
^r4/lib/sprite.txt
^r4/lib/trace.txt

^r4/system/edit-1spr.txt

#lugar
#nombre )( 64
#actual
#hojas )( $10000 #hojas>
#indice )( $1000 #indice>

|--------------------------------------------------
:load-car | --
	actual @ 0? ( drop ; )
	'dibujo swap zcopy 4 - 'dibujo> !
	;

:save-car | --
	hojas> dup actual ! 'dibujo zcopy 'hojas> !
	;

:newline 13 ,c 10 ,c ; | m#@$#!%@$#

|--- quita los tokens innecesarios ( los degrade y la matriz )
#buffopt )( $4000
#buffopt> 'buffopt

:printbuff
	0 'buffopt
	buffopt> >=? ( 2drop ; )
	"#:dib" ,s pick3 ,h 32 ,c
	( buffopt> <? )(
		@+ 0? ( " %d" )( " $%h" ) ,print
		swap 1+ 8 >? ( drop 0 newline )
		swap ) 2drop
	newline
	;

:procbuff | adr --
	'buffopt swap
	( @+ 1? )(
		dup $f and
		$d =? ( drop rot 12 - rot rot dup )
		drop
		rot !+ swap
		)
	rot !+ 'buffopt> !
	drop
	;

:save-code
	"./inc/" dir
	mark
	1 'indice
	( @+ 1? )(
		procbuff
		printbuff
		swap 1+ swap
		) 3drop

	newline
|	"#:cntdib " ,s dup ,n newline
|	"#:dibs" ,s 0
|	( over <? )( 1+ " 'dib" ,s dup ,h
|		dup $f and 0? ( newline ) drop
|		) drop
|	newline

	'nombre "%s.inc" mprint savemem
	empty
	;

:copy-dibu
	actual @ 0? ( drop ; )
	hojas> indice> !+ 'indice> !
	hojas> swap zcopy 'hojas> ! ;

:ante-dibu
	actual 'indice =? ( drop ; )
	dup 4 - over @ over @ swap rot ! swap !
	-4 'actual +! ;

:post-dibu
	actual indice> 4 - >=? ( drop ; )
	dup 4+ over @ over @ swap rot ! swap !
	4 'actual +! ;

:borra-dibu
	actual @ 0? ( drop ; ) drop
	"Borro Dibujo ?" dlgsino
	1? ( actual dup 4+ zcopy 4 - 'indice> !
		0 'actual !
		) drop
;

|--------------------------------------------------
:cleardibujos
	'indice >r 128 ( 1? )( 0 r!+ 1- ) drop rdrop
	'dibujo 'dibujo> !
	'hojas 'hojas> !
	'indice dup 'indice> ! 'actual  ! ;

:load-dibu
	cleardibujos
	'hojas 'nombre load
	dup 'hojas> ! 0 swap 4+ !+ 0 swap !
	'indice 'hojas ( dup @ 1? )( drop dup
		rot !+ swap ( @+ 1? )( drop ) drop
		) 2drop 'indice> ! ;

:save-dibu
	mark
	'indice ( @+ 1? )( ( @+ 1? )( , ) , drop ) , drop
	'nombre savemem
	empty ;

#tactual

:editahoja
	actual 'tactual !
	load-car
	editandosprite
	save-car
	"spr/" dir
	save-dibu load-dibu
	tactual 'actual !
	;

:eligearchivo
	save-dibu
	"spr/" dir
	'nombre dlgfile
	load-dibu
	;

|--------------------------------------------------
:cdibu
	actual =? ( blanco gc.fbox )
	[ dup 'actual ! ; ] onClick
	[ 1.2 ; ] [ 0.9 ; ] onInOut fzoom
	@+ 1? ( nsprite )( drop )
	home verde
	dup 'indice - 2 >> "%h" print
	;

:tabladibujos
	fonti
	'indice
	0.7 ( -0.7 >? )(
		-0.7 ( 0.7 <? )(
			2dup swap fpos 0.1 dup fdim
        	rot cdibu rot rot
			0.2 + ) drop
		0.2 - ) drop
	drop
	;


:titulo
	$333333 $aaaaaa [ swap ; ] onIn  vbtn
	1 sfont home negro dup "r%dSpr:" print
	blanco 'nombre printc
	'eligearchivo onClick
	;


:menu-dibujos
	inigui
	'exit >esc<
	'editahoja <f1>
	'copy-dibu <f2>
	'ante-dibu <f3>
	'post-dibu <f4>
	'borra-dibu <f5>
	'save-code <f6>
	'eligearchivo <esp>
	'editahoja <enter>
	[ 4 'actual +! ; ] <ri>
	[ actual 'indice >? ( 4 - ) 'actual ! ; ] <le>
	show clrscr
		1.0 0.1 fdim
		0 0.9 fpos home
		titulo

		0.1 dup fdim
		-0.9 -0.9 fpos home
		rojo
		'exit 'ifin btnd gc>>
		verde
		'editahoja 'idibuja btnd gc>>
		'copy-dibu 'icopia	btnd gc>>
		'ante-dibu 'iizq btnd gc>>
		'post-dibu 'ider btnd gc>>
		'borra-dibu 'iborrar btnd gc>>
		'save-code 'irecicla btnd

		tabladibujos
|		8 8 'cadadib table

		cmano ;

|--------------------------------------------------
: 0 paper
	mark
	'nombre "mem/edit-spr.mem" load drop
	"media/spr/" dir
	load-dibu
	'indice 'actual !
	4 menu-dibujos
	"media/spr/" dir
 	save-dibu
	"" dir
	'nombre count "mem/edit-spr.mom" save
	empty ;
