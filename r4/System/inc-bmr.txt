| Editor de includes en bitmaps
| PHREDA 2010
|--------------------------------------------------
^r4/lib/btn.txt
^r4/lib/input.txt
^r4/lib/parse.txt
^r4/lib/bmr.txt
^r4/lib/fonta.txt

^r4/lib/trace.txt

#nombre )( 256

#orden 0
#nrobmr 0
#nombmr )( 32

#actual
#cntvar
#indexn )( 3200 | 100 nombres de 32
#index )( 400

|--- parse
:getnro | txt -- txt' 0 / nro
	( dup c@ 33 <? )( 0? ( ; ) drop 1+ ) drop
	dup ?numero 0? ( ; )
	drop rot drop ;

:cpynom | adr en -- adr''
	swap ( c@+ $ff and 32 >? )(
		rot c!+ swap )
	drop 0 rot c!
	;

:cadadibujo | adr $23 -- adr'
	drop c@+ $3a <>? ( drop ; ) drop | #:
	cntvar 5 << 'indexn + cpynom
	here cntvar 2 << 'index + !

	getnro dup , bmr.size@
	( 1? )( 1- swap getnro , swap )
	drop

	1 'cntvar +!
	;

:parsefile | "" --
	here dup rot load 0 swap !+ 'here !
	0 'cntvar !
	( c@+ 1? )(
		$23 =? ( cadadibujo dup )
		drop
		) 2drop
	;

|--- write
:,codigo | adr --
	dup @ bmr.size@ 1+
	( 1? )( 1- swap
		@+ " $%h" ,print
		swap $f nand? ( ,nl )
		) 2drop ,nl ;

:writefile | "" --
	mark
	"| BMR file" ,s ,nl
	0 ( cntvar <? )(
		'indexn over 5 << + "#:%s" ,print
		dup 2 << 'index + @ ,codigo
		1+ ) drop
	savemem
	empty ;


|--------------------------------------------------
:editdib
	1 'orden !
	actual 'nrobmr !
	actual 5 << 'indexn + 'nombmr strcpy
	'orden 40 "mem/inc-bmro.mem" save
	mark
	actual 2 << 'index + @
	@+ dup , bmr.size@
	( 1? )( 1- swap @+ , swap )
	2drop
	"mem/notepad.bmr" savemem
	empty
	"r4/system/edit-bmr.txt" run
	;

:newdib
	1 'orden !
	cntvar dup 'nrobmr !
	"new%d" mprint 'nombmr strcpy
	'orden 40 "mem/inc-bmro.mem" save
	mark
	$10010 ,
	256 ( 1? )( 0 , 1- ) drop
	"mem/notepad.bmr" savemem
	empty
	"r4/system/edit-bmr.txt" run
	;

:copydib
	here cntvar 2 << 'index + !
	actual 2 << 'index + @

	@+ dup , bmr.size@
	( 1? )( 1- swap @+ , swap )
	2drop
	cntvar "new%d" mprint cntvar 5 << 'indexn + strcpy
	1 'cntvar +!
	'nombre writefile ;

:deldib
	cntvar 0? ( drop ; ) drop
	actual 2 << 'index +
	actual 1+ 2 << 'index +
	cntvar actual - 1+ move

	actual 5 << 'indexn +
	actual 1+ 5 << 'indexn +
	cntvar actual - 5 << cmove

	-1 'cntvar +!
	'nombre writefile ;

|----------------------------
:insertamodificado
	'orden "mem/inc-bmro.mem" load drop
	orden 1- 1? ( drop ; ) drop

	here
	dup nrobmr 2 << 'index + !
	"mem/notepad.bmr" load 0 swap !+ 'here !
	'nombmr nrobmr 5 << 'indexn + strcpy

	nrobmr cntvar =? ( 1 'cntvar +! ) 'actual !
	'nombre writefile

	0 'orden !
	'orden 4 "mem/inc-bmro.mem" save
	;

|------------------------------

:actual! | nro
	-? ( 0 nip )
	cntvar >=? ( cntvar 1- nip )
	'actual !
	refreshfoco ;

:cadadib | n n -- n n
	pick2 cntvar >=? ( drop ; )
	0.09 dup fdim
	[ dup actual! ; ] guiBtn
	actual =? ( blanco gc.box )
	xc w 2/ - yc h 2/ -
	pick2 2 << 'index + @
	w h 
	bmr.drawscale

	home verde
	5 << 'indexn + printc
	|1+ "%h" print
	rot 1+ rot rot
	;

:tabladib
	0
	0.7 ( -0.7 >=? )(
		-0.7 ( 0.7 <=? )(
			2dup swap fpos
			cadadib
			0.2 + ) drop
		0.2 - )
	2drop ;

#namedraw 0

:editname
	namedraw 0? ( drop ; )
	negro
	24 input
	[ 0 'namedraw ! 'nombre writefile ; ] lostfoco
	;

:rendib
	actual 5 << 'indexn + 'namedraw ! ;

|----------------------------
:main
	'nombre "mem/inc-bmr.mem" load drop
	'nombre parsefile
	insertamodificado
	show clrscr
		50 gc.top $0ff888 gc.hfill
		fonta-verdana14b home
		verde dup " :R%d" print
		blanco "eDIT BMR" print

		'nombre "%s " printr cr cr2
	| acciones
		fonta-verdana10b
		rojo
		sp 'exit dup >esc< "esc-Exit" link
		sp verde
		sp 'newdib dup <f1> "f1-New" link
		sp 'editdib dup <f2> "f2-Edit" link
		sp 'copydib dup <f3> "f3-Copy" link
		sp 'deldib dup <f4> "f4-Del" link
		sp 'rendib dup <f5> "f5-Rename" link
        sp editname

		[ actual 1+ actual! ; ] <ri>
		[ actual 1- actual! ; ] <le>

		32 26 gc.vbetween
		tabladib

		cmano ;

|--------------------------------------------------
:	4 0 paper
	mark
	main
	;

