| Editor de includes en sprites
| PHREDA 2010
|--------------------------------------------------
^r4/lib/btn.txt
^r4/lib/input.txt
^r4/lib/parse.txt
^r4/lib/sprite.txt
^r4/lib/fonta.txt
^r4/lib/trace.txt

#nombre )( 256

#orden 0
#nrospr 0
#nomspr )( 32

#actual
#cntvar
#indexn )( 3200 | 100 nombres de 32
#index )( 400

|--- parse
:getnro | txt -- txt' 0 / nro
	( dup c@ $ff and 33 <? )( 0? ( ; ) drop 1+ ) drop
	dup ?numero 0? ( ; )
	drop rot drop ;

:cpynom | adr en -- adr''
	swap ( c@+ $ff and 32 >? )(
		rot c!+ swap )
	drop 0 rot c!
	;

:cadadibujo | adr $23 -- adr'
	drop c@+ $3a <>? ( drop ; ) drop | #:
	cntvar 5 << 'indexn + cpynom
	here cntvar 2 << 'index + !
	( getnro 1? )( , ) ,
	1 'cntvar +!
	;

:parsefile | "" --
	here dup rot load 0 swap !+ 'here !
	0 'cntvar !
	( c@+ 1? )(
		$23 =? ( cadadibujo dup )
		drop
		) 2drop
	;

|--- write
#buffopt )( $4000

:clearcode | adr -- | falta agregarlo antes del ,codigo
	'buffopt swap
	( @+ 1? )(
		dup $f and
		$d =? ( drop rot 12 - rot rot dup )
		drop
		rot !+ swap
		)
	rot ! | termina con 0
	drop
	;

:,codigo | adr --
	0 swap
	( @+ 1? )(
		" $%h" ,print
		swap 1+ 16 >? ( 0 nip ,nl ) swap
		) 3drop
	" 0" ,s ,nl ;

:writefile | "" --
	mark
	"| Nsprite file" ,s ,nl
	0 ( cntvar <? )(
		'indexn over 5 << + "#:%s" ,print ,nl
		dup 2 << 'index + @ ,codigo
		1+ ) drop
	savemem
	empty ;


|--------------------------------------------------
:editdib
	1 'orden !
	actual 'nrospr !
	actual 5 << 'indexn + 'nomspr strcpy
	'orden 40 "mem/inc-spro.mem" save
	mark
	actual 2 << 'index + @
	( @+ 1? )( , ) 2drop
	"mem/notepad.spr" savemem
	empty
	"r4/system/edit-spr.txt" run
	;

:newdib
	1 'orden !
	cntvar dup 'nrospr !
	"new%d" mprint 'nomspr strcpy
	'orden 40 "mem/inc-spro.mem" save
	mark
	"mem/notepad.spr" savemem
	empty
	"r4/system/edit-spr.txt" run
	;

:import
	1 'orden !
	cntvar dup 'nrospr !
	"svg%d" mprint 'nomspr strcpy
	'orden 40 "mem/inc-spro.mem" save
	mark
	"mem/notepad.spr" savemem
	empty
	"r4/system/svg-spr.txt" run
	;

:copydib
	here cntvar 2 << 'index + !
	actual 2 << 'index + @
	( @+ 1? )( , ) , drop
	cntvar "new%d" mprint cntvar 5 << 'indexn + strcpy
	1 'cntvar +!
	'nombre writefile
	;

:deldib
	cntvar 0? ( drop ; ) drop
	actual 2 << 'index +
	actual 1+ 2 << 'index +
	cntvar actual - 1+ move

	actual 5 << 'indexn +
	actual 1+ 5 << 'indexn +
	cntvar actual - 5 << cmove

	-1 'cntvar +!
	actual 1? ( -1 'actual +! ) drop
	'nombre writefile
	;

|----------------------------
:insertamodificado
	'orden "mem/inc-spro.mem" load drop
	orden 1- 1? ( drop ; ) drop

	here
	dup nrospr 2 << 'index + !
	"mem/notepad.spr" load 0 swap !+ 'here !
	'nomspr nrospr 5 << 'indexn + strcpy

	nrospr cntvar =? ( 1 'cntvar +! ) 'actual !
	'nombre writefile

	0 'orden !
	'orden 4 "mem/inc-spro.mem" save
	;

|----------------------------
:actual! | nro
	-? ( 0 nip )
	cntvar >=? ( cntvar 1- nip )
	'actual !
	refreshfoco ;

:cadadib | n n -- n n
	0.09 dup fdim
	pick2 cntvar >=? ( drop ; )
	[ dup actual! ; ] guiBtn
	actual =? ( blanco gc.box )
	dup 2 << 'index + @ 1? ( nsprite )( drop )
	home verde
	cch neg 'ccy +!	
	5 << 'indexn + printc
	|1+ "%h" print
	rot 1+ rot rot
	;

:tabladib
	0 0.7 ( -0.7 >=? )(
		-0.7 ( 0.7 <=? )(
			2dup swap fpos
			cadadib
			0.2 + ) drop
		0.2 - )
	2drop ;

#namedraw 0

:editname
	namedraw 0? ( drop ; )
	24 input
	[ 0 'namedraw ! 'nombre writefile ; ] lostfoco
	;

:rendib
	actual 5 << 'indexn + 'namedraw ! ;

:main
	'nombre "mem/inc-spr.mem" load drop
	'nombre parsefile
	insertamodificado
	0 'namedraw !
	show clrscr
		50 gc.top $f0f888 gc.hfill
		fonta-verdana14b home
		verde dup " :R%d" print
		blanco "eDIT SPR" print

		'nombre "%s " printr cr cr2
	| acciones
		fonta-verdana10b
		rojo
		sp 'exit dup >esc< "esc-Exit" link
		sp verde
		sp 'newdib dup <f1> "f1-New" link
		sp 'editdib dup <f2> "f2-Edit" link
		sp 'copydib dup <f3> "f3-Copy" link
		sp 'deldib dup <f4> "f4-Del" link
		sp 'rendib dup <f5> "f5-Rename" link
|		sp 'import dup <f6> "f6-SVG" link
        sp editname

		[ actual 1+ actual! ; ] <ri>
		[ actual 1- actual! ; ] <le>

		32 26 gc.vbetween
		tabladib
		cmano ;

|--------------------------------------------------
:	4 0 paper
	mark
	main
	;

