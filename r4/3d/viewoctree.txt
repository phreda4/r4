| Editor de Voxels
| PHREDA 2014
|-----------------------------
^r4/lib/gui.txt
^r4/lib/dlgfile.txt

^r4/3d/octree/iso_16.txt

^r4/lib/trace.txt

#xcam 0 #ycam 0 #zcam 1.2
#rx #ry

|------------- load octree
#vistaoctree

:loadoctree
	mark
	"media/3do" dlgfileload 0? ( drop empty ; )
	load3do 'vistaoctree !
	empty ;

|------ vista
#xm #ym
:dnlook
	xymouse 'ym ! 'xm ! ;

:movelook
	xymouse
	ym over 'ym ! - 7 << 'rx +!
	xm over 'xm ! - neg 7 << neg 'ry +!  ;

|-----------------
#fps
#fpsc
#mseca

:main
	4
	show clrscr

		matini
		rx mrotx ry mroty
		xcam ycam zcam mtrans

		zb.clear
		vistaoctree 1? ( drawoctree )( drop )
		0 0 zdraw

		'dnlook 'movelook guiDnMove

		fonti
		verde
		dup ":r%d VoxelView  " print
		1 'fpsc +!
		msec dup mseca - fps "[fps:%d %dms]" print 'mseca !
		cr
		zcam ycam xcam "cam %f %f %f" print
		1 rows 2 - gotoxy
		cyan
		'loadoctree dup <f1> "F1-Load" sp btnt
		cr cr2

		[ -0.01 'zcam +! ; ] <up>
		[ 0.01 'zcam +! ; ] <dn>
		[ -0.01 'xcam +! ; ] <le>
		[ 0.01 'xcam +! ; ] <ri>
		[ -0.01 'ycam +! ; ] <pgup>
		[ 0.01 'ycam +! ; ] <pgdn>

		'exit >esc<
		cminiflecha
		1000 .mseg .restart
		fpsc 'fps ! 0 'fpsc !
		;

|--------------------------
:memory
	mark
	sw sh ini3do
	;

: memory main ;
