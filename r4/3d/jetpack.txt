| jetpack 3d
| PHREDA 2016
|

^r4/lib/gui.txt
^r4/lib/btn.txt

^r4/lib/part16.txt
^r4/lib/anima.txt
^r4/3d/octree/iso_17.txt

^r4/lib/trace.txt


#ldisparos 0 0
#lenemigos 0 0
#lfx 0 0

#xcam #ycam 1.5 #zcam 5.0

#Oshuttle
#oclon1 #oclon2 #oclon3
#oclon4 #oclon5 #oclon6
#oov1 #oov2 #oov3
#ometeoro
#obala
#ovox

#clonrun 0 0 oclon1 5 oclon5 5 oclon1 5 oclon6 5 0
#clonfly 0 0 oclon2 1 oclon3 1 oclon4 1 0

#ovnirun 0 0 oov1 30 oov2 30 oov3 30 0

|-----------------------------
:3dop transform p3d op ;
:3dline transform p3d line ;

#sizx #sizy #sizz

:drawz | z --
	sizx neg sizy neg pick2 3dop
	sizx sizy neg pick2 3dline
	sizx sizy pick2 3dline
	sizx neg sizy pick2 3dline
	sizx neg sizy neg rot 3dline ;

:drawl | x1 x2 --
	2dup sizz neg 3dop sizz 3dline ;

:box3d | sx sy sz --
	'sizz ! 'sizy ! 'sizx !
	sizz neg drawz sizz drawz
	sizx neg sizy neg drawl sizx sizy neg drawl
	sizx sizy drawl sizx neg sizy drawl
	;

|---------------------------------------------
:r0001 rand 0.001 mod ;
:r001 rand 0.01 mod ;
:r01 rand 0.1 mod ;
:r1 rand 1.0 mod ;
:r10 rand 10.0 mod ;
:r100 rand 100.0 mod ;

:cosa
	dup >b
	mpush
	b@+ b@+ b@+ mtransi
	b@+ 1? ( mrotxi )( drop )
	b@+ 1? ( mrotyi )( drop )
	b@+ 1? ( mrotzi )( drop )
|	0.5 0.5 0.5 box3d
|	ocoso drawoctree
	'ovnirun seqAnima @ drawoctree
|	ometeoro drawoctree
	mpop
	24 +
	@+ over 28 - +! @+ over 28 - +! @+ over 28 - +!
	@+ over 28 - +! @+ over 28 - +! @+ over 28 - +!
	drop
	;

:cosa+xy | x y --
	'cosa 'lenemigos p!+ >a
	a!+ 0 a!+ a!+ | x y z
	0 a!+ 0 a!+ 0 a!+ | rx ry rz

	0 a!+ r0001 a!+ 0 a!+
	r0001 a!+ r0001 a!+ r0001 a!+
|	0 r!+ 0 r!+ 0 r!+
	;

|------ fx
:particula
	dup @ 1- 0? ( nip ; ) swap !+
	dup >b
	mpush
	b@+ b@+ b@+ mtransi
	b@+ mrotxi b@+ mrotyi b@+ mrotzi
	ovox drawoctree
	mpop
	24 +
	@+ over 28 - +! @+ over 28 - +! @+ over 28 - +!
	@+ over 28 - +! @+ over 28 - +! @+ over 28 - +!
	drop
	;

:humo
	dup @ 1- 0? ( nip ; ) swap !+
	dup >b
	mpush
	b@+ b@+ b@+ mtransi
|	r@+ mrotxi r@+ mrotyi r@+ mrotzi
	ovox drawoctree
	mpop
	24 +
	@+ over 28 - +! @+ over 28 - +! @+ over 28 - +!
|	@+ over 28 - +! @+ over 28 - +! @+ over 28 - +!
	drop
	;

:+humo | x y z -- x y z
	rnd $10 and? ( drop ; ) drop	| no todos los puntos
	'humo 'lfx p!+ >a
	20 a!+ | tiempo de vida
	pick2 a!+ over a!+ dup a!+
	0 a!+ 0 a!+ 0 a!+
	r001 a!+ r001 a!+ r001 a!+
	0 a!+ 0 a!+ 0 a!+
	;

|------ disparo
:bdisparo
	dup @ 1- 0? ( nip ; ) swap !+ | tiempo de vida
	dup >b
	mpush
	b@+ b@+ b@+ +humo mtransi
	b@+ 1? ( mrotxi )( drop )
	b@+ 1? ( mrotyi )( drop )
	b@+ 1? ( mrotzi )( drop )
	obala drawoctree
	mpop
	24 +
	@+ over 28 - +! @+ over 28 - +! @+ over 28 - +!
|	@+ over 28 - +! @+ over 28 - +! @+ over 28 - +! |no rota
	drop
	;

|------ player
#plx #ply #plz
#plrx #plry #plrz

#pla	| avance
#plr #pldr	| rotacion
#pli	| impulso en z
#pljet

#oxcam #oycam #ozcam
#orxcam #orycam #orzcam

:disparo
	'bdisparo 'ldisparos p!+ >a
	150 a!+ | tiempo de vida
	plx a!+ ply a!+ plz a!+
	plrx a!+ plry a!+ plrz a!+
	plr sincos 0.3 *. a!+ 0 a!+ 0.3 *. a!+
	0 a!+ 0 a!+ 0 a!+
	;

:player
	[ 0.01 'pldr ! ; ] <le>
	[ -0.01 'pldr ! ; ] <ri>
	[ 0 'pldr ! ; ] dup >le< >ri<

	[ 0.02 'pla ! ; ] <up> 	| avanzar
	[ -0.02 'pla ! ; ] <dn>	| retroceder
	[ 0 'pla ! ; ] dup >up< >dn<
	[ 1 'pljet ! ;  ] <spc>
	[ 0 'pljet ! ; ] >spc<
	'disparo <ctrl>

	pldr 'plr +!

	pla ply 1? ( drop 2* )( drop )
	plr dup 'plry !
	sincos pick2 *. 'plx +! *. 'plz +!

	pljet 1? ( -0.01 'pli +! )( 0.01 'pli +! )  drop

	ply pli +
	0 >? ( 0 nip 0 'pli ! )	| suelo
	'ply !

	mpush
	plx ply plz mtransi
	plrx 1? ( mrotxi )( drop )
	plry 1? ( mrotyi )( drop )
	plrz 1? ( mrotzi )( drop )

	pljet 1? ( 'clonfly seqAnima @ )( pla 0? ( oclon1 )( ply 1? ( drop oclon1 )( drop 'clonrun seqAnima @ ) ) nip ) nip
	drawoctree

	mpop
	;

:camara
	plry 0.25 - 'orycam !
	0 0 'orxcam ! 'orzcam !
	plry sincos -2.0 *. plx + 'oxcam ! -2.0 *. plz + 'ozcam !
	ply 1.0 - 'oycam !
	mpush
	oxcam oycam ozcam mtransi
	orxcam 1? ( mrotxi )( drop )
	orycam 1? ( mrotyi )( drop )
	orzcam 1? ( mrotzi )( drop )
	ometeoro drawoctree
	mpop ;

:nave
	mpush
	5.0 -1.0 0 mtransi
	0.75 mrotxi
	5.0 Oshuttle drawsoctree
	mpop
	;

:grilla
	-100.0 ( 100.0 <? )(
		-100.0 ( 100.0 <? )(
			|over rand 0.5 mod + over rand 0.5 mod +
			2dup
			cosa+xy
			25.0 + ) drop
		25.0 + ) drop
	;

:reset
	'ldisparos p.clear
	'lenemigos p.clear
	'lfx p.clear

	0 0 0 'plx ! 'ply ! 'plz !
	0 0 0 'plrx ! 'plry ! 'plrz !

	grilla
	;

|-----------------------------------
| version 5 - full 8 bits channel

:shader
	@+ $7fffffff =? ( drop 4 a+ ; ) drop
	dup zbo + @ | adr color
	over @ pick2 8 - @ - 2 >>
	160 + 256 clamp0max
	shadow8 a!+ ;

::zdraw | x y --
	setxy
	sw w3do - 2 <<
	zb
	h3do ( 1? )( 1-
		w3do ( 1? )( 1-
			rot shader rot rot
			) drop
		pick2 a+
		) 3drop ;

|-----------------------------------
:freelook
	xymouse
	sh 2/ - 7 << swap
	sw 2/ - 7 << swap
	neg mrotx mroty ;


:main
	reset
	33
	show0 clrscr
      	dup "%d" print cr
		zcam "%f" print cr

		matini

		plx neg ply neg plz neg mtrans	| sigue a pl1
		plry 0.25 - mroty				| atras de el
        xcam ycam zcam mtrans

		zb.clear
		player

|		camara
	    nave


		'lenemigos p.draw
		'ldisparos p.draw
		'lfx p.draw
		0 0 zdraw


		'exit >esc<
		cminiflecha  ;


:inimem
	mark

	sw sh ini3do

	"media/3do/jp1.3do" load3do 'Oclon1 !

	"media/3do/jpf1.3do" load3do 'Oclon2 !
	"media/3do/jpf2.3do" load3do 'Oclon3 !
	"media/3do/jpf3.3do" load3do 'Oclon4 !

	"media/3do/jpc1.3do" load3do 'Oclon5 !
	"media/3do/jpc2.3do" load3do 'Oclon6 !

	"media/3do/ovni31.3do" load3do 'Oov1 !
	"media/3do/ovni32.3do" load3do 'Oov2 !
	"media/3do/ovni33.3do" load3do 'Oov3 !

	"media/3do/meteoro.3do" load3do 'Ometeoro !
	"media/3do/shuttle.3do" load3do 'Oshuttle !
	"media/3do/bala.3do" load3do 'Obala !

	"media/3do/voxel.3do" load3do 'Ovox !

	50 'ldisparos p.ini
	100 'lenemigos p.ini
	1000 'lfx p.ini
	;

: inimem main ;