| bvh load
| PHREDA 2016
|----------------
^r4/lib/gui.txt
^r4/lib/parse.txt
^r4/lib/3dmat.txt

^r4/lib/trace.txt

#bvhfile
#$bvhfile

#nombres )( 1024
#nombres> 'nombres

#level
#ox #oy #oz
#chcnt
#chpos
#chtype )( 1024
#offend

#chsum
#model
#frames
#frametime
#animation

#framenow

#xcam 0 #ycam 0 #zcam 400.0
#xr 0 #yr 0

:,off+cha
	level chpos 8 << or , ox , oy , oz , ;
:,off
	level , ox , oy , oz ,  ;

:parsechp
	trim
	dup "Xposition" scanp 1? ( nip 1 ; ) drop
	dup "Yposition" scanp 1? ( nip 2 ; ) drop
	dup "Zposition" scanp 1? ( nip 3 ; ) drop
	dup "Xrotation" scanp 1? ( nip 4 ; ) drop
	dup "Yrotation" scanp 1? ( nip 5 ; ) drop
	dup "Zrotation" scanp 1? ( nip 6 ; ) drop
	0 ;

:savetype | nro mask adr type
	4 <? ( 0 )( 1 )	| pos o ang
	pick4 chsum + chcnt - 'chtype + c!
	;

:parsecha
	trim
	getnro dup 'chcnt ! 'chsum +!
	0 0 ( chcnt <? )( | mask nro
		swap rot
		parsechp
		savetype
		pick3 2 << <<
		rot or rot 1+ ) drop
	'chpos !
	,off+cha ;

:parseoff
	trim
	getfnro 'ox !
	getfnro 'oy !
	getfnro 'oz !
	offend 1? ( ,off 0 'offend ! ) drop
	;

:parsejoi
	trim
|	dup nombres> scanstr 'nombres> !
	>>cr
	;

:atom
	trim
	dup "{" scanp 1? ( nip 1 'level +! ; ) drop
	dup "}" scanp 1? ( nip -1 'level +! ; ) drop
	dup "OFFSET" scanp 1? ( nip parseoff ; ) drop
	dup "CHANNELS" scanp 1? ( nip parsecha ; ) drop
	dup "JOINT" scanp 1? ( nip parsejoi ; ) drop
	dup "End Site" scanp 1? ( nip 1 'offend ! ; ) drop
	dup memmap
	;

:parseroo
	trim
|	dup nombres> scanstr 'nombres> !
	>>sp
	0 'level !
	0 'chsum !
	0 'offend !
	( atom level 1? )( drop ) drop
	;

:parsehie
	trim
	dup "ROOT" scanp 1? ( nip parseroo ; ) drop
	>>sp
	;

:typech | fram chsum adr nro
	chsum pick3 - 'chtype + c@
	0? ( drop ; ) drop
	360.0 /.	| angulo
	;

:parsemot
	0 , 0 , 0 , 0 ,
	here 'animation !
	trim dup "Frames:" scanp 0? ( drop ; ) nip
	trim getnro 'frames !
	trim dup "Frame Time:" scanp 0? ( drop ; ) nip
	trim getfnro 'frametime !
	frames ( 1? )(
		chsum ( 1? )(
			rot trim getfnro
			typech
|			360.0 /.	| angulo
			, rot rot
			1- ) drop
		1- ) drop ;

:nextword
	trim
	dup "HIERARCHY" scanp 1? ( nip parsehie ; ) drop
	dup "MOTION" scanp 1? ( nip parsemot ; ) drop
	;

:parsebvh | --
	bvhfile ( $bvhfile <? )( nextword >>sp ) drop ;

|------------------------------------------
:dumpmod | adr
	( @+ 1? )(
		"%h " print
		@+ "%f " print
		@+ "%f " print
		@+ "%f " print
			cr
		) 2drop
	;

:3dop project3d op ;
:3dline project3d line ;

:drawboxz | z --
	-0.5 -0.5 pick2 3dop
	0.5 -0.5 pick2 3dline
	0.5 0.5 pick2 3dline
	-0.5 0.5 pick2 3dline
	-0.5 -0.5 rot 3dline ;

:drawlinez | x1 x2 --
	2dup -0.5 3dop 0.5 3dline ;

:drawcube |
	-0.5 drawboxz
	0.5 drawboxz
	-0.5 -0.5 drawlinez
	0.5 -0.5 drawlinez
	0.5 0.5 drawlinez
	-0.5 0.5 drawlinez ;
#anip
:val
	anip @+ swap 'anip ! ;

:xpos val 0 0 mtransi ;
:ypos val 0 swap 0 mtransi ;
:zpos val 0 0 rot mtransi ;
:xrot val mrotxi ;
:yrot val mrotyi ;
:zrot val mrotzi ;
#lani xpos ypos zpos xrot yrot zrot

:anibones | flags --
	8 >> $ffffff and
	( 1? )( dup $f and 1- 2 << 'lani + @ exec 4 >> )
	drop ;

:drawbones | bones --
	>r
	framenow chsum * 2 << animation + 'anip !
	0 ( r@+ 1? )( dup $ff and
		rot over - 1+ clamp0 | anterior-actual+1
		nmpop
		mpush
		r@+ r@+ r@+ mtransi
		swap anibones
		drawcube
		) drop
	rdrop
	nmpop
	;

:drawbones0 | bones --
	>r
	0 ( r@+ 1? )( $ff and
		swap over - 1+ clamp0 nmpop
		mpush
		r@+ r@+ r@+ mtransi
		drawcube
		) drop
	rdrop
	nmpop
	;

:freelook
	xymouse
	sh 2/ - 7 << swap
	sw 2/ - neg 7 << swap
	neg mrotx
	mroty ;

:3dscreen
	omode
	freelook
	xcam ycam zcam mtrans

	verde
	model drawbones
	blanco
	model drawbones0
	framenow 1+ frames >=? ( 0 nip ) 'framenow !
	;

:main
	0 'framenow !
	33
	show clrscr
		dup "%d" print cr
		chsum "chsum:%d " print
		frames "frames:%d " print
		frametime " frame time:%f " print
		here model - "%d bytes" print cr
		framenow "%d " print cr
|		model dumpmod  cr
		3dscreen

		'exit >esc<
		;
:ini
	mark
	here dup 'bvhfile !
|	"media/bvh/Example1.bvh"
|	"media/bvh/0008_ChaCha001.bvh"
	"media/bvh/Bunny Girl/serv_drink2.bvh"
	load dup '$bvhfile ! dup 'model ! 'here !
	parsebvh
	;

: ini main ;