| vox import
| PHREDA 2016
|-------------------

^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/sort.txt
^r4/lib/bsearch.txt
^r4/lib/morton.txt
^r4/lib/dlg.txt
^r4/lib/dlgfile.txt
^r4/lib/dlgcol.txt

^r4/lib/ricons.txt
^inc/ric/fontawesomewebfont.ric

^r4/3d/octree/iso_12.txt
^r4/3d/buildoctree.txt
^r4/3d/wirelib.txt

^r4/3d/loadvox.txt

#filename )( 1024

|------ vista
#xcam 0 #ycam 0 #zcam 1.6
#rx #ry

|------ ajustes
:setvoxsize | level -- level
	dup 'deepvox !
	1 over << 1- 'qsize !
	;

:drawvoxel
	$inifile dup @ 16 >> $3d00 <>? ( 2drop ; ) drop
	maskb.clear
	drawoctree ;

|----------------
:clearvox
	memvox 'memvox> ! ;

:vox-cnt-mem | -- cnt mem
	memvox memvox> over - 3 >> 1+ swap ;

:vox! | c x y z --
	mortonlut
:mvox! | c m --
	memvox> !+ !+ 'memvox> ! ;

:vox@ | x y z -- c
	memvox> memvox - 0? ( 4drop -1 ; ) drop
	mortonlut vox-cnt-mem binsearch
	0? ( -1 nip ; ) 4+ @ ;

:vox@a | x y z -- a
	memvox> memvox - 0? ( 4drop -1 ; ) drop
	mortonlut vox-cnt-mem binsearch
	0? ( -1 nip ; ) 4+ ;

:vox- | x y z --
	memvox> memvox - 0? ( 4drop ; ) drop
	mortonlut vox-cnt-mem binsearch
	0? ( drop ; ) | adr
	dup 8 +   | dest src
	memvox> over - 2 >> move
	-8 'memvox> +! ;

|------ remove occluded
:occ? | x y z -- 1/0
	pick2 1+ pick2 pick2 vox@a -1 =? ( 4drop 0 ; ) drop
	pick2 1- pick2 pick2 vox@a -1 =? ( 4drop 0 ; ) drop
	pick2 pick2 1+ pick2 vox@a -1 =? ( 4drop 0 ; ) drop
	pick2 pick2 1- pick2 vox@a -1 =? ( 4drop 0 ; ) drop
	pick2 pick2 pick2 1+ vox@a -1 =? ( 4drop 0 ; ) drop
	1- vox@a -1 =? ( drop 0 ; ) drop
	1 ;

#list
:remocc
	mark
	here 'list !
	memvox ( memvox> <? )(
		@+
		invmorton3d | x y z
		occ? 1? ( over 4 - @ , ) drop
		) drop
	list ( here <? )(
		@+ invmorton3d vox- ) drop
	empty
	;

|----- load magicavoxel
:impvox
	"media/vox" dlgfileload 0? ( drop ; )
	mark
	dup 'filename strcpy
	loadvox
	here '$inifile !
	empty
	voxqsize
	setvoxsize drop
	clearvox
	[ vox! ; ] mapvox32
	centravoxel
	buildoctree
	;

|------------- load import
:imp3dm | "filename" --
	"media/3dm" dlgfileload 0? ( drop ; )
	memvox swap
	load dup 'memvox> ! 'here !

	|..quta 0 del final
	memvox> 8 - ( dup @ 0? )( drop
		8 - ) drop 8 + 'memvox> !

	repetidos
	getminmax
	centravoxel
	buildoctree
	;

|------------- save/load import
:saveoctree
	"media/3do" dlgfilesave 0? ( drop ; )
	'filename strcpy
	'filename ".3do" =pos
	0? ( ".3do" pick2 strcat ) drop
	$inifile $end over - rot save ;

:saveoctreeopt
	"media/3do" dlgfilesave 0? ( drop ; )
	'filename strcpy
	'filename ".3do" =pos
	0? ( ".3do" pick2 strcat ) drop
	remocc 
	buildoctree
	optoctree
	$inifile $end over - rot save ;

|------ rotar
#xm #ym

:dnlook
	xymouse 'ym ! 'xm ! ;

:movelook
	xymouse
	ym over 'ym ! - 7 << 'rx +!
	xm over 'xm ! - neg 7 << neg 'ry +! ;

:main
	4 setvoxsize drop
	voxelbox
	clearvox
	buildoctree
	$f0f0f paper
	4 show clrscr
		fonti
		verde oscuro 2 linesfill blanco
		dup ":r%d VoxelImport " print
		octre> octre - 2 >> memvox> memvox - 3 >>
		"[ %d Pnts %d Nods ]" print
		qsize 1+ dup dup " %dx%dx%d" print
		cr
		'filename sp print
		cr

		|------
		3dini
		rx mrotx ry mroty
		xcam ycam zcam mtrans
		draw3dgrid

		drawvoxel
		'dnlook 'movelook guiDnMove

		|------
		0 3 gotoxy
		naranja
		'voxelcuad "Q"  sp btnt
		'voxelbox "L"  sp btnt
		'voxelboxc "B"  sp btnt cr cr
		azul
		'impvox dup "F1-Load VOX" sp btnt <f1>
		'imp3dm dup "F2-Load 3dm" sp btnt <f2> cr cr
		'saveoctree dup "F5-Save 3DO" sp btnt <f5>
		'saveoctreeopt dup "F6-Save VOX Opt" sp btnt <f6>

		24 qdim
		sw 12 - 12 pos
		rojo 'exit dup >esc< btne blanco 'i.off drawric

		|------ key vista
		[ zcam 0.1 >? ( 0.1 - ) 'zcam ! ; ] <up>
		[ zcam 1.8 <? ( 0.1 + ) 'zcam ! ; ] <dn>
		[ 0.01 'ry +! ; ] <le>
		[ -0.01 'ry +! ; ] <ri>

		cminiflecha ;

|--------------------------
:memory
	mark
	3dini
	maskb.ini
	here
	dup 'memvox !
	dup 'memvox> !
	$ffffff +
	'here !
	;

: memory main ;
