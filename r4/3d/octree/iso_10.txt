| iso test 10
| PHREDA 2015
| ultimo buffer de 16bits
| utilizar el maskbuffer para no escribir afuera de la pantalla
| vectores de suma en vez de cubos, base es vertice 0
| vectores de suma en vez de cajas, base es minimo
|---------------------
^r4/lib/gui.txt
^r4/lib/morton.txt
^r4/lib/mfbuffer.txt

^r4/lib/trace.txt

#$base
#$magic
#$octree
#$pixels
#$paleta

#ymin #ymax
#xmin #xmax

#wiso #wison
#hiso #hison

#nzmin
#zlen

#aocull

#x1 #y1 #z1	| Nx
#x2 #y2 #z2	| Ny
#x4 #y4 #z4	| Nz

#level 0
#vecpos )( 128

#ilevel 0
#ivecpos )( 64

:fillrotvec | -- x y z
	1.0 clz 4 - 'zlen !
	0 0 0 transform	| origen
	0 0 1.0 transform pick3 - 'z1 ! pick3 - 'y1 ! pick3 - 'x1 !
	0 1.0 0 transform pick3 - 'z2 ! pick3 - 'y2 ! pick3 - 'x2 !
	1.0 0 0 transform pick3 - 'z4 ! pick3 - 'y4 ! pick3 - 'x4 !
	'vecpos >r
	x1 x2 + x4 + r!+ y1 y2 + y4 + r!+ z1 z2 + z4 + r!+ 4 r+  	| 111
	x2 x4 + r!+ y2 y4 + r!+ z2 z4 + r!+ 4 r+  	| 110
	x1 x4 + r!+ y1 y4 + r!+ z1 z4 + r!+ 4 r+  	| 101
	x4 r!+ y4 r!+ z4 r!+ 4 r+  		| 100
	x1 x2 + r!+ y1 y2 + r!+ z1 z2 + r!+ 4 r+  	| 011
	x2 r!+ y2 r!+ z2 r!+ 4 r+  		| 010
	x1 r!+ y1 r!+ z1 r!+ 4 r+  		| 001
	0 dup r!+ dup r!+ r> !			| 000
	|........... mover a borde
	rot x1 2/ x2 2/ + x4 2/ + -
	rot y1 2/ y2 2/ + y4 2/ + -
	rot z1 2/ z2 2/ + z4 2/ + -
	|........... normales en 8bits
	x1 8 >> 'x1 ! y1 8 >> 'y1 ! z1 8 >> 'z1 !
	x2 8 >> 'x2 ! y2 8 >> 'y2 ! z2 8 >> 'z2 !
	x4 8 >> 'x4 ! y4 8 >> 'y4 ! z4 8 >> 'z4 !
	;


|-------------------------------
:octcolor | oct -- color
    $octree - $pixels + @ ;

:octcolor3 | oct -- color
	$octree - 2 >> dup 2* + 1- $pixels + @ $ffffff and ;

:octcolor8 | oct -- color
	$octree - 2 >> $pixels + c@ $ff and 2 << $paleta + @ ;

:octcolor16 | oct -- color
	$octree - 2/ $pixels + w@ ; |16to32 ;

|-------- octree in octree
:adjustmem | octree --
	dup '$base !
	dup 28 + '$octree !
	@+ '$magic !
	@ $octree + '$pixels !
	;

#opila )( 1024
#opila> 'opila

:getoct | octree -- octree
	$base opila> !+ 'opila> !
	$octree - $pixels + @
	2 << $base +
	adjustmem
	$octree
	;

:getocti | --
	-4 'opila> +! opila> @
	adjustmem
	;

|-------------------------------
:drawpoint | x y oct --
	octcolor rot rot maskpxyo ;

:drawpoint1 | x y oct --
	hison 0? ( drop drawpoint ; )
	swap octcolor ink	| x y h
	over + maskfpy ;

:drawpoint2 | x y oct --
	octcolor ink
	wison pick2 + maskfpx ;

|---- quad cube
:drawbox | x y oct --
	octcolor ink
	wison pick2 + hison pick2 +
	maskfpxy ;

|---- real cube
#faces (
0 1 3 2 0 2 6 4 0 1 5 4
1 3 2 0 1 3 7 5 1 0 4 5
2 3 1 0 2 0 4 6 2 3 7 6
3 1 0 2 3 1 5 7 3 2 6 7
4 5 7 6 4 6 2 0 4 5 1 0
5 4 6 7 5 1 3 7 5 1 0 4
6 4 5 7 6 2 0 4 6 2 3 7
7 6 4 5 7 5 1 3 7 3 2 6
)

:getn | x y a id -- x y a x y
	3 << 'ivecpos + >r
	pick2 r@+ ilevel >> + pick2 r> @ ilevel >> + ;
|	pick2 r@+ ilevel 1- >> 1+ 2/ + pick2 r> @ ilevel 1- >> 1+ 2/ + ; | level=0!!

:aclara
    ink@ $f0f0f colavg ink ;

:dface | adr -- adr'
	c@+ getn 2dup >r >r maskop
	c@+ getn maskline
	c@+ getn maskline
	c@+ getn maskline
	r> r> maskline
	maskpoly ;

:drawboxd | x y oct --
	octcolor ink
	nzmin 2 << dup 2* + 'faces +
	dface aclara dface aclara dface
	3drop
	;

|---- 1 color cube
#contorno (
1 5 4 6 2 3 0 0
0 2 3 7 5 4 0 0
0 1 3 7 6 4 0 0
0 2 6 7 5 1 0 0
0 2 6 7 5 1 0 0
0 1 3 7 6 4 0 0
0 2 3 7 5 4 0 0
1 5 4 6 2 3 0 0
)

:drawboxc | x y oct --
	octcolor ink
	nzmin 3 << 'contorno +
	c@+ getn 2dup >r >r maskop
	c@+ getn maskline
	c@+ getn maskline
	c@+ getn maskline
	c@+ getn maskline
	c@+ getn maskline 3drop
	r> r> maskline
	maskpoly ;

#dbox 'drawbox

|-----------------------
:idsum0 | x y z id -- x y z
	4 << 'vecpos + >r
	rot r@+ + rot r@+ + rot r> @ + ;

:idsum | x y z id -- x y z
	4 << 'vecpos + >r
	rot r@+ level >> + rot r@+ level >> + rot r> @ level >> + ;

:idsumw | x y z id -- x y z
	4 << 'vecpos + >r
	rot r@+ level >> 1+ + rot r@+ level >> 1+ + rot r> @ level >> 1+ + ;
|	rot r@+ level 1- >> 1+ 2/ + rot r@+ level 1- >> 1+ 2/ + rot r> @ level 1- >> 1+ 2/ + ;

:idsumc | x y z -- xc yc zc
	'vecpos >r
	rot r@+ level 1+ >> + rot r@+ level 1+ >> + rot r> @ level 1+ >> + ;

:idsumz | z id -- z
	4 << 'vecpos + 8 + @ level >> + ;

:calcmask | x y z -- x y z mask
	pick2 pick2 pick2
	idsumc
	x1 pick3 * y1 pick3 * + z1 pick2 * + +? ( 1 )( 0 ) >r drop
	x2 pick3 * y2 pick3 * + z2 pick2 * + +? ( 2 r+ ) drop
	x4 pick3 * y4 pick3 * + z4 pick2 * + +? ( 4 r+ ) drop
	3drop r> ;

:idsumi | x y id -- x y
	3 << 'ivecpos + >r
	r@+ ilevel 1- >> 1+ 2/ rot + r> @ ilevel 1- >> 1+ 2/ rot + ;
|	r@+ ilevel >> rot + r> @ ilevel >> rot + ;

:oct++ | adr -- adr bitmask
|	@ dup 8 >> 2 << $octree + swap $ff and | adr bitmask	| absoluto
	@+ dup 8 >> 2 << rot + swap $ff and 	| adr bitmask  | relativo
	;

|----------------------
:childi | x y node bitm nro place -- node bitm
	1- pick2 and popcnt 2 << pick3 +
	>r >r pick3 pick3 r> idsumi r>

|	ilevel wiso over >> 'wison ! hiso swap >> 'hison !

	ilevel 1- wiso over >> 1+ 2/ 'wison ! hiso swap >> 1+ 2/ 'hison !
	;

:viewi | x y node --
	pick2 pick2 wison pick2 + hison pick2 +
	maskb.rget 0? ( 4drop ; ) drop
|	wison 0? ( drop drawpoint1 ; ) drop
|	hison 0? ( drop drawpoint2 ; ) drop

	$pixels >=? ( $magic $100 nand? ( drop dbox exec ; ) drop getoct viewi getocti ; )

	1 'ilevel +!
	oct++

	nzmin 1 over << pick2 nand? ( 2drop )( childi viewi )
	nzmin 1 xor 1 over << pick2 nand? ( 2drop )( childi viewi )
	nzmin 2 xor 1 over << pick2 nand? ( 2drop )( childi viewi )
	nzmin 4 xor 1 over << pick2 nand? ( 2drop )( childi viewi )
	nzmin 3 xor 1 over << pick2 nand? ( 2drop )( childi viewi )
	nzmin 5 xor 1 over << pick2 nand? ( 2drop )( childi viewi )
	nzmin 6 xor 1 over << pick2 nand? ( 2drop )( childi viewi )
	nzmin 7 xor 1 over << pick2 nand? ( 2drop )( childi viewi )
	4drop
	-1 'ilevel +!
	;

|-----------------------
:id3di | x y z -- xy
	3dproj | x y
	dup 'ymin ! dup 'ymax !
	swap
	dup 'xmin ! dup 'xmax !
	;

:id3d | x y z -- xy
	3dproj | x y
	ymin <? ( dup 'ymin ! )
	ymax >? ( dup 'ymax ! )
	swap
	xmin <? ( dup 'xmin ! )
	xmax >? ( dup 'xmax ! )
	;

:isonow | x y z oct --
	>r
	'ivecpos
	pick3 pick3 pick3 0 idsumw id3di rot !+ !+
   	pick3 pick3 pick3 1 idsumw id3d rot !+ !+
   	pick3 pick3 pick3 2 idsumw id3d rot !+ !+
   	pick3 pick3 pick3 3 idsumw id3d rot !+ !+
   	pick3 pick3 pick3 4 idsumw id3d rot !+ !+
   	pick3 pick3 pick3 5 idsumw id3d rot !+ !+
   	pick3 pick3 pick3 6 idsumw id3d rot !+ !+
   	pick3 pick3 pick3 7 idsumw id3d rot !+ !
	3drop
	xmin xmax over - dup 'wison ! 'wiso !
	ymin ymax over - dup 'hison ! 'hiso !
	'ivecpos
	dup @ pick3 - swap !+ dup @ pick2 - swap !+
	dup @ pick3 - swap !+ dup @ pick2 - swap !+
	dup @ pick3 - swap !+ dup @ pick2 - swap !+
	dup @ pick3 - swap !+ dup @ pick2 - swap !+
	dup @ pick3 - swap !+ dup @ pick2 - swap !+
	dup @ pick3 - swap !+ dup @ pick2 - swap !+
	dup @ pick3 - swap !+ dup @ pick2 - swap !+
	dup @ pick3 - swap !+ dup @ pick2 - swap !
	r> viewi ;

|---------------- search iso ratio
:childr | node bitm nro -- node bitm
	1- pick2 and popcnt 2 << pick3 +    | node bitm nro node'
	>r >r pick4 pick4 pick4 r>
	idsum
	calcmask
	'nzmin !
	r> ;

:viewr | x y z node --
	over nzmin |7 xor
	idsumz clz zlen level + <=? ( drop isonow ; ) drop
	$pixels >=? ( $magic $100 nand? ( drop isonow ; ) drop getoct viewr getocti ; )

	1 'level +!
	oct++

	nzmin >r
	r 1 over << pick2 nand? ( 2drop )( childr viewr )
	r 1 xor 1 over << pick2 nand? ( 2drop )( childr viewr )
	r 2 xor 1 over << pick2 nand? ( 2drop )( childr viewr )
	r 4 xor 1 over << pick2 nand? ( 2drop )( childr viewr )
	r 3 xor 1 over << pick2 nand? ( 2drop )( childr viewr )
	r 5 xor 1 over << pick2 nand? ( 2drop )( childr viewr )
	r 6 xor 1 over << pick2 nand? ( 2drop )( childr viewr )
	r> 7 xor 1 over << pick2 nand? ( 2drop )( childr viewr )
	nip 4drop
	-1 'level +! ;

|----------- search cube in screen
:culling | xyz -- cull
	0.005 <? ( $10 )( 0 ) >r
	swap -? ( neg <? ( $1 r+ ) )( <? ( $2 r+ ) )
	swap -? ( neg <? ( $4 r+ ) )( <? ( $8 r+ ) )
	drop r> ;

:id3doi | xyz --
	culling dup 8 << or 'aocull ! ;

:id3do | xyz --
	culling dup 8 << $ff or aocull and or 'aocull ! ;

:addchildo | x y z id -- x' y' z'
	idsum
	calcmask 'nzmin !
	pick2 pick2 pick2 0 idsumw id3doi
	pick2 pick2 pick2 1 idsumw id3do
	pick2 pick2 pick2 2 idsumw id3do
	pick2 pick2 pick2 3 idsumw id3do
	pick2 pick2 pick2 4 idsumw id3do
	pick2 pick2 pick2 5 idsumw id3do
	pick2 pick2 pick2 6 idsumw id3do
	pick2 pick2 pick2 7 idsumw id3do
	;

:childo
	1- pick2 and popcnt 2 << pick3 +    | node bitm nro node'
	>r >r pick4 pick4 pick4 r> addchildo r> ;

:viewo | x y z node --
	aocull
	0? ( drop viewr ; )
	$ff00 and? ( nip 4drop ; )
	drop

	$pixels >=? ( $magic $100 nand? ( drop isonow ; ) drop getoct viewo getocti ; )

	1 'level +!
	oct++

	nzmin >r
	r 1 over << pick2 nand? ( 2drop )( childo viewo )
	r 1 xor 1 over << pick2 nand? ( 2drop )( childo viewo )
	r 2 xor 1 over << pick2 nand? ( 2drop )( childo viewo )
	r 4 xor 1 over << pick2 nand? ( 2drop )( childo viewo )
	r 3 xor 1 over << pick2 nand? ( 2drop )( childo viewo )
	r 5 xor 1 over << pick2 nand? ( 2drop )( childo viewo )
	r 6 xor 1 over << pick2 nand? ( 2drop )( childo viewo )
	r> 7 xor 1 over << pick2 nand? ( 2drop )( childo viewo )
	nip 4drop
	-1 'level +! ;


|--------- exportadas

::drawoctree | moctree --
	0 dup 'level ! 'ilevel !
	adjustmem
	fillrotvec
	calcmask 'nzmin !
	pick2 pick2 pick2 0 idsum0 id3doi
	pick2 pick2 pick2 1 idsum0 id3do
	pick2 pick2 pick2 2 idsum0 id3do
	pick2 pick2 pick2 3 idsum0 id3do
	pick2 pick2 pick2 4 idsum0 id3do
	pick2 pick2 pick2 5 idsum0 id3do
	pick2 pick2 pick2 6 idsum0 id3do
	pick2 pick2 pick2 7 idsum0 id3do
	$octree viewo ;


::voxelcuad 'drawbox 'dbox ! ;
::voxelbox 'drawboxd 'dbox ! ;
::voxelboxc 'drawboxc 'dbox ! ;

::load3do | "" -- moctree
	here dup rot load 'here ! ;

|--------------------------------------------------------
#xcam 0 #ycam 0 #zcam 1.0

:freelook
	xymouse
	sh 2/ - 7 << swap
	sw 2/ - neg 7 << swap
	neg mrotx mroty
|	2drop
	;

|-------------
#Ocaballo
#Omario
#Oluigi
#Oearth

#Onow

#fps
#fpsc
#mseca

:main
	mark
    maskb.ini
	"media/3do/horse.3do" load3do 'Ocaballo !
	"media/3do/earth.3do" load3do 'Omario !
	"media/3do/map.3do" load3do 'Oluigi !
	"media/3do/horse1.3do" load3do 'Oearth !
|	"media/3do/luigi.3do" load3do 'Oearth !

	Ocaballo 'oNow !
	33
	$88 paper
	show clrscr

		3dini
		freelook
		xcam ycam zcam mtrans

		maskb.clear
		oNow drawoctree

		1 'fpsc +!

		verde fonti
		dup "%d " print cr
		msec fps over mseca - "%d ms fps:%d" print cr 'mseca !

		[ -0.01 'zcam +! ; ] <up>
		[ 0.01 'zcam +! ; ] <dn>
		[ -0.01 'xcam +! ; ] <le>
		[ 0.01 'xcam +! ; ] <ri>
		[ -0.01 'ycam +! ; ] <pgup>
		[ 0.01 'ycam +! ; ] <pgdn>

		[ Oluigi 'Onow ! ; ] <f1>
		[ Omario 'Onow ! ; ] <f2>
		[ Ocaballo 'Onow ! ; ] <f3>
		[ Oearth 'Onow ! ; ] <f4>

		'voxelcuad <f5>
		'voxelbox <f6>
		'voxelboxc <f7>

		'exit >esc< cminiflecha
		1000 .mseg .restart
		fpsc 'fps ! 0 'fpsc !
		;

: main ;

