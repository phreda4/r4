| token run r4
| PHREDA 2013
|----------------------------------
^r4/lib/macrosr4.txt

^r4/ide/r4-dicc.txt
^r4/ide/r4-vstack.txt

#msg

#memdicc	| memoria del diccionario
#memvars	| memoria de variables
#freemem	| memoria libre

#:<<ip
#:ip.word

#stackcode )( 2048
#stackcode> 'stackcode

:>code | new --
	ip.word stackcode> !+ 'stackcode> !
	trim 'ip.word !
	;
:code> | --
	-4 'stackcode> +!
	stackcode> @ 'ip.word ! ;

:pushvmcode | nro --
	dic>str @ 1- >code ;

|---- Comprobaciones
:dstack? | cnt --
	3 << neg dpila> +
	'dpila >=? ( drop ; ) drop
	"data stack empty" 'msg ! 0 nip ;

:rstack? | cnt --
	3 << neg rpila> +
	'rpila >=? ( drop ; ) drop
	"return stack empty" 'msg ! 0 nip ;

:progmem? | adr -- adr
|	'tokencod >=? ( tokencod> <=? ( ; ) )
	"fuera de codemem" 'msg ! 0 nip ;

:datamem? | adr -- adr 0/1
|	memvar >=? ( freemem <=? ( ; ) )
	"fuera de datamem" 'msg ! 0 nip ;

|--- interprete
:i0 :i: :i:: :i# :i#: :i| :i^ drop ;

:idec	tok>cte d.pushDec ;
:ihex	tok>cte d.pushHex ;
:ibin	tok>cte d.pushBin ;
:ifix	tok>cte d.pushFix ;
:istr	tok>mem d.pushStr ;

|--------- ejecuta palabra
:iwor	8 >>
		dup pushvmcode
		dic>tok @ swap r.pushExe ;

|-------- ejecuta palabra

:ivar	tok>mem @ d.pushVar ;
:idvar	tok>mem d.pushDVar ;
:idwor	8 >> dic>tok @ d.pushDCod ;

|--- retorna
:i;		drop r.cnt 0? ( nip ; ) 2drop r.pop code> ;

|--- estructuras de control
:i)(
:i)		8 >> + ;
:i(     drop ;
:i[     over d.pushExe 8 >> + ;
:i]     drop ;

|--ahora sin value desde aca
:iEXEC
	d.pop 0? ( drop ; )
	swap r.pushExe
	|	progmem?
	;

:i0?	@+ d.TOS 0? ( 2drop ; ) drop 8 >> + ;
:i+?	@+ d.TOS +? ( 2drop ; ) drop 8 >> + ;
:i-?	@+ d.TOS -? ( 2drop ; ) drop 8 >> + ;
:i1?	@+ d.TOS 1? ( 2drop ; ) drop 8 >> + ;
:i=?	@+ d.NOS d.pop =? ( 2drop ; ) drop 8 >> + ;
:i<?	@+ d.NOS d.pop <? ( 2drop ; ) drop 8 >> + ;
:i>?	@+ d.NOS d.pop >? ( 2drop ; ) drop 8 >> + ;
:i<=?	@+ d.NOS d.pop <=? ( 2drop ; ) drop 8 >> + ;
:i>=?   @+ d.NOS d.pop >=? ( 2drop ; ) drop 8 >> + ;
:i<>?   @+ d.NOS d.pop <>? ( 2drop ; ) drop 8 >> + ;
:iAND?  @+ d.NOS d.pop AND? ( 2drop ; ) drop 8 >> + ;
:iNAND? @+ d.NOS d.pop NAND? ( 2drop ; ) drop 8 >> + ;

|--- memoria
:i@     d.pop @ d.pushVar ;
:iC@	d.pop c@ d.pushVar ;
:iW@    d.pop w@ d.pushVar ;
:i!     d.pop2 ! ;
:iC!    d.pop2 c! ;
:iW!    d.pop2 w! ;
:i+!	d.pop2 +! ;
:iC+!	d.pop2 c+! ;
:iW+!	d.pop2 w+! ;
:i@+	d.pop @+ swap d.pushVar d.pushVar ;
:iC@+	d.pop c@+ swap d.pushVar d.pushVar ;
:iW@+	d.pop w@+ swap d.pushVar d.pushVar ;
:i!+	d.pop2 !+ d.pushVar ;
:iC!+	d.pop2 c!+ d.pushVar ;
:iW!+	d.pop2 w!+ d.pushVar ;

|---- pantalla
:iSW 		sw d.pushDec ;
:iSH  		sh d.pushDec ;
:iFRAMEV	framev d.pushVar ;
:iREDRAW    ; |redraw >xfb ;
:iUPDATE    ; |1 update drop ;

|---- dibujo
:iOP    	d.pop2 op ;
:iLINE  	d.pop2 line ;
:iCURVE 	d.pop4 curve ;
:iCURVE3 	d.pop6 curve3 ;
:iPLINE 	d.pop2 pline ;
:iPCURVE 	d.pop4 pcurve ;
:iPCURVE3 	d.pop6 pcurve3 ;
:iPOLI   	poli ;
:iFCOL   	d.pop2 fcol ;
:iFCEN   	d.pop2 fcen ;
:iFMAT   	d.pop2 fmat ;
:iSFILL  	sfill ;
:iLFILL  	lfill ;
:iRFILL  	rfill ;
:iTFILL  	d.pop tfill ;

:iSETXY  	d.pop2 setxy ;
:iPX+!   	d.pop px+! ;
:iPX!+   	d.pop px!+ ;
:iPX@    	px@ d.pushVar ;

:iXFB		xfb d.pushVar ;
:i>XFB		|>xfb ;
:iXFB>		|xfb> ;
			;
:iPAPER  	d.pop paper ;
:iINK    	d.pop ink ;
:iINKF   	ink@ d.pushVar ;
:iALPHA  	d.pop ALPHA ;
:iCLS    	cls ;
|----- sis
:iMSEC   	msec d.pushVar ;
:iTIME   	time rot d.pushVar swap d.pushVar d.pushVar ;
:iDATE   	date rot d.pushVar swap d.pushVar d.pushVar ;
:iEND    	drop 0 ;
:iRUNi   	vDROP ;
|-----mouse
|:iTPEN		tpen d.pushVar ;
:iXYMOUSE	xymouse swap d.pushVar d.pushVar ;
:iBMOUSE    bmouse d.pushVar ;
|-----teclado
:iSKEY  	d.pop key! ;
:iKEY		key d.pushVar ;
|-----joystick
:iCNTJOY    cntjoy d.pushVar ;
:iGETJOY    d.pop GETJOY d.pushVar ;
|--- carpetas
:iFFIRST	d.pop FFIRST d.pushVar ;
:iFNEXT		FNEXT d.pushVar ;
|--- archivos
:iLOAD      d.pop2 load d.pushVar ;  | 'from "filename" -- 'to
:iSAVE      d.pop3 save ;   |  'from cnt "filename" --
:iAPPEND	d.pop3 append ;   |  'from cnt "filename" --

:iMEM       freemem d.pushVar ; | mem

|--- mueve mem
:iMOVE      d.pop3 move ;
:iMOVE>     d.pop3 move> ;
:iCMOVE     d.pop3 cmove ;
:iCMOVE>    d.pop3 cmove> ;

|--- sonido
:iSLOAD     d.pop SLOAD d.pushVar ;  | "" -- n
:iSPLAY     d.pop SPLAY ;  | n/0 --
:iSINFO     d.pop SINFO d.pushVar ;  | "" -- n
:iSSET		d.pop4 SSET ;  | pan vol freq "" --

:iOPENURL	d.pop3 openurl d.pushVar ;

| 0
| 1 :
| 2 ::
| 3 #
| 4 #:
| 5 |
| 6 ^
| 7 dec
| 8 hex
| 9 bin
| A fix
| B str
| C wor
| D var
| E dwor
| F dvar
| 10.. macro ; ..
|---------- tabla de saltos
#tablai	| tabla de saltos de instrucciones
i0 i: i:: i# i: i| i^
idec ihex ibin ifix istr
iwor ivar idwor idvar
i;
i(		i)(		i)		i[		i]		iEXEC
i0?		i+?		i-?		i1?		i=?		i<?		i>?		i<=?	i>=?	i<>?	iAND?	iNAND?
vDUP	vDROP	vOVER	vPICK2	vPICK3	vPICK4	vSWAP	vNIP	vROT
v2DUP	v2DROP	v3DROP	v4DROP	v2OVER	v2SWAP
v>R		vR>		vR		vR+		vR@+	vR!+	vRDROP
vAND	vOR		vXOR	vNOT	v+		v-		v*		v/		v*/
v*>>	v/MOD	vMOD	vABS	vSQRT   vCLZ	v<</
vNEG	v1+		v4+		v1-		v2/		v2*		v<<		v>>
i@		iC@		iW@		i!		iC!		iW!		i+!		iC+!	iW+!	i@+		i!+		iC@+	iC!+	iW@+	iW!+
iMOVE	iMOVE>	iCMOVE	iCMOVE>
iMEM	iFFIRST	iFNEXT
iLOAD	iSAVE	iAPPEND
iUPDATE
iXYMOUSE iBMOUSE
iSKEY	iKEY
iCNTJOY	iGETJOY
iMSEC	iTIME	iDATE	iEND	iRUNi
iSW		iSH		iCLS	iREDRAW	iFRAMEV
iSETXY	iPX+!	iPX!+	iPX@
iXFB	i>XFB	iXFB>
iPAPER	iINK	iINKF	iALPHA
iOP		iLINE	iCURVE	iCURVE3 iPLINE	iPCURVE	iPCURVE3 iPOLI
iFCOL	iFCEN	iFMAT	iSFILL	iLFILL	iRFILL	iTFILL
iSLOAD	iSPLAY	iSSET	iSINFO					|--- Sonido
iOPENURL

|-------------------------------
| Asigma memoria para ejecucion
|-------------------------------

:mem0 | nro --
	freemem !+ 'freemem ! ;
:mem1 | nro --
	freemem w!+ 'freemem ! ;
:mem2 | nro --
	freemem c!+ 'freemem ! ;
:mem3 | nro --
	'freemem +! ;

#gmem 'mem0

:gstr | adr --
	freemem
	( swap c@+ 1? )( rot c!+ ) rot
	c!+ 'freemem ! drop ;

:nro>tok	dic>tok	@ ;
:nro>mem    2 << memdicc + @ ;

:accpal | val instr --
	11 <? ( drop tok>cte gmem exec ; ) 	| nro
	11 =? ( drop tok>mem gstr ; )		| str
	16 <? ( drop 8 >>
			dup dic>inf @
			%10 and? ( drop nro>mem )( drop nro>tok )
			mem0 ; )	| pal
	17 =? ( 2drop 'mem2 'gmem ! ; ) | (
	18 =? ( 2drop 'mem3 'gmem ! ; )	| )(
	21 =? ( 2drop 'mem1 'gmem ! ; ) | [
	2drop 'mem0 'gmem ! ;

|i0 i: i:: i# i: i| i^ |6
|idec ihex ibin ifix istr | 11
|iwor ivar idwor idvar	| 15
|i; i( i)( i) i[ i]		| 21

:addmem | adr1token -- adr1token
	'mem0 'gmem !
	( @+ 1? )(
		dup $ff and
		7 <? ( 3drop ; ) 21 >? ( 3drop ; )
		accpal
       ) 2drop ;

:cadavar | nro -- nro
	freemem ,	| para esta palabra...
	dup dic>call@ 0? ( drop ; ) drop 		| sin llamadas
	dup dic>inf @ 1 nand? ( drop ; ) drop	| es word

	freemem
	over dic>tok @ addmem
	freemem =? ( 4 'freemem +! ) | si es variable sin nros
	drop ;

::tokenmem

	here dup 'memdicc !
	cntwords 2 << + dup 'freemem ! 'memvars !
	0 ( cntwords <? )( cadavar 1+ ) drop
	freemem 'here !
	;

|-------------------------------
| palabras de interaccion
|-------------------------------
::resetvm | --
	d.ini	| inicia pilad
	r.ini	| inicia pilar
	<<boot '<<ip !

	cntwords 1- dic>str @
	trim 'ip.word !
	;

:nextw | adr -- adr'
	( c@+ 32 >? )( drop ) drop 1-
	( c@+ 33 <? )( 0? ( drop 1- ; ) drop ) drop 1- ;

::stepvm | --
	<<ip 0? ( drop ; )

	ip.word nextw 'ip.word !

	@+ dup $ff and
	21 >? ( nip )
	2 << 'tablai + @ exec
	'<<ip !
	;

::stepvmn | --
	<<ip 0? ( drop ; )
	dup @ $ff and $c <>? ( drop stepvm ; ) drop

	ip.word nextw 'ip.word !

	dup 4+ swap
	( over <>? )(
		@+ dup $ff and
		21 >? ( nip )
		2 << 'tablai + @ exec
		0? ( '<<ip ! drop ; )
		) nip
	'<<ip ! ;

::tokenexec | token --
	dup $ff and
	21 >? ( nip )
	2 << 'tablai + @ exec ;

