| pilas virtuales (datos y retorno)
| para compilador
| PHREDA 2015
|------------------------------------
|^r4/dev/ide/r4-token.txt
^r4/ide/r4-post.txt
^r4/lib/trace.txt

#:inivpcnt		| nivel de inicio de pila D

|-------- constantes del sistema
#syscons "XRES" "YRES" "FREE_MEM" "SYSFRAME" "XFB"
"SYSPAPER" "SYSXYM" "SYSBM" "SYSKEY" | []

|-------- Registros del CPU
#sysregs "eax" "ebx" "ecx" "edx" "edi" "esi" "ebp"
|#sysregx "xmm1" "xmm2" "xmm3" "xmm4" "xmm5" "xmm6" "xmm7" "xmm8"
#sysregw "ax" "bx" "cx" "dx" "di" "si" "bp" "
#sysregb "al" "bl" "cl" "dl" "*" "*" "*"

|------ Celda de la pila
| T Tipo (NUMERO CONSTANTE 'STRING CODIGO DATO REGISTRO)
| M Indireccion de memoria
| S tiene escalaysuma***
| ..SMTTTT
|-------- tipos de celdas
#celln 0
#cellt )( 1024	| tipo
#cellv )( 1024	| valor
#cella )( 1024  | suma
#cells )( 1024  | shift

:value	over 1024 + @ ;
:suma	over 2048 + @ ;
:shift	over 3072 + @ ;

|--- PILA DATO
#:dpila )( 1024
#:dpila> 'dpila

|--- PILA RETORNO
#:rpila )( 1024
#:rpila> 'rpila

|---- pila para celdas en bloques
#vpst )( 1024
#vpst> 'vpst
#vpmem )( $3fff
#vpmem> 'vpmem

|---- auxiliar de usadas
#regused )( 1024
#regused> 'regused

|----------------------------------------
::cellNRO? | nro -- 1/0
	2 << 'cellt + @ $1f and
	0? ( 1 )( 0 ) nip ;

::cellCTE? | nro -- 1/0
	2 << 'cellt + @ $1f and
	1 <>? ( 0 nip )
	;

::cell0? | nro -- 1/0 (1 si es 0 literal)
	2 << dup 'cellt + @ $1f and
	1? ( 2drop 0 ; ) drop
	'cellv + @ 1? ( drop 0 ; ) drop
	1 ;

::cellVAL | nro -- valor
	2 << 'cellv + @ ;

::cellREG? | nro -- nroreg
	2 << dup 'cellt + @ $1f and
	5 <>? ( 2drop -1 ; ) drop
	'cellv + @ ;

::cellREGM?
	2 << dup 'cellt + @ $f and
	5 <>? ( 2drop -1 ; ) drop
	'cellv + @ ;

::cellSUM | nro -- sum
	2 << 'cella + @ ;

::cellADR? | nro -- 0/1
	2 << 'cellt + @ $10 and ;

::cellSUM? | nro -- 0/1
	2 << 'cellt + @ $20 and ;

::cellMEM! | nro --
	2 << 'cellt + dup @ $10 or swap ! ;

::cellVAR | cell -- cell -1/nrovar
	dup 2 << 'cellt + @ $1f and
	$14 <>? ( -1 nip ; ) drop
	dup 2 << 'cellv + @ ;

::cellDVAR | cell -- cell -1/nrovar
	dup 2 << 'cellt + @ $1f and
	$4 <>? ( -1 nip ; ) drop
	dup 2 << 'cellv + @ ;

::cellSTACK | cell -- -1/esi+?
	2 << dup 'cellt + @ $1f and
	$15 <>? ( 2drop -1 ; ) drop
	'cella + @ ;

|----- dpila
::d.rel		1+ 2 << dpila> swap - @ ;
::d.cnt		dpila> 'dpila - 2 >> ;
::d.pop		-4 'dpila> +! dpila> @ ;
::d.push 	dpila> !+ 'dpila> ! ;
::d.TOS		dpila> 4 - @ ;
::d.NOS 	dpila> 8 - @ ;
::d.PK2 	dpila> 12 - @ ;
::d.PK3 	dpila> 16 - @ ;
::d.PK4 	dpila> 20 - @ ;

::d.drop	d.pop drop ;

::d.swap	dpila> 8 - >r r@+ r@+ r> 8 - !+ ! ;
::d.rot		dpila> 12 - >r r@+ r@+ r@+ swap r> 12 - !+ !+ ! ;
::d.2swap	dpila> 16 - >r r@+ r@+ swap r@+ r@+ swap r> 16 - !+ !+ !+ ! ;

:newcell | valor tipo -- nro
	celln 2 << 'cellt +
	!+
	1020 + !+
	0 swap 1020 + !+ | suma
	0 swap 1020 + ! | shift
	celln
	1 'celln +!
	;


::pushNRO	0 newcell d.push ;
::pushCTE	1 newcell d.push ;
::pushSTR	2 newcell d.push ;
::pushCOD	3 newcell d.push ;
::pushDAT	4 newcell d.push ;
::pushVAR	4 $10 or newcell d.push ;
::pushREG	5 newcell d.push ;

::pushANN	6 newcell d.push ;

|---- tokenrum
::d.pushDec pushNRO ;
::d.pushHex pushNRO ;
::d.pushBin pushNRO ;
::d.pushFix pushNRO ;
::d.pushStr pushSTR ;
::d.pushVar	pushVar ;
::d.pushDVar pushDAT ;
::d.pushDCod pushCOD ;
::d.pushExe pushCOD ;

::pushCPY | celda --
	celln 2 << 'cellt +
	swap 2 << 'cellt +
	@+ rot !+
	swap 1020 + @+ rot 1020 + !+
	swap 1020 + @+ rot 1020 + !+
	swap 1020 + @ swap 1020 + !
	celln d.push
	1 'celln +!
	;

::d.dup		d.TOS pushCPY ;
::d.over	d.NOS pushCPY ;
::d.pick2	d.PK2 pushCPY ; |PICK2  abc -- abca
::d.pick3	d.PK3 pushCPY ; |PICK3  abcd -- abcda
::d.pick4	d.PK4 pushCPY ; |PICK4  abcde -- abcdea
::d.2dup	d.over d.over ;	|2DUP   ab -- abab
::d.2over	d.pick3 d.pick3 ; |2OVER  abcd -- abcdab
::d.nip		d.swap d.drop ;
::d.4DROP	d.drop
::d.3DROP	d.drop
::d.2DROP	d.drop d.drop ;

::r.cnt		rpila> 'rpila - 2 >> ;
::r.pop		-4 'rpila> +! rpila> @ ;
::r.push	rpila> !+ 'rpila> ! ;
::r.tos		rpila> 4 - @ ;
::r.drop	r.pop d.push d.drop ;

|--- tokenrun
::r.pushExe 3 newcell r.push ;
|---

:r.rel		1+ 2 << rpila> swap - @ ;
:r.ini 		'rpila 'rpila> ! ;

|---- imprime celda
:list2str swap ( 1? )( 1- swap >>0 swap ) drop ;

:mt0 value -? ( "(%d)" ,print ; ) "$%h" ,print ;			|--	0 nro 	33
:mt1 value 'syscons list2str ,s ;	|--	1 cte	XRES
:mt2 value "str%h" ,print ;			|--	2 str   "hola"
:mt3 value "w%h" ,print ;			|--	3 cod  'func
:mt4 value "w%h" ,print ;			|--	4 dat  'var
:mt5 value 'sysregs list2str ,s ;	|-- 8 reg 	eax
:mt6 value "_aa%h" ,print ;			|--	6 ANONIMA  'func

#tiposrm mt0 mt1 mt2 mt3 mt4 mt5 mt6 0 0

:mt5b value 'sysregb list2str ,s ;
#tiposrmb mt0 mt1 mt2 mt3 mt4 mt5b mt6 0 0

:mt5w value 'sysregw list2str ,s ;
#tiposrmw mt0 mt1 mt2 mt3 mt4 mt5w mt6 0 0

::,REG | nro --
	'sysregs list2str ,s ;

::,cell | nro --
	2 << 'cellt + dup @ dup $f and
	swap $10 and? ( "dword [" ,s )
	swap
	1 =? ( "(" ,s )
	2 << 'tiposrm + @ exec
	shift 1? ( "*" ,s dup ,d ) drop
	suma 1? ( +? ( "+" ,s ) dup ,d ) drop
	dup $f and 1 =? ( ")" ,s ) drop
	$10 and? ( "]" ,s )
	2drop ;

:,cellb | nro --
	2 << 'cellt + dup @ dup $f and
	swap $10 and? ( "byte [" ,s )
	swap 2 << 'tiposrmb + @ exec
	shift 1? ( "*" ,s dup ,d ) drop
	suma 1? ( +? ( "+" ,s ) dup ,d ) drop
	$10 and? ( "]" ,s )
	2drop ;

:,cellw | nro --
	2 << 'cellt + dup @ dup $f and
	swap $10 and? ( "word [" ,s )
	swap 2 << 'tiposrmw + @ exec
	shift 1? ( "*" ,s dup ,d ) drop
	suma 1? ( +? ( "+" ,s ) dup ,d ) drop
	$10 and? ( "]" ,s )
	2drop ;

:,cell2REG | cell reg --
	over cell0?
	1? ( drop nip "xor " ,s dup ,REG "," ,s ,REG ,cr ; ) drop
	"mov " ,s ,REG "," ,s ,CELL ,cr
	;

::cell+ | add nro --
	| si hay mem resolver
	2 << 'cella + ! ;

::cell+! | add nro --
	| si hay mem resolver
	2 << 'cella + +! ;

::cell<< | shi nro --
	| si hay mem resolver
	| si hay suma resolver
	2 << 'cells + ! ;

::cell[] | nro --
	| si hay mem resolver
	cellMEM! ;

::cell! | valor tipo nro --
	2 <<
	'cellt + !+
	1020 + !+
	0 swap 1020 + !+ | suma
	0 swap 1020 + ! | shift
	;

|------------------
:estareg | val reg dir cell -- val reg dir
	cellREG? pick2 <>? ( drop ; ) drop
	rot drop
	dup 4 - @
	rot rot ;

| en que celda esta el registro
:REGcell | reg -- cell/-1
	5 =? ( drop $ff ; )	| esi ocupado
	6 =? ( drop $ff ; )	| ebp ocupado
	-1 swap
	'dpila ( dpila> <? )( @+ estareg ) drop
	'rpila ( rpila> <? )( @+ estareg ) drop
	drop ;


|-------------------------------------------
| DEBUG: imprime pila virtual
|-------------------------------------------
:,cellp | nro --
	2 << 'cellt + dup @ dup $f and
	swap $10 and? ( "[" ,s )
	swap 2 << 'tiposrm + @ exec
	shift 1? ( "*" ,s dup ,d ) drop
	suma 1? ( +? ( "+" ,s ) dup ,d ) drop
	$10 and? ( "]" ,s )
	2drop ;

::printddvtack
	'dpila
	( dpila> <? )( @+ ,cellp ,sp ) drop
	'rpila
	rpila> <>? ( " :R " ,s )
	( rpila> <? )( @+ ,cellp ,sp ) drop
	,cr ;

::dumpcells, | -- ; imprime info celdas
|	"Nro:Read:Write:T" ,print ,cr
	0 ( celln <? )(
		dup ,cell
		dup " ; %h:" ,print
		1+ ) drop
	;

|------------------------------------------------
:sinuse | n -- 0/1
	'dpila ( dpila> <? )(
		@+ cellSTACK
		-? ( drop )( pick2
			=? ( 3drop 1 ; ) drop )
		) 2drop 0 ;

:freelreg | adr cell reg --

	pick2 'dpila - inivpcnt 1- 2 << swap -
	"mov [esi" ,s 1? ( +? ( "+" ,s ) ,d )( drop ) "]," ,s ,REG ,cr
	5 $15 pick2 cell!
	swap
	'dpila - inivpcnt 1- 2 << swap -

	swap cell+!
	"; post !!" ,s printddvtack
	;

::freelastreg | --
	"; libera !!!" ,s printddvtack
	'dpila ( dpila> 4 - <? )(
		dup 'dpila - inivpcnt 2 - 2 << swap -
		sinuse 0? ( drop
			@+ dup cellREG? 0 >=? ( freelreg ; )
			2drop
			)( drop 4+ )
		) drop
	 ;

::newreg | -- reg
	0 ( dup REGcell -1 <>? )( drop 1+ ) drop
	7 <? ( ; ) drop
	freelastreg 
	newreg ;

::newRegX | -- reg ; no ecx ni edx ni eax
	1 regcell -1 =? ( drop 1 ; ) drop | ebx
	2 regcell -1 =? ( drop 2 ; ) drop | ecx
	4 regcell -1 =? ( drop 4 ; ) drop | edi
	freelastreg
	newRegX ;

|---- corre esi
:cellESI | cell deltasi --
	over cellREGM? 5 <>? ( 3drop ; ) drop | si es ESI
	neg 2 << swap Cell+! ;

:correESI | deltaesi --
	0? ( drop ; )
	"lea esi,[esi" ,s dup +? ( "+" ,s ) 2 << ,d "]" ,ln
	'dpila ( dpila> <? )( @+ pick2 cellESI ) drop
	'rpila ( rpila> <? )( @+ pick2 cellESI ) drop
	drop ;

|---------------------------
:printstate
	";* " ,s
	'regused 0 ( d.cnt <? )( swap
     	c@+ 1? ( pick2 2 << "[esi+%d] " ,print )
		drop swap 1+ ) 2drop
	,cr
|	"r4asm/cod.asm" savemem
	;

|--- cambia registros en pila
:inchange | reg dest cnt 'dpila cell sum
	drop pick4 5 rot cell! 2dup ;

:changeREG | dest cell reg -- dest cell
	pick2 d.cnt 1- 'dpila | dest cell reg dest cnt 'dpila
	( dpila> <? )( swap 1- swap @+
		dup cellSTACK
		-? ( 2drop )( pick3 2 << <>? ( pick4 2 << =? ( inchange ) ) 2drop )
		) 4drop ;

:noregchg | dest cell -- ; celda no es reg!!
 	newReg
	"mov " ,s dup ,REG ",dword [esi" ,s pick2 2 << 1? ( +? ( "+" ,s ) ,d )( drop ) "]" ,s ,cr
	"mov dword [esi" ,s pick2 2 << 1? ( +? ( "+" ,s ) ,d )( drop ) "]," ,s over ,CELL ,cr
	changeReg
	5 $15 pick2 cell! swap 2 << swap cell+!
	;

:chg2stack | dest cell --
	dup cellREG? -? ( drop noregchg ; ) drop
	"xchg dword [esi" ,s
	over 2 << 1? ( +? ( "+" ,s ) ,d )( drop )
	"]," ,s dup ,CELL ,cr
	dup cellREG? changeREG | dest cell reg
	5 $15 pick2 cell! swap 2 << swap cell+! ;

:mov2stack | dest cell --
|	over ";(%d )" ,print "; " ,s printddvtack

	over 'regused + c@ 1? ( drop chg2stack ; ) drop
	"mov dword [esi" ,s
	over 2 << 1? ( +? ( "+" ,s ) ,d )( drop )
	"]," ,s dup ,CELL ,cr

	5 $15 pick2 cell! swap 2 << swap cell+! ;

|--- mueve los que son registros
:movereg
	d.cnt 1- 'dpila
	( dpila> 4 - <? )( swap 1- swap @+
		dup cellREG? 0 >=? ( drop nip mov2stack 1 ; ) 2drop
		) 2drop 0 ;

|--- mueve constantes iguales !!!! optimizacion !!!!
:movecte
	'dpila
	( dpila> <? )(
		@+ cellNRO? drop |*******
		) drop ;

|--- mueve los que quedan
:resolveADR | cnt adr cell xx -- cnt adr cell xx
	newReg pick2 over ,cell2REG
	|--- cnt adr cell xx reg
	pick2 cellSTACK 2 >> pick3 pick2 changeREG | dest cell reg
	2drop
	5 pick3 cell!
	;

:realmove | cnt adr cell -- cnt adr x x
	dup cellADR? 1? ( resolveADR ) drop
	| cnt adr cell
	pick2 swap mov2stack | dest cell
	2dup
	;

:movecell
	d.cnt 1- 'dpila
	( dpila> 4 - <? )( swap 1- swap @+
		dup cellSTACK pick3 2 <<
		<>? ( drop realmove ) 2drop
		) 2drop ;

|----
:usado | nro XX cell -- nro XX
	dup cellSTACK pick3 2 << <>? ( 1 swap 2 >> 'regused + c! drop ; )
	2drop ;

:fillstackuse
	'regused d.cnt
	( 1? )( 1- 0 rot c!+ swap ) 2drop
	d.cnt 1- 'dpila
	( dpila> <? )( swap 1- swap @+
		usado ) 2drop ;

:vplast
	inivpcnt 0? ( drop ; ) drop
	"mov eax,dword [esi-4]" ,s ,cr
	0 5 d.tos cell! ;

:vpcopia2
	d.cnt 0? ( drop vplast ; ) drop
	fillstackuse
|	printstate
	( movereg 1? )( drop ) drop
	movecte
	movecell
	d.tos
	dup cellREG? 0? ( 2drop ; ) drop	| ya es eax!
	dup 0 ,cell2REG
	0 5 rot cell! ;


|---- transforma la pila actual a normal
::vpila2normal | --
	inivpcnt d.cnt -
	correESI			| corre esi a nuevo lugar
	vpcopia2			| copia los registros/ctes en pila
	d.cnt 'inivpcnt !	| ahora vpila tiene...
	;

::vpila2normal? | -- 0/1 ; esta en normal?
|	"; NORMAL?" ,s printddvtack
	d.cnt 1 =? ( drop 'dpila @ cellREG? 1? ( drop 0 ; ) drop 1 ; )
	inivpcnt <>? ( drop 0 ; )
	1- 'dpila
	( swap 1? )( 1- swap
		@+ cellSTACK pick2 2 << <>? ( 3drop 0 ; ) drop
		) drop
	cellREG? 1? ( drop 0 ; ) drop 1 ;

|------------------------------------------
| inicia vpila como normal con USADO celdas
| de forma normal, guarda nivel en INIVPCNT
| [esi+4] [esi] eax -- para usado=3
|------------------------------------------
:inivstack
	0 'celln !
	'dpila 'dpila> !
	'rpila 'rpila> !
	'vpst 'vpst> !
	'vpmem 'vpmem> !
	;

::inivpila | deep --
	0? ( 1 nip )
	dup 'inivpcnt !
	inivstack
	1- ( 1? )( 1-
		5 $15 newcell
		over 2 << over
		cell+ d.push 	| [esi+..
		) drop
	0 5 newcell d.push	| eax
	;

::vpila2v | deep --
	1? ( -? ( ( 1? )( 1+ d.drop ) )( ( 1? )( 1- d.dup ) ) )
	drop
	d.cnt dup 'inivpcnt ! | ahora vpila tiene...
	0? ( drop ; )
	1- ( 1? )( 1-
		dup 1+ d.rel
		5 $15 pick2 cell!
		over 2 << swap cell+
		)
	d.rel 0 5 rot cell! ;

::vpila2callV | nro --
	dic>mov @ 24 << 24 >> vpila2v ; | uso de pila

|-------------------------------------
:toREG | cell --
	dup cellREG? 0 >=? ( 2drop ; ) drop
	newReg 			| cell reg
	2dup ,cell2REG 5 rot cell! ;

::vpila2fixdeep | deep --
	dpila> over 1+ 2 << -
	( dpila> 	|4 - NO incluye TOS
		<? )(
		@+ toREG
		) 2drop ;

::vpila2fixdeepTOS | deep --
	dpila> over 1+ 2 << -
	( dpila> 4 - | incluye TOS
		<? )(
		@+ toREG
		) 2drop ;

|------ acceso a celdas
::,TOS	d.tos ,cell ;
::,TOSb	d.tos ,cellb ;

::,NOS	d.nos ,cell ;
::,NOSb	d.nos ,cellb ;
::,NOSw	d.nos ,cellw ;

::,NOS2	d.pk2 ,cell ;
::,RTOS	r.tos ,cell ;

::cell2cell! | origen destino --
	2 << 'cellt +
	swap 2 << 'cellt +
	@+ rot !+
	swap 1020 + @+ rot 1020 + !+
	swap 1020 + @+ rot 1020 + !+
	swap 1020 + @ swap 1020 + ! ;

:cell=? | cell cell -- cell 1/0
	over 2 << 'cellt + @ over 2 << 'cellt + @ <>? ( 2drop 0 ; ) drop
	over 2 << 'cellv + @ over 2 << 'cellv + @ <>? ( 2drop 0 ; ) drop
	over 2 << 'cella + @ over 2 << 'cella + @ <>? ( 2drop 0 ; )
	2drop 1 ;

:cell2newReg | cell --
	newREG
	over cell0? 0? ( drop
		"mov " ,s dup ,REG "," ,s over ,CELL
		)( drop
		"xor " ,s dup ,REG "," ,s dup ,REG
		)
	,cr 	| cell reg ; celdas iguale con el mismo registro
	'dpila ( dpila> <? )(
		@+ | cell reg adr cell
		pick3 cell=? 1? ( drop pick2 5 rot cell! )( 2drop )
		) 3drop ;

:cell2newRegW | cell --
	newREG
	over cell0? 0? ( drop
		"mov " ,s dup ,REG "," ,s over ,CELL
		)( drop
		"xor " ,s dup ,REG "," ,s dup ,REG
		)
	,cr 	| solo reemplaza el unico
	5 rot cell! ;

#regdup -1

:realmov | cell reg adr cell -- cell reg adr
	regdup -? ( drop NewReg
		"mov " ,s dup ,REG "," ,s pick3 ,REG ,nl
		dup 'regdup ! )
	5 rot cell! ;

::removedup | cell reg --
	-1 'regdup !
	'dpila ( @+ pick3 <>? )(
		dup cellREG? pick3 =? ( drop realmov )( 2drop )
		) 4drop
	;

::cell2REG | cell --
	dup cellREG? 0 >=? ( removedup ; ) drop | es de lectura!!
    cell2newReg ;

::cell2REGW | cell --
	dup cellREG? 0 >=? ( removedup ; ) drop
    cell2newRegW ;

::cell2NMEM | cell --
	dup cellADR? 0? ( 2drop ; ) drop
    cell2newReg ;

::cell2REGorMEM | cell --
	dup cellREG? 0 >=? ( 2drop ; ) drop
	dup cellADR? 1? ( 2drop ; ) drop
	cell2newReg ;

::cell2REGorCTE | cell --
	dup cellNRO? 1? ( 2drop ; ) drop
	dup cellREG? 0 >=? ( 2drop ; ) drop
	cell2newReg ;

::setEAX | cell --
	0 5 rot cell! ;

::setEDX | cell --
	3 5 rot cell! ;

:noregdesde | reg --
	'dpila
	( dpila> 16 - <? )( swap 1- swap @+
		dup cellREG? 0 >=? ( drop nip mov2stack 1 ; ) 2drop
		) 2drop 0 ;

:pk2EAX | -- ; anterior a pk2 sin eax, no usar edx
	0 'dpila ( dpila> 12 - <? )(
		@+ cellREG? 0? ( rot 1+ rot rot ) drop ) drop
	0? ( drop ; ) drop | no hay eax abajo de pk2
	newRegX
	"mov " ,s dup ,REG ",eax" ,s ,nl
	'dpila ( dpila> 12 - <? )(
		@+ dup cellREG? 0? ( drop pick2 5 rot cell! )( 2drop ) ) 2drop
|	"; " ,s printddvtack
	;

:nosEAX | anterior a nos sin eax, no usar edx
	0 'dpila ( dpila> 8 - <? )(
		@+ cellREG? 0? ( rot 1+ rot rot ) drop ) drop
	0? ( drop ; ) drop | no hay eax abajo de nos
	newRegX
	"mov " ,s dup ,REG ",eax" ,s ,nl
	'dpila ( dpila> 8 - <? )(
		@+ dup cellREG? 0? ( drop pick2 5 rot cell! )( 2drop ) ) 2drop
|	"; " ,s printddvtack
	;

|--- ECX o constante
::cteorECX | cell --
	dup cellNRO? 1? ( 2drop ; ) drop
	dup cellREG? 2 =? ( 2drop ; ) drop
	"mov ecx," ,s dup ,CELL ,cr
	2 5 rot cell! ;

::needREGorMEM | cell --
	dup cellREG? 0 >=? ( 2drop ; ) drop
	dup cellADR? 1? ( 2drop ; ) drop
	cell2newReg ;

:needREGorCTE | cell --
	dup cellNRO? 1? ( 2drop ; ) drop
	dup cellREG? 0 >=? ( 2drop ; ) drop
	cell2newReg ;

:inverteaxreg
	d.tos d.nos "xchg " ,s ,CELL "," ,s ,CELL ,cr
	;

::nosEAXtosREGMEMfreeEDX
	d.tos cellREG? 0? ( d.nos cellREG? 0 >=? ( 2drop inverteaxreg ; ) drop ) drop
	nosEAX
	d.tos needREGorMEM
	;

:inv1eaxreg
	d.pk2 d.nos
	2dup "xchg " ,s ,CELL "," ,s ,CELL ,cr
	cell2cell! ;

:inv2eaxreg
	d.pk2 d.tos
	2dup "xchg " ,s ,CELL "," ,s ,CELL ,cr
	cell2cell! ;

::pk2EAXtosnosREGMEMfreeEDX
	d.nos cellREG? 0? ( d.pk2 cellREG? 0 >=? ( 2drop inv1eaxreg ; ) drop ) drop
	d.tos cellREG? 0? ( d.pk2 cellREG? 0 >=? ( 2drop inv2eaxreg ; ) drop ) drop
	d.nos needREGorMEM
	d.tos needREGorMEM
	pk2EAX ;

::pk2EAXnosREGMEMtosECXfreeEDX
	d.nos cellREG? 0? ( d.pk2 cellREG? 0 >=? ( 2drop inv1eaxreg ; ) drop ) drop
	d.tos cellREG? 0? ( d.pk2 cellREG? 0 >=? ( 2drop inv2eaxreg ; ) drop ) drop
	d.nos needREGorMEM
	d.tos cteorECX
	pk2EAX ;


|--- pila de estado de pila y registros para bloques
:,vp vpmem> !+ 'vpmem> ! ;
:vp@ vpmem> @+ swap 'vpmem> ! ;

:,cpcell | nro --
	2 <<
	dup 'cellt + @ ,vp
	dup 'cellv + @ ,vp
	dup 'cella + @ ,vp
	'cells + @ ,vp
	;

:cpcell@ | nro --
	2 <<
	vp@ over 'cellt + !
	vp@ over 'cellv + !
	vp@ over 'cella + !
	vp@ swap 'cells + !
	;

::vpPush | --
	vpmem> vpst> !+ 'vpst> !
	dpila> 'dpila - 2 >>
	,vp
	'dpila ( dpila> <? )(
		@+ dup ,vp ,cpcell
		) drop
	rpila> 'rpila - 2 >>
	,vp
	'rpila ( rpila> <? )(
		@+ dup ,vp ,cpcell
		) drop
	inivpcnt ,vp

|	" ;VPUSH " ,s printddvtack        |*********
	;

::vpPop | --
	-4 'vpst> +!
	vpst> @ 'vpmem> !
	'dpila 'dpila> !
	vp@
	( 1? )( 1-
		vp@ dup d.push cpcell@
		) drop
	'rpila 'rpila> !
	vp@
	( 1? )( 1-
		vp@ dup r.push cpcell@
		) drop
	vp@ 'inivpcnt !

|	" ;VPOP " ,s  printddvtack        |*********
	;

::vpPick | -- ; pop del segundo
	vpst> 8 - @ >r
	'dpila 'dpila> !
	r@+
	( 1? )( 1-
		r@+ dup d.push 2 <<
		r@+ over 'cellt + !
		r@+ over 'cellv + !
		r@+ over 'cella + !
		r@+ swap 'cells + !
		) drop
	'rpila 'rpila> !
	r@+
	( 1? )( 1-
		r@+ dup r.push 2 <<
		r@+ over 'cellt + !
		r@+ over 'cellv + !
		r@+ over 'cella + !
		r@+ swap 'cells + !
		) drop
	r@+ 'inivpcnt !
    rdrop

|	" ;VPICK " ,s	printddvtack        |*********
	;

::vpDrop | --
	-4 'vpst> +!
	vpst> @ 'vpmem> ! ;

::vpNip | --
	-4  'vpst> +!
	vpst> dup @ swap 4 - ! ;


|---- transforma pila actual en copia apilada
:cell=? | a b -- a b 1/0
	over 2 << over 2 <<
	over 'cellt + @ over 'cellt + @ <>? ( 3drop 0 ; ) drop
	over 'cella + @ over 'cella + @ <>? ( 3drop 0 ; ) drop
	over 'cellv + @ over 'cellv + @ <>? ( 3drop 0 ; ) drop
	over 'cells + @ over 'cells + @ <>? ( 3drop 0 ; )
	3drop 1
	;

|------------------
:estareg | val reg dir cell -- val reg dir
	cellREG? pick2 <>? ( drop ; ) drop
	rot drop
	dup 4 - @
	rot rot ;

:searchused | dest src rr -- dest src rr userr
	'regused 'dpila
	( @+ cellReg? pick3 <>? )(	| rr used dpila rd
		drop swap 4+ swap )
	2drop @ ;

:chageregreg | regs regd -- regs
	'dpila ( dpila> <? )(
		@+ cellReg? pick3 =? ( pick2 5 pick3 4 - @ cell! )
		drop
		) 2drop
	;

:srcnotreg | dest src -- ; src no reg, dest ocupado
	over cellREG?	| dest src regdes
	searchused		| dest src regdes userr
	"mov " ,s dup ,cell "," ,s over ,REG ,cr
	cellREG? chageregreg
|	";;;" ,s printddvtack        |*********
	rot drop
	2dup ,cell2REG 5 rot cell!
|	";;;" ,s printddvtack        |*********
	;

:xchgcellcell | dest src --
	dup cellREG? -? ( drop srcnotreg ; ) nip
	swap cellREG?
	2dup "xchg " ,s ,REG "," ,s ,REG ,cr
	| src dst
	'dpila ( dpila> <? )(
		@+ dup cellREG? pick3 =? ( pick4 5 pick3 cell! ) 2drop
		) drop
	2drop ;

:copycellcell | dest src --
	cell=? 1? ( 3drop ; ) drop
	over cellREG? REGcell 0 >=? ( drop xchgcellcell ; ) drop
	swap cellREG?
	2dup ,cell2REG 5 rot cell!
	;

:makemap
	vpst> 4 - @ >r
	'regused 'regused> !
	r@+ |dup "D:%d) " ,print
	( 1? )( 1-
		r@+ drop
		r@+ r@+ swap newcell
		r@+ over cell+
		r@+ over cell<<

|		dup ,cell ,sp	| **DEBUG

		regused> !+ 'regused> !
		) drop
	r@+ | r:
	( 1? )( 1- r@+ drop 16 r+ ) drop
	r@+
	|" inivpcnt:%d  " ,print
	inivpcnt over - correESI
	'inivpcnt !
    rdrop
	;

::vpFitAll | --
	"; vFitall " ,print d.cnt "d.cnt:%d " ,print ,cr

	celln makemap 'celln ! | usa celdas pero no las ocupa, restaura celln!

|	"r4asm/cod.asm" savemem

|	";;;" ,s printddvtack        |*********
|	"r4asm/cod.asm" savemem

	'regused 'dpila d.cnt	| de abajo hasta arriba
	( 1? )( 1-
		pick2 @ pick2 @ copycellcell
		rot 4+ rot 4+ rot ) 3drop

|	";;;" ,s printddvtack        |*********
|	"r4asm/cod.asm" savemem

	;

|-----------------
:esvari | cell var adr c0 -- cell var adr
	cellVAR |-? ( 2drop ; ) | c0 var
	pick3 <>? ( 2drop ; )
	newReg  | c0 var reg
	"mov " ,s dup ,REG ",dword [w" ,s swap ,h "]" ,s ,cr
	5 rot cell!
	;

::resguardavar | cell --
	cellDVAR -? ( 2drop ; ) | cell var
	'dpila ( dpila> 4 - <? )( | cell var adr
		@+ esvari
		) 3drop ;
