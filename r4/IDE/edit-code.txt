| edit-code.txt
| PHREDA 2007
|---------------------------------------
^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/parse.txt
^r4/lib/codecolor.txt

^r4/lib/fontm.txt
^inc/fntm/droidsans13.txt

^r4/system/mem-ed.txt
^r4/dev/ide/r4-token.txt
^r4/dev/ide/r4-info.txt

^r4/dev/ide/r4-tokenprint.txt

^r4/lib/trace.txt

#pantaini>	| comienzo de pantalla
#pantafin>	| fin de pantalla
#prilinea	| primera linea visible
#cntlinea

#inisel		| inicio seleccion
#finsel		| fin seleccion

#fuente  	| fuente editable
#fuente> 	| cursor
#$fuente	| fin de texto

#clipboard	|'clipboard
#clipboard>

#undobuffer |'undobuffer
#undobuffer>

|---- arreglo de info
#infomode 0
#textinfo 0

|---- palabra actual
#wordact

|---- debug.err
#nrolin )( 32
#error )( 256
#errorlin
#mensaje )( 1024

:cursor>carlin | --
	1 fuente ( fuente> <? )( c@+ 13 =? ( rot 1+ rot rot ) drop ) | cuenta lineas
	swap 'ed.nlin !
	( dup c@ 13 <>? )( drop 1- ) drop | cuenta caracters a inicio de linea
	fuente> swap - 'ed.ncar ! ;

|----- edicion
:lins  | c --
	fuente> dup 1- $fuente over - 1+ cmove>
	1 '$fuente +!

:lover | c --
	fuente> c!+ dup 'fuente> !
	$fuente >? ( dup '$fuente ! ) drop
:0lin | --
	0 $fuente c! ;

#modo 'lins

:back
	fuente> fuente <=? ( drop ; )
	dup 1- c@ undobuffer> c!+ 'undobuffer> !
	dup 1- swap $fuente over - 1+ cmove
	-1 '$fuente +!
	-1 'fuente> +! ;

:del
	fuente>	$fuente >=? ( drop ; )
    1+ fuente <=? ( drop ; )
	9 over 1- c@ undobuffer> c!+ c!+ 'undobuffer> !
	dup 1- swap $fuente over - 1+ cmove
	-1 '$fuente +! ;

:<<13 | a -- a
	( fuente >=? )( dup c@
		13 =? ( drop ; )
		drop 1- ) ;

:>>13 | a -- a
	( $fuente <? )( dup c@
		13 =? ( drop 1- ; ) | quitar el 1-
		drop 1+ )
	drop $fuente 2 - ;

#1sel #2sel

:selecc	| agrega a la seleccion
	mshift 0? ( dup 'inisel ! 'finsel ! ; ) drop
	inisel 0? ( fuente> '1sel ! ) drop
	fuente> dup '2sel !
	1sel over <? ( swap )
	'finsel ! 'inisel !
	;

:khome
	selecc
	fuente> 1- <<13 1+ 'fuente> !
	selecc ;

:kend
	selecc
	fuente> >>13  1+ 'fuente> !
	selecc ;

:scrollup | 'fuente -- 'fuente
	pantaini> 1- <<13 1- <<13  1+ 'pantaini> !
	prilinea 1? ( 1- ) 'prilinea !
	selecc ;

:scrolldw
	pantaini> >>13 2 + 'pantaini> !
	pantafin> >>13 2 + 'pantafin> !
	1 'prilinea +!
	selecc ;

:colcur
	fuente> 1- <<13 swap - ;

:karriba
	fuente> fuente =? ( drop ; )
	selecc
	dup 1- <<13		| cur inili
	swap over - swap	| cnt cur
	dup 1- <<13			| cnt cur cura
	swap over - 		| cnt cura cur-cura
	rot min +
	fuente <? ( fuente nip )
	'fuente> !
	selecc ;

:kabajo
	fuente> $fuente >=? ( drop ; )
	selecc
	dup 1- <<13 | cur inilinea
	over swap - swap | cnt cursor
	>>13 1+    | cnt cura
	dup 1+ >>13 1+ 	| cnt cura curb
	over -
	rot min +
	'fuente> !
	selecc ;

:kder
	selecc
	fuente> $fuente <?
	( 1+ 'fuente> ! selecc ; ) drop
	;

:kizq
	selecc
	fuente> fuente >?
	( 1- 'fuente> ! selecc ; ) drop
	;

:kpgup
	selecc
	20 ( 1? )( 1- karriba ) drop
	selecc ;

:kpgdn
	selecc
	20 ( 1? )( 1- kabajo ) drop
	selecc ;

|---------------   mover fuente
:e.clear
	fuente dup 'pantaini> ! dup 'fuente> ! '$fuente !
	0lin ;

|------------------------------------------------
:loadtxt | -- cargar texto
	mark
	here 'ed.nombre load 0 swap c!

	|-- quesa solo cr al fin de linea
	fuente dup 'pantaini> ! dup 'fuente> !
	here ( c@+ 1? )(
		13 =? ( over c@ 10 =? ( drop swap 1+ swap )( drop ) )
		10 =? ( drop c@+ 13 <>? ( drop 1- 13 ) )
		rot c!+ swap ) 2drop '$fuente !
	0lin

	|-- ubicar el cursor
	ed.nlin 0 >? ( ( 1? )( 1- fuente> >>13 2 + 'fuente> ! ) ) drop
	ed.ncar +? ( dup 'fuente> +! ) drop
	0 'prilinea !
	empty
	;

:savetxt
	mark	| guarda texto
	fuente ( c@+ 1? )( 13 =? ( ,c 10 ) ,c ) 2drop
	'ed.nombre savemem
	empty
	0 dup "debug.err" save
	0 dup "runtime.err" save
	ed.save
	;

|-------------------------------------------
:copysel
	inisel 0? ( drop ; )
	clipboard swap
	finsel over - pick2 over + 'clipboard> !
	cmove
	;

:borrasel
	inisel finsel $fuente finsel - 4+ cmove
	finsel inisel - neg '$fuente +!
	fuente> inisel >=? ( finsel <=? ( inisel 'fuente> !
		)(  finsel inisel - over swap - 'fuente> ! ) )  drop
	0 dup 'inisel ! 'finsel ! ;

:kdel
	inisel 0? ( del )( borrasel ) drop ;

:kback
	inisel 0? ( back )( borrasel ) drop ;

|-------------
| Edit CtrE
|-------------
:posfijo? | adr -- desde/0
	( c@+ 1? )(
		46 =? ( drop ; )
		drop )
	nip ;

:controle
	ed.save
	savetxt
	fuente> ( dup 1- c@ $ff and 32 >? )( drop 1- ) drop | busca comienzo
	dup c@
|	$23 <>? ( 2drop ; ) | no es #
	$5E <>? ( 2drop ; ) | no es ^
	drop
	dup fuente - 'ed.lugar !
	dup posfijo? 0? ( 2drop ; )

	dup "mem/inc-%w.mem" mprint
	pick2 1+ dup ( c@+ $ff and 32 >? )( drop ) drop | ini fin
	1- over -
	rot save

	"r4/system/inc-%w.txt" mprint run ;

|-------------
:controlc | copy
	copysel ;

|-------------
:controlx | move
	controlc
	borrasel ;

|-------------
:controlv | paste
	clipboard clipboard> over - 0? ( 3drop ; ) | clip cnt
	fuente> dup pick2  + swap | clip cnt 'f+ 'f
	$fuente over - 1+ cmove>	| clip cnt
	fuente> rot rot | f clip cnt
	dup '$fuente +!
	cmove
	clipboard> clipboard - 'fuente> +!
	;

|-------------
:controlz | undo
	undobuffer>
	undobuffer =? ( drop ; )
	1- dup c@
	9 =? ( drop 1- dup c@ [ -1 'fuente> +! ; ] >r )
	lins 'undobuffer> ! ;

|-------------
:=w | w1 w2 -- 1/0
	( c@+ 32 >? )( toupp rot c@+ toupp rot - 1? ( 3drop 0 ; ) drop swap ) 3drop 1 ;

:exactw | adr 1c act cc -- adr 1c act 1/0
	drop dup pick3 =w ;

:setcur | adr 1c act 1
	drop nip nip dup 'wordact ! 'fuente> ! ;

:controla | -- ;find prev
	wordact dup c@ 33 <? ( 2drop ; ) | adr 1c
	toupp
	over 1- | adr 1c act
	( fuente >? )(
		dup c@ toupp pick2 =? ( exactw 1? ( setcur ; ) )
		drop 1- ) 3drop ;

:controls | -- ;find next
	wordact dup c@ 33 <? ( 2drop ; ) | adr 1c
	$3A =? ( drop 1+ dup c@ )		| $3a :  Definicion
	$23 =? ( drop 1+ dup c@  )		| $23 #  Variable
	toupp
	over 1+ | adr 1c act
	( $fuente <? )(
		dup c@ toupp pick2 =? ( exactw 1? ( setcur ; ) )
		drop 1+ ) 3drop ;

|-------------
:controld | buscar definicion
	;

:controln  | new
	| si no existe esta definicion
	| ir justo antes de la definicion
	| agregar palabra :new  ;
	;

|------ Dibuja codigo
:drawcur | com -- com
	blink 1? ( drop ; ) drop
	fuente> >? ( ; )
	dup	( fuente> <? )( c@+ 13 =? ( 2drop ; ) gemit )
	modo 'lins =? ( $ffffff )( $ffff00 ) ink drop
	printcur drop ;

:drawsel | com -- com
	inisel 0? ( drop ; ) drop
	finsel >=? ( ; )
	dup ( inisel <? )( c@+ 13 =? ( 2drop ; ) gemit )
	sel>> $88 ink
	( finsel <? )( c@+ 13 =? ( 2drop sel<< ; ) gemit )
	drop sel<< ;

|------ info code
:moreword
	mark ,printinfowor empty
	here amarillo oscuro
	'exit swap link |fillp blanco print
	;

:idef
	$f not and 'indicepal +
	sp
		|	dup @ print sp	| !!!nombre
	mark
	dup 12 + @ ,printmovword
	empty
	here azul
	'exit swap link |fillp blanco print
	8 + @ $a0 and 1? ( moreword )( drop )

	;

:ivar
	$f not and 'indicepal +
	sp
	8 + @
	6 >> vtype " %s "
	azul fillp blanco print ;

:ierr
	4 >> errormsg sp "!! %s "
	rojo fillp blanco print ;

:iwar
	amarillo " ! " print
	drop
	;

#tipoinfo 0 'idef 'ivar 'ierr 'iwar 0 0 0 0 0 0 0 0 0 0 0 0

:infoline
	infomode 0? ( drop ; ) drop
	over prilinea + 2 << textinfo + @ | infolinea
	0? ( drop ; )
	dup $f and 2 << 'tipoinfo + @ exec ;

:drawcode
	pantaini>
	0 ( cntlinea <? )(
        gris
		dup prilinea + "%d" print
       	ccw 2 << dup 'tx1 ! 'ccx !
		swap
		drawsel lf drawcur lf >>lineacolor0
		infoline
		0 'tx1 !
		0? ( 2drop cntlinea $fuente )( cr )
		swap 1+ ) drop
	$fuente <? ( 1- ) 'pantafin> !
	fuente>
	( pantafin> >? )( scrolldw )
	( pantaini> <? )( scrollup )
	drop
	;

:tokerror
	4 << $03 or
	errornrolinea 2 << textinfo + !
	1 'infomode !
	;

:clearti
	textinfo cntlinea 1+ ( 1? )( 0 rot !+ swap 1- ) 2drop ;

|------------ recompilar
:recompilar
	home
	$6600 ink 1 linesfill
	verde dup ":R%d" print
	rojo "cOMPILANDO " printx
   'ed.nombre "[%s] " print
	redraw
	clearti
	mark
	fuente tokeniza
	empty
	1? ( tokerror ; )
	drop
	tokenpostall | analisis de palabras
|---- arma array info
	fuente textinfo arrayinfo
	1 'infomode !
	;

|------------ panel info
#panelinfo

:panelinfos
	panelinfo 1? ( drop 0 'panelinfo ! ; ) drop
	1 'panelinfo !
	|...........REHACER DEBUG
	recompilar
	;

:drawpanelinfo
	panelinfo 0? ( drop ; ) drop
	sw 2/ gc.right
	$3f alpha azul gc.fbox $ff alpha
	home cr blanco

|	codeprint cr
|	infodic cr
	;

|-------------- panel control
#panelcontrol

:controlon	1 'panelcontrol ! ;
:controloff 0 'panelcontrol ! ;

:drawpanelcontrol
	panelcontrol 0? ( drop ; ) drop
	blanco
	'controle 18 ?key " E-Edit" print | ctrl-E dit
|	'controlh 35 ?key " H-Help" print  | ctrl-H elp
	'controlz 44 ?key " Z-Undo" print

	'controlx 45 ?key " X-Cut" print
	'controlc 46 ?key " C-Copy" print
	'controlv 47 ?key " V-Paste" print

	'controld 32 ?key " D-Def" print

	'controln 49 ?key " N-New" print
|	'controlm 50 ?key " M-Mode" print

	'controla <up>
	'controls <dn>
	;

:showclip
	cr
 	clipboard> clipboard <>? (
		cyan cr
		clipboard ( over <? )( c@+ emit ) drop
		cr
		) drop
	;

|----------------------------
:directrun	savetxt 'ed.nombre run  ;
:compilai86	savetxt "r4/dev/ide/r4-i86opt.txt" run ;

|------------ profile
#cntprofile 0
#tablaprofile

:dumppro
	tablaprofile >r
	cntprofile ( 1? )(
		r@+ drop |"%s " print
		r@+ "%d msec " print
		r@+ "%d call " print
		cr
		1- ) drop
	rdrop
	;

:wordcntok | adr -- adr token cnt
	dup 4+ @ over 12 + @ 20 >> $fff and ;

:printtokenlin | tok cnt --
	( 1? )( 1- swap @+
			" " ,s tokenstr ,print swap
			$1f nand? ( ,nl ) ) 2drop ;

:espal | es recursiva..
	@ " _%w_" ,print ;

:ptoken
	dup $ff and $c =? ( drop dup 8 >> 4 << 'indicepal + pick4 =? ( nip espal ; ) ) drop
	" " ,s tokenstr ,print ;

:printtokenlinX | adr -- adr
	wordcntok 1- swap 4+ swap
	( 1? )( 1- swap @+ ptoken
			swap $1f nand? ( ,nl ) ) 2drop ;

:printword | nro adr -- nro adr
	dup 8 + @ | info
	%1 and? ( drop wordcntok printtokenlin ; ) | variable
	drop
	dup @ ":_%s_ " ,print
	printtokenlinX
	2dup @ 2dup ":%s %d profile_start _%s_ %d profile_end ;" ,print ,nl
	swap 1+ swap ;

:profile
	savetxt
	mark
	fuente tokeniza
	empty 1? ( tokerror empty ; ) drop
	mark
	"^r4/lib/r4-incprofile.txt" ,print ,nl
	'indiceinc ( indiceinc< <? )(
		@+ "^%w" ,print ,nl
		4+ ) drop
	0 indicepal< ( indicepal> 16 - <? )(
		printword
		16 + ) 					| cnt adr
	":_run_ " ,print
	wordcntok 1- swap 4+ swap printtokenlin drop
	'ed.nombre swap ": %d ""%s"" profile_mem _run_ profile_save ;" ,print ,nl
	"mem/profrun.txt" savemem
	empty empty
	"mem/profrun.txt" run
	;

|--- sobre pantalla
:mmemit | adr x xa -- adr x xa
	rot c@+
	13 =? ( drop 1- rot rot sw + ; )
	9 =? ( drop swap $ffffffe0 and $20 + )( drop swap ccw + ) | x adr xa
	rot swap ;

:cursormouse
	xymouse
	pantaini>
	swap cch 2*			| x adr y ya
	( over <? )( cch + rot >>13 2 + rot rot ) 2drop
	swap ccw 2 << ccw +	| adr x xa
	( over <? )( mmemit ) 2drop
	'fuente> ! ;

:dns
	cursormouse
	fuente> '1sel ! ;

:mos
	cursormouse
	fuente> 1sel over <? ( swap )
	'finsel ! 'inisel ! ;

:ups
	cursormouse
	fuente> 1sel over <? ( swap )
	'finsel ! 'inisel ! ;

|---------- manejo de teclado
:teclado
	keypad
	panelcontrol 1? ( drop 'controloff >ctrl< ; ) drop

	[ key toasc modo exec ; ] <visible>
	'kback	<back>		'kdel	<del>
	'karriba <up>		'kabajo	<dn>
	'kder	<ri>		'kizq	<le>
	'khome	<home>		'kend	<end>
	'kpgup	<pgup>		'kpgdn	<pgdn>
	[ modo 'lins =? ( 'lover )( 'lins ) 'modo ! drop  ; ] <ins>
	[ 13 modo exec ; ] <enter>
	[ 9 modo exec ; ] <tab>

	'directrun <f1>
	'recompilar <f2>
	'profile <f3>
	'compilai86 <f10>

	'exit  >esc<
	'controlon <ctrl>
	;

:editor
	0 paper
	'fontdroidsans13 fontm
|	fonti
	clrscr
	rows 2 - 'cntlinea !
	show clrscr
		'dns 'mos 'ups guiMap |------ mouse

		$6600 ink 1 linesfill
		verde dup ":R%d" print

		blanco "dEBUG " printx
        'ed.nombre "[%s] " print
		cntprofile "%d" print

|		cyan "  |F1-RUN|F2-DEBUG|F3-PROFILE|F9-PLAIN|F10-COMP|F11-OCOMP|" printx
		"|F1-RUN|F2-ANALISIS|F3-PROFILE " printr

|------------------------------
		cr chome!
		drawcode

		0 cntlinea gotoxy
		$6600 ink 1 linesfill | linea abajo
		drawpanelcontrol
		drawpanelinfo
|------------------------------
		teclado
		cminiflecha ;

|---- Mantiene estado del editor
:cpy|
    swap ( c@+ $7c <>? )( rot c!+ swap ) drop 0 rot c! ;

|----
:ram
	here	| --- RAM
	dup 'fuente !
	dup 'fuente> !
	dup '$fuente !
	$3ffff +			| 256kb texto
	dup 'clipboard !
	dup 'clipboard> !
	$3fff +				| 16KB
	dup 'undobuffer !
	dup 'undobuffer> !
	$ffff +         	| 64kb
	dup 'textinfo !
	$fff +
	'here  ! | -- FREE
	0 'infomode !
	mark
	;

:cargaestado
	mark
	-1 dup 'errorlin ! 'ed.nlin ! 0 'ed.ncar !

	here dup "debug.err" load
	empty
	over 4+ <? ( 2drop ed.load ; )
	0 swap !
	| -- here  ...vacia antes de usarla
	'ed.nombre cpy|
	'nrolin cpy|
	'nrolin ?numero 1? ( drop nip ) -1 =? ( drop ; ) dup 'errorlin ! 'ed.nlin !
	'nrolin cpy|
	'nrolin ?numero 1? ( drop nip )  'ed.ncar !
	'error swap ( c@+ 1? )( rot c!+ swap ) rot c! drop
	'error 'mensaje strcpy
	;

:cargaprofile
	mark
	here dup "mem/profile.mem" load
	over 4+ <? ( 2drop ; )
	'here !
	@+ 'cntprofile !
	'ed.nombre over =s
	0? ( nip dup "mem/profile.mem" save ; ) drop
	>>0 'tablaprofile !
	;

|----------- principal
:main
	ram
	cargaestado
	cargaprofile
	loadtxt
	editor
	savetxt ;

: mark 4 main ;
