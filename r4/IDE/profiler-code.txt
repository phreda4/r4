| profiler code
| PHREDA 2015
|------------
^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/sort.txt

^r4/lib/fontm.txt
^inc/fntm/droidsans13.txt

^r4/system/mem-ed.txt
^r4/ide/r4-token.txt
^r4/ide/r4-info.txt

#fuente
#cntprofile 0
#tablaprofile
#cntlinea

#lista
#clista
#itable 0

:wordcntok | adr -- adr token cnt
	dup 4+ @ over 12 + @ 20 >> $fff and ;

:printtokenlin | tok cnt --
	( 1? )( 1- swap @+
			" " ,s tokenstr ,print swap
			$1f nand? ( ,nl ) ) 2drop ;

:espal | es recursiva..
	@ " _%w_" ,print ;

:ptoken
	dup $ff and $c =? ( drop dup 8 >> 4 << 'indicepal + pick4 =? ( nip espal ; ) ) drop
	" " ,s tokenstr ,print ;

:printtokenlinX | adr -- adr
	wordcntok 1- swap 4+ swap
	( 1? )( 1- swap @+ ptoken
			swap $1f nand? ( ,nl ) ) 2drop ;

:printword | nro adr -- nro adr
	dup 8 + @ | info
	%1 and? ( drop wordcntok printtokenlin ; ) | variable
	drop
	dup @ ":_%w_ " ,print
	printtokenlinX
	2dup @ 2dup ":%w %d profile_start _%w_ %d profile_end ;" ,print ,nl
	swap 1+ swap ;

:loadtxt | -- cargar texto
	here dup 'fuente !
	'ed.nombre
	load 0 swap c!+ 'here !
	;

:profile
	loadtxt
	fuente tokeniza 1? ( drop ; ) drop
	mark
	"^r4/lib/r4-incprofile.txt" ,print ,nl
	'indiceinc ( indiceinc< <? )(
		@+ "^%w" ,print ,nl
		4+ ) drop
	0 indicepal< ( indicepal> 16 - <? )(
		printword
		16 + ) 					| cnt adr
	":_run_ " ,print
	wordcntok 1- swap 4+ swap printtokenlin drop
	'ed.nombre swap ": %d ""%s"" profile_mem _run_ profile_save ;" ,print ,nl
	"mem/profrun.txt" savemem
	empty
	"mem/profrun.txt" run
	;

:cargaprofile
	here dup "mem/profile.mem" load
	over 4+ <? ( 2drop ; )
	'here !
	@+ 'cntprofile !
	'ed.nombre over =s
	0? ( nip dup dup "mem/profile.mem" save 'cntprofile ! ; ) drop
	>>0 'tablaprofile !
	;

|----
:+lista | adr --
	dup 4+ @+ 16 << swap @ 0? ( 1+ ) / neg
	, , ;

:calclist
	here 'lista !
	tablaprofile >r
	indicepal< ( indicepal> 16 - <? )(
		dup 8 + @ %1 nand? ( r +lista over r!+ 8 r+ ) drop
		16 + )
	drop rdrop
	here lista - 3 >> 1+ 'clista !
	clista lista shellsort
	;


:printline
	@+ @ " %w" print
	20 col @+ "%d" 8 cntprintr
	30 col @ "%d" 8 cntprintr
	40 col neg "%f" 14 cntprintr
	cr ;

:drawtable
	verde
	" Word" print
	20 col "  Msec" print
	30 col "  Calls" print
	40 col "  MS/Calls" print
	cr
	blanco
	lista >r
	0 ( cntlinea <? )( 1+
		clista <? ( r@+ r@+ printline )
		) drop
	rdrop ;

:screenprofile
	loadtxt
	fuente tokeniza 1? ( drop ; ) drop
	calclist
	0 paper
	'fontdroidsans13 fontm
	clrscr
	rows 3 - 'cntlinea !
	4
	show clrscr
		$6600 ink 1 linesfill
		verde dup ":R%d" print
		blanco "pROFILER " printx
        'ed.nombre "[%s] " print
		"|F1-RERUN " printr cr

        drawtable

		'profile <f1>
		'exit >esc<

		cminiflecha ;


:main
	mark
	ed.load
	cargaprofile
	cntprofile
	0? ( profile ) drop
	screenprofile
	;

: main ;