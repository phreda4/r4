| Navegador de Colores
| PHREDA 2006
|---------------------------------------
^r4/lib/gui.txt
^r4/lib/color.txt
^r4/lib/ricons.txt

^inc/ric/fontawesome.ric

^r4/lib/trace.txt

|--- precalculo
#:coloractual

#vhue #yluma 64 #xluma 8
#alp
#colorrampa $ff0000

|---- Dialogo Color
:setcol | color --
	alp 24 << or
	'coloractual ! ;

:vhue! | nro --
	dup 'vhue !
	8 << 1.0 1.0 hsv2rgb 'colorrampa !
	tx1 xluma + ty1 yluma + getpixel setcol ;

:xyluma! | x y --
	ty1 64 + <? ( ty1 64 + nip )
	ty1 255 + 64 + >? ( ty1 255 + 64 + nip )
	swap
	tx1 8 + <? ( tx1 8 + nip )
	tx1 255 + 8 + >? ( tx1 255 + 8 + nip )
	swap
	2dup
	getpixel setcol
	ty1 - 'yluma ! tx1 - 'xluma ! ;

:lumagrid
	tx1 8 + ty1 64 +
	256 ( 1? )( 1-
		dup 0 colorrampa lerpcol
		over dup 8 << dup 8 << or or
		degrade32!
		>r 2dup setxy
		255 ( 1? )( 1- deg@+ px!+ ) drop
		1+ r> ) drop
	setxy 255 ( 1? )( 1- 0 px!+ ) drop

	gc.push
	tx1 8 + ty1 64 + 255 dup gc.xywh
    [ xymouse xyluma! ; ] guiMove
	gc.pop
	blanco
	tx1 xluma + ty1 yluma + pos
	6 dup dim
	gc.box
	;

:cromaline
	tx1 270 + ty1 64 + vhue +
	over 32 + over op line

	tx1 278 + ty1 64 + | ini croma
	256 ( 1? )( 1-
		255 over - 8 <<  1.0 1.0 hsv2rgb ink
		pick2 pick2 over 16 + over op line
		swap 1+ swap ) drop
	2drop

	gc.push
	tx1 270 + ty1 64 + 32 256 gc.xywh | x y w h
    [ xymouse nip ty1 64 + - vhue! ; ] guiMove
	gc.pop
	;

:setvxy | color -- ; no anda
	rgb2hsv
	neg 2/ $7f and 64 + 'yluma !
	$ff >? ( $ff nip )
	2/ $7f and 8 + 'xluma !
	$ff $600 */
	dup 8 << 1.0 1.0 hsv2rgb 'colorrampa !
	2/ 'vhue ! ;

|---------------------------------------------------------
::dlgcolora | --
	show clrscr
		0 0 fpos 0.9 0.9 fdim
		$f0f7f ink gc.fbox
		$ff ink gc.box
		home
        lumagrid
		cromaline

		48 qdim verde
		tx2 32 - ty1 32 + pos
        'exit dup >esc< btn
		blanco 'i.ok drawric

		tx1 32 + ty1 32 + pos
		coloractual ink gc.fbox

		cminimano ;
