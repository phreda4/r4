| bbuffer - convex polyfill
| PHREDA 2016
|-------------------------
#bb
#bbw #bbh
#bcnt

#segs	| mem polys
#px #py
#ymin #ymax

::bo! ink ;

::bb.clear
	bb >r bcnt ( 1? )( 1- -1 r!+ ) drop rdrop ;

::bb.ini | w h --
	swap dup $1f and 1? ( $20 nip ) + 5 >> | h w/32
	dup 'bbw !
	over * dup 'bcnt !	| h
	here dup 'bb !
	swap 2 << +
	dup 'segs !
	over 3 << + 'here !
	dup 1- 'ymax ! 'bbh !
	bb.clear
	;

:bb.adr | x y -- m d
	1 pick2 $1f and << rot 5 >> rot bbw * + 2 << bb + ;

::bb.get | x y -- b
	bb.adr @ and ;

::bb.set | x y --
	bb.adr dup @ rot not and swap ! ;

:gmask1
	$1f and -2 swap << not ;

:gmask2
	$1f and -1 swap << ;

|----------------------
:rgetline | x1 y1 x2 y2 a -- b
	pick3 bbw * + 2 << bb + >r
	rot - | x1 x2 cnt
	rot gmask2 rot gmask1 and
	swap 1+	| mask cnt
	( 1? )( 1-
		r @ pick2 and? ( 3drop rdrop 1 ; ) drop
		bbw 2 << r+
		) 2drop
	rdrop 0 ;

::bb.occ | x1 y1 x2 y2 -- b
	pick3 5 >>
	pick2 5 >> =? ( rgetline ; )
	pick3 bbw * + 2 << bb + >r
	rot - 							| x1 x2 cnty
	over 5 >> pick3 5 >> - 1- swap	| x1 x2 cntx cnty
	2swap gmask1 swap gmask2
	2swap 1+						| maska maskb cntx cnty
	( 1? )( 1- r @+
		pick4 and? ( 4drop 2drop rdrop 1 ; ) drop	| maska maskb cntx cnty adr
		pick2 ( 1? )( 1- swap
			@+ -1 <>? ( 4drop 3drop rdrop 1 ; ) drop
			swap ) drop
		@ pick4 and? ( 4drop drop rdrop 1 ; ) drop
		bbw 2 << r+
		) 4drop
	rdrop 0 ;

|------------------------------------
::bb! | x y --
	2dup bb.adr swap over @
	nand? ( 4drop ; )
	not over @ and swap !
	setxy ink@ px!+ ;

:proc8 | mask -- mask
	$ff nand? ( 8 px+! ; )	| todos 0
	$01 and? ( ink@ px!+ )( 1 px+! )
	$02 and? ( ink@ px!+ )( 1 px+! )
	$04 and? ( ink@ px!+ )( 1 px+! )
	$08 and? ( ink@ px!+ )( 1 px+! )
	$10 and? ( ink@ px!+ )( 1 px+! )
	$20 and? ( ink@ px!+ )( 1 px+! )
	$40 and? ( ink@ px!+ )( 1 px+! )
	$80 and? ( ink@ px!+ )( 1 px+! )
	;

:oneword | nro --
	0? ( 32 px+! drop ; )
	-1 =? ( 32 ( 1? )( ink@ px!+ 1- ) 2drop ; )
	proc8 8 >> proc8 8 >> proc8 8 >> proc8 drop ;

:maskfpxl | x1 y x2 --
	pick2 $ffffffe0 and pick2 setxy 		| x y x2
	pick2 5 >> rot bbw * + 2 << bb + >r	| x x2
	gmask1 swap gmask2 and
	r @ 2dup and oneword swap not and r> ! ;

::maskfpx | x y x --
	dup 5 >> pick3 5 >> -
	0? ( drop maskfpxl ; ) 1- >r				| x1 y x2 r:cntx
	pick2 $ffffffe0 and pick2 setxy 		| x y x2   r:cntx
	pick2 5 >> rot bbw * + 2 << bb + 			| x1 x2 adr
	r> swap >r                              | x1 x2 cntx
	rot gmask2 r @ 2dup and oneword swap not and r!+			| x2 cntx
	( 1? )( r @ oneword 0 r!+ 1- ) drop
	gmask1 r @ 2dup and oneword swap not and r> !
	;

::bbw! | x1 y x2 --
	dup 5 >> pick3 5 >> -
	0? ( drop maskfpxl ; ) 1- >r				| x1 y x2 r:cntx
	pick2 $ffffffe0 and pick2 setxy 		| x y x2   r:cntx
	pick2 5 >> rot bbw * + 2 << bb + 			| x1 x2 adr
	r> swap >r                              | x1 x2 cntx
	rot gmask2 r @ 2dup and oneword swap not and r!+			| x2 cntx
	( 1? )( r @ oneword 0 r!+ 1- ) drop
	gmask1 r @ 2dup and oneword swap not and r> !
	;

::bbh! | x y1 y2 --
	pick2 pick2 setxy 		| x y1 y2
	pick2 5 >> pick2 bbw * + 2 << bb + >r	| x y1 y2
	swap - 1 rot $1f and << | cnty mask
	swap 1+
	( 1? )( 1- | mask cnty
		r @ pick2 and? ( ink@ px!+ )( 1 px+! )
		pick2 not and r!+
		sw 1- px+!
		bbw 1- 2 << r+
		) 2drop rdrop ;


:maskfpxyl | x1 y1 x2 y2 --
	pick3 $ffffffe0 and pick3 setxy
	pick3 5 >> pick3 bbw * + 2 << bb + >r
	rot - 						| x1 x2 cnty
	rot gmask2 rot gmask1 and	| cntx mask
	swap 1+ ( 1? )(
		r @ pick2 over and oneword pick2 not and r!+
		sw 32 - px+!
		bbw 1- 2 << r+
		1- ) 2drop rdrop ;

::maskfpxy | x1 y1 x2 y2 --
	over 5 >> pick4 5 >> -
	0? ( drop maskfpxyl ; ) 1- >r	| x1 y1 x2 y2 r:cntx
	pick3 $ffffffe0 and pick3 setxy
	pick3 5 >> pick3 bbw * + 2 << bb +
	r> swap >r >r					| x1 y1 x2 y2   r:cntx adr
	rot -							| x1 x2 cnty
	rot gmask2 rot gmask1 			| cnty m1 m2
	swap rot
	r> swap	1+			| m2 m1 cntx cnty
	( 1? )(
		pick2 r @ 2dup and oneword swap not and r!+			| x2 cntx
		over ( 1? )( r @ oneword 0 r!+ 1- ) drop
		pick3 r @ 2dup and oneword swap not and r!+			| x2 cntx
		bbw pick2 2 + - 2 << r+
		sw pick2 2 + 5 << - px+!
		1- ) 4drop rdrop ;


|--- convex polygon
#dx #dy #sx #sy

::bop | x y --
	ymin 3 << segs + >r
	ymax ymin - 1+ ( 1? )( 1- $7fffffff r!+ -1 r!+ ) drop rdrop
	dup 'ymin ! dup 'ymax !
	over dup pick2 3 << segs + !+ !
	'py ! 'px !
	;

::bop2 | x y --
	ymin <? ( dup 'ymin ! )
 	ymax >? ( dup 'ymax ! )
	dup 'py !
	3 << segs + >r
	r@+ <? ( dup r 4 - ! )
	r@+ >? ( dup r 4 - ! )
	rdrop
	'px ! ;

:bdot | x y --
	3 << segs + >r
	r@+ <? ( dup r 4 - ! )
	r@+ >? ( dup r 4 - ! )
	drop rdrop ;

:bline2
	dy dup neg 2/ swap
	( 1? )( 1- >r
		pick2 pick2 bdot
		dx + swap sy + swap
		dx neg >? ( dy - rot sx + rot rot )
		r> ) 2drop
	bdot ;

::bline | x y --
	py over - 0? ( drop over 'px ! bdot ; )
	sign 'sy ! abs 'dy !
	px pick2 -
	sign 'sx ! abs 'dx !
	ymin <? ( dup 'ymin ! )
 	ymax >? ( dup 'ymax ! )
	2dup 'py ! 'px !
	dx dy <=? ( drop bline2 ; )
	dup 2/ swap | err n
	2over bdot
	1- ( 1? )( 1- >r
		dy - rot sx + rot rot
		dy <? ( dx +
				pick2 sx - pick2 bdot
				swap sy + swap
				pick2 pick2 bdot
				)
		r> ) 2drop
	bdot ;

::bpoly
	ymax
	ymin dup 3 << segs + >r
	( over <=? )(
		r@+ over r@+ maskfpx
		1+ ) 2drop rdrop ;

