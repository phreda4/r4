| VESPRITE   Sprites Vectoriales (v3)
| PHREDA 2013
|-------------------------------------------
^r4/lib/gui.txt
^r4/lib/3dmat.txt

|^r4/lib/parse.txt

#:paltex
|--------- formato vesprite
#yp #xp
:finpoli xp yp pline $80000000 'xp ! poli ;

|--------??????
:a0 drop ; 						| el valor no puede ser 0
:a1 8 >> ink ; 					| color0
:a2 swap >r gc>xy r@+ gc>xy 	| xc yc xm ym ; centro y matriz
	>r neg pick2 + r> neg pick2 +
	1.0 pick2 dup * pick2 dup * +
	1? ( / )( drop ) >r 		| xc yc xm ym d
	swap neg r 16 *>> swap r> 16 *>>
	fmat fcen r> ;
:a3 gc>xy fcen @+ gc>xy fmat ;	| sirve esto???
|-------- poligono
:a4 xp $80000000 <>? ( yp pline )( drop )
	gc>xy 2dup 'yp !+ ! op ;  | punto
:a5 gc>xy pline ; | linea
:a6 swap >r gc>xy r@+ gc>xy pcurve r> ;  | curva
:a7 swap >r gc>xy r@+ gc>xy r@+ gc>xy pcurve3 r> ; | curva3
|-------- linea
:a8 gc>xy op ; | punto de trazo
:a9 gc>xy line ; | linea
:aa swap >r gc>xy r@+ gc>xy curve r> ;  | curva
:ab swap >r gc>xy r@+ gc>xy r@+ gc>xy curve3 r> ; | curva3
|-------- pintado de poligonos
:ac 8 >> ink sfill finpoli ; 			| solido
:ad 8 >> ink@ fcol lfill finpoli ; 	| ldegrade
:ae 8 >> ink@ fcol rfill finpoli ; 	| rdegrade
:af 8 >> 2 << paltex + @ tfill finpoli ; 	| tdegrade

#jves a0 a1 a2 a3 a4 a5 a6 a7 a8 a9 aa ab ac ad ae af

::vesprite | 'rf --
	$80000000 'xp !
	( @+ 1? )( dup $f and 2 << 'jves + @ exec ) 2drop ;

|--------- R vesprite
#cosa #sina | para rotar
:r>xy
	d>xy over sina * over cosa * + 16 >> h * 14 >> yc + >r
	swap cosa * swap sina * - 16 >> w * 14 >> xc + r> ;

|--------??????
:a2 swap >r r>xy r@+ r>xy 	| xc yc xm ym ; centro y matriz
	>r neg pick2 + r> neg pick2 +
	1.0 pick2 dup * pick2 dup * +
	1? ( / )( drop ) >r 		| xc yc xm ym d
	swap neg r 16 *>> swap r> 16 *>>
	fmat fcen r> ;
:a3 r>xy fcen @+ r>xy fmat ;	| sirve esto???
|-------- poligono
:a4 xp $80000000 <>? ( yp pline )( drop )
	r>xy 2dup 'yp !+ ! op ;  | punto
:a5 r>xy pline ; | linea
:a6 swap >r r>xy r@+ r>xy pcurve r> ;  | curva
:a7 swap >r r>xy r@+ r>xy r@+ r>xy pcurve3 r> ; | curva3
|-------- linea
:a8 r>xy op ; | punto de trazo
:a9 r>xy line ; | linea
:aa swap >r r>xy r@+ r>xy curve r> ;  | curva
:ab swap >r r>xy r@+ r>xy r@+ r>xy curve3 r> ; | curva3

#jves a0 a1 a2 a3 a4 a5 a6 a7 a8 a9 aa ab ac ad ae af

::rvesprite | adr ang --
	dup cos 'cosa ! sin 'sina !
	$80000000 'xp !
	( @+ 1? )( dup $f and 2 << 'jves + @ exec ) 2drop ;

|--------- 3d vesprite
| 3 << porque usa 14 bits a 17 bits queda 1.0
:3d>xy
	dup  18 >> 3 << swap 14 << 18 >> 3 << 0 project3d ;

|--------??????
:a2 swap >r 3d>xy r@+ 3d>xy 	| xc yc xm ym ; centro y matriz
	>r neg pick2 + r> neg pick2 +
	1.0 pick2 dup * pick2 dup * +
	1? ( / )( drop ) >r 		| xc yc xm ym d
	swap neg r 16 *>> swap r> 16 *>>
	fmat fcen r> ;
:a3 3d>xy fcen @+ 3d>xy fmat ;	| sirve esto???
|-------- poligono
:a4 xp $80000000 <>? ( yp pline )( drop )
	3d>xy 2dup 'yp !+ ! op ;  | punto
:a5 3d>xy pline ; | linea
:a6 swap >r 3d>xy r@+ 3d>xy pcurve r> ;  | curva
:a7 swap >r 3d>xy r@+ 3d>xy r@+ 3d>xy pcurve3 r> ; | curva3
|-------- linea
:a8 3d>xy op ; | punto de trazo
:a9 3d>xy line ; | linea
:aa swap >r 3d>xy r@+ 3d>xy curve r> ;  | curva
:ab swap >r 3d>xy r@+ 3d>xy r@+ 3d>xy curve3 r> ; | curva3

#jves a0 a1 a2 a3 a4 a5 a6 a7 a8 a9 aa ab ac ad ae af

::3dvesprite | adr --
	$80000000 'xp !
	( @+ 1? )( dup $f and 2 << 'jves + @ exec ) 2drop ;

|--------------------------------------------------------------
:dumprf | 'rf
	$80000000 'xp !
	( @+ 1? )( "%h " allowcr print ) 2drop
	cr
	;
