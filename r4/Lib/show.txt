| show.txt
| :r4 PHREDA 2005
|-------------------------
^r4/lib/math.txt
^r4/lib/keyboard.txt

|--- pantalla&interaccion
#:lastime
#startime

#:.exit
#:foconow

::show60
	msec dup 'lastime !+ ! 0 '.exit !
	( .exit 0? )( drop
		10 update drop
		r@ exec
		redraw )
	drop r> drop 0 '.exit ! 0 key! ;

::show30
	msec dup 'lastime !+ ! 0 '.exit !
	( .exit 0? )( drop
		20 update drop
		r@ exec
		redraw )
	drop r> drop 0 '.exit ! 0 key! ;

::show0 | ; maximo framerate
	msec dup 'lastime !+ ! 0 '.exit !
	( .exit 0? )( update drop
		r@ exec
		redraw )
	drop r> drop 0 '.exit ! 0 key! ;


#lastgui 0

::show |
	msec dup 'lastime !+ ! 0 '.exit !
	-1 dup 'foconow ! 'lastgui !
	( .exit 0? )( drop
		10 update drop
		r@ exec
		lastgui 1? ( redraw )( 1 'lastgui ! -1 'foconow ! ) drop )
	drop r> drop
	0 dup dup 'lastgui ! '.exit ! key! ;

::waitkey | -- ; Espera una tecla
	redraw show [ -1 '.exit ! ; ] >esc< ;

|---- tiempo

::.restart
	msec 'startime ! ;
::.segs | seg --
	1000 *
::.mseg | seg --  R:X?
	msec startime - >? ( r> drop ) drop ;

|--- genera

::blink | -- 0/1
	msec $100 and ;

::late | amp -- n
	msec $1ff and 7 << *. ;

::late2 | amp -- n
	msec $1fff and 7 << *. ;

::pinpon | amp -- n
	msec $3ff and $1ff >? ( $3ff swap - ) 7 << *. ;

::pinpon2 | amp -- n
	msec $3fff and $1ff >? ( $3ff swap - ) 7 << *. ;

|--- reemplazo de show
| el unico lugar que usa R?
::display | 'vector --
	0 '.exit !
	-1 'foconow !
	0 ( drop
		10 update drop
		dup exec
		redraw
		.exit 1? )
	2drop
	-1 'foconow !
	0 update '.exit ! |	0 key!
	;