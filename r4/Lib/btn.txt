|--------------------
| BTN.TXT PHREDA 2010
|--------------------
^r4/lib/gui.txt
^r4/lib/grpix.txt

^r4/lib/font.txt
^r4/lib/fontt.txt

^r4/lib/sprite.txt

^r4/lib/parse.txt

|--- BOTONES
:botonsimplef | --
	ink@ $444444 over
	[ swap 4+gc ; ] guiI
	vbtn ink ;

:botonsimple
	ink@ $444444 over
	[ swap ; ]	guiI
	vbtn ink ;

:botonbox2 | --
	ink@ $444444
	[ 2+gc ; ] [ 2-gc swap ; ] guiIO
	vbtn ;

:botonsimpleh | --
	ink@ $444444 over
	[ swap ; ] guiI
	hbtn ink ;

:botonsimplee | --
	[ ink@ $444444 over vbtn ink ; ] guiI ;

|-------- teclado para boton
:bordeyfoco
    bordefoco
:keys-btn | 'acc xx --  ; boton con foco
	dup >r <enter> r>
	'ktab <tab>
	'nextfoco dup <dn> <ri>
	'prevfoco dup <up> <le>
	;

::.btnt | acc "txt" --
	gc.push
	ccw 'ccx +!
	print2gc
	ccw 2* 'w +!
	cch 2/ 'h +!
	botonsimple
	ink@ >r blanco printx r> ink
	'bordeyfoco in/foco
	guiBtn
	ccw 'ccx +!
	gc.pop
	;


::.link | acc "txt" --
	gc.push
	ccw 2/ 'ccx +!
	print2gc
	ccw 2/ 'w +!
	botonsimple
	ink@ >r blanco printx r> ink
	'bordeyfoco in/foco
	guiBtn
	ccw 2/ 'ccx +!
	gc.pop
	;

::.btnd | acc 'v8d --
	gc.push
	botonsimple
	ink@ >r blanco v8draw r> ink
	'bordeyfoco in/foco
	guiBtn
	gc.pop
	;

::cmenu | 'vec "label" cnt --
	gc.push
	tx2 tx1 - ccw pick2 * dup 2/ + - 2/ tx1 + 'ccx !
	cntprintbox drop
	ccw 2* 'w +!
	cch 'h +!
	botonsimple
	ink@ >r blanco printc r> ink
	'bordeyfoco in/foco
	guiBtn
	ccw 'ccx +!
	gc.pop
	;

::btnt | 'event "texto" --
	gc.push
	ccw 2/ 'ccx +!
	print2gc
	ccw 'w +!
	cch 2/ 'h +!
	botonsimple
	ink@ >r blanco printx r> ink
	guiBtn
	ccw 2/ 'ccx +!
	gc.pop
	;

::link | acc "txt" --
	gc.push
	ccw 2/ 'ccx +!
	print2gc
	ccw 2/ 'w +!
	botonsimple
	ink@ >r blanco printx r> ink
	guiBtn
	ccw 2/ 'ccx +!
	gc.pop
	;

::ilink | acc 'ico --
	gc.push
	dup
	@ dup $ff and dup 2/ ccx + 'xc ! 'w !
	8 >> $ff and dup 2/ ccy + 'yc ! 'h !
	botonsimplee
	drawico
	guiBtn
	gc.pop
	;

:gcxy+!
	dup 'ccx +! 'ccy +! ;

::ibtn | acc 'ico --
	gc.push
	dup
	@ dup $ff and 4+ dup 2/ ccx + 'xc ! 'w !
	8 >> $ff and 4+ dup 2/ ccy + 'yc ! 'h !
	gc.frod
	2 [ 1+ ; ] guiI gcxy+!
	0 swap drawcico  | negro
	-2 [ 1- ; ] guiI gcxy+! 
	guiBtn
	6 'ccx +!
	gc.pop
	;

|#accd 'icofi 'afirs 'icopr 'aprev 'icopl 'aplay 0
::icotool | 'list --
	>r ( r@+ 1? )(
		r@+ swap ibtn
		) drop rdrop ;

|#modosd 'icofi 'icopr 'icopl 'icofi 0
::icomode | 'mode 'list --
	>r 0 (
		over @ =? ( cyan oscuro )( negro )
		r@+ 1? )( | modo act ico
		[ dup pick2 ! ; ] swap
		ibtn
		1+ ) 3drop rdrop ;

::btntf | 'event "texto" cnt --
	gc.push
	cntprintbox drop ccx w + ccw +  >r
	ccw 'w +!
	cch 2/ 'h +!
	botonsimple
	ink@ >r blanco sp print r> ink
	guiBtn
	r> 'ccx !
	gc.pop
	;

::btnpx | 'event px py --
	gc.push
	ccy over 2/ + 'yc ! 'h !
	ccx over 2/ + 'xc ! 'w !
	botonsimple
	guiBtn
	gc.pop ;

::btntfe | 'event "texto" cnt --
	gc.push
	cntprintbox drop ccx w + ccw +  >r
	ccw 'w +!
	cch 2/ 'h +!
	botonsimplee
	ink@ >r blanco sp print r> ink
	guiBtn
	r> 'ccx !
	gc.pop
	;

::btnd | 'event 'dibujo --
	gc.push
	botonsimple
	ink@ swap blanco v8draw ink
	guiBtn
	gc.pop
	;

::btn | 'event --
	gc.push botonsimple	guiBtn gc.pop ;

::btne | 'event --
	gc.push botonbox2 guiBtn gc.pop ;

::btnbox | 'event alto --
	gc.push
	dup 2/ dup
	ccx + 'xc !
	ccy + 'yc !
	dup 'w ! 'h !
	gc.fbox
	guiBtn
	gc.pop
	;


::btnm | 'vec "label" cnt --
	gc.push
	tx2 tx1 - ccw pick2 * dup 2/ + - 2/ tx1 + 'ccx !
	cntprintbox drop
	ccw 'w +!
	cch 2/ 'h +!
	botonsimple
	ink@ >r blanco printc r> ink
|	'bordefoco in/foco
	guiBtn
	ccw 'ccx +!
	gc.pop
	;

::btnv | 'event 'dibujo -- | dibujo
	gc.push
	cch dup 2* dup 'w ! 'h !
	2/ dup ccx + 'xc !
	ccy + 'yc !
	botonsimple
	ink@ swap blanco v8draw ink
	guiBtn
	cch dup 2/ + 'ccx +!
	gc.pop
	;

:btnv | 'event 'dibujo -- | dibujo
	gc.push
	cch dup 2* dup 'w ! 'h !
	2/ dup ccx + 'xc !
	ccy + 'yc !
	botonsimple
	ink@ swap blanco v8draw ink
	guiBtn
	cch dup 2/ + 'ccx +!
	gc.pop
	;

::btnve | var dibujos cnt --
	gc.push
	cch dup 2* dup 'w ! 'h !
	2/ dup ccx + 'xc !
	ccy + 'yc ! gris
	0 ( over <? )(
		pick3 @ =? ( 4+gc blanco )
		rot @+ v8draw rot rot
		pick3 @ =? ( 4-gc gris )
		[ dup pick4 ! ; ] guiBtn
		cch dup 2/ + 'xc +!
		1+ ) 4drop
	xc 'ccx !
	gc.pop
	;


::btnvs | var dibujos cnt --
	gc.push
	cch dup 2* dup 'w ! 'h !
	2/ dup ccx + 'xc !
	ccy + 'yc ! gris
	0 ( over <? )(
		pick3 @ =? ( 8+gc ccw neg 'yc +! blanco )
		rot @+ nsprite rot rot
		pick3 @ =? ( 8-gc ccw 'yc +! gris )
		[ dup pick4 ! ; ] guiBtn
		cch dup 2/ + 'xc +!
		1+ ) 4drop
	xc cch 2/ - 'ccx !
	gc.pop
	;

|---------------------- Mas botones
::btnex | 'acc "leyenda" "titulo" --
	gc.push
	ink@ dup >r $222222
	[ swap ; ] [ 0.95 fzoom ; ] guiIO
	vbtn
	gc2win home
	font-vard-12-bold-out
	cr2 printc cr cr2
	font-vard-8

	blanco linefill
	1 col negro print

	guiBtn
	r> ink
	gc.pop
	;

::btnee | 'acc DB --
	gc.push
	ink@ dup >r $222222
	[ swap ; ] [ 0.95 fzoom ; ] guiIO
	vbtn
	gc2win home
	font-vard-12-quad-out
	cr2 linefill
	dup "%a" printc cr cr2
	font-vard-8-bold-out
	>>f
	( dup c@ 1? )( drop
|		blanco linefill negro
		1 col dup "* %a" print >>s cr
		) 2drop
	gc.pop
	guiBtn
	r> ink ;

|------------- lista de elecciones
#focovar
#focopar1

#col1

:keys-choice | 'var xx --  ; boton con foco
	over 'focovar !
	pick2 lstcnt 'focopar1 !
	0 <enter>
	0 <visible>
	0 <ins> 0 <back> 0 <del>
	[ focovar @ 1+ focopar1 >=? ( 0 nip ) focovar ! ; ] <spc>
	'ktab <tab>
	'nextfoco <dn> 'prevfoco <up>
	;

:draw-choice
	$cccc00 'col1 ! ;

::choice | lista 'var --
	$cc00 'col1 !
	'draw-choice 'keys-choice w/foco
|	@ lstget "%a" print ;
    dup @ rot 0 swap ( dup c@ 1? )( drop
		dup " %a " mprint print2gc
		>r >r
		[ dup pick3 ! ; ] guiBtn
		over =? ( col1 $222222 vbtn ) 1+
		r> r>
		blanco printx >>f ) 4drop
	drop
	;

|--------- radio btn
::rbtn | lista 'var --
	$cc00 'col1 !
	'draw-choice 'keys-choice w/foco
	2dup @ lstget
	"%a" mprint print2gc
	col1 $222222 vbtn | lista 'var sop
	[ over @ 1+ pick3 lstcnt >=? ( 0 nip ) pick2 ! ; ] guiBtn
	blanco printx
	2drop ;

|------- check btn
::btncheck | 'var "text" --
	gc.push
	ccw 2/ 'ccx +!
	print2gc
	ccw 2/ 'w +!
	over @ 0? ( rojo )( verde ) drop
	botonsimple
	blanco printx
	[ dup @ 1 xor over ! ; ] guiBtn
	ccw 2/ 'ccx +!
	gc.pop
	drop
	;

|--------------
::menur | 'event "texto" cnt --
	gc.push
	cntprintbox drop ccx w + ccw +  >r
	ink@ >r
	ccw 'w +!
	cch 2/ 'h +!
	[ ink@ $444444 ; ] [ $bbbbbb $222222 2-gc ; ] guiIO
	vbtn
	blanco sp print
	guiBtn
	r> ink r> 'ccx !
	gc.pop
	;

::menuc | 'event "texto" cnt --
	gc.push
	cntprintbox drop ccx w + ccw + >r
	ink@ >r
	ccw 'w +!
	cch 2/ 'h +!
	[ ink@ $444444 ; ] [ $bbbbbb $222222 2-gc ; ] guiIO
	vbtn
	blanco sp printc
	guiBtn
	r> ink r> 'ccx !
	gc.pop
	;

|-------- SLIDES
::hslide | 'var --
	[ xymouse drop xc - 16 << w / over ! ; ] guiMove
	gc.push 0.98 0.2 fscala
	gc.frod gc.popush
	@ w 16 *>> 'xc +!
 	0.1 0.98 fscala ink@
	$dddddd $222222 vbtn
	gc.pop ink
	;

::vslide | 'var --
	[ xymouse nip yc - 16 << h / over ! ; ] guiMove
	gc.push 0.2 0.98 fscala
	gc.frod gc.popush
	@ h 16 *>> 'yc +!
 	0.98 0.1 fscala ink@
	$dddddd $222222 hbtn
	gc.pop ink
	;

|--------------- botonchato fino
::flatbtn | acc "txt" --
	gc.push
	print2gc
	ink@ negro botonsimplef ink
	printx
	guiBtn
	gc.pop ;

::flatbtn2 | acc "txt" colb colt --
	>r >r
	gc.push
	print2gc
	r> ink botonsimplef
	r> ink printx
	guiBtn
	gc.pop ;

::flink | acc "txt" $1212 --
	gc.push
	>r print2gc r>
	[ swap12bits ; ] guiI
	dup ink12 gc.fbox ink21
	printx
	guiBtn
	gc.pop ;

::fprint | "" $fond/frente
	gc.push
	>r print2gc r>
	dup ink12 gc.fbox ink21
	printx
	gc.pop ;
