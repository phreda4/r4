| RICONS Iconos importados de SVG
| PHREDA 2013
|----------------------------------------------------
^r4/lib/3dmat.txt

#yp #xp 
:a0 drop ; | el valor no puede ser 0
:a1 xp $80000000 <>? ( yp pline )( drop )
	gc>xy 2dup 'yp !+ ! op ;  | punto
:a2 gc>xy pline ; | linea
:a3 swap >r gc>xy r@+ gc>xy pcurve r> ;  | curva
:a4 swap >r gc>xy r@+ gc>xy r@+ gc>xy pcurve3 r> ; | curva3

#gfont a0 a1 a2 a3 a4 0 0 0

|*****************
::drawric | 'rf --
	$80000000 'xp !
	( @+ 1? )( dup $7 and 2 << 'gfont + @ exec ) 2drop
	xp $80000000 <>? ( yp pline )( drop )
	poli ;

#cosa #sina | para rotar
:r>xy
	d>xy over sina * over cosa * + 16 >> h * 14 >> yc + >r
	swap cosa * swap sina * - 16 >> w * 14 >> xc + r> ;

:a1 xp $80000000 <>? ( yp pline )( drop )
	r>xy 2dup 'yp !+ ! op ;  | punto
:a2 r>xy pline ; | linea
:a3 swap >r r>xy r@+ r>xy pcurve r> ;  | curva
:a4 swap >r r>xy r@+ r>xy r@+ r>xy pcurve3 r> ; | curva3

#gfont a0 a1 a2 a3 a4 0 0 0

|*****************
::rdrawric | 'rf ang --
	dup cos 'cosa ! sin 'sina !
	$80000000 'xp !
	( @+ 1? )( dup $7 and 2 << 'gfont + @ exec ) 2drop
	xp $80000000 <>? ( yp pline )( drop )
	poli ;

| 3 << porque usa 14 bits a 17 bits queda 1.0
:3d>xy
	dup  18 >> 3 << swap 14 << 18 >> 3 << 0 project3d ;

:a1 xp $80000000 <>? ( yp pline )( drop )
	3d>xy 2dup 'yp !+ ! op ;  | punto
:a2 3d>xy pline ; | linea
:a3 swap >r 3d>xy r@+ 3d>xy pcurve r> ;  | curva
:a4 swap >r 3d>xy r@+ 3d>xy r@+ 3d>xy pcurve3 r> ; | curva3

#gfont a0 a1 a2 a3 a4 0 0 0

|*****************
::3ddrawric | 'rf --
	$80000000 'xp !
	( @+ 1? )( dup $7 and 2 << 'gfont + @ exec ) 2drop
	xp $80000000 <>? ( yp pline )( drop )
	poli ;
