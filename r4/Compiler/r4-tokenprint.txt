| tokenprint
| 2013 PHREDA
|------------------------7
^r4/lib/macrosr4.txt
^r4/Compiler/r4-dicc.txt

|-- color de token

#codec $ffffff $ff0000 $ff0000 $ff00ff $ff00ff $7f7f7f $ffff00 $ffff00 $ffff00 $ffff00 $ffff00 $ffffff $ffffff $ff88ff $ffff $88ff

::tokencolor | a -- a
	dup $ff and
	16 <? ( 2 << 'codec + @ ink ; )
	drop verde ;

|-- token a string

:c1 tok>name ":%w" ;
:c2 tok>name "::%w" ;
:c3 tok>name "#%w" ;
:c4 tok>name "#:%w" ;
:c5 tok>mem "|%s" ;
:c6 tok>mem "^%w" ;
:c7 tok>cte "%d"  ;
:c8 tok>cte "$%h" ;
:c9 tok>cte "%%%b" ; | %% no permite mprint mprint
:cA tok>cte "%f" ;
:cB tok>mem """%s""" ;
:cC tok>name "%w" ;
:cE tok>name "'%w" ;

#codeis 0 c1 c2 c3 c4 c5 c6 c7 c8 c9 cA cB cC cC cE cE

::tokenstr | a -- mem
	dup $ff and 16 <? ( 2 << 'codeis + @ exec ; )
	nip 16 - macroname ;

|-- nombre a nro
:c1 8 >> ":w%h" ;
:c3 8 >> "#w%h" ;
:cC 8 >> "w%h" ;
:cE 8 >> "'w%h" ;

#codeisa 0 c1 c1 c3 c3 c5 c6 c8 c8 c8 c8 cB cC cC cE cE

::tokenstrA | a -- mem
	dup $ff and 16 <? ( 2 << 'codeisa + @ exec ; )
	nip 16 - macroname ;

|-- nombre a nombre

:c1 tok>name ":%w" ;
:c3 tok>name "#%w" ;
:cC tok>name "%w" ;
:cE tok>name "'%w" ;

#codeisa 0 c1 c1 c3 c3 c5 c6 c8 c8 c8 c8 cB cC cC cE cE

::tokenstrN | a -- mem
	dup $ff and 16 <? ( 2 << 'codeisa + @ exec ; )
	nip 16 - macroname ;

|------------------------------------------------
:cpycad | adr --
	( c@+ 1? )( 34 =? ( dup emit ) emit ) 2drop ;

:coldefw
	cr ":" print tok>name printx sp ;
:coldefv
	cr "#" print tok>name printx sp ;
:coldefwe
	cr "::" print tok>name printx sp ;
:coldefve
	cr "#:" print tok>name printx sp ;

:colcom
	"|" print tok>mem printx cr ;

:colinc
	"^" print tok>mem printx cr ;

:colitd tok>cte "%d " print ;
:colith tok>cte "$%h " print ;
:colitb tok>cte "%%%b " print ;
:colitf tok>cte "%f " print ;

:colits	34 emit tok>mem cpycad 34 emit sp ;

:colwor
:colvar
	tok>name printx sp ;
:coldwo
:coldva
	"'" print tok>name printx sp ;

:nada
	drop ;

#codei nada coldefw coldefwe coldefv coldefve colcom colinc colitd colith colitb colitf colits colwor colvar coldwo coldva

::drawtoken | token --
	dup $ff and
	16 <? ( 2 << 'codei + @ exec ; ) nip
	16 - macroname print sp ;

|--------------------------------------------------
::drawword | nro --
	dup dic>tok @
	swap dic>len@
	( 1? )( 1- >r
		@+ drawtoken
		r> )
	2drop ;

