| r4-asmdata.txt - PHREDA 2010,2017
| Generacion de data
|--------------------------------------------
^r4/Compiler/r4-post.txt

:nro>dicn 8 >> "w%h" mprint ;			| numero de palabra

|----- pila de compilador
#pilac )( 256	#pilac> 'pilac

:>pilac pilac> ! 4 'pilac> +! ;
:<pilac -4 'pilac> +! pilac> @ ;
:pilac! 'pilac 'pilac> ! ;

|----- nro de str y label
#nstr	| nro de string

|------------ compila DATO
#d1 "dd "
#d2 "dw "
#d3	"db "
#d4 "rb "
#dtipo 'd1
#dini 0
#dcnt 0
#instr 0

:pasoinstr	| cuando hay un string en otro tipo
	,cr dtipo ,s 0 'dini ! 0 'instr ! ;

:stringdd | cuando hay string dentro de otro tipo
	dtipo 'd3 =? ( drop "," ,s ; ) drop
	,cr "db " ,s 1 'instr ! ;

:dfin
	instr 1? ( drop pasoinstr ; ) drop
	dini 1? ( dtipo )( "," ) ,s drop 0 'dini ! ;
:dfins
	dini 1? ( "db " )( drop stringdd ; ) ,s drop 0 'dini ! ;
:dfind
	instr 1? ( drop pasoinstr ; ) drop
	dini 1? ( "dd " )( "," ) ,s drop 0 'dini ! ;

:dtipoch dini 0? ( ,cr 1 'dini ! ) drop  ;

:cpycad | adr --
	( c@+ 1? )( 34 =? ( dup ,c ) ,c ) 2drop ;

:stringwith0
	34 ,c here swap cpycad here - 34 ,c drop ",0" ,s ;

:,ddefw
:,ddefv drop ;
:,dlit  1 'dcnt +!
		dfin tok>cte
		99 >? ( "$" ,s ,h ; )
		-9999999 <? ( "$" ,s ,h ; )
		,d
		;

:,dlits	1 'dcnt +! dfins tok>mem |tok>str
	stringwith0 ;
:,dwor	1 'dcnt +! dfind nro>dicn ,s ;
:,d;	drop ;
:,d(	drop dtipo >pilac 'd3 'dtipo ! dtipoch ;
:,d)(	drop 'd4 'dtipo ! dtipoch ;
:,d)	drop <pilac 'dtipo ! dtipoch ;
:,d[	drop dtipo >pilac 'd2 'dtipo ! dtipoch ;
:,d]	drop <pilac 'dtipo ! dtipoch ;

#coded 0
,ddefw ,ddefw ,ddefv ,ddefv 0 0 ,dlit ,dlit ,dlit ,dlit ,dlits ,dwor ,dwor ,dwor ,dwor
,d; ,d( ,d)( ,d) ,d[ ,d]

|---- datos
:compvar | nropal --
	dup dic>str @ "; %w" ,print ,cr
	dup "w%h " ,print
	'd1 'dtipo !
	1 'dini ! 0 'dcnt ! pilac!
	0 'instr !
	dic>tok @ 4+
	( @+ dup $ff and
		5 >? )( 	 | dirv n c
		24 <? ( 2 << 'coded + @ exec )( 2drop )
		,sp ) 3drop
	dini 0? ( drop ,cr ; ) drop
	dcnt 1? ( drop ,cr ; ) drop
	dtipo ,s 0 ,d ,cr ;

:otrostr
	over
	1 'dini !
	nstr "str%h " ,print
	,dlits
	1 'nstr +!
	,cr ;

:compvarw | nropal --
	dup 1- dic>inf @ 1 nand? ( $80 and? ( 2drop ; ) ) drop

	dup dic>tok @ swap dic>len@ 1-
	( 1? )( 1- swap
		@+ dup $ff and
		11 =? ( otrostr ) 2drop
		swap )
	2drop ;

::,generateDATA
	0 'nstr !
	"; *** STRINGS ***" ,s ,cr
	0 ( cntwords <? )(
		dup dic>inf @ 1 nand? ( over compvarw )
		drop 1+ ) drop

	"; *** VARS ***" ,s ,cr
	"align 16 " ,s ,cr
	0 ( cntwords <? )(
		dup dic>inf @ 1 and? ( over compvar )
		drop 1+ ) drop
	;
