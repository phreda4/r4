| atv
| PHREDA 2015
|-----------------------------
^r4/lib/gui.txt
^r4/lib/img.txt

^r4/lib/loadjpg.txt
^r4/lib/loadpng.txt
^r4/lib/loadbmp.txt

^r4/lib/trace.txt

^r4/lib/rfont.txt
^../r4/inc/rft/droidsansbold.rft

#recarga 0

#videos
#videonow
#placas
#placasnow


#tshadow $ffffff


#frases
#frasescnt
#marquesina

#img1

|------------------------------------------------
#amem

:alertamem
	here dup 'amem !
	$fff + 'here !
	;

:eventocheck
	amem "atv/alerta.mem" load
	amem =? ( 0 swap ! ; ) 0 swap !


	;

|------------------- OS DEp ---------------------
:FNAME | adr -- adrname
|WIN|	44 +
|AND|	19 +
	;

:FDIR? | adr -- 1/0
|WIN|	@ $10
|AND|	18 + c@ 4
	=? ( 1 )( 0 ) nip ;

|------------------------------------------------
:addfile | adr --
	dup FDIR? 1? ( 2drop ; ) drop
	FNAME here strcpyl 'here ! ;

:getfilenames | path -- files
	here swap
	ffirst drop fnext drop | quita . y ..
	( fnext 1? )( addfile ) drop
	0 ,c ;

|-----------------------------------------------
:playffm | "" --
	"atv/ffplay -fs -autoexit -loglevel quiet ""atv/videos/%s""" mprint
	system drop ;

:waitvideo
	( -1 system -1 <>? )( drop
		eventocheck
		100 update drop ) drop ;

:waitvideo
	show
		'exit >esc<
		-1 system -1 =? ( exit 0 system drop ) drop ;

:playvideo
	videonow playffm
	videonow >>0 dup c@ 0? ( 2drop videos )( drop ) 'videonow !
 	;

:player
	-1 system -1 <>? ( drop ; ) drop
	playvideo ;

|------------------------------------------------
:sprint | "" --
	mprint
	ccx ccy ink@
	2 2 +atxy pick3
	tshadow ink printx
	ink atxy
	printx ;

:semit | char --
	ccx ccy ink@
	2 2 +atxy pick3
	tshadow ink emit
	ink atxy emit ;

|--------------------------------------
#timestr )( 16

:hms
	10 <? ( "0" ,s ) ,d ":" ,s
	10 <? ( "0" ,s ) ,d ":" ,s
	10 <? ( "0" ,s ) ,d ;

:hmss
	10 <? ( "0" ,s ) ,d
	swap 1 and? ( ":" )( " " ) nip ,s
	10 <? ( "0" ,s ) ,d ;

:reloj
	mark 'timestr 'here !
	time | s m h
	hms
	0 ,c empty
	'timestr sizeprint 8 +
	sw over - 4 - sh cch - 8 -
	sw 4 - sh 4 - gris cajaf
	sw swap - sh cch - 4 - atxy
	negro sprint
	;

|------------------------------------------------
:loadimg | filename -- img
	placasnow
	dup ".jpg" =pos 1? ( drop "atv/placas/%s" mprint loadjpg ; ) drop
	dup ".png" =pos 1? ( drop "atv/placas/%s" mprint loadpng ; ) drop
	dup ".bmp" =pos 1? ( drop "atv/placas/%s" mprint loadbmp ; )
	2drop 0 ;

:nextplaca
	placasnow >>0 dup c@ 0? ( 2drop placas )( drop ) 'placasnow ! ;

#placaimg

:playcartelpre
	( loadimg 0? )( drop nextplaca ) 'placaimg !
	nextplaca ;

:playcartel
	0 0 sw sh placaimg img.drawsize
|	0 0 placaimg img.draw
	>xfb
	;

|----------------------------------------
:fadeout1
	framev >r
	sw sh * ( 1? )( 1-
		r @ 2/ $7f7f7f and r!+ ) drop
	rdrop ;

:fadeout
	8 ( 1? )( 1-
		fadeout1
		30 update drop
		redraw
		) drop ;

|----------------------------------------
:fadein1 | nro -- nro
	xfb
	framev >r
	sw sh * ( 1? )( 1- swap
		@+ pick3 >> $10101 and
		r @ 2* or r!+
		swap ) 2drop
	rdrop ;

:fadein
	cls
	8 ( 1? )( 1-
		fadein1
		30 update drop
		redraw
		) drop ;



|---------------
:info
	-1 system nip
	-1 =? ( "cartel" )( "video" )
	print cr
	;


:reread
	recarga 1? ( empty ) drop
	1 'recarga !
	mark
	"atv/videos/" getfilenames 'videos !
	"atv/placas/" getfilenames 'placas !

	videos 'videonow !
	placas 'placasnow !

	;

:dump
	"videos" print cr
	videos ( dup c@ 1? )( drop
		dup print cr
		>>0 ) 2drop
	cr
	"placas" print cr
	placas ( dup c@ 1? )( drop
		dup print cr
		>>0 ) 2drop
	;

|------------- frases
#frasenow

:>>cr | adr -- adr'	; proximo espacio
	( c@+ 1? )(
		13 =? ( drop ; )
		drop ) drop ;

:loadfrases
	here dup "atv/frases.txt" load 0 swap !+ dup 'frases ! 'here !
	( trim dup c@ 1? )( drop
		dup , >>cr 0 over 1- c!
		) 2drop
	here frases - 2 >> 'frasescnt !
	;

#place )( 8192
#place>
#time>
#autor>

:place!
	$ff and ccx 12 << ccy or 8 << or place> !+ 'place> ! ;

:place2xy | v -- v x y
	dup 8 >> dup 12 >> swap $fff and ;

:emitplace | str car -- str car
	32 <>? ( gemit ; ) gemit
	sizeword ccx + tx2 >? ( cch 'ccy +! tx1 'ccx ! ) drop ;

:placefrase
	0 0 fpos
	0.8 dup fdim
	gc2win
	tx1 'ccx !
	ty1 'ccy !
	'place dup 'place> ! 'time> !
	0 'autor> !
	cr
	frasenow ( c@+ 1? )(
		$28 =? ( cr place> 'autor> ! )
		dup place!
		emitplace
		) 2drop
	autor> 0? ( drop ; )
	tx2 ccx - >r
	( place> <? )(
		dup @ place2xy
		swap r + 12 << or 8 <<
		swap $ff and or
		swap !+ ) drop
	rdrop ;

:runfrase
	'place ( time> <? )(
		@+ place2xy atxy
		$ff and semit
		) drop
	time> place> <? ( 4 'time> +! ) drop
	;

:shuffle
	'place ( place> <? )(
		dup @ place2xy
		swap rand 16 >> $3 and 1- + 12 <<
		swap rand 13 >> $3 and 1- + or 8 <<
		swap $ff and or
		swap !+
		) drop ;

:printfrase
	rojo |frasenow sprint
	runfrase
|	shuffle
	;

:elijefrase
	rand
	$7fffffff frasescnt clz 1- >>
	and
	( frasescnt >=? )( frasescnt - )
	2 << frases + @ 'frasenow !
	placefrase ;


|--------------------------------------
#xm 0
:runmarque
	xm sh cch - atxy
	azul
	marquesina sprint
	verde
	ccx -? ( sw 'xm !  ) drop
	-5 'xm +! ;

:setmarque
	sw 'xm ! ;

|--------------------------------------

:main
	playcartelpre playcartel fadein
	elijefrase
	setmarque
	33
	show xfb>scr
		negro
		amem print cr

		printfrase

		runmarque

		reloj
|		info
|		playvideo
|		player

		[ playcartelpre fadeout playcartel fadein elijefrase ; ] <f1>
		'eventocheck <F2>
		'exit >esc<
		cminiflecha ;


:ini
	mark
	rerand
	loadfrases
	alertamem
	reread
	here dup 'marquesina !
|	"atv/marquesina.txt" load 'here !
 	"hola" ,s 0 ,c
	droidsansbold 80 rfont!
	;

: ini main ;
