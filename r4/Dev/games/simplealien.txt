| simple alien shot
| PHREDA 2011

^r4/lib/gui.txt
^r4/lib/sprite.txt
^r4/dev/games/simple.spr

#xa 1.3 #ya 0.3
#vxa 0.01 #vya 0.0
#xn 0.0 #yn -0.9
#vxn 0.0 #vyn 0.0
#xd 0 #yd -2.0

#puntos 0


:alienborde
	vxa neg 'vxa ! ;

:muevealien
	xa vxa +
	1.4 >? ( alienborde )
	-1.4 <? ( alienborde )
	dup sin 2 >> 0.4 + 'ya !
	'xa !
	;

:muevenave
	xn vxn +
	1.0 >? ( 1.0 nip )
	-1.0 <? ( -1.0 nip )
	'xn ! ;

:dibujanaves
	scr home blanco
	puntos "%d" print
	0.1 dup fdim
	xa ya fpos
	'alien nsprite
	xd yd fpos
	'disparo nsprite
	xn yn fpos
	'nave nsprite
	;

:disparo
	yd -2.0 <>? ( drop ; ) drop
	xn 'xd !
	yn 'yd ! ;

:muevedisparo
	yd -2.0 =? ( drop ; )
	0.02 +
	1.3 >? ( -2.0 nip )
	'yd !
	;

:acierto
	xd xa - abs 0.05 >? ( drop ; ) drop
	yd ya - abs 0.05 >? ( drop ; ) drop
	1 'puntos +!
	1.3 'xa !
	-2.0 'yd !
	;

:teclas
	[ -0.01 'vxn ! ; ] <le>
	[ 0.01 'vxn ! ; ] <ri>
	[ 0 'vxn ! ; ] dup >le< >ri<

	'disparo <spc>
	'exit >esc< ;

:main
	show clrscr
		dibujanaves
		muevealien
		muevenave
		muevedisparo
		acierto
		teclas ;

: main ;



