| pool
| PHREDA 2012
| simple pool
| PHREDA 2012
|--------------------------
^r4/lib/gui.txt
^r4/lib/trace.txt
^r4/lib/part16.txt
^r4/dev/games/pool.spr

#FRICTION	0.982

#power 2.0
#x1 #y1
#x2 #y2

|c m dy dx y x
#balllist 0 0

#xcam #ycam #zcam 50.0

:hitwall | adr --
	dup @ neg swap ! ;

:pball
	mpush
	>b
	b@+	| X
	15.0 >? ( 15.0 b> 4 - ! b> 4+ hitwall ) -15.0 <? ( -15.0 b> 4 - ! b> 4+ hitwall )
	b@+	| Y
	20.0 >? ( 20.0 b> 4 - ! b> 4+ hitwall ) -20.0 <? ( -20.0 b> 4 - ! b> 4+ hitwall )
	0 mtransi
	b@+ b> 12 - +!	| VX
	b> 4 - dup @
|	-? ( neg FRICTION *. neg )( FRICTION *. )
	FRICTION *.
	swap !
	b@+ b> 12 - +!	| VY
	b> 4 - dup @
|	-? ( neg FRICTION *. neg )( FRICTION *. )
	FRICTION *.
	swap !
	b@+ drop		| MASS
	b> @ 4 << $f or 'sball 12 2 << + ! | COLOR
	'sball 3dnsprite
	mpop ;

:+pball | color x y --
	'pball 'balllist p!+ >b
	b!+ b!+
	0 0 b!+ b!+
	1.0 b!+
	b! ;

|------
:everyb | p1 p2 -- p1 p2
	over 4+ @ over 4+ @ - dup *. | (x1-x2)^2
	pick2 8 + @ pick2 8 + @ - dup *. +
	4.0 >=? ( drop ; ) sqrt. 2.0 swap -
	2/ >a
	over 4+ @ over 4+ @ -
	pick2 8 + @ pick2 8 + @ -
	atan2 sincos swap				| p1 p2 si co
	dup a> *. pick4 4+ +!
	dup a> *. neg pick3 4+ +!
	over a> *. pick4 8 + +!
	over a> *. neg pick3 8 + +!

	dup a> *. pick4 12 + +!
	dup a> *. neg pick3 12 + +!
	over a> *. pick4 16 + +!
	over a> *. neg pick3 16 + +!

	2drop
	;
|------
:hitmove
	rojo x1 y1 op xymouse 2dup line
	'y2 ! 'x2 !
	x1 x2 - dup * y1 y2 - dup * + 2* 'power !
	;

:hit
	'balllist 4+ @ | 1er
	12 + >a | vx vy
	x1 x2 - y1 y2 -
	atan2 sincos swap
	power *. a!+
	power *. a!+
	;

:resethit
	0 'x1 ! 0 'y1 ! 0 'x2 ! 0 'y2 ! ;

:resetgame
	'balllist p.clear
	 $f0f0f0 -9.0 0.0 +pball
	5 ( 1? )(
		1 ( over <? )(
				rand
				pick2 5 - 2.0 * 10.0 +
				pick3 1.0 * pick3 2.0 * -
				+pball
			1+ ) drop
		1- ) drop
	;

:3dop project3d op ;
:3dline project3d line ;

:drawmesa
	blanco
	15.0 20.0 0 3dop
	-15.0 20.0 0 3dline
	-15.0 -20.0 0 3dline
	15.0 -20.0 0 3dline
	15.0 20.0 0 3dline
	;

:freelook
	xymouse
	sh 2/ - 7 << swap
	sw 2/ - 7 << swap
	neg mrotx mroty ;

|-----
:main
	mark
	50 'balllist p.ini
	resetgame
	resethit
	fonti2
	33
	show clrscr blanco
|		dup "%d" print cr
|		power " POWER:%f" print
		[ xymouse 'y1 ! 'x1 ! ; ] 'hitmove 'hit  guiMap

		omode
|		freelook
		xcam ycam zcam mtrans

		drawmesa
		'balllist p.draw
		'everyb 'balllist p.map2
		'hit <spc>
		'exit >esc<
		cflecha ;

: main ;

