| gestos
| PHREDA 2010
|--------------
^r4/lib/dlg.txt
^r4/lib/fontt.txt
^r4/lib/trace.txt

^r4/lib/polygr.txt

#dibujo )( $3ffff
#dibujo>
#dibujo<

#seleccion )( $ffff
#seleccion> 'seleccion

#colorultimo

#xa -1 #ya #xb #yb
#cursorp
#campocursor
#modot	| modo aux para trazo

#zoom
#xd #yd
#xcycwh 0 0 0 0

|----------------------------------------
#color $ff0000
#trazo )( $ffff
#trazo> 'trazo

:++trazo | px --
	trazo> 4 - @ =? ( drop ; )
:+trazo | px --
	trazo> !+ 'trazo> ! ;
:-trazo
	'trazo 'trazo> ! ;
:trazo.cnt | -- cntpx
	trazo> 'trazo - 2 >> ;

:trazo.map | 'vec --
	>r
	'trazo @+ 0? ( 2drop ; ) r exec
	( trazo> <? )( @+ r exec ) drop
	rdrop ;

:enlineas
	'trazo @+ 0? ( 2drop ; ) gc>xy op
	( trazo> <? )( @+ gc>xy line ) drop ;

:engrosor
	linegr 0? ( drop enlineas ; ) drop
	'trazo @+ 0? ( 2drop ; ) gc>xy gop
	( trazo> <? )( @+ gc>xy gline ) drop ;

|----------------------------------------
:savebox
	xc yc w h 'xcycwh !+ !+ !+ ! ;

:loadbox
	'xcycwh @+ 'h ! @+ 'w ! @+ 'yc ! @ 'xc ! ;

|----------------
:,dib
	dibujo> !+ 0 over ! 'dibujo> ! ;

:,dibcolor
	color 4 << $b  or ,dib ;

:makepoli

	,dibcolor ;

|-------------
:reset
	'dibujo dup 'dibujo> !+ !
	0 'dibujo !
	color 1- 'colorultimo !
	-trazo
    ;

::savegst | "nombre" --
	mark
	'dibujo dibujo> over - 2 >> ,
	( dibujo> <? )( @+ , ) drop
	savemem
	empty ;

:savemem    "mem/test.gst" savegst ;

::loadgst | "nombre" --
	mark
	here dup rot load
	'here !
	here =? ( drop empty reset ; )
	@+ 'dibujo swap
	( 1? )( 1- >r
		swap @+ rot !+
		r> ) drop 'dibujo> !
	drop
	dibujo> 'dibujo< !
	empty ;

:loadmem	"mem/test.gst" loadgst ;

|---- cursor de dibujo
:creapoli | tr --
	gc>xy
|	2dup whin 0? ( 3drop ; ) drop   | si no esta adentro no agrega
	xy>gc modot 1 =? ( 5 'modot ! ) or
	,dib
	;

:creapolif | tr --
	modot 1 =? ( 5 'modot ! ) or
	,dib
	;

:addline
	trazo.cnt 2 <? ( drop ; ) drop
	color colorultimo <>? ( 'colorultimo ! ,dibcolor )( drop )
	1 'modot !
	linegr 0? ( drop 'creapoli trazo.map ; )

	0 d>xy xy>gc drop linegg! | falta ajustar grosor real

	'trazo trazo> polygr.do
    'creapolif polygr.map
	;

:dibujando
	loadbox
	color ink
	[ xymouse xy>gc +trazo ; ]
	[ xymouse xy>gc ++trazo ; ]
	[ addline -trazo ; ]
	guiMap
	;

:bbdib
	'reset "Clear" sp btnt
	cminilapiz
	;

|--- modo hoja
#xv #yv

:vista.move
	xymouse
	dup yv - 'yc +! 'yv !
	dup xv - 'xc +! 'xv !
	savebox
	;

:vista.dn
	xymouse 'yv ! 'xv ! ;

:hojayzoom
	'vista.dn 'vista.move guiDnMove ;

:zoomin
	loadbox
	w $3fff >? ( drop ; )
	2* 'w !
	h 2* 'h !
	savebox ;

:zoomout
	loadbox
	w $3f <? ( drop ; )
	2/ 'w !
	h 2/ 'h !
	savebox ;

:bbhyz | zoom
	'zoomin "Z+" sp btnt
	'zoomout "Z-" sp btnt
	cminimano ;

|---- modo linea
:,dibline
	xa -? ( drop xb yb 2dup 'ya ! 'xa ! xy>gc 1 or ,dib ; )
	xb - abs ya yb - abs + 0? ( drop ; ) drop
	xb yb 2dup 'ya ! 'xa ! xy>gc 5 or ,dib
	;

:dibulinea
	[ xymouse 'yb ! 'xb ! ; ] dup
|	[ xymouse 'yb ! 'xb ! ; ]
	[ ,dibline ; ]
	guiMap
	xa -? ( drop ; )
	ya op xb yb line
	;

:bbtra
	'reset "Clear" sp btnt
	[ -1 'xa ! ; ] "Break" sp btnt
	'makepoli "Poli" sp btnt
	cminilapiz
	;

|---- modo caja
:,dibcaja
	xa xb =? ( drop ; ) ya yb =? ( 2drop ; )
	xy>gc dup 1 or ,dib
	xa yb xy>gc 5 or ,dib
	xb yb xy>gc 5 or ,dib
	xb ya xy>gc 5 or ,dib
	5 or ,dib
	;

:dibucaja
	[ xymouse 2dup 'ya ! 'xa ! 'yb ! 'xb ! ; ]
	[ xymouse 'yb ! 'xb ! ; ]
	[ ,dibcaja -1 'xa ! ; ]
	guiMap
	xa -? ( drop ; ) ya xb yb caja
	;
:bbcaj
	'reset "Clear" sp btnt
	cminilapiz
	;

|---- modo circulo
:,dibelip
	xa xb =? ( drop ; ) ya yb =? ( 2drop ; )
	yb ya - abs xb xa - abs
	>r >r r - 2dup
	2dup xy>gc 1 or ,dib |op
    swap r> r> dup >r swap >r  - swap 2dup xy>gc 2 or ,dib	|cp
	r + 2dup xy>gc 6 or ,dib |curve
	r + 2dup xy>gc 2 or ,dib |cp
	swap r> r> dup >r swap >r + swap 2dup xy>gc 6 or ,dib |curve
	swap r> r> dup >r swap >r + swap 2dup xy>gc 2 or ,dib |cp
	r - 2dup xy>gc 6 or ,dib |curve
	r> - xy>gc 2 or ,dib |cp
	xy>gc 6 or ,dib |curve
	rdrop
	;

:dibucirc
	[ xymouse 2dup 'ya ! 'xa ! 'yb ! 'xb ! ; ]
	[ xymouse 'yb ! 'xb ! ; ]
	[ ,dibelip -1 'xa ! ; ]
	guiMap
	xa -? ( drop ; ) xb =? ( drop ; ) ya yb =? ( 2drop ; )
	yb ya - abs xb xa - abs ellipse ;

:bbcir
	'reset "Clear" sp btnt
	cminilapiz
	;

|---- modo seleccion
:sortab
	xa xb >? ( xb 'xa ! dup 'xb ! ) drop
	ya yb >? ( yb 'ya ! dup 'yb ! ) drop
	;

:inab | x y -- 1/0
	ya <? ( 2drop 0 ; ) yb >? ( 2drop 0 ; ) drop
	xa <? ( drop 0 ; ) xb >? ( drop 0 ; ) drop
	1
	;


|-------- edicion
#ycen #xcen

:dd1 gc>xy 2 box ;
:dd2 gc>xy 3 cruz ;
:dd3 gc>xy 2 fbox ;
:dd4 gc>xy 3 cruz ;
:dd5 gc>xy 2 fbox ;
:dd6 gc>xy 3 cruz ;
:dd7 gc>xy 2dup 'ycen !+ ! 2 box ; | centro
:dd8 gc>xy 2dup 2 cruz op xcen ycen line ; | relativo

#acdd 0 dd1 dd2 dd3 dd4 dd5 dd6 dd7 dd8

:selecc.draw
	'seleccion ( seleccion> <? )(
		@+ @
		dup $f and 8 >? ( 2drop )( 2 << 'acdd + @ exec )
		) drop ;

:selecc.reset
	'seleccion 'seleccion> ! ;

:selecc.set
	sortab
	'dibujo ( dibujo> <? )(
		@+ gc>xy inab 1? ( over 4 - seleccion> !+ 'seleccion> ! ) drop
		) drop
	;

:seleccion | --
	[ xymouse 2dup 'ya ! 'xa ! 'yb ! 'xb ! ; ]
	[ xymouse 'yb ! 'xb ! ; ]
	[ selecc.set -1 'xa ! ; ]
	guiMap cyan
	selecc.draw
	xa -? ( drop ; ) ya xb yb caja
	;


:bbsel
	'selecc.reset "Clear" sp btnt
	'exit "<" sp btnt
	'exit cursorp " %d " sp btnt
	'exit ">" sp btnt

	cminiflecha
	;

|--- grosor
#winGr 0 0 0 60 200 "GR"
:dlgGr
	linegr
	0 ( 18 <? )(
		over =? ( azul )( cyan )
		[ dup linegg! ; ] over "%d" 3 sp btntf cr cr2
		2 + ) drop
	blanco "= %d =" printc
	;

|--- modo
#modov 0
| dibujo
#listam 'idibuja 'itrazo 'istop 'irec  'ifin 'ilupa
| accion sobre hoja
#listaa 'dibujando 'dibulinea 'dibucaja 'dibucirc 'seleccion 'hojayzoom
| botnes
#listab bbdib bbtra bbcaj bbcir bbsel bbhyz

|--- MAIN ---
:main
	0 dup fpos
	sw sh
	2dup max dup dim
	2/ 'yc !
	2/ 'xc !
	savebox

|	sh 230 - 200 'winGr 4+ !+ !

	show clrscr

|------ DIBUJO
		loadbox


		'listaa modov 2 << + @ exec | modo de cursor
		'dibujo nsprite
	    color ink
	    engrosor
		gc.box


|------  TOOLBAR
		scr home
		verde dup "  :R%d" print
		blanco "GESTO" print

		sp rojo 'exit dup >esc< "X" btnt
		cyan
		cr cr2
		'modov 'listam 6 sp sp btnve
		'color dlgcolor

|		0 rows 2 - gotoxy
|		azul 'dlgGr 'winGr guiWin | grosor
|		azul 'dlgPa 'winPa guiWin | paleta

		'listab modov 2 << + @ exec | botones+cursor de modo

		;

:
mark
reset

loadmem
4 main
savemem
;

