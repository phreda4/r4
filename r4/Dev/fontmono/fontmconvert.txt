| font mono convert
| PHREDA 2014
|------------------------
^r4/lib/gui.txt
^r4/lib/dlgfile.txt
^r4/lib/leebmp.txt

#bmp
#fuente
#fuente>
#txt

#sx #sy
#accline

#minh
#maxh

:,fuente
	accline	fuente> !+ 'fuente> ! ;

#ax #ay
:loadchar | y x -- y x
	2dup sx * 'ax ! sy * 'ay !
	0 ( sy <? )(
		0 'accline !
		0 ( sx <? )(
			2dup ax + swap ay + bmp.getm @
			22 >>	| escala de grises
    		over 2* << accline or 'accline !
			1+ ) drop
		,fuente
		1+ ) drop ;

:loadfont
	"r4/dev/fontmono" dlgfileload 0? ( drop ; )
	bmp.load 'bmp !
	here dup 'fuente ! 'fuente> !
	bmp.alto 4 >> 'sy !
	bmp.ancho 4 >> 'sx !
	0 ( 16 <? )(
		0 ( 16 <? )(
			loadchar
			1+ ) drop
		1+ ) drop
	fuente> dup 'here ! 'txt !

	mark
	fuente >r
	"#:font",s 'dlgfilename ,s
	sx sy " %d %d " ,print ,nl
	0 ( 256 <? )(
		sy ( 1? )( 1-
			r@+ "$%h " ,print
			) drop
		,nl
		1+ ) drop rdrop
	,nl
	'dlgfilename "r4/dev/fontmono/%s.txt" mprint  savemem
	empty
	;

:generafont
	mark
	fuente >r
	"#:font",s 'dlgfilename ,s
	sx sy minh - maxh - " %d %d " ,print ,nl
	0 ( 256 <? )(
		minh ( 1? )( 1- 4 r+ ) drop
		sy minh - maxh - ( 1? )( 1-
			r@+ "$%h " ,print
			) drop
		maxh ( 1? )( 1- 4 r+ ) drop
		,nl
		1+ ) drop rdrop
	,nl
	'dlgfilename "r4/dev/fontmono/%s.txt" mprint  savemem
	empty
	;

:hlinea | y --
	0 swap setxy
	bmp.ancho ( 1? )( 1-
		px@ not px!+ ) drop
	;
:viewbmp
	0 64 bmp.xyreal
	minh maxh or 0? ( drop ; ) drop
	0 ( 16 <? )(
		dup sy * 64 + minh + hlinea
		dup 1+ sy * 63 + maxh - hlinea
		1+ ) drop
	;
:main
	33
	show clrscr
		cr2 verde
		'loadfont " LOAD FONT " btnt
		[ 1 'minh +! ; ] "m+" sp btnt
		[ minh 1? ( 1- ) 'minh ! ; ] "m-" sp btnt
		[ 1 'maxh +! ; ] "M+" sp btnt
		[ maxh 1? ( 1- ) 'maxh ! ; ] "M-" sp btnt
		violeta
        'generafont " GENERA FONT " sp btnt
		cr cr2
		blanco
		dup "%d" print cr
		bmp.alto bmp.ancho "%dx%d" print cr
		maxh minh "%d %d" print cr

		viewbmp

		'exit >esc<
		cminiflecha ;

:memo
	mark
	here 'fuente !
	;

: memo main ;