| consola fontm
| PHREDA 2014
|-------------------
^r4/lib/gui.txt
^r4/lib/fontm.txt

|^inc/fntm/inconsolata13.txt
|^inc/fntm/sourcecode13.txt
^inc/fntm/droidsans13.txt
^inc/fntm/droidsans.txt
^inc/fntm/sourcecode16.txt

|^inc/fntm/ubuntumono14.txt

^r4/lib/trace.txt

#fontstyle
#wcon #hcon
#advcha
#advlin
#memcon
#ccur>
#clast>
#ccolor $f0

:setvars
	sw cch * neg ccw + 'advcha !
	sw cch * ccw wcon * - 'advlin !
	here 'memcon !
	wcon hcon * 2 << + 'here +!
	here 4 - 'clast> !
	memcon 'ccur> !
	;

:fullconsola | 'font --
	dup 'fontstyle ! fontm
	sw ccw / 'wcon !
	sh cch / 'hcon !
	setvars ;

:iniconsola | w h 'font --
	dup 'fontstyle ! fontm
	'hcon ! 'wcon !
	setvars ;

:cuscroll
	memcon wcon 2 << over + wcon hcon * move
	clast> wcon 1- 2 << - wcon ( 1? )( 1- ccolor 8 << rot !+ swap ) 2drop
	;

:setcur | adr --
	clast> 1+ >? ( cuscroll clast> wcon 1- 2 << - nip ) 'ccur> ! ;
:cemit | c --
	$ff and ccolor 8 << or ccur> !+ setcur ;
:cat | x y --
	wcon * + 2 << memcon + setcur ;
:chome
	memcon 'ccur> ! ;
:ccr ccur> memcon - 2 >> wcon / 1+ wcon * 2 << memcon + setcur ;
:cup ccur> wcon 2 << - memcon <? ( drop ; ) 'ccur> ! ;
:cdn ccur> wcon 2 << + setcur ;
:cle ccur> 4 - memcon <? ( memcon nip ) 'ccur> ! ;
:cri ccur> 4+ setcur ;

:cprint | "" --
	mprint ( c@+ 1? )( cemit ) 2drop ;

:ccls
	memcon dup 'ccur> ! >r wcon hcon * ( 1? )( 1- ccolor 8 << r!+ ) drop rdrop ;

:catr | col --
	'ccolor ! ;

:showconsola | x y -
	setxy
	fontstyle fontm
    blink
	1? ( ccur> @ $100000 xor ccur> ! )
	memcon >r
	hcon ( 1? )( 1-
		wcon ( 1? )( 1-
			r@+ dup 8 >> ink12
			$100000 and? ( charmtn )( charmt )
			advcha px+!
			) drop
		advlin px+!
		) drop rdrop
	1? ( ccur> @ $100000 xor ccur> ! )
	drop ;

:main
|	'fontdroidSans
	|'fontdroidSans13
	|'fontInconsolata13
|	'fontUbuntuMono14
	|'fontSourceCode16
	fullconsola
	ccls

	ccr
	":R4 - 2014" cprint ccr
	33
	show clrscr
|      	dup "%d" print cr
		ccur> "%h" print
		0 0 showconsola

        [ key toasc cemit ; ] <visible>
        'ccr <enter>
		'cle <le>
		'cri <ri>
		'cdn <dn>
		'cup <up>


		'exit >esc<
		cminiflecha ;

: mark main ;