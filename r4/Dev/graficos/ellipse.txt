| rasterize ellipse
| PHREDA 2016
^r4/lib/gui.txt
#xm #ym
#dx #dy

#x #y
#err
#2bb
#2aa

:qfill
	xm x - ym y - op
	xm x + ym y - line
	xm x - ym y + op
	xm x + ym y + line ;
:qfill1
	xm x - ym op
	xm x + ym line ;

:ellipse1 | a b xm ym
	'ym ! 'xm !
	over neg 'x !
	0 'y !
	over neg 2* 1+ over dup * * 'dx !
	over dup * 'dy !
	dx dy + 'err !
	dup * 2* '2bb !
	dup * 2* '2aa !
	qfill1
	1 'y +! 2aa 'dy +! dy 'err +!
	( x -? )( drop
		err 2*
		dx >=? ( 1 'x +! 2bb 'dx +! dx 'err +! )
		dy <=? ( qfill 1 'y +! 2aa 'dy +! dy 'err +! )
		drop
		) drop ;
|---------------------------------------------
| v2 - remove x y vars

:qfill | x y
	ym over - xm pick3 - over op
	xm pick3 + swap line
	ym over + xm pick3 - over op
	xm pick3 + swap line ;

:qfill1 | x y
	xm pick2 - ym op
	xm pick2 + ym line ;

:ellipse2 | a b xm ym
	'ym ! 'xm !
	over dup * dup 2* '2aa ! 'dy !
	over neg 2* 1+
	swap dup * dup 2* '2bb ! * 'dx !
	dx dy + 'err !
	neg 0			| x y
	qfill1
	1+ 2aa 'dy +! dy 'err +!
	( swap -? )( swap 	| x y
		err 2*
		dx >=? ( rot 1+ rot rot 2bb 'dx +! dx 'err +! )
		dy <=? ( rot rot qfill 1+ rot 2aa 'dy +! dy 'err +! )
		drop
		) 2drop ;

|--------------------
| v3 - remove err
:ellipse3 | a b xm ym
	'ym ! 'xm !
	over dup * dup 2* '2aa ! dup >b 'dy !
	over neg 2* 1+
	swap dup * dup 2* '2bb ! * dup b+ 'dx !
	neg 1			| x y
	2aa 'dy +! dy b+
	qfill1
	( swap -? )( swap 	| x y
		b> 2*
		dx >=? ( rot 1+ rot rot 2bb 'dx +! dx b+ )
		dy <=? ( rot rot qfill 1+ rot 2aa 'dy +! dy b+ )
		drop
		) 2drop
	;

|--------------------
| v4 - remove 2aa y 2bb , +x
:ellipse | a b xm ym
	'ym ! 'xm !
	over dup * dup 2*		| a b c 2aa
	swap dup >b 'dy ! 		| a b 2aa
	rot rot over neg 2* 1+	| 2aa a b c
	swap dup * dup 2* 		| 2aa a c b 2bb
	rot rot * dup b+ 'dx !	| 2aa a 2bb
	swap 1				| 2aa 2bb x y
	pick3 'dy +! dy b+
	qfill1
	( swap +? )( swap 		| 2aa 2bb x y
		b> 2*
		dx >=? ( rot 1- rot rot pick3 'dx +! dx b+ )
		dy <=? ( rot rot qfill 1+ rot pick4 'dy +! dy b+ )
		drop
		)
	4drop ;

:main
	33
	show clrscr
		dup "%d" print
		azul
		180 160 220 140 cajaf

		verde
		128 alpha
		41 41 100 100 ellipse
		xymouse  300 300 ellipse
		40 20 300 100 ellipse
		'exit >esc<

			;
: main ;