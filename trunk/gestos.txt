| gestos
| PHREDA 2010
|--------------
^lib/gui.txt
^lib/trace.txt
^lib/fontt.txt

#bx1 #by1 #bx2 #by2
#memmap

#boxlist
#boxlist>
#dibujo
#dibujo>
#dibujo<
#string
#string>

#campocursor


:ram
	mark
	here
	dup 'boxlist ! $ffff +
	dup 'dibujo ! $7ffff +
	dup 'string ! $ffff +
	'here !
:reset
	boxlist 'boxlist> !
	dibujo dup 'dibujo> !+ !
	string 'string> !
	0 boxlist !
	0 dibujo !
    ;

:,aux | c --
	string> c!+ 'string> ! ;

:+str | "" -- index
	string> swap
	( c@+ 1? )( ,aux ) ,aux
	drop string - ;

:+box | param --
	bx2 bx1 - abs 8 <? ( drop ; ) drop
	by2 by1 - abs 8 <? ( drop ; ) drop
	bx2 16 << by2 or
	bx1 16 << by1 or
	boxlist> !+ !+ !+ 'boxlist> ! ;

:0box
	0 boxlist> ! ;

:dbox | x1 y1 x2 y2 --
	2over op
	over pick3 line
	swap over line
	pick2 swap line
	line ;

:drawbox
	boxlist ( boxlist> <? )( >r
		r@+ dup 16 >> swap $ffff and
		r@+ dup 16 >> swap $ffff and
		dbox
		r@+ print
		r> ) drop ;

:campodraw
	campocursor 0? ( drop ; ) >r
	r@+ dup 16 >> swap $ffff and
	r@+ dup 16 >> swap $ffff and
	violeta
	dbox
	r@+ print
	rdrop ;

|-------------
:,dib | v --
	dibujo> !+ 'dibujo> ! ;

:0dib
	0 dibujo> ! ;

|-------------
#linea )( 32768
#linea> 'linea

:,linea | xy --
	linea> 4 - @ =? ( drop ; )
	linea> !+ 'linea> ! ;

:resetlinea
	'linea 'linea> ! ;

:addline
	tpen @+ 0? ( 2drop ; )
	( 1? )( 1- swap @+ ,linea swap )
	2drop ;

:drawlinea
	'linea
	@+ 0? ( 2drop ; ) >uv op
	( linea> <? )( @+ >uv line ) drop ;


|-------tipo dibujo
| ...0 00..
:t000 | color	$00 000000
	drop ;
:t001 | color	$04 000000
	$ffffff and ink ;
:t010 | op		$08 000000
	dup $fff and swap 12 >> $fff and op ;
:t011 | line	$0c 000000
	dup $fff and swap 12 >> $fff and line ;
:t100 | cv-curve
	dup $fff and swap 12 >> $fff and cp @+ dup $fff and swap 12 >> $ffff and curve ;
:t101 | pline
	dup $fff and swap 12 >> $fff and pline ;
:t110 | cv-pcurve
	dup $fff and swap 12 >> $fff and cp @+ dup $fff and swap 12 >> $ffff and pcurve ;
:t111 | endpoli
	drop poli ;

#acc t000 t001 t010 t011 t100 t101 t110 t111

:drawdraw | str --
	( @+ 1? )(
		dup 24 >> $1c and 'acc + @ exec
		) 2drop ;

|----------------
:colineal | q1 q2 q3 -- 0/1
	pick2 pick2 * 2 << >r | 4q1q2
	- + dup * r> - ;

:Qab | a b -- q
	over $fff and
	over $fff and - dup * >r
	swap 12 >> $fff and
	swap 12 >> $fff and - dup * r> + ;

| a-b q1
| b-c q2
| a-c q3
:checkline | --
	dibujo> 12 -
	@+ swap @+ swap @ and and $c000000 and
	$c000000 <>? ( drop ; ) drop | 3 ultimos puntos son lineas

	dibujo> 12 - >r r@+	r@+	r> @ | a b c
	pick2 over Qab >r	| a b c r:q3
	over swap Qab >r	| a b 	r:q2 q3
	Qab r> r>
	colineal 1? ( drop ; ) drop
	dibujo> 8 - dup 4+ @ swap !+ 'dibujo> !
	;

|----------------
:16to12
	>uv 12 << $fff000 and swap $fff and or ;

:cadapunto
	16to12 $0c000000 or ,dib
	checkline ;

:pasatrazo
	'linea linea> >=? ( drop ; )
	@+ 16to12 $08000000 or ,dib
	( linea> <? )(
		@+ cadapunto
		) drop
	0dib
	 ;

|-------tipo coord
:+xydata | x y --
	by1 <? ( dup 'by1 ! )
	by2 >? ( dup 'by2 ! ) drop
	bx1 <? ( dup 'bx1 ! )
	bx2 >? ( dup 'bx2 ! ) drop ;

:xx drop ;
:dd dup $fff and swap 12 >> $fff and +xydata ;
:ddc dup dd dd ;

#accXY xx xx dd dd ddc dd ddc xx

:gestobox | str --
	sh sw 'bx1 ! 'by1 !
	0 0 'bx2 ! 'by2 !
	( @+ 1? )(
		dup 24 >> $1c and 'accXY + @ exec
		) 2drop ;

|----------------
:makebox
	dibujo<
	dibujo> =? ( drop ; ) | no agrega si no hay trazo
	gestobox
	"hola" +box
	dibujo> 'dibujo< !
	;

|---------------------
:cursordibuja
	[ tpen drop resetlinea ; ] onDn
	'addline onMove
	'pasatrazo onClick
	clapiz ;

|---- cursor de caja
:cursorcaja | --
|	[ xymouse xyr ycc - 'hcc ! xcc - 'wcc ! ; ] onMove
|	[ xymouse xyr 'ycc ! 'xcc ! 0 dup 'hcc ! 'wcc ! ; ] onDn
|	[ +box 0 dup 2dup 'bx1 !+ !+ !+ ! ; ] onClick
	cflecha ;

|---- cursor de seleccion

:cursorselec | --
	[ 0 'campocursor ! ; ] onDn
	campodraw
	cmano ;

#modoc 'cursordibuja

|---------------------
:main
    inigui
	'exit >esc<
|	'pasatrazo <f1>
	[ rand $ffffff and $04000000 or ,dib ; ] <f2> | color al azar
	[ modoc 'cursordibuja =? ( 'cursorselec )( 'cursordibuja ) nip 'modoc ! ; ] <enter>
	show clrscr
		32 gc.top $f gc.hfill
		font-vard-12-bold home cr2
		verde dup " :R%d" print
		blanco "GESTO " print

		32 gc.botton $00000f gc.hfill
		font-vard-8-bold home cr2

	| acciones
		rojo
		sp 'exit "esc-Exit" btnt
		sp verde
		sp 'makebox dup <f1> "f1-Box" btnt

		scr fonti
		cr cr verde
		linea> 'linea - 2 >> "%d " print cr
    	dibujo> dibujo - 2 >> "%d " print
|		'linea ( linea> <? )( @+ "%h " print ) drop cr
|	    dibujo ( @+ 1? )( "%h " print ) 2drop cr
		home
		boxlist ( boxlist> <? )( 8 + "* " print @+ print cr ) drop

		32 32 gc.vbetween
		dibujo drawdraw
		verde drawlinea
		verde drawbox
		modoc >r ;


: 4 ram main ;