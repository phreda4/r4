| gestos
| PHREDA 2010
|--------------
^lib/gui.txt
^lib/trace.txt
^lib/fontt.txt

|----------
#dibujo
#dibujo>

:resetdibujo
	0 here dup dup 'dibujo !+ ! ! ;

:,dib | v --
	dibujo> !+ 'dibujo> ! ;

:0dib
	0 dibujo> ! ;

|-------tipo dibujo
| ...0 00..
:t000 | color	$00 000000
	drop ;
:t001 | color	$04 000000
	$ffffff and ink ;
:t010 | op		$08 000000
	dup $fff and swap 12 >> $fff and op ;
:t011 | line	$0c 000000
	dup $fff and swap 12 >> $fff and line ;
:t100 | cv-curve
	dup $fff and swap 12 >> $fff and cp @+ dup $fff and swap 12 >> $ffff and curve ;
:t101 | pline
	dup $fff and swap 12 >> $fff and pline ;
:t110 | cv-pcurve
	dup $fff and swap 12 >> $fff and cp @+ dup $fff and swap 12 >> $ffff and pcurve ;
:t111 | endpoli
	drop poli ;

#acc t000 t001 t010 t011 t100 t101 t110 t111

:drawdraw | str --
	( @+ 1? )(
		dup 24 >> $1c and 'acc + @ exec
		) 2drop ;

|----------
#linea )( 43768
#linea> 'linea

:,linea | xy --
	linea> 4 - @ =? ( drop ; )
	linea> !+ 'linea> ! ;

:reslinea
	'linea 'linea> ! ;

:drawlinea
	'linea
	( linea> <? )(
		@+ 1? ( >uv line )( drop @+ >uv op )
		) drop ;

|----------------
:colineal | q1 q2 q3 -- 0/1
	pick2 pick2 * 2 << >r | 4q1q2
	- + dup * r> - ;

:Qab | a b -- q
	over $fff and
	over $fff and - dup * >r
	swap 12 >> $fff and
	swap 12 >> $fff and - dup * r> + ;

| a-b q1
| b-c q2
| a-c q3
:checkline | --
	dibujo> 12 -
	@+ swap @+ swap @ and and $c000000 and
	$c000000 <>? ( drop ; ) drop | 3 ultimos puntos son lineas

	dibujo> 12 - >r r@+	r@+	r> @ | a b c
	pick2 over Qab >r	| a b c r:q3
	over swap Qab >r	| a b 	r:q2 q3
	Qab r> r>
	colineal 1? ( drop ; ) drop
	dibujo> 8 - dup 4+ @ swap !+ 'dibujo> !
	;
|----------------

:16to12
	>uv 12 << $fff000 and swap $fff and or ;

:cadapunto
	@+ 0? ( drop @+ 16to12 $08000000 or ,dib ; )
	16to12 $0c000000 or ,dib
	checkline ;

:pasatrazo
	'linea linea> 4 - >=? ( drop ; )
	rand $ffffff and $04000000 or ,dib | color al azar
	( linea> <? )(
		cadapunto
		) drop
	0dib
	reslinea ;

|---------------------
:gestos
	dibujo drawdraw
	verde drawlinea
|	[ pasatrazo ; ] onClick
	bmouse 0? ( ,linea tpen drop pasatrazo cflecha ; ) drop
	tpen @+ 0? ( 2drop ; )
	( 1? )( 1- swap
		@+ ,linea
		swap )
	2drop
	cflecha
	;

|---------------------
:dibujar
	dibujo drawdraw
	verde drawlinea
	bmouse 0? ( ,linea tpen drop pasatrazo clapiz ; ) drop
	tpen @+ 0? ( 2drop ; )
	( 1? )( 1- swap
		@+ ,linea
		swap )
	2drop
	clapiz
	 ;

|---------------------
:cajas
	cmano ;

|---------------------
:anima
	cmano ;


#modo 'dibujar 'cajas 'gestos 'anima
#modoa 'dibujar

|---------------------
:main
	$ffffff paper
	resetdibujo
    inigui
	'exit >esc<
	'pasatrazo <f1>
	show clrscr
		32 gc.top $f gc.hfill
		font-vard-12-bold home cr2
		verde dup " :R%d" print
		blanco "GESTO " print
		|-------------
		| modos
		'modo 0 ( 4 <? )(
			sp [ over @ 'modoa ! ; ]
			over "%d" mprint btnt
			1+ swap 4+ swap ) 2drop
		|-------------
		scr fonti
		cr cr negro
		linea> 'linea - 2 >> "%d " print cr
    	dibujo> dibujo - 2 >> "%d " print
|		'linea ( linea> <? )( @+ "%h " print ) drop cr
|	    dibujo ( @+ 1? )( "%h " print ) 2drop cr

		modoa exec ;

: 4 mark main ;