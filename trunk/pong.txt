| Version del pong traducida a :r4
| PHREDA 2009
|----------------------------------------------------------
^lib/gui3.txt
^lib/graf.txt

|-------------------------------------------
#dig0 ( %1111 %1001 %1001 %1001 %1111 )
#dig1 ( %0001 %0001 %0001 %0001 %0001 )
#dig2 ( %1111 %0001 %1111 %1000 %1111 )
#dig3 ( %1111 %0001 %1111 %0001 %1111 )
#dig4 ( %1001 %1001 %1111 %0001 %0001 )
#dig5 ( %1111 %1000 %1111 %0001 %1111 )
#dig6 ( %0001 %0001 %1111 %1001 %1111 )
#dig7 ( %1111 %0001 %0001 %0001 %0001 )
#dig8 ( %1111 %1001 %1111 %1001 %1111 )
#dig9 ( %1111 %1001 %1111 %0001 %0001 )

#digits 'dig0 'dig1 'dig2 'dig3 'dig4 'dig5 'dig6  'dig7 'dig8 'dig9

#xx #yy
#startx #starty

:** | 0/1 --
	and 1? ( 20 dup xx yy vfill ) drop
	20 'xx +! ;

:drawd | addr --
	starty 'yy !
	5 ( 1? )( 1- swap c@+ startx 'xx !
			dup %1000 ** dup %0100 ** dup %0010 ** %0001 **
			20 'yy +! swap ) 2drop ;

:drawdigit | n --
  2 << 'digits + @ drawd ;

:drawnumber | n x y --
  'starty ! 'startx !
  dup 10 / 10 mod drawdigit
  100 'startx +! 10 mod drawdigit ;

|-------------------------------------------
#ldir #rdir
#ballx #bally
#balldx #balldy
#leftbaty 0 #rightbaty 0
#leftscore 0 #rightscore 0

:doreset |( -- )
	0 'leftscore ! 0 'rightscore !
	0 dup 'leftbaty ! 'rightbaty !
:resetball
	0 dup 'ballx ! 'bally !
|	rand 3 >> 0.02 mod 'balldx !
	0.02 'balldx !
	0.02 rand $10000 and? ( drop neg dup ) drop 'balldy !
	;

:golpe
	balldx 1.001 *. neg 'balldx !
	over 4 >> neg 'balldy +! ;

:paleta | xact xcorr ypaleta -- xact
	bally - dup abs 0.2 >? ( 3drop ; ) drop
	3 >> neg 'balldy +!
	balldx neg 1.04 *. 'balldx !
	nip ;

:juego
	doreset
	inigui
	'exit >esc<
 	'doreset >esp<
	[ -0.06 'ldir ! ; ] $1e >key
	[ 0.06 'ldir ! ; ] $10 >key
	[ 0 'ldir ! ; ] dup $10 >ukey $1e >ukey
	[ -0.06 'rdir ! ; ] <dn>
	[ 0.06 'rdir ! ; ] <up>
	[ 0 'rdir ! ; ] dup >up< >dn<
	show clrscr
		$2200 ink
		0.52 0.96 fdim
		0.55 0 fpos gc.fbox
		-0.55 0 fpos gc.fbox

		rojo
		leftscore sw 2/ 200 - 20 drawnumber
		claro
		0.025 0.2 fdim |	20 100
		-0.98 leftbaty fpos
		gc.fbox

		azul
		rightscore sw 2/ 20 + 20 drawnumber
		claro
		0.025 0.2 fdim
		0.98 rightbaty fpos
		gc.fbox

		leftbaty ldir +
		-0.75 <? ( -0.75 nip ) 0.75 >? ( 0.75 nip )
		'leftbaty !
		rightbaty rdir +
		-0.75 <? ( -0.75 nip ) 0.75 >? ( 0.75 nip )
		'rightbaty !

		0.04 dup fdim
		ballx balldx +
		-0.92 <? (
			-0.92 leftbaty paleta
			-1.1 <? ( 1 'rightscore +! 'resetball >r )
			 )
		0.92 >? (
			0.92 rightbaty paleta
			1.1 >? ( 1 'leftscore +! 'resetball >r )
			)
		dup 'ballx !

		bally balldy +
		-0.92 <? ( -0.92 nip balldy neg 'balldy ! )
		0.92 >? ( 0.92 nip balldy neg 'balldy ! )
		dup 'bally !
		fpos
		blanco gc.fcircle

|		scr fonti blanco ballx bally "%f %f" print
|		leftbaty bally - " %f" print
		;

: 0 paper juego ;
