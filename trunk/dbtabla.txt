| dbtabla.txt - PHREDA
| archivo de texto con los siguientes separadores
| ~ registro
| | campos
| _ multivalores
|-------------------
^parse.txt
^trace.txt

#:dbnombre
#:cntregs
#:cntflds

#actreg

#indice0
#sumapos
#inimem
#inimemdb 0

#flds )( 512	| nombre 128 campos
#fldl )( 512	| largo
#fldc )( 512	| campos en cursor
#fldm )( 512	| memoria auxiliar

|||||||| accesos
::dbtitle | n -- adr	; titulo
	2 << 'flds + @ ;

::dblen | n -- largo	; largo
	2 << 'fldl + @ ;

::dbfld | n -- adr		; lugar en db
	2 << 'fldc + @ ;

::dbmfld | n -- adr		; lugar en memaux
	2 << 'fldm + @ ;

::dbreg | n -- adr
	2 << indice0 + @ ;

||||||||| recorrido
:>>.. | adr -- adr'
	( c@+ 32 <? )( 0? ( drop 1- ; ) drop ) drop 1- ;

:>>f | adr -- adr' |
	( c@+ 1? )(
		$7c =? ( drop ; )
		$7e =? ( drop ; )
		drop ) drop 1- ;

:>>s | adr -- adr'  _
	( c@+ 1? )(
		$5f =? ( drop ; )
		$7c =? ( drop ; )
		$7e =? ( drop ; )
		drop ) drop 1- ;

:>>fld | reg fld -- reg'
	( 1? )( 1- swap
		>>f
		swap ) drop ;

||||||||| cargar tabla
:recorre
	( c@+ $7e <>? )(
		0? ( drop nip 1- ; )
		$7c =? ( 1 'cntflds +! rot pick2 swap !+ rot rot )
		drop ) drop nip | ind mem
	;

::dbload | "app.db" --
	0 'cntregs !
	1 'cntflds !
	dup 'dbnombre !

	inimemdb
	0? ( here dup 'inimemdb ! nip )
	swap load inimemdb =? ( drop ; )

	0 over ! 4+
	dup 'indice0 !
	inimemdb | <-- inicio
	dup 'flds !+ | ind mem fld
	swap					|----- campos
	recorre
	>>..
	dup rot !+ swap 		|---- registros
	( c@+ 1? )(
		$7e =? ( swap >>.. swap 1 'cntregs +! rot pick2 swap !+ rot rot )
		drop ) 2drop
	'inimem !
	0 'sumapos !
	'fldl 'flds 			|---- ancho de campos, mem aux
	0 ( cntflds <? )( >r
		@+ >>s ?0int
		sumapos inimem + r 2 << 'fldm + !
		dup 1+ 'sumapos +!
		rot !+ swap
		r> 1+ ) 3drop
	sumapos inimem + 'here !
	;


|||||||| tipos de campos
| T- Texto
| M- Memo
| S- Si/no
| 0- Entero
| 1- 1 decima
| 2- 2 decimales
| 3- 3 decimales
| 4- 4 decimales

|||||||| memoria auxiliar de registros
::clearreg
	'fldm cntflds ( 1? )( >r
		@+ 0 swap c!
		r> 1- )
	2drop ;

::getreg | nro --
	dup 'actreg !
	cntregs >=? ( drop ; )
	2 << indice0 + @
	'fldc swap
	cntflds ( 1? )( >r
		dup rot !+ swap
		>>f r> 1- )
	3drop ;

::dbcpy | dst scr --
	( c@+ 1? )(
		$5f =? ( 2drop 0 swap c! ; )
		$7c =? ( 2drop 0 swap c! ; )
		$7e =? ( 2drop 0 swap c! ; )
		rot c!+ swap ) rot c!+
	2drop ;

::dbset | str nro --
	dbmfld swap
	( c@+ 1? )( rot c!+ swap ) rot c! drop ;

::cpyreg | --
	actreg
	cntregs >=? ( drop clearreg ; )
	drop
	'fldm 'fldc
	0 ( cntflds <? )( >r
		@+ rot @+ rot dbcpy swap
		r> 1+ )
	3drop ;

|||||||||
::dbedit | nro --
	dup dbtitle "%a:" print cr
	dup dbmfld swap dblen getline ;

::dbprint | nro --
	dup dbtitle "%a: " print
	dup dbmfld swap dblen boxprint ;

|||||||||
::printreg
	'fldc
	0 ( cntflds <? )( >r
		r dbtitle "%a:" verde print
		@+ "%a" blanco print cr
		r> 1+ )
	2drop ;

::editreg
	'fldm negro
	0 ( cntflds <? )( >r
		r dbtitle "%a:" 10 boxprintr
		@+ r dblen getline cr
		r> 1+ )
	2drop ;

::editreg2
	'fldm negro
	0 ( cntflds <? )( >r
		r dbtitle "%a:" print cr
		@+ r dblen getline cr
		r> 1+ )
	2drop ;

||||||||| actualizacion
::dbupdate | nro --
	drop
	scr
	fonti cr cr blanco
	0 dbmfld "%s" print
	1 ( cntflds <? )( >r
		r dbmfld "|%s" print
		r> 1+ ) drop
	"~" print
	redraw
	trace

	;

::dbdelete | nro --
	drop
	;

|||||| copia en memoria
#pl
:,c1 pl c!+ 'pl ! ;
:,s1 ( dup c@ 1? )( ,c1 1+ ) 2drop ;

|||||| inserta registro
::dbinsert | --   ; inserta memoria al final del archivo
	indice0 4 - 'pl ! | graba mem
	0 ( cntflds <? )(
		1? ( "|" ,s1 )
		dup dbmfld ,s1
		1+ ) drop
	"~" ,s1 13 ,c1 10 ,c1
	mem pl over - dbnombre save 	| copia a archivo
	;

||||||||| funciones comunes
::dbmax | campo -- valor
	0 dup >r
	( cntregs <? )(
		dup dbreg | campo nroreg reg
		pick2 >>fld | campo nroreg creg
		?0int r >? ( r> drop >r )( drop )
		1+ ) 2drop
	r> ;

||||||||| aux
::dbdump
	dbnombre print cr
|	cntflds "fld:%d " print cr
|	cntregs "reg:%d " print cr
	0 ( cntflds <? )( 1+
		dup dbtitle "%a" pick2 dblen boxprint
		1+ ) drop
	cr
	indice0
	0 ( cntregs <? )(
		swap @+ "%a" print cr
		swap
		1+ ) 2drop
	cr ;

