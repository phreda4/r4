| Fuente 8x12
| phreda
|-------------------------------
^gui3.txt

||||| 8x8 simple
:chars | c --
	$ff and charsiz + c@ ;

:char | c --
	ccx ccy setxy
	$ff and 4 << charrom +
	0 ( charlin <? )( swap
		c@+ $ff and
		sw swap
		( 1? )( swap 1- swap
			1 and? ( ink@ px!+ )( 1 px+! )
			2/ ) drop
		px+!
		swap 1+ )
	2drop ;


||||| 16x16 simple
:chard | c --
	ccx ccy setxy
	$ff and 5 << charrom +
	0 ( charlin <? )( swap
		w@+ $ffff and
		sw swap
		( 1? )( swap 1- swap
			1 and? ( ink@ px!+ )( 1 px+! )
			2/ ) drop
		px+!
		swap 1+ )
	2drop ;

|| 8x8 doble
:charbs | c --
	$ff and charsiz + c@ 1+ ;

:charb | c --
	ccx ccy setxy
	$ff and 4 << charrom +
	0 ( charlin <? )( swap
		c@+ $ff and dup 2* or
		sw swap
		( 1? )( swap 1- swap
			1 and? ( ink@ px!+ )( 1 px+! )
			2/ ) drop
		px+!
		swap 1+ )
	2drop ;

||||| 16x16 doble
:chardb | c --
	ccx ccy setxy
	$ff and 5 << charrom +
	0 ( charlin <? )( swap
		w@+ $ffff and dup 2* or
		sw swap
		( 1? )( swap 1- swap
			1 and? ( ink@ px!+ )( 1 px+! )
			2/ ) drop
		px+!
		swap 1+ )
	2drop ;

||||| OUTLINE
:charrs | c --
	$ff and charsiz + c@ 2 + ;

:linea | mask maskb --
	( 1? )(
		1 and? ( 2/ swap 1 and? ( inka )( inkb ) px!+ )( 2/ swap 1 px+! )
		2/ swap )
	2drop ;

|| 8x8
:charr | c --
	$ff and 4 << charrom +
	0 0 rot
	charlin ( 1? )( 1-
		swap c@+ $ff and	| 0 0 lin adr v
		rot rot 			| 0 0 v lin adr
		swap ) 2drop
	0 0 					| 0 0 .... 0 0
	charlin 2 + ( 1? )( 1-
		ccx ccy pick2 + setxy
		pick2 2*
		pick4 pick4 or pick3 or
		dup 2* or dup 2* or
		linea
		nip ) drop
	2drop
	;

||||| 16x16 borde
:chardr | c --
	$ff and 5 << charrom +
	0 0 rot
	charlin ( 1? )( 1-
		swap w@+ $ffff and	| 0 0 lin adr v
		rot rot 			| 0 0 v lin adr
		swap ) 2drop
	0 0 					| 0 0 .... 0 0
	charlin 2 + ( 1? )( 1-
		ccx ccy pick2 + setxy
		pick2 2*
		pick4 pick4 or pick3 or
		dup 2* or dup 2* or
		linea
		nip ) drop
	2drop
	;


||||| OUTLINE
:charrbs | c --
	$ff and charsiz + c@ 3 + ;

|| 8x8
:charbr | c --
	$ff and 4 << charrom +
	0 0 rot
	charlin ( 1? )( 1-
		swap c@+ $ff and	| 0 0 lin adr v
		dup 2* or
		rot rot 			| 0 0 v lin adr
		swap ) 2drop
	0 0 					| 0 0 .... 0 0
	charlin 2 + ( 1? )( 1-
		ccx ccy pick2 + setxy
		pick2 2*
		pick4 pick4 or pick3 or
		dup 2* or dup 2* or
		linea
		nip ) drop
	2drop
	;

||||| 16x16 borde doble
:chardbr | c --
	$ff and 5 << charrom +
	0 0 rot
	charlin ( 1? )( 1-
		swap w@+ $ffff and	| 0 0 lin adr v
		dup 2* or
		rot rot 			| 0 0 v lin adr
		swap ) 2drop
	0 0 					| 0 0 .... 0 0
	charlin 2 + ( 1? )( 1-
		ccx ccy pick2 + setxy
		pick2 2*
		pick4 pick4 or pick3 or
		dup 2* or dup 2* or
		linea
		nip ) drop
	2drop
	;

||||||||||||||||||||||||||||||||||||||||
::tfont | 'fuente --
	@+ 'charlin !
	@+ 'charrom !
	@+ 'charsiz !
	@
	0? ( 'char )( 'chard ) 'chars
	setfont drop
	8 charlin fixsize
	;

::tfontb
	@+ 'charlin !
	@+ 'charrom !
	@+ 'charsiz !
	@
	0? ( 'charb )( 'chardb ) 'charbs
	setfont drop
	8 charlin fixsize
	;

::tfonto
	@+ 'charlin !
	@+ 'charrom !
	@+ 'charsiz !
	@
	0? ( 'charr )( 'chardr ) 'charrs
	setfont drop
	8 charlin fixsize
	;

::tfontbo
	@+ 'charlin !
	@+ 'charrom !
	@+ 'charsiz !
	@
	0? ( 'charbr )( 'chardbr ) 'charrbs
	setfont drop
	8 charlin fixsize
	;

