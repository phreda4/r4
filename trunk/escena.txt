| escena
| PHREDA 2009
|------------------------------
^lib/sprite.txt
^lib/gui.txt
^lib/trace.txt
^inc/escena.inc

#x
#y
#z

#vara -2.0
#varb 0

|**** mapa
#dibujos 0 'dib1 'dib2 'dib3 'dib4 'dib5 'dib6 'dib7 'dib8 'dib9 'diba 'dibb 'dibc 'dibd 'dibe 'dibf 'dib10

#mapa )( 1024
#fill
#xref

:drawm
	mpush
|			dup 2 >> mrotxi
|    		0.23 pinpon over + mrotxi
	$f and
	1? ( 2 << 'dibujos + @ 3dnsprite )( drop )
	mpop ;

:drawmapa
'mapa >r
	mpush
	-8.5 xref - -7.5 0 mtransi
	-8.0 ( 8.0 <? )(
		-8.0 ( 8.0 <? )(
			1.0 0.0 0.0 mtransi
			r@+ 1? ( drawm )( drop )
			1.0 + ) drop
		-16.0 1.0 0.0 mtransi
		1.0 + ) drop
	mpop rdrop ;

:randmap
	'mapa >r
	256 ( 1? )( 1-
		dup r!+
		) drop
	rdrop ;

:leftscroll
	'mapa
	dup 960 + @ 'fill !
	dup 4+ 256 move ;

:softscroll | vel --
	xref +
    1.0 >? ( 1.0 - leftscroll )
	'xref ! ;

|*** 1alto

|*** multisprite
#multisprite
'dib1 0.0 0.0 0.0 $0
'dib9 0.0 0.0 0.0 $0
'dib9 0.0 0.6 0.0 $0
0

:drawmultisprite | m --
	>r
	( r@+ 1? )(
		mpush
		r@+ r@+ 0 mtransi
		r@+ mrotzi
		3dnsprite
        r@+ drop
		mpop
		) drop rdrop
	;

:ms.move | x y 'ms --
	>r
	( r@+ 1? )( drop
		over  r @ + r!+
		dup r @ + r!+
		r> 8 + >r
		) drop
	rdrop ;

:s.move | x y 'ms nro --
	20 * + 4+
	>r
	swap
	r +! r> 4+ +! ;

:s.rot | r 'ms nro --
	20 * + 16 +
	+! ;


#lunar
'dibC 0.14 0.55 0.0
'dibC -0.10 0.4 0.0
'dibD 0.0 0.0 0.0
'dibC 0.10 0.5 0.0
'dibC -0.14 0.6 0.0
0

:mulispr | msr --
	>r ( r@+ 1? )(
		mpush
		r@+ r@+ polar 0 mtransi
		r@+ mrotzi
		3dnsprite
		mpop
		) drop
	rdrop ;

:sr.rot
	4 << + 12 + +! ;

:msr.rot | r 'msr--
	>r
	( r@+ 1? )( drop
		r> 8 + >r
		dup r @ + r!+
		) drop
	rdrop ;


|**** particulas
| x  y  s r  vx vy acc tl
|    4  8 12 16 20 24  28
|-28-24-20-16-12 -8 -4

:sxy@ | adrrl -- adrrl x y
	dup 28 - @+ swap @ ;

:svxy! | adrrl +vy +vx -- adrrl
	pick2 12 - !+ ! ;

:sz! | adrrl scale -- adrrl
	over 20 - ! ;

:sr! | adrrl rotacion -- adrrl
	over 16 - ! ;
:+sr! | adrrl +rotacion -- adrrl
	over 16 - +! ;
:sr@ | adrrl  -- adrrl rotacion
	dup 16 - @ ;

:draw2d | adr -- adr tl
	mpush
	>r
	r@+ r@+ r@+ mtransi
	r@+ mrotzi
	r@+ r 20 - +!
	r@+ r 20 - +!
	r> @+ exec | adrtl t1
	mpop ;

:delspr | 'lsp 'lsig 'act -- 'lsig
	dup 32 - over | nueva vieja
	pick3 3 << move | de sr cnt
	32 - ;

:+layer | tl 'acc vy vx r z y x  'layer --
	dup @+ | 'listfx 'dirini cantact
	>r r
	5 << + | 'listfx 'diractual
	swap >r | ... y x 'diractual
	!+ !+ !+ !+ !+ !+ !+ !
	r> r> | 'diract cantact
	1+ swap ! ;

:layerdraw | adr --
	dup @+ ( 1? )( 1- swap  | nexl cant lis'
		draw2d
        1? ( swap !+ )( drop 4+
				delspr |trace
				-1 pick3 +! 	| resta 1 a cantidad
				)
		swap )
	3drop ;

#layerfx 0 )( 8192

:+layerfx | tl 'acc vy vx r z y x --
	'layerfx +layer ;

|--- rand
:r0.1 | -- r
	rand 0.1 mod ;

:r01 | -- r
	rand 1.0 mod ;

|----  ROCKS
:rocks
	0.01 +sr!
	sxy@
	abs 6.0 >? ( 0 nip nip ; ) drop
	abs 6.0 >? ( 0 nip ; ) drop
    -1
    'dib6 3dnsprite
	;

:+rocks
	-1 'rocks
	r0.1 r0.1
	r01 0.4 r01 +              | rot y tam
	r01 2* r01 2*
	+layerfx
	;

|-----------------------

#vx

:principal
	inigui
	'exit >esc<
	[ 0.1 'vx ! ; ] <ri>
	[ -0.1 'vx ! ; ] <le>
	[ 0 'vx ! ; ] dup >ri< >le<
	[ 1 'varb +! ; ] <up>
	[ -1 'varb +! ; ] <dn>
	[ +rocks ; ] <esp>
	randmap
	show clrscr
		1.0 3dmode

	|***** camara
|		1.0 late mrotx
|		0.01 pinpon  mroty
|		0.6 late mrotz
		0 0 16.0 |1.0 pinpon +
				mtrans

	|*** actores
|		'dib2 3dnsprite
		drawmapa

		'layerfx layerdraw

	vx 'x +!
	x 0 0 mtransi
|	0.01 pinpon mrotxi
    'multisprite drawmultisprite
|	0.1 -0.1 'multisprite ms.move
	-0.001 0.001 'multisprite 2 s.move
	0.5 'multisprite 2 s.rot


	'lunar mulispr
	0.1 softscroll
	-0.0164	'lunar
		2dup 0 sr.rot
		2dup 1 sr.rot
		2dup 3 sr.rot
		4 sr.rot


	|*** debug
		scr fonti home verde
		varb vara "a:%f b:%f" print cr
		dup "%d" print
		cmano ;

: 33 principal ;
