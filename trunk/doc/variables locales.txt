VERSION CON VARIABLES

|-----------------[ dibujar lineas ]---------------
:colorcursor
	ink@ >r
	modo 'lins =? ( blanco )( rojo ) drop
	printcur
	r> ink ;

#_color 0

:colores | adr c col -- adr c
	drop 33 <? ( ; )
	$7c =? ( gris 1 '_color !  ; )	| $7c |	 Comentario
	$22 =? ( azul 2  '_color !  ; )	| $22 "	 Cadena
	over 1- ?numero 1? ( 2drop amarillo )( drop
			over 1- ?fnumero 1? ( 2drop amarillo )(	verde ) ) drop
	$3A =? ( rojo )			| $3a :	Definicion
	$23 =? ( violeta )		| $23 #  Variable
	$5e =? ( amarillo )		| $5e ^  Include
	$27 =? ( cyan ) 			| $27 '	Direccion

	3 '_color ! ;

:yacolor | adr c col -- adr c
	1- 0? ( drop ; )
	1- 0? ( drop $22 <>? ( ; ) )( drop 32 >? ( ; )  )
	0 '_color ! ;

:drawcode
|	28 fontl
	25 sfont
| 	fonti
	0 '_color !
	pantaini>
	( inisel >=? ( finsel <=? ( ink@ >r $4444ff ink printcur r> ink ) )
	fuente> =? ( blink 1? ( drop colorcursor )( drop )  )
		c@+ 1? )(
			$d =? ( 0 '_color ! )( _color 0? ( colores )( yacolor ) )
			emit emitout? 1? ( 2drop ; ) drop | corta cuando sale de ventana
			) 2drop  ;


VERSION SIN VARIABLE

:lineacom | adr c -- adr++
	(	13 =? ( emit ; )
		emit c@+ 0? )
	drop 1- ;

:palstr | adr c -- adr++
	(	emit c@+
		34 =? ( emit ; )
	  0? )
	drop 1- ;

:palabracol | adr c -- ar..
	32 =? ( emit ; )
	9 =? ( emit ; )
	verde
	$5e =? ( amarillo )			| $5e ^  Include
	$7c =? ( gris lineacom ; )	| $7c |	 Comentario
	$3A =? ( rojo )				| $3a :  Definicion
	$23 =? ( violeta )			| $23 #  Variable
	$22 =? ( blanco palstr ; )	| $22 "	 Cadena
	$27 =? ( cyan )				| $27 ' Direccion
	( 32 >? )( emit c@+ )
	drop 1- ;

:>>lineacolor>> | adr -- adr++
	( c@+ 1? )(
		13 =? ( emit ; )
		palabracol ) drop 1- ;

:drawcode2
	28 sfont
|	fonti
	12
	pantaini>
	0 ( pick2 <? )( swap
		>>lineacolor>>
		swap 1+ ) 3drop
	;
