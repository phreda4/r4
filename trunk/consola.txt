| consola r4
^reda4.txt
^gui.txt

#cmax 
#padi>	| inicio
#pad>	| cursor
#padf>	| fin

|---------------------------
:<<cr | a -- a
	( padi> >? )( dup c@ 13 =? ( drop ; ) drop 1- ) drop padi> 1- ;

:>>cr | a -- a
	( padf> <? )( dup c@ 13 =? ( drop 1- ; ) drop 1+ ) drop padf>  1- ;

|------------------- edita linea
:lins  | c --
	pad> dup 1- padf> over - 1+ cmove> 1 'padf> +!
:lover | c -- 
	$7f and
	pad> c!+ dup 'pad> !
	padf> >? ( dup 'padf> ! ) drop
:0lin | --
	0 padf> ! ;

:kdel
	pad> padf> >=? ( drop ; ) drop
	1 'pad> +!
:kback
	pad> padi> <=? ( drop ; )
	dup 1- swap padf> over - 1+ cmove -1 'padf> +! -1 'pad> +! ;

:khome
	pad> 1-	<<cr 1+ 'pad> ! ;

:kend
	pad> >>cr  1+ 'pad> ! ;

:karriba
	pad> padi> =? ( drop ; )
	1- <<cr 1- <<cr 1+
	'pad> ! ;

:kabajo
	pad> padf> >=? ( drop ; )
	>>cr 2 + padf> >? ( 1- )
	'pad> ! ;

#modo 'lins

:kder
	pad> padf> <? ( 1+ )  'pad> ! ; 
:kizq
	 pad> padi> >? ( 1- ) 'pad> !  ; 

::.cons | mem --
	dup dup 'padi> ! 'pad> !  'padf> !  
	0 padf> ! 'cmax !
	[ key toasc modo exec ; ] <visible>
|	[ 9 modo exec ; ] <tab>
	[ modo 'lins =? ( 'lover )( 'lins ) 'modo ! drop  ; ] <ins>
	'kback <back>	
	'kdel <del>
	'khome <home>	 'kend <end>
	'karriba <arr>	'kabajo <aba>
	'kder <der>		'kizq <izq>
|	[ 13 lins ; ] <enter>
	;

:drawtext |  --
	padi> ( c@+ 1? )( emit ) 2drop ;

:bcursor
	pad> dup c@ $80 xor swap c! ;

::cons.draw
	blink 1? ( bcursor )
	drawtext 	
	1? (  bcursor ) drop ;

::cons.clear
	padi> dup 'pad> !  'padf> !  0 padf> ! ;

::cons.set | adr --
	'pad> ! ;
	