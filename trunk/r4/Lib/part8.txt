| lista de 8 valores
|------ 

::p.ini | cantidad list --
	here dup rot !+ ! 5 << 'here +! ;

::p.clear | list --
	dup 4+ @ swap ! ;

::p!+ | 'act list -- adr
	dup >r @ !+
	32 r> +! ;

:delp | list end now -- list end- now-
	swap 32 - 2dup 8 move
	dup pick3 !
	swap 32 - ;

::p.draw | list --
	dup @+ swap @
	( over <? )(
		dup @+ exec 0? ( drop delp )
		32 + ) 3drop ;

::p.del | adr list --
	>r r @ 32 - 8 move r> dup @ 32 - swap ! ;

::p.mapv | 'vector list --
	@+ swap @
	( over <? )(
		pick2 exec
		32 + ) 3drop ;

|.......
:isin | x y 'end 'now -- x y end now/ x y now 0
	dup 4+ @ pick4 - abs 0.06 >? ( drop ; ) drop
	dup 8 + @ pick3 - abs 0.06 >? ( drop ; ) drop
	nip 0 ; | 'now 0

:pin | x y 'list -- x y 'how/x y 0
	@+ swap @
	( over <? )(
		isin 0? ( drop ; )
		32 + )
	2drop 0 ;

|........
:pmapmap | 'adr 'list 'vec --
	>r
	@+ swap @
	( over <? )(
		pick2 over <>? ( r exec )( drop )
		32 + )
	2drop rdrop ;

::p.map2 | 'vec 'list ---
	@+ swap @
	( over <? )(
		dup 32 + ( pick2 <? )(
			pick3 exec
			32 + ) drop
		32 + )
	3drop ;
