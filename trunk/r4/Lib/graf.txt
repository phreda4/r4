| libreria de graficos y cursor
| PHREDA
|---------------------------------------
^r4/lib/math.txt
^r4/lib/colores.txt
^r4/lib/gc.txt


|----- figuras geometricas
::cajau | x2 y2 x1 y1 --
	ink@ >r
	oscuro over pick3 op 2dup line pick3 over line
	r ink claro 2over line drop swap line drop
	r> ink ;

::cajad | x2 y2 x1 y1 --
	ink@ >r
	claro over pick3 op 2dup line pick3 over line
	r ink oscuro 2over line drop swap line drop
	r> ink ;

::caja | x2 y2 x1 y1 --
	over pick3 op 2dup line pick3 over line
	2over line drop swap line drop ;

::cajaf | x2 y2 x1 y1 --
	over pick3 op 2dup pline pick3 over pline
	2over pline drop swap pline drop poli ;

|---------------------------------
::hline | x y x --
	over op line ;
::vline | x y y --
	r> over r> op line ;

::box | x y r --
	>r r - swap r - swap 2dup 2dup op r 2* + 2dup line
	swap r 2* + swap 2dup line r> 2* - line line ;

::cruz | x y r --
	>r
	over r - over op over r + over line
	2dup r - op r> + line ;


::vfill2 | ancho alto x y --
	2swap >r >r
	2dup op over r> + over 2dup pline r + pline
	2dup r> + pline pline poli ;

::vfill | w h x y --
	rot ( 1? )( 1- >r 2dup setxy
		1+  pick2 ( 1? )( 1-
			ink@ px!+ ) drop r> ) 4drop ;

::fbox2 | xc yc r --
	2/ dup rot swap - | xc r y
	rot pick2 - swap | r x y
	rot dup + dup 2swap vfill2
	;

::fbox | xc yc r --
	>r r - swap r - swap 2dup 2dup op r 2* + 2dup pline
	swap r 2* + swap 2dup pline r> 2* - pline pline poli ;

|::circle | xc yc r --
|	>r r - 2dup 2dup op	swap r - swap 2dup cp
|	r + 2dup curve	r + 2dup cp swap r + swap 2dup curve
|	swap r + swap 2dup cp r - 2dup curve r> - cp curve ;

::circle | xc yc r --
	>r r - 2dup 2dup op	swap r - swap 2dup
	r + 2swap 2over curve
	r + 2dup over r + over 2swap 2over curve
	swap r + swap 2dup r - 2swap 2over curve r> - curve ;


|::fcircle | xc yc r --
|	>r r - 2dup 2dup op swap r - swap 2dup cp
|	r + 2dup  pcurve r + 2dup cp	swap r + swap 2dup pcurve
|	swap r + swap 2dup cp r - 2dup
|	pcurve r> - cp pcurve poli ;

::fcircle | xc yc r --
	>r r - 2dup 2dup op	swap r - swap 2dup
	r + 2swap 2over pcurve
	r + 2dup over r + over 2swap 2over pcurve
	swap r + swap 2dup r - 2swap 2over pcurve r> - pcurve poli ;

::point
	4 fcircle
	;

|Procedure Circulo(x1,y1,R:integer);
|Var  x,y,delta:integer;
|delta:=3-2*R;
|x:=0;y:=R;
|while (x<=y) do
|  Punto(x+x1,y+y1);
|  Punto(-x+x1,y+y1);
|  Punto(x+x1,-y+y1);
|  Punto(-x+x1,-y+y1);
|  Punto(y+x1,x+y1);
|  Punto(-y+x1,x+y1);
|  Punto(y+x1,-x+y1);
|  Punto(-y+x1,-x+y1);
|  if (delta<0) then
|   inc(delta,(x shl 2)+6)
|  else
|    inc(delta,((x-y) shl 2)+10);
|    dec(y);
|  inc(x);

|::ellipse | xc yc rx ry --
|	>r >r r - 2dup 2dup op
|	swap r> r> dup >r swap >r  - swap 2dup cp
|	r + 2dup curve	r + 2dup cp
|	swap r> r> dup >r swap >r + swap 2dup curve
|	swap r> r> dup >r swap >r + swap 2dup cp r - 2dup curve r> - cp curve rdrop ;

::ellipse | xc yc rx ry --
	2swap | rx ry xc yc --
	over over pick4 - op
	over pick4 + over pick4 - over pick3 curve
	over pick4 + over pick4 + pick3 over curve
	over pick4 - over pick4 + over pick3 curve
	over pick4 - over pick4 - over pick3 curve
	4drop ;

|------- Grilla
:hlinea | x --
	dup yc h 2/ - op yc h 2/ + line ;
:vlinea | y --
	dup xc w 2/ - swap op xc w 2/ + swap line ;

::rgrilla | escala --
	0? ( drop ; )
	w over / h pick2 / rot 2/ 1+
	( 1? )( 1-
		pick2 over * dup xc + hlinea neg xc + hlinea
		over over * dup yc + vlinea neg yc + vlinea
		) 3drop ;

::gctexdraw | 'tex --
	tfill
	w h min dup 'h ! 'w !
	gc.all gc.fbox sfill ;

