| fracciones
| PHREDA 2014
|
| #f 1 3 | 1/3

^r4/lib/math.txt

::f@ | 'var -- n d
	@+ swap @ ;

::f! | n d 'var --
	>r swap r!+ r> ! ;

::gcd | n1 n2 -- gcd
	( 1? )(	swap over mod ) drop ;

::fsimp | n1 n2 -- n1 n2
	2dup gcd >r r / swap r> / swap 
	-? ( swap neg swap neg ) ;

::f+ | n1 d1 n2 d2 -- n3 d3
	>r rot r *		| d1 n2 n1*d2
	swap pick2 * +	| d1 n1*d2+n2*d1
	swap r> *
	fsimp ;

::f- | n1 d1 n2 d2 -- n3 d3
	>r rot r *		| d1 n2 n1*d2
	swap pick2 * -	| d1 n1*d2-n2*d1
	swap r> *
	fsimp ;

::f* | n1 d1 n2 d2 -- n3 d3
	>r rot * swap r> * fsimp ;

::f/ | n1 d1 n2 d2 -- n3 d3
	swap f* ;

::fpow | n1 d1 p -- n2 d2
	rot over pow rot rot pow fsimp ;

::froot | n1 d1 r -- n2 d2
	rot 16 << over 16 << root. 0.5 + 16 >> rot 16 << rot 16 << root. 0.5 + 16 >> fsimp ;

::f2fix | n1 d1 -- fix
	1 <=? ( drop 16 << ; )
	16 <</ ;
