|------------------------------
| gui.txt - PHREDA
| Immediate mode gui for :r4
|------------------------------
^r4/lib/gc.txt
^r4/lib/parse.txt
^r4/lib/color.txt
^r4/lib/cursor.txt
^r4/lib/font.txt
^r4/lib/show.txt

|--- state
#active
#hot	#hotnow
#foco	#foconow
#pbmouse
#evUp #evDn

|--- id
#id		| id gui actual
#idf	| id hot actual
#idl	| id foco ultimo

|---  Edita linea
#cmax
#padi>	| inicio
#pad>	| cursor
#padf>	| fin

|---------
::gui.debug
	idf id "ID:%d IDF:%d " print cr
	foconow foco "foco:%d now:%d" print cr
	hotnow hot "hot:%d now:%d" print cr
	active "active:%d " print cr
    idl foconow foco "foco:%d foconow:%d idl:%d" print cr

|---debug texto
|	cmax "cmax%d " print cr
|	padi> "padi>	| inicio%d " print cr
|	pad> "pad>| cursor%d " print cr
|	padf> "padf>| fin%d " print cr
	;

:apreto
	hotnow dup 'active ! 'evDn ! ;

:solto
	active 0? ( drop ; )
	hotnow =? ( 'evUp ! )( drop )
	0 'active !
	;

:guistart
	idf 'idl !	| ultimo
	hot 'hotnow !

	0 'idf !	| primero foco
	0 'id !     | primero hot
	0 'hot !
	0 'evUp !
	0 'evDn !

	bmouse pbmouse =? ( drop ; )
	0? ( solto )( apreto ) 'pbmouse !
	;

|----- esto
::clrscr
	cls scr home guistart ;

::xfb>scr
	xfb> scr home guistart ;

::usogui
	0 'active !
	0 'foco  ! -1 'foconow !
	;

|----- o esto
::showgui
	msec dup 'lastime !+ !
	0 '.exit !
	( ( .exit 0? )( drop
		10 update drop
		guistart
		r exec
		redraw )
	-1 =? ( drop rdrop 0 '.exit ! ; )
	|zoomin
	exec
	|zoomut
	drop rdrop
	0 '.exit !
	0 key!
	) ;

::exit
	-1 '.exit !
	0 'foco  !
	-1 'foconow !
	0 key!	;

::exec!
	'.exit ! ;

|---- pre (requiere estar antes de guiBtn o guiMap
::guiInPre | 'vec --
	id 1+ hotnow <>? ( 2drop ; ) drop exec ;

::guiInOutPre | 'vecin 'vecout --
	id 1+ hotnow =? ( 2drop )( drop nip ) exec ;

::guiIn? | -- 1/0=yes
	id 1+ hotnow - ;

|-- boton
::guiBtn | 'click --
	1 'id +!
	id
	evUp =? ( drop exec ; )
	xymouse whin 0? ( 3drop ; ) drop
	nip 'hot ! ;

::guiBtn2 | 'click --
	1 'id +!
	id
	evUp =? ( drop exec ; )
	xymouse whin 0? ( 3drop ; ) drop
	nip 'hot ! ;

::guiOut | 'out --
	1 'id +!
	xymouse whin 1? ( 2drop ; ) drop
	exec ;

|-- move
::guiMove | 'move --
	1 'id +!
	xymouse whin 0? ( 2drop ; ) drop
	id dup 'hot !
	active =? ( drop exec ; )
	2drop
	;

|-- dnmove
::guiDnMove | 'dn 'move --
	1 'id +!
	xymouse whin 0? ( 3drop ; ) drop
	id dup 'hot !
	active =? (
		evDn =? ( 2drop exec ; )
		drop nip exec ; )
	3drop ;

|-- mapa
::guiMap | 'dn 'move 'up --
	1 'id +!
	xymouse whin 0? ( 4drop ; ) drop
	id dup 'hot !
	evUp =? ( drop nip nip exec ; )
	active =? (
		evDn =? ( 3drop exec ; )
		2drop nip exec ; )
	4drop ;

::guiEmpty | --		; si toca esta zona no hay interaccion
	1 'id +!
	xymouse whin 1? ( id 'hot ! )
	drop ;


|---------------------------
::onLineMove | 'vec --
	xymouse whin 0? ( 2drop ; ) drop
	bmouse 0? ( 2drop ; ) drop
	xymouse
	swap tx1 <? ( 3drop ; ) tx2 >? ( 3drop ) drop
	ccy <? ( 2drop ; )
	ccy cch + >? ( 2drop ; )
	drop
	exec ;

|---------------------------------------------------
| manejo de foco (teclado)

::nextfoco
	foco 1+ idl >? ( 0 nip ) 'foco !
	0 key!
	;

::prevfoco
	foco 1- 0 <=? ( idl nip ) 'foco !
	0 key!
	;

::setfoco | nro --
	'foco ! ;

::ktab
	mshift 0? ( nextfoco )( prevfoco ) drop ;

::clickfoco
	idf foco =? ( drop ; ) 'foco ! ;

::clickfoco1
	idf 1+ 'foco ! -1 'foconow ! ;

::refreshfoco
	-1 'foconow ! ;

::w/foco | 'in 'start --
	idf 1+
	foco 0? ( drop dup dup 'foco ! ) | quitar?
	<>? ( 'idf ! 2drop ; )
	foconow <>? ( dup 'foconow ! swap exec )( nip )
	'idf !
	exec ;

::focovoid | --
	idf 1+
	foco 0? ( drop dup dup 'foco ! ) | quitar?
	<>? ( 'idf ! ; )
	foconow <>? ( dup 'foconow ! )
	'idf ! ;

::esfoco? | -- 0/1
	idf 1+ foconow =? ( 1 )( 0 ) nip ;

::in/foco | 'in --
	idf 1+
	foco 0? ( drop dup dup 'foco ! )
	<>? ( 'idf ! drop ; )
	'idf !
	exec ;

::bordefoco
	ink@
	blanco 1 dup 'w +! 'h +! gc.rod
	negro 1 dup 'w +! 'h +! gc.rod
	1 dup 'w +! 'h +! gc.rod
	ink
	-3 dup 'w +! 'h +!
	;

 | no puedo retroceder!
::lostfoco | 'acc --
	idf 1+ foco <>? ( 'idf ! drop ; ) 'idf !
	exec
	nextfoco
	;

|-------  Edita linea
:lins  | c --
	padf> padi> - cmax >=? ( 2drop ; ) drop
	pad> dup 1- padf> over - 1+ cmove> 1 'padf> +!
:lover | c --
	pad> c!+ dup 'pad> !
	padf> >? (
		dup padi> - cmax >=? ( swap 1- swap -1 'pad> +! ) drop
		dup 'padf> ! ) drop
:0lin | --
	0 padf> c! ;
:kdel
	pad> padf> >=? ( drop ; ) drop
	1 'pad> +!
:kback
	pad> padi> <=? ( drop ; )
	dup 1- swap padf> over - 1+ cmove -1 'padf> +! -1 'pad> +! ;
:kder
	pad> padf> <? ( 1+ )  'pad> ! ;
:kizq
	 pad> padi> >? ( 1- ) 'pad> !  ;

#modo 'lins

|----- getline
:dcursor
	blink 1? ( drop ; ) drop
	modo 'lins =? ( verde )( rojo ) oscuro drop
	printcur 1- ;

:inigetline | 'var max 'dr IDF -- 'var max 'dr IDF
	pick2 'cmax !
	pick3 dup 'padi> !
	( c@+ 1? )( drop ) drop 1- dup
	'pad> ! 'padf> !
	'lins 'modo !
	;

:drawcursorl
	padi> ( pad> =? ( dcursor drop ; ) c@+ 1? )( gemit ) 2drop ;

:drgetline | --
	2 dup 'h +! 'w +!
	gc.fbox
	ccx drawcursorl 'ccx !
	negro

	[ key toasc modo exec ; ] <visible>
	[ modo 'lins =? ( 'lover )( 'lins ) 'modo ! drop  ; ] <ins>
	'kback <back>	'kdel <del>
	'kder <ri>		'kizq <le>
	[ padi> 'pad> ! ; ] <home>
	[ padf> 'pad> ! ; ] <end>
	'ktab dup <tab> <enter>
	'nextfoco <dn> 'prevfoco <up>
	;

|------- GETLINE
::getline | 'var max --
	gc.push
	cntprintbox
	2 dup 'w +! 'h +!
	blanco gc.box
	-2 dup 'w +! 'h +!
	'drgetline 'inigetline w/foco
	'clickfoco guiBtn
	drop
	ccx w + >r
	printx blanco
	gc.pop
	r> 'ccx !
	;

:inigetlines | 'var max 'dr IDF -- 'var max 'dr IDF
	pick2 'cmax !
	pick3 dup 'padi> !
	( c@+ 1? )( drop ) drop 1- dup
	'pad> ! 'padf> !
	'lins 'modo !

	[ key toasc modo exec ; ] <visible>
	[ modo 'lins =? ( 'lover )( 'lins ) 'modo ! drop  ; ] <ins>
	'kback <back>	'kdel <del>
	'kder <ri>		'kizq <le>
	[ padi> 'pad> ! ; ] <home>
	[ padf> 'pad> ! ; ] <end>
	;

|------- INPUTLINE (sin foco)
::inputline | 'var max --
	'drgetline 0 inigetlines
	3drop
	( pad> =? ( dcursor ) c@+ 1? )( emit )
	2drop ;

|------ getmemo
:inigetmemo | 'var max 'dr IDF -- 'var max 'dr IDF
	pick2 'cmax !
	pick3 dup 'padi> !
	( c@+ 1? )( drop ) drop 1- dup
	'pad> ! 'padf> !
	'lins 'modo !
	;

:cursormemo
	padi> ( pad> =? ( dcursor drop ; ) c@+ 1? )(
		allowcrx gemit ) 2drop ;

:drgetmemo | --
	2 dup 'h +! 'w +!
	gc.fbox
	ccy ccx cursormemo 'ccx ! 'ccy !

	[ key toasc modo exec ; ] <visible>
	[ modo 'lins =? ( 'lover )( 'lins ) 'modo ! drop  ; ] <ins>
	'kback <back>	'kdel <del>
	'kder <ri>		'kizq <le>
	[ padi> 'pad> ! ; ] <home>
	[ padf> 'pad> ! ; ] <end>
	'ktab <tab>
	[ 13 modo exec ; ] <enter>
	;

|------- GETMEMO
::getmemo | 'var max --
	tgc.push
	memprintbox
	2 dup 'w +! 'h +!
	blanco gc.box
	-2 dup 'w +! 'h +!
	gc2win
	'drgetmemo 'inigetmemo w/foco
	'clickfoco guiBtn
	drop
	ccx w + >r
	negro text
	tgc.pop
	r> 'ccx !
	;

|-------- SLIDES
::hslide | 'var --
	[ xymouse drop xc - 16 << w / over ! ; ] guiMove
	gc.push 0.98 0.2 fscala
	gc.frod gc.popush
	@ w 16 *>> 'xc +!
 	0.1 0.98 fscala ink@
	$dddddd $222222 vbtn
	gc.pop ink
	;

::vslide | 'var --
	[ xymouse nip yc - 16 << h / over ! ; ] guiMove
	gc.push 0.2 0.98 fscala
	gc.frod gc.popush
	@ h 16 *>> 'yc +!
 	0.98 0.1 fscala ink@
	$dddddd $222222 hbtn
	gc.pop ink
	;

