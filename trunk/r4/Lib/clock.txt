| Reloj y Almanaque
| PHREDA 2011
|-----------------------
^r4/lib/fonta.txt
^r4/lib/polygr.txt

|--------- reloj
:aguja | ang largo --
	polar
	xc pick2 3 >> - yc pick2 3 >> + op
	xc rot + yc rot - line ;

::clock | --
	blanco
	w h min 2/
	0 ( 1.0 <? )(
		dup pick2 polar
		swap xc + swap yc + op
		dup pick2 dup 4 >> - polar
		swap xc + swap yc + line
		0.0834 + ) drop
	4 - >r
	time | s m h --
	rojo 1.0 12 */ r 2/ aguja
	verde 1.0 60 */ r dup 2 >> - aguja
	blanco 1.0 60 */ r> aguja ;


::agujaex | ang largo --
	polar
	xc pick2 3 >> - yc pick2 3 >> + gop
	xc rot + yc rot - gline ;

:circle | largo -- largo
	0 dup pick2 polar swap xc + swap yc + op
	( 1.0 <? )( 0.001 +
		dup pick2 dup 4 >> - polar swap xc + swap yc + pline
		) drop
	poli ;

::clockexp
	w h min 2/
	negro 8 + circle
	blanco 16 - circle
	negro 3 linegr!
	0 ( 1.0 <? )(
		dup pick2 polar swap xc + swap yc + gop
		dup pick2 dup 3 >> - polar swap xc + swap yc + gline
		0.0834 + ) drop
	8 - >r
	time | s m h --
	4 linegr! 60 * over + 1.0 720 */ r 2/ agujaex
	3 linegr! 60 * over + 1.0 3600 */ r dup 2 >> - agujaex
	2 linegr! rojo 1.0 60 */ r> 32 - agujaex ;


|--------- almanaque
#meses "Enero" "Febrero" "Marzo" "Abril" "Mayo" "Junio" "Julio" "Agosto" "Septiembre" "Octubre" "Noviembre" "Diciembre"

::almanaque
	wgc.push
	fonta-arial10b
	gc2win home
	date rot
	rojo linefill blanco
	"%d" printc cr |cr2
	fonta-arial14b
	blanco linefill negro
	"%d" printc cr
	1- 'meses ( swap 1? )( 1- swap >>0 ) drop
	fonta-arial10
	blanco linefill negro
	printc
	wgc.pop
	;