|--------------------
| BTN.TXT PHREDA 2010
|--------------------
^r4/lib/gui.txt
^r4/lib/grpix.txt

^r4/lib/font.txt
^r4/lib/fontt.txt

^r4/lib/sprite.txt

^r4/lib/parse.txt

|--- BOTONES
:botonsimple | --
	ink@ $444444 over
	[ swap 4+gc ; ] guiInPre
	vbtn ink ;

:botonsimpleh | --
	ink@ $444444 over
	[ swap ; ] guiInPre
	hbtn ink ;

|-------- teclado para boton
:bordeyfoco
    bordefoco
:keys-btn | 'acc xx --  ; boton con foco
	dup <enter>
	'ktab <tab>
	'nextfoco dup <dn> <ri>
	'prevfoco dup <up> <le>
	;

::.btnt
	gc.push
	ccw 'ccx +!
	print2gc
	ccw 2* 'w +!
	cch 2/ 'h +!
	botonsimple
	ink@ >r blanco printx r> ink
	'bordeyfoco in/foco
	guiBtn
	ccw 'ccx +!
	gc.pop ;

::.btnd | acc 'v8d --
	gc.push
	botonsimple
	ink@ >r blanco v8draw r> ink
	'bordeyfoco in/foco
	guiBtn
	gc.pop ;

::cmenu | 'vec "label" cnt --
	gc.push
	tx2 tx1 - ccw pick2 * dup 2/ + - 2/ tx1 + 'ccx !
	cntprintbox drop
	ccw 2* 'w +!
	cch 'h +!
	botonsimple
	ink@ >r blanco printc r> ink
	'bordeyfoco in/foco
	guiBtn
	ccw 'ccx +!
	gc.pop ;

::btnt | 'event "texto" --
	gc.push
	ccw 2/ 'ccx +!
	print2gc
	ccw 'w +!
	cch 2/ 'h +!
	botonsimple
	ink@ >r blanco printx r> ink
	guiBtn
	ccw 2/ 'ccx +!
	gc.pop
	;

::btntf | 'event "texto" cnt --
	gc.push
	cntprintbox drop ccx w + ccw +  >r
	ccw 'w +!
	cch 2/ 'h +!
	botonsimple
	ink@ >r blanco sp print r> ink
	guiBtn
	r> 'ccx !
	gc.pop
	;

::btnd | 'event 'dibujo --
	botonsimple
	ink@ swap blanco v8draw ink
	guiBtn
	;

::btnbox | 'event alto --
	gc.push
	dup 2/ dup
	ccx + 'xc !
	ccy + 'yc !
	dup 'w ! 'h !
	gc.fbox
	guiBtn
	gc.pop
	;

::btnm | 'vec "label" cnt --
	gc.push
	tx2 tx1 - ccw pick2 * dup 2/ + - 2/ tx1 + 'ccx !
	cntprintbox drop
	ccw 'w +!
	cch 2/ 'h +!
	botonsimple
	ink@ >r blanco printc r> ink
|	'bordefoco in/foco
	guiBtn
	ccw 'ccx +!
	gc.pop
	;

::btnv | 'event 'dibujo -- | dibujo
	gc.push
	cch dup 2* dup 'w ! 'h !
	2/ dup ccx + 'xc !
	ccy + 'yc !
	botonsimple
	ink@ swap blanco v8draw ink
	guiBtn
	cch dup 2/ + 'ccx +!
	gc.pop
	;

:btnv | 'event 'dibujo -- | dibujo
	gc.push
	cch dup 2* dup 'w ! 'h !
	2/ dup ccx + 'xc !
	ccy + 'yc !
	botonsimple
	ink@ swap blanco v8draw ink
	guiBtn
	cch dup 2/ + 'ccx +!
	gc.pop
	;

::btnve | var dibujos cnt --
	gc.push
	cch dup 2* dup 'w ! 'h !
	2/ dup ccx + 'xc !
	ccy + 'yc ! gris
	0 ( over <? )(
		pick3 @ =? ( 4+gc blanco )
		rot @+ v8draw rot rot
		pick3 @ =? ( 4-gc gris )
		[ dup pick4 ! ; ] guiBtn
		cch dup 2/ + 'xc +!
		1+ ) 4drop
	xc 'ccx !
	gc.pop
	;


::btnvs | var dibujos cnt --
	gc.push
	cch dup 2* dup 'w ! 'h !
	2/ dup ccx + 'xc !
	ccy + 'yc ! gris
	0 ( over <? )(
		pick3 @ =? ( 8+gc ccw neg 'yc +! blanco )
		rot @+ nsprite rot rot
		pick3 @ =? ( 8-gc ccw 'yc +! gris )
		[ dup pick4 ! ; ] guiBtn
		cch dup 2/ + 'xc +!
		1+ ) 4drop
	xc 'ccx !
	gc.pop
	;

|---------------------- Mas botones
::btnex | 'acc "leyenda" "titulo" --
	gc.push ink@ dup >r $222222
	[ swap ; ] [ 0.95 fzoom ; ] guiInOutpre
	vbtn
	gc2win home
	font-vard-12-bold-out
	cr2 printc cr cr2
	font-vard-8

	blanco linefill
	1 col negro print

	guiBtn
	r> ink gc.pop ;

::btnee | 'acc DB --
	gc.push ink@ dup >r $222222
	[ swap ; ] [ 0.95 fzoom ; ] guiInOutPre
	vbtn
	gc2win home
	font-vard-12-quad-out
	cr2 linefill
	dup "%a" printc cr cr2
	font-vard-8-bold-out
	>>f
	( dup c@ 1? )( drop
|		blanco linefill negro
		1 col dup "* %a" print >>s cr
		) 2drop
	gc.pop
	guiBtn
	r> ink ;

|--------- reloj
:aguja | ang largo --
	polar
	xc pick2 3 >> - yc pick2 3 >> + op
	xc rot + yc rot - line ;

::reloj | --
	blanco
	w h min 2/
	0 ( 1.0 <? )(
		dup pick2 polar
		swap xc + swap yc + op
		dup pick2 dup 4 >> - polar
		swap xc + swap yc + line
		0.0834 + ) drop
	4 - >r
	time | s m h --
	rojo 1.0 12 */ r 2/ aguja
	verde 1.0 60 */ r dup 2 >> - aguja
	blanco 1.0 60 */ r> aguja ;

|----------- win
#xy0
#ya #xa

::guiWin | 'acc 'aw -- ; aw= tipo x0 y0 w h titulo
	dup 'xy0 !
	[ xy0 @ 1? ( 0 )( 1 ) nip xy0 ! ; ] over 20 + "%s" sp btnt
	@+
	0? ( 3drop ; ) drop
	tgc.push
	@+ 'tx1 !
	@+ 'ty1 !
	@+ dup tx1 over 2/ over + 'xc ! + 'tx2 ! 'w !
	@+ dup ty1 over 2/ over + 'yc ! + 'ty2 ! 'h !

|--- mueve la ventana donde no hay otro btn
	[ xymouse 'ya !+ ! ; ]
	[ xymouse 2dup
		ya - xy0 8 + +!
		xa - xy0 4+ +!
		'ya !+ ! ; ]
	guiDnMove

|	guiEmpty |--- no mueve ventana

	ink@ >r
	gris
	gc.fbox
	tx1 ty1 tx2 ty2 cajau
	home cr2
	[ 0 xy0 ! ; ] "x" sp rojo btnt
	r> ink " %s" print
	cr cr2
	exec
|	ya xa "%d %d" print
	tgc.pop
	;
