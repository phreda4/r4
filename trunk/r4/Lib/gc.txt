|-------------------------------------
| cursor grafico  PHREDA 2009

|
| cursor centro (xc,yc) ancho (w) alto (h)
|
^r4/lib/math.txt
^r4/lib/grpix.txt

#:xc	| centro x
#:yc	| centro y
#:w		| width
#:h		| heigth


|--- posicion por pixels
::gc.top | alto --
	dup 'h ! 2/ 'yc ! ;

::gc.botton | alto --
	dup 'h ! sh swap 2/ - 'yc ! ;

::gc.vbetween | up dn --
	sh swap - over - dup 'h !
	2/ + 'yc ! ;

::gc.left | px --
	dup 'w ! 2/ 'xc ! ;

::gc.rigth | px --
	dup 'w ! sw swap 2/ - 'xc ! ;

::xymousef | -- x y
	xymouse
	yc - 17 << h / swap
	xc - 17 << w / swap neg ;

::fgetpixel | x y -- c
	sh 2/ swap over neg 16 *>>  + >r
	sw 2/ swap over 16 *>> + r>
	setxy px@
|	sw * +  -? ( -1 nip ; ) sw sh * >? ( -1 nip ; )
|	2 << framev + @
	;

::tox w 5 *>> xc + ;
::toy h 5 *>> yc + ;

#:ccx 0
#:ccy 0			| cursor
#:ccw 8
#:cch 12		| tamaño

::toxt ccw 5 *>> ccx + ;
::toyt cch 5 *>> ccy + ;

|---- pila de cursores
#pilacursor )( 1024
#pilacursor> 'pilacursor

::gc.push
	pilacursor> 'xc 4 move
	16 'pilacursor> +!
	;
::gc.pop
	-16 'pilacursor> +!
	'xc pilacursor> 4 move
	;
::gc.popush
	'xc pilacursor> 16 - 4 move
	;

|---- ubicacion y desplazamiento
::pos	| xy --
	'yc ! 'xc ! ;
::+pos	| xy --
	'yc +! 'xc +! ;
::dim | wh --
	'h ! 'w ! ;
::+dim | wh --
	'h +! 'w +! ;

::cubo | v --
	dup 'w !+ ! ;

::scr |  --   ; toda la pantalla es el cursor
	sw dup 2/ 'xc ! 'w !
	sh dup 2/ 'yc ! 'h !
	;

::gcxyxy | x1 y1 x2 y2 --
	pick2 over - 'w !
	pick3 pick2 - 'h !
	rot + 2/ 'xc !
	+ 2/ 'yc ! ;


|---- mover gc

::gc>>	w 'xc +! ;
::gc<<  w neg 'xc +! ;
::gcup  h neg 'yc +! ;
::gcdn	h 'yc +! ;

|----------------------------------
::fdim | wh --
	sh 16 *>> 'h !
	sw 16 *>> 'w ! ;

::fpos | xy --
	sh 2/ swap over neg 16 *>>  + 'yc !
	sw 2/ swap over 16 *>> + 'xc ! ;

::fzoom | rzoom -- ; relativo a su tamaño
	dup w 16 *>> 'w !
	h 16 *>> 'h !
	;

::fscala | ws hs --
	h 16 *>> 'h !
	w 16 *>> 'w ! ;


::gc.conv | w h xc yc --
	h 16 *>> 'yc +!
	w 16 *>> 'xc +!
	h 16 *>> 'h !
    w 16 *>> 'w ! ;

|----------------------------------
::@cursor | 'adr -- 'adr+
	@+ sw 2/ swap over 16 *>> + 'xc !
	@+ sh 2/ swap over neg 16 *>>  + 'yc !
	@+ sw 16 *>> 'w !
	@+ sh 16 *>> 'h ! ;

::mem>dimpos | adr --
	@+ 'xc ! @+ 'yc ! @+ 'w ! @ 'h !
	w xc 2dup + 2/ 'xc ! - 'w ! h yc 2dup + 2/ 'yc ! - 'h ! ;


|--- x y en cursor ?
::whin | x y -- 1/0
	yc - abs h 2/ >? ( 2drop 0 ; ) drop
	xc - abs w 2/ >? ( 0 )( 1 ) nip ;
|	yc - h 2/ + -? ( 2drop 0 ; ) h - +? ( 2drop 0 ; ) drop
|	xc - w 2/ + -? ( 0 nip ; ) w - +? ( 0 )( 1 ) nip ;


|------------------ dibujo sobre cursor
::gc.box		| borde recto
	w xc over 2/ - h yc over 2/ - rot swap | w h 0 0
	2dup op pick3 pick2 + dup pick2 line
	pick3 pick2 + line over pick3 pick2 + line line 2drop ;

::gc.fbox		| lleno recto
	w xc over 2/ - h yc over 2/ - rot swap | w h 0 0
	2dup op	pick3 pick2 + dup pick2 pline
	pick3 pick2 + pline over pick3 pick2 + pline pline poli 2drop ;

::gc.rod | --	; redondo
	w h min 3 >> >r xc w 2/ - yc h 2/ - r + 2dup 2dup op
	r - 2dup cp swap r + swap 2dup curve swap r 2* - w + swap 2dup line
	swap r + swap 2dup cp r + 2dup curve r 2* - h + 2dup line
	r + 2dup cp swap r - swap 2dup curve swap r 2* + w - swap 2dup line
	swap r - swap 2dup cp r> - curve line ;

::gc.frod | --	; lleno redondo
	w h min 3 >> >r xc w 2/ - yc h 2/ - r + 2dup 2dup op
	r - 2dup cp swap r + swap 2dup pcurve swap r 2* - w + swap 2dup pline
	swap r + swap 2dup cp r + 2dup pcurve r 2* - h + 2dup pline
	r + 2dup cp swap r - swap 2dup pcurve swap r 2* + w - swap 2dup pline
	swap r - swap 2dup cp r> - pcurve pline poli ;

::gc.fcircle
	xc w 2/ - yc 2dup op	| i2
	2dup h 2/ - 2dup cp     | i2 j2
	over w 2/ + over pcurve
	swap w + swap 2dup cp
	2dup h 2/ + pcurve
	h + 2dup cp
	over w 2/ - over pcurve
	swap w - swap cp
	pcurve poli ;

|--- Ajusta mat de colores p/degrade
::gc.all
	xc w 2/ - yc h 2/ - fcen
	2.0 w / 0 fmat
	;

::gc.vdeg | col1 col2 --
	fcol
	xc yc h 2/ + fcen
	0 1.0 h / fmat ;

::gc.hdeg | col1 col2 --
	fcol
	xc w 2/ - yc fcen
	1.0 w / 0 fmat ;

::vbtn | col1 col2 --
	gc.vdeg lfill gc.frod sfill ;

::hbtn | col1 col2 --
	gc.hdeg lfill gc.frod sfill ;

::vfil | col1 col2 --
	gc.vdeg lfill gc.fbox sfill ;

::hfil | col1 col2 --
	gc.hdeg lfill gc.fbox sfill ;

|--- con pixels
::gc.vfill | colores --
	w swap degrade!
	xc w 2/ - yc h 2/ - .at
	w h .fboxdv ;

::gc.hfill | colores --
	h swap degrade!
	xc w 2/ - yc h 2/ - .at
	w h .fboxdh ;



