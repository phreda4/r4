| ShellSort
| PHREDA 2009
|-----------------------------------------
|^r4/lib/gui.txt

#trylist 2391484 797161 265720 88573 29524 9841 3280 1093 364 121 40 13 4 1 0
#pl

:xch | j i -- j i
	over 1- 3 << pl + 			| j i dj
	over 1- 3 << pl + dup >r	| j i dj di r:di
	@ over @ 					| j i dj vi vj
	r!+ swap !+					| j i dj+ r:di+
	r @ over @
	r> ! swap ! ;

:sort | len h i v j -- len h i
	( pick3 >? )(
      	dup pick4 - 		| len h i v j j-h
		dup 1- 3 << pl + @ 	| len h i v j j-h list[j-h]
		pick3 <=? ( 4drop ; )
		drop xch nip )
	2drop ;

::shellsort | len lista -- ; lista es valor-dato
	'pl !
	'trylist >r
	( r@+ 1? )( dup 			| len h i
		( pick2 <? )(
			dup 1- 3 << pl + @	| list[i]=v
			over 				| len h i v j
			sort 1+ )
		2drop )
	rdrop 2drop ;

:sort2 | len h i v j -- len h i
	( pick3 >? )(
      	dup pick4 - 		| len h i v j j-h
		dup 1- 3 << pl + 4+ @ 	| len h i v j j-h list[j-h]
		pick3 <=? ( 4drop ; )
		drop xch nip )
	2drop ;

::shellsort2 | len lista -- ; lista es dato-valor
	'pl !
	'trylist >r
	( r@+ 1? )( dup 			| len h i
		( pick2 <? )(
			dup 1- 3 << pl + 4+ @	| list[i]=v
			over 				| len h i v j
			sort2 1+ )
		2drop )
	rdrop 2drop ;


|#lista 5 5 4 4 8 8 3 3 1 1 6 6 7 7 8 8 9 9

|:test
|	show clrscr blanco
|		'lista 9 ( 1? )( 1- swap @+ "%d" print @+ "->%d" print cr swap ) 2drop 
|		'exit >esc<
|		[ 9 'lista shellsort ; ] <f1>
|		;

|: 33 test ;
