| :r4 base - bangle
| 0--360
| 0.0--1.0
| resultado = [0..1.0) [0..$ffff]
|---------------------------------

|------- punto fijo 16.16
::*.	| a b -- c
	16 *>> ;

::/.	| a b -- c
	16 <</ ;

::cos | v -- r
	$3fff +
::sin | v -- r
	$3fff + $8000 and? ( [ neg ; ] >r )
	$7fff and $3fff -
	6.2831 *. | radianes->bangle
	dup dup *. | f sqr
	dup 498 *. | f sqr resul
	10882 -
	swap *.
	1.0 +
	*. ;

| tan en radianes
::tan | v -- f
	$3fff +
	$7fff and $3fff -
	6.2831 *. | radianes->bangle
	dup dup *.
	dup 13323 *.
	20810 +
	swap *.
	1.0 +
	*. ;

::sincos | bangle -- sin cos
	dup sin swap cos ;

::xy+polar | x y bangle r -- x y
	>r sincos r 16 *>> rot + swap r> 16 *>> rot + swap  ;

::ar>xy | xc yc bangle r -- xc yc x y
	>r sincos r 16 *>> pick2 + swap r> 16 *>> pick3 + swap  ;

::polar | bangle largo -- dx dy
	>r sincos r 16 *>> swap r> 16 *>> swap ;

::atan2 | x y -- bangle
	swap
	-? ( [ neg ; ] >r neg )
	swap
	+? (		| absy x
		swap 2dup + >r - >r
		$1fff
        dup
	)(
		2dup - >r + >r
		$5fff
		$1fff
	)
	r> r>
	0? ( nip nip nip ; )
	*/ - ;

::distfast | dx dy -- dis
	-? ( neg ) swap -? ( neg ) swap
	over <? ( swap ) | min max
	dup 8 << over 3 << + over 4 << - swap 2* -
	over 7 << + over 5 << - over 3 << + swap 2* -
	8 >> ;

::average | x y -- v
	2dup xor 2/ rot rot and + ;

::min	| a b -- m
	over <? ( nip )( drop ) ;

::max	| a b -- m
	over >? ( nip )( drop ) ;

::sqrt. | n -- v
	1 <? ( drop 0 ; )
	0 0 rot | root remhi remlo | 15 + bits/2+1
	24 ( 1? )( 1- >r
		dup 30 >> $3 and	| ro rh rl rnh
		rot 2 << or			| ro rl rh
		swap 2 << swap
		rot 2* dup 2* 1+		| rl rh ro td
		pick2 <=? ( swap 1+ >r - r> )( drop ) | rl rh ro
		swap rot r> )
    3drop ;

::sqrti2 | n -- v
	1 <? ( drop 0 ; )
	0 swap | res op
	$40000000
	( over >? )( 2 >> ) | res op one
	( 1? )(
		pick2 over + | res op one r+o
		pick2 <=? (
			rot swap - | res one nop
			rot pick2 2* + | one nop nres
			swap rot
			)( drop )
		rot 2/
		rot rot 2 >> )
	2drop ;

