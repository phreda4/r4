| :r4 base - bangle
| 0--360
| 0.0--1.0
| resultado = [0..1.0) [0..$ffff]
|---------------------------------

|------- punto fijo 16.16
::*.	| a b -- c
	16 *>> ;

::/.	| a b -- c
	16 <</ ;

::cos | v -- r
	$3fff +
::sin | v -- r
	$3fff + $8000 and? ( [ neg ; ] >r )
	$7fff and $3fff -

	6.2831 *. | radianes->bangle
	dup dup *. | f sqr
	dup 498 *. | f sqr resul
	10882 -
	swap *.
	1.0 +
	*. ;

| tan en radianes
::tan | v -- f
	$3fff +
	$7fff and $3fff -

	6.2831 *. | radianes->bangle

	dup dup *.
	dup 13323 *.
	20810 +
	swap *.
	1.0 +
	*. ;

::sincos | bangle -- sin cos
	dup sin swap cos ;

::xy+polar | x y bangle r -- x y
	>r sincos r 16 *>> rot + swap r> 16 *>> rot + swap  ;

::ar>xy | xc yc bangle r -- xc yc x y
	>r sincos r 16 *>> pick2 + swap r> 16 *>> pick3 + swap  ;

::polar | bangle largo -- dx dy
	>r sincos r 16 *>> swap r> 16 *>> swap ;

::atan2 | x y -- bangle
	swap
	-? ( [ neg ; ] >r neg )
	swap
	+? (		| absy x
		swap 2dup + >r - >r
		$1fff
        dup
	)(
		2dup - >r + >r
		$5fff
		$1fff
	)
	r> r>
	0? ( nip nip nip ; )
	*/ - ;

::sqrt. | n -- v
	dup 65536 + 2/ | s n
	swap 2dup /. + 2/ swap
	swap 2dup /. + 2/ swap
	swap 2dup /. + 2/ swap
	swap 2dup /. + 2/ swap
	swap 2dup /. + 2/ swap
	swap 2dup /. + 2/ swap
	swap 2dup /. + 2/ swap
	swap 2dup /. + 2/ nip
	;

::distfast | dx dy -- dis
	-? ( neg ) swap -? ( neg ) swap
	over <? ( swap ) | min max
	dup 8 << over 3 << + over 4 << - swap 2* -
	over 7 << + over 5 << - over 3 << + swap 2* -
	8 >> ;

::average | x y -- v
	2dup xor 2/ rot rot and + ;

::min	| a b -- m
	over <? ( nip )( drop ) ;

::max	| a b -- m
	over >? ( nip )( drop ) ;
