| form.txt - Formularios para db
| PHREDA 2010
|---------------------
^r4/lib/fontt.txt
^r4/lib/fonta.txt

^r4/lib/btn.txt
^r4/lib/dbtabla.txt

|---------------- Grilla
#colums
#ancho
#factor
#xi
#tabcols )( 256
#tabcols>
#xf

#yi
#tabrows )( 256
#tabrows>
#yf

#col>

#xxi

|-------
::grid | grids cols ancho% --
	tx2 tx1 - 16 *>> 'ancho !
	tx2 tx1 - 2/ ancho 2/ - tx1 + 'xi !

	over >r
	0 over ( 1? )( 1- r@+ rot + swap )
	drop rdrop
	ancho 4 << swap / 'factor !
	dup 'colums !

	swap >r
	'tabcols xi rot		| grid xi cols
	( 1? )( 1- 			| adr xi cnt
		r@+ swap >r		| adr xi act
		factor * 4 >> +
		dup rot !+ 		| tab xi -- xi tab
		swap r> )
	drop nip 'xf !
	rdrop
	ccy 'yi !
	'tabrows 'tabrows> !
::rowg | --
	0 'col> !
	xi 'xxi !
	2 'ccy +!
	ccy cch + dup 'yf ! tabrows> !+ 'tabrows> !
	;

|-------------------
| #lista Act Pag c1 c2c3 .. 0
| cx $TLLFF
::grid2 | cols ancho --
	tx2 tx1 - 16 *>> 'ancho !
	tx2 tx1 - 2/ ancho 2/ - tx1 + 'xi !

	0 'colums !

	0 over 8 + ( @+ 1? )( | 0 dd aa
		8 >> $ff and rot + swap | 1 dd
		1 'colums +!
		) 2drop

	ancho 4 << swap / 'factor !

	'tabcols >r
	xi swap 8 + | xi cols
	( @+ 1? )( | i dd aa
		8 >> $ff and factor * 4 >> rot +
		dup r!+
		swap ) 2drop rdrop
	'xf !
	ccy 'yi !
	'tabrows 'tabrows> !
	rowg ;

|-------------------
::gfill | color --
	ink@ swap ink
	xi ccy 2dup 1- op cch + pline
	xf ccy 2dup cch + pline 1- pline
	poli ink ;

:xxf | -- xf
	col> dup 1+ 'col> !
	2 << 'tabcols + @ ;

:centerprint | "" x2 x1 --
	'ccx ! 'xxi !
	mprint sizeprint
	xxi ccx - swap - 2/ 'ccx +!
	printx ;

:rigthprint | "" x2 x1 --
	drop 'xxi !
	mprint sizeprint
	xxi swap - 'ccx !
	printx ;

:leftprint | "" x2 x1 --
	'ccx ! 'xxi !
	print ;

::gcell | "texto" --
	xxf xxi 4+ leftprint ;

::gcellc | "texto" --
	xxf xxi centerprint ;

::gcellr | "texto" --
	xxf xxi 4 - rigthprint ;

::$grid | --
	'xi colums 1+ ( 1? )( 1- swap
		@+ dup yi op yf line
		swap ) 2drop
	'yi ( tabrows> <? )(
		@+ xi over op xf swap line
		) drop ;


| 'filas -> 4 9 8 0
|			[ dup pick3 ! ; ] onLineMove | 'vec --
|			pick2 @ =? ( $ff gfill blanco )( $ffffff gfill negro )

::strgrilla | "" 'filas ancho--
	tx2 tx1 - 16 *>> 'ancho !
	tx2 tx1 - 2/ ancho 2/ - tx1 + 'xi !

	0 over 1 >r  | "" 'filas 0 'filas
	( @+ 1? )( rot + swap r> 1+ >r ) 2drop | cnt filas
	ancho 4 << swap / 'factor !
	r> 1- 'colums !

	xi over 'tabcols >r | "" 'filas xi 'filas
	( @+ 1? )(
		factor * 4 >> rot +
		dup r!+
		swap ) drop rdrop
	swap 'xf !
	ccy 'yi !
	'tabrows 'tabrows> !
	rowg
	$cccccc gfill negro
	( dup c@ 1? )( drop
			dup gcellc
			( c@+ 1? )( drop ) drop
			) 3drop cr
	dup c@ 0? ( 2drop $grid ; ) drop
	rowg
	dup ( c@+ 1? )(
		$7c =? ( pick2 "%a" mprint gcellc  rot drop over swap cr rowg )
		$5f  =? ( pick2 "%a" mprint gcellc  rot drop over swap )
		drop ) 2drop
	"%a" mprint gcellc
	$grid 	;

|----- formulario 1
:t0	4 >> ( 1? )( cr 1- ) drop ; | cr's
:t1 4 >> dbprint sp sp ;
:t2 4 >> dup
	dbtitle cr
	dbedit cr cr2 ;
:t3
:t4
:t5 drop ;
:t6 4 >> dup dbtitle cr dup dbedit sp ink@ >r
	verde [ date "%d/%d/%d" mprint over dbset ; ] " Hoy " btnt drop cr cr2 r> ink ;
:t7
:t8
:t9
:ta
:tb
:tc
:td
:te
:tf drop ;

#accf t0 t1 t2 t3 t4 t5 t6 t7 t8 t9 ta tb tc td te tf

::form | 'form --
	( @+ 1? )(
    	dup $f and 2 << 'accf + @ exec
		) 2drop ;

|----- formulario 2
:c0	4 >> ( 1? )( cr 1- ) drop ; | cr's
:c1 4 >> dbprint sp sp ;
:c2 4 >> dup
	sp |font-vard-8
	fonta-dejavu10
	dbtitle	cr 2 'ccy +!
	fonta-dejavu14
	sp dbedit ;

:c3 4 >> dup
	sp |font-vard-8
	fonta-dejavu10
	dbtitle	cr 2 'ccy +!
	fonta-dejavu14 |	font-vard-12
	sp dbprint ;

:c4 4 >> dup
	sp |font-vard-8
	fonta-dejavu10
	dbtitle	cr 2 'ccy +!
	fonta-dejavu14 |font-vard-12
	sp dbeditmemo cr cr cr ;

:c5 drop ;
:c6 4 >> dup
	sp |font-vard-8
	fonta-dejavu10
	dbtitle
	ink@ >r verde [ date "%d/%d/%d" mprint over dbset ; ] " Hoy " link r> ink
	cr 2 'ccy +!
	fonta-dejavu14 |font-vard-12
	dup sp dbedit
	drop ;
:c7
:c8
:c9
:ca
:cb
:cc
:cd
:ce drop ;
:cf drop cr cr2 ;

#accf2 c0 c1 c2 c3 c4 c5 c6 c7 c8 c9 ca cb cc cd ce cf

::form2 | 'form --
	( @+ 1? )(
    	dup $f and 2 << 'accf2 + @ exec
		) 2drop ;

