| dialogos comunes gui
| PHREDA 2010
|---------------
^r4/lib/gui.txt
^r4/lib/fontt.txt
^r4/lib/color.txt

||||||||||||||||||||
||  Dialogo Color
#:coloractual $ffffff
#vhue #yluma #xluma
#colorrampa $ff0000

#dl.c
:cl.set dl.c not 'dl.c ! ;

:col! | color --
	dup col.clapiz! 'coloractual ! ;

:vhue! | nro --
	dup 'vhue !
	8 <<  1.0 1.0 hsv2rgb 'colorrampa !
	xluma yluma getpixel col! ;

:xyluma! | x y --
	2dup
	getpixel col!
	'yluma ! 'xluma ! ;

#col.ram
::dlg.color | --
	coloractual ink
	'cl.set sp 30 btnboxb
	dl.c 0? ( drop ; ) drop
	-30 'ccx +!

	gris
	304 290
	ccx ccy 290 -	| ini box
	rect
|	bmnoiseb | ancho alto x y --

	ccx 8 + ccy 284 -	| ini box
	over 128 + over 128 + 'yc ! 'xc ! 256 dup 'h ! 'w !
	256 ( 1? )( 1-
		dup 0 colorrampa lerpcol
		over dup 8 << dup 8 << or or
		degrade32!
		>r
		2dup setxy
		256 ( 1? )( 1- deg@+ px!+ ) drop
		1+ r> ) drop
	2drop
	xluma 0? ( yluma 0? ( ccx 8 + ccy 284 - xyluma! ) drop ) drop
    [ xymouse xyluma! ; ] onMove
	blanco
	xluma yluma pos
	6 dup dim
	gc.box

|	blanco
	ccx 272 + ccy 284 - vhue +
	over 32 + over op line

	ccx 280 + ccy 284 - | ini croma
	over 20 + over 128 + 'yc ! 'xc ! 256 'h ! 40 'w !
	256 ( 1? )( 1-
		255 over - 8 <<  1.0 1.0 hsv2rgb ink
		>r
		2dup op
		over 16 + over line
		1+ r> ) drop
	2drop
    [ xymouse nip ccy 284 - - vhue! ; ] onMove
|	colum blanco coloractual "%h" print
|	vhue " %h " print
|	yluma xluma "x:%d y:%d " print
|	colorrampa "%h" print
	30 'ccx +!
	;

|---------------------------------------------
||||||||||||||||||||
||  Dialogos Si No
#fl
:sifl 1 'fl ! exit ;
:nofl 0 'fl ! exit ;

#tt
:dsino | --
	inigui
	'nofl >esc<
	'sifl <spc>
	show
		xfb>scr
		0 0 fpos 0.6 0.5 fdim
		$ff $11 vbtn
		0.15 dup fdim -0.2 -0.3 fpos
		rojo oscuro
		'nofl 'ifin .btnd
		0.2 -0.3 fpos
		verde oscuro
		'sifl 'iok .btnd
		0.5 0.4 fdim 0 0 fpos
		font-vard-12-quad-out home
		cr cr cr tt printc cr cr
		cmano ;

::dlgsino | "" -- 0/1
	'tt !
	oscurecetodo
	>xfb
	dsino
	fl ;


||||||||||||||||||||
||  Dialogo FILES

#:filename )( 64

#dl.f
:fl.set dl.f not 'dl.f ! ;

::dlg.file | --
	coloractual ink
	'fl.set 'filename sp btnt
	dl.f 0? ( drop ; ) drop

	tgc.push
	128 256 gc.window
	gc.fbox home
	0 ( dup file 1? )(
		print cr
		1+ ) 2drop

	tgc.pop
	;

