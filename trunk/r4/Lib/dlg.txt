| dialogos comunes gui
| PHREDA 2010
|---------------
^r4/lib/btn.txt
^r4/lib/cursor.txt
^r4/lib/fontt.txt
^r4/lib/color.txt

||||||||||||||||||||
||  Dialogo Color
#:coloractual $ffffff
#vhue #yluma #xluma
#colorrampa $ff0000

:col! | color --
	dup col.clapiz! 'coloractual ! ;

:vhue! | nro --
	dup 'vhue !
	8 <<  1.0 1.0 hsv2rgb 'colorrampa !
	xluma yluma getpixel col! ;

:xyluma! | x y --
	2dup
	getpixel col!
	'yluma ! 'xluma ! ;

#col.ram

:lumagrid
	tx1 4+ ty1 4+
	256 ( 1? )( 1-
		dup 0 colorrampa lerpcol
		over dup 8 << dup 8 << or or
		degrade32!
		>r
		2dup setxy
		256 ( 1? )( 1- deg@+ px!+ ) drop
		1+ r> ) drop
	2drop
	xluma 0? ( yluma 0? ( tx1 4+ ty1 4+ xyluma! ) drop ) drop

	gc.push
	256 dup 'w ! 'h !
	tx1 132 + ty1 132 + 'yc ! 'xc !
    [ xymouse xyluma! ; ] guiMove
	gc.pop

	blanco
	xluma yluma pos
	6 dup dim
	gc.box
	;

|--- precalculo
#cromacol )( 1024

:cromaset
	'cromacol >r
	0 ( 256 <? )(
		dup 8 << 1.0 1.0 hsv2rgb r!+
		1+ ) drop
	rdrop ;

:cromadraw | x y --
	'cromacol >r
	256 ( 1? )( 1-
		r@+ ink
		pick2 pick2
		over 16 + over op line
		swap 1+ swap ) drop
	rdrop ;
|----

:cromaline
|	blanco
	tx1 262 + ty1 4+ vhue +
	over 32 + over op line

	tx1 270 + ty1 4+ | ini croma
	256 ( 1? )( 1-
		255 over - 8 <<  1.0 1.0 hsv2rgb ink
		pick2 pick2 over 16 + over op line
		swap 1+ swap ) drop
	2drop

	gc.push
	tx1 262 + 32 + 'xc !
	ty1 4+ 128 + 'yc !
	32 'w ! 256  'h !
    [ xymouse nip ty1 4+ - vhue! ; ] guiMove
	gc.pop
	;

:ventanacolor
	tgc.push
	300 300 gc.window
	gris gc.fbox
	guiEmpty
	home cr2

	lumagrid
	cromaline

|	colum blanco coloractual "%h" print
|	vhue " %h " print
|	yluma xluma "x:%d y:%d " print
|	colorrampa "%h" print

	tgc.pop
	;

#dl.c
:cl.set dl.c not 'dl.c ! ;

::dlg.color | --
	coloractual ink
	'cl.set sp 32 btnbox
	dl.c 1? ( ventanacolor ) drop
	;

|---------------------------------------------
||||||||||||||||||||
||  Dialogos Si No
#fl
:sifl 1 'fl ! exit ;
:nofl 0 'fl ! exit ;

#tt
:dsino | --
	inigui
	'nofl >esc<
	'sifl <spc>
	show
		xfb>scr
		0 0 fpos 0.6 0.5 fdim
		$ff $11 vbtn
		0.15 dup fdim -0.2 -0.3 fpos
		rojo oscuro
		'nofl 'ifin .btnd
		0.2 -0.3 fpos
		verde oscuro
		'sifl 'iok .btnd
		0.5 0.4 fdim 0 0 fpos
		font-vard-12-quad-out home
		cr cr cr tt printc cr cr
		cmano ;

::dlgsino | "" -- 0/1
	'tt !
	oscurecetodo
	>xfb
	dsino
	fl ;


||||||||||||||||||||
||  Dialogo FILES

#:filename "                "

#dl.f
:fl.set dl.f not 'dl.f ! ;

::dlg.file | archivo --
	"%w" mprint 'filename strcpy
	'fl.set 'filename sp btnt
	dl.f 0? ( drop ; ) drop

	tgc.push
	128 256 gc.window
	guiEmpty
	gc.fbox home
	0 ( dup file 1? )(
		print cr
		1+ ) 2drop

	tgc.pop
	;

