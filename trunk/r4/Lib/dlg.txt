| dialogos comunes gui
| PHREDA 2010
|---------------
^r4/lib/btn.txt
^r4/lib/cursor.txt
^r4/lib/fontt.txt
^r4/lib/color.txt

||||||||||||||||||||
||  Dialogo Color
#:coloractual $ffffff
#vhue #yluma #xluma
#colorrampa $ff0000

::col! | color --
	dup col.clapiz! 'coloractual ! ;

:vhue! | nro --
	dup 'vhue !
	8 <<  1.0 1.0 hsv2rgb 'colorrampa !
	tx1 xluma + ty1 yluma + getpixel col! ;

:xyluma! | x y --
	2dup
	getpixel col!
	ty1 - 'yluma ! tx1 - 'xluma ! ;

#col.ram

:lumagrid
	tx1 4+ ty1 28 +
	256 ( 1? )( 1-
		dup 0 colorrampa lerpcol
		over dup 8 << dup 8 << or or
		degrade32!
		>r
		2dup setxy
		256 ( 1? )( 1- deg@+ px!+ ) drop
		1+ r> ) drop
	2drop
	xluma 0? ( yluma 0? ( 4 28 xyluma! ) drop ) drop

	gc.push
	256 dup 'w ! 'h !
	tx1 128 + 4+ ty1 128 + 28 + 'yc ! 'xc !
    [ xymouse xyluma! ; ] guiMove
	gc.pop

	blanco
	tx1 xluma + ty1 yluma + pos
	6 dup dim
	gc.box
	;

|--- precalculo
#cromacol )( 1024

:cromaset
	'cromacol >r
	0 ( 256 <? )(
		dup 8 << 1.0 1.0 hsv2rgb r!+
		1+ ) drop
	rdrop ;

:cromadraw | x y --
	'cromacol >r
	256 ( 1? )( 1-
		r@+ ink
		pick2 pick2
		over 16 + over op line
		swap 1+ swap ) drop
	rdrop ;

|----
:cromaline
|	blanco
	tx1 262 + ty1 28 + vhue +
	over 32 + over op line

	tx1 270 + ty1 28 + | ini croma
	256 ( 1? )( 1-
		255 over - 8 <<  1.0 1.0 hsv2rgb ink
		pick2 pick2 over 16 + over op line
		swap 1+ swap ) drop
	2drop

	gc.push
	tx1 262 + 32 + 'xc !
	ty1 4+ 128 + 'yc !
	32 'w ! 256  'h !
    [ xymouse nip ty1 28 + - vhue! ; ] guiMove
	gc.pop
	;

#dlgwin 1 20 20 300 300 "Color"

:dlgvie
	lumagrid
	cromaline
	;

::dlg.color | --
	coloractual ink
	'dlgvie 'dlgwin guiWin
	;

::dlgColat! | x y --
	'dlgwin 8 + !
	'dlgwin 4+ ! ;

|---------------------------------------------
||||||||||||||||||||
||  Dialogos Si No

#tt
#salida

:sifl 1 'salida ! exit ;
:nofl 0 'salida ! exit ;

:dsino | --
	usogui
	show
		xfb>scr
		0 0 fpos 0.6 0.5 fdim
		$ff $11 vbtn

		0.15 dup fdim -0.2 -0.3 fpos
		rojo oscuro
		'nofl dup >esc< 'ifin .btnd

		0.2 -0.3 fpos
		verde oscuro
		'sifl dup <spc> 'iok .btnd

		0.5 0.4 fdim 0 0 fpos
		font-vard-12-quad-out home
		cr cr cr tt printc cr cr
		cmano ;

::dlgsino | "" -- 0/1
	'tt !
	oscurecetodo
	>xfb
	dsino
	salida ;


||||||||||||||||||||
||  Dialogo FILES

#:filename "                "

#dl.f
:fl.set dl.f not 'dl.f ! ;

::dlg.file | archivo --
	"%w" mprint 'filename strcpy
	'fl.set 'filename sp btnt
	dl.f 0? ( drop ; ) drop

	tgc.push
	128 256 gc.window
	guiEmpty
	gc.fbox home
	0 ( dup file 1? )(
		print cr
		1+ ) 2drop

	tgc.pop
	;


|---------------------------------
::dlgprinter
	oscurecetodo
	>xfb
	usogui
	show xfb>scr
		0 0 fpos 0.6 0.5 fdim
		$ff $11 vbtn
|		0.15 cubo -0.2 -0.3 fpos
		0.5 0.4 fdim 0 0 fpos
|		8 hfont
	font-vard-12-quad-out
		home cr cr
		"Imprimiendo..." printc cr cr
		"Espere un momento..." printc
		1 .segs exit ;
