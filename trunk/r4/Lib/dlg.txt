| dialogos comunes gui
| PHREDA 2010
|---------------
^r4/lib/btn.txt
^r4/lib/dlg.txt
^r4/lib/cursor.txt
^r4/lib/fontt.txt
^r4/lib/color.txt

#vhue #yluma #xluma
#colorrampa $ff0000

|---- Dialogo Color
#:dlg.color@ $ffffff

::dlg.color! | color --
	dup
	>rgbf
	rgb2hsv

	17 >> 'vhue !
	17 >> 'yluma !
	17 >> 'xluma !

:setcol | color --
	dup col.clapiz!
	'dlg.color@ ! ;

:vhue! | nro --
	dup 'vhue !
	8 <<  1.0 1.0 hsv2rgb 'colorrampa !
	tx1 xluma + ty1 yluma + getpixel setcol ;

:vhue!2 | nro --
	dup 'vhue !
	9 <<  1.0 1.0 hsv2rgb 'colorrampa !
	tx1 xluma + ty1 yluma + getpixel setcol ;

:xyluma! | x y --
	2dup
	getpixel setcol
	ty1 - 'yluma ! tx1 - 'xluma ! ;

#col.ram

:lumagrid
	tx1 4+ ty1 28 +
	256 ( 1? )( 1-
		dup 0 colorrampa lerpcol
		over dup 8 << dup 8 << or or
		degrade32!
		>r
		2dup setxy
		256 ( 1? )( 1- deg@+ px!+ ) drop
		1+ r> ) drop
	2drop
	gc.push
	tx1 4+ ty1 28 +	256 dup gc.xywh | x y w h
    [ xymouse xyluma! ; ] guiMove
	gc.pop

	blanco
	tx1 xluma + ty1 yluma + pos
	6 dup dim
	gc.box
	;

:lumagridmini
	tx1 4+ ty1 28 +
	256 ( 1? )( 2 -
		dup 0 colorrampa lerpcol
		over dup 8 << dup 8 << or or
		degrade32!
		>r
		2dup setxy
		128 ( 1? )( 1- deg@+ px!+ deg+! ) drop
		1+ r> ) drop
	2drop

	gc.push
	tx1 4+ ty1 28 + 128 dup gc.xywh
    [ xymouse xyluma! ; ] guiMove
	gc.pop

	blanco
	tx1 xluma + ty1 yluma + pos
	6 dup dim
	gc.box
	;

|--- precalculo
#cromacol )( 1024

:cromaset
	'cromacol >r
	0 ( 256 <? )(
		dup 8 << 1.0 1.0 hsv2rgb r!+
		1+ ) drop
	rdrop ;

:cromadraw | x y --
	'cromacol >r
	256 ( 1? )( 1-
		r@+ ink
		pick2 pick2
		over 16 + over op line
		swap 1+ swap ) drop
	rdrop ;

|----
:cromaline
	tx1 262 + ty1 28 + vhue +
	over 32 + over op line

	tx1 270 + ty1 28 + | ini croma
	256 ( 1? )( 1-
		255 over - 8 <<  1.0 1.0 hsv2rgb ink
		pick2 pick2 over 16 + over op line
		swap 1+ swap ) drop
	2drop

	gc.push
	tx1 262 + ty1 32 256 gc.xywh | x y w h
    [ xymouse nip ty1 28 + - vhue! ; ] guiMove
	gc.pop
	;


:cromalinemini
	tx1 142 + ty1 28 + vhue +
	over 32 + over op line

	tx1 150 + ty1 28 + | ini croma
	256 ( 1? )(
		255 over - 8 <<  1.0 1.0 hsv2rgb ink
		pick2 pick2 over 16 + over op line
		swap 1+ swap 2 - ) drop
	2drop

	gc.push
	tx1 142 + ty1 28 + 32 128 gc.xywh | x y w h
    [ xymouse nip ty1 28 + - vhue!2 ; ] guiMove
	gc.pop
	;

#winCol 0 10 30 290 330 "Color"
:dlgCol
	lumagrid
	cromaline
	ty1 290 + 'ccy !
	blanco
	dlg.color@ " $%h" print
	;

::dlg.color | --
	dlg.color@ ink
	'dlgCol 'winCol guiWin
	;

|-----
#winColm 0 10 30 180 190 "Color"
:dlgColm
	lumagridMini
	cromalineMini
	ty1 160 + 'ccy !
	negro
	dlg.color@ " $%h" print
	;

::dlg.colormini | --
	dlg.color@ ink
	'dlgColm 'winColm guiWin
	;

::dlgColat! | x y --
	'winCol 8 + !
	'winCol 4+ ! ;

|---- Dialogos Si No

#tt
#salida

:sifl 1 'salida ! exit ;
:nofl 0 'salida ! exit ;

:dsino | --
	usogui
	show
		xfb>scr
		0 0 fpos 0.6 0.5 fdim
		$ff $11 vbtn

		0.15 dup fdim -0.2 -0.3 fpos
		rojo oscuro
		'nofl dup >esc< 'ifin .btnd

		0.2 -0.3 fpos
		verde oscuro
		'sifl dup <spc> 'iok .btnd

		0.5 0.4 fdim 0 0 fpos
		font-vard-12-quad-out home
		cr cr cr tt printc cr cr
		cmano ;

::dlgsino | "" -- 0/1
	'tt !
	oscurecetodo
	>xfb
	dsino
	salida ;

|---------------------------------
::dlgprinter
	oscurecetodo
	>xfb
	usogui
	show xfb>scr
		0 0 fpos 0.6 0.5 fdim
		$ff $11 vbtn
|		0.15 cubo -0.2 -0.3 fpos
		0.5 0.4 fdim 0 0 fpos
|		8 hfont
		font-vard-12-quad-out
		home cr cr
		"Imprimiendo..." printc cr cr cr
		"Espere un momento..." printc
		1 .segs exit ;
