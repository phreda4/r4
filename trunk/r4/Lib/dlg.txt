| dialogos comunes gui
| PHREDA 2010
|---------------
^r4/lib/btn.txt
^r4/lib/win.txt
^r4/lib/cursor.txt
^r4/lib/fontt.txt
^r4/lib/color.txt
^r4/lib/trace.txt

|--- precalculo
#cromacol )( 516
#varc
#xo #yo
#xw #yw

#vhue #yluma #xluma
#colorrampa $ff0000

|---- Dialogo Color
:setcol | color --
	dup col.clapiz!
	varc ! ;

:vhue! | nro --
	dup 'vhue !
	9 << 1.0 1.0 hsv2rgb 'colorrampa !
	xw xluma + yw yluma + getpixel setcol ;

:xyluma! | x y --
	yw 28 + <? ( yw 28 + nip )
	yw 156 + >? ( yw 156 + nip )
	swap
	xw 8 + <? ( xw 8 + nip )
	xw 136 + >? ( xw 136 + nip )
	swap
	2dup
	getpixel setcol
	yw - 'yluma ! xw - 'xluma ! ;

:lumagridmini
	xw 8 + yw 28 +
	255 ( +? )(
		dup 0 colorrampa lerpcol
		over dup 8 << dup 8 << or or
		degrade32!
		>r 2dup setxy
		129 ( 1? )( 1- deg@+ px!+ deg+! ) drop
		1+ r> 2 - ) drop
	setxy 129 ( 1? )( 1- 0 px!+ ) drop

	gc.push
	xw 4+ yw 24 + 136 dup gc.xywh
    [ xymouse xyluma! ; ] guiMove
	gc.pop

	blanco
	xw xluma + yw yluma + pos
	6 dup dim
	gc.box
	;

:cromalinemini
	xw 142 + yw 28 + vhue +
	over 32 + over op line
	xw 150 + yw 28 + | ini croma
	'cromacol >r
	129 ( 1? )( 1-
		r@+ ink
		pick2 pick2
		over 16 + over op line
		swap 1+ swap ) drop
	rdrop
	2drop

	gc.push
	xw 142 + yw 28 + 32 128 gc.xywh | x y w h
    [ xymouse nip yw 28 + - vhue! ; ] guiMove
	gc.pop
	;

:showcolor
	show scr guistart
		xw yw 180 190 gc.xywh
		gris gc.fbox
		'exit guiOut
		xo yo 16 16 gc.xywh
		varc @ ink gc.fbox
		blanco 2+gc gc.box
|		'exit guiBtn
		lumagridmini
		cromalinemini
		xw 32 + yw 4+ atxy blanco varc @ "$%h" print
|		varc @ 	rgb2hsr " %h %h %h" print
		cminiflecha ;

:cromaset
	'cromacol >r
	0 ( 128 <=? )(
		dup 9 << 1.0 1.0 hsv2rgb r!+
		1+ ) drop
	rdrop ;

:setvxy | color -- ; no anda
	rgb2hsv
	neg 2/ $7f and 28 + 'yluma !
	$ff >? ( $ff nip )
	2/ $7f and 8 + 'xluma !
	$ff $600 */
	dup 8 << 1.0 1.0 hsv2rgb 'colorrampa !
	2/ 'vhue ! ;

:indlgcolor
	wgc.push
	cromaset
	ccx 1+ 'xo !
	ccy 1+ 'yo !
	ccx 180 + sw >? ( -158 'ccx +! ) drop
	ccy 190 + sh >? ( -168 'ccy +! ) drop
	ccx 'xw ! ccy 'yw !
	varc @ setvxy
	showcolor
	wgc.pop ;

::dlgcolor | 'var --
	dup 'varc ! @ ink
	ccx 1+ ccy 1+ 16 16 gc.xywh
	'indlgcolor guiBtn
	gc.fbox
	blanco 2+gc gc.box
	18 'ccx +! ;

|---- Dialogos Si No

#tt
#salida

:sifl 1 'salida ! exit ;
:nofl 0 'salida ! exit ;

:dsino | --
	usogui
	show
		xfb>scr
		0 0 fpos 0.6 0.5 fdim
		$ff $11 vbtn

		0.15 dup fdim -0.2 -0.3 fpos
		rojo oscuro
		'nofl dup >esc< 'ifin .btnd

		0.2 -0.3 fpos
		verde oscuro
		'sifl dup <spc> 'iok .btnd

		0.5 0.4 fdim 0 0 fpos
		font-vard-12-quad-out home
		cr cr cr tt printc cr cr
		cmano ;

::dlgsino | "" -- 0/1
	'tt !
	oscurecetodo
	>xfb
	dsino
	salida ;

|---------------------------------
::dlgprinter
	oscurecetodo
	>xfb
	usogui
	show xfb>scr
		0 0 fpos 0.6 0.5 fdim
		$ff $11 vbtn
|		0.15 cubo -0.2 -0.3 fpos
		0.5 0.4 fdim 0 0 fpos
|		8 hfont
		font-vard-12-quad-out
		home cr cr
		"Imprimiendo..." printc cr cr cr
		"Espere un momento..." printc
		1 .segs exit ;
