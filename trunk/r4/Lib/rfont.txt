|  Fuentes Vectoriales
|  PHREDA 2013
|  uso:
|	^r4/lib/rfont.txt
|   ^inc/rft/...fuente.rtf
|
|   ...fuente size rfont!
|--------------------------------------
^r4/lib/print.txt

|---------- rfont
#fontrom
#fontsize
#fycc #fxcc	| ajustes

:v>rfw ccw 14 *>> ;
:rf>xy | value -- x y
	dup 18 >> ccw 14 *>> ccx + fxcc +
	swap 14 << 18 >> ccw 14 *>> ccy + fycc + ;

|--------- formato fuente
#yp #xp
:a0 drop ; | el valor no puede ser 0
:a1 xp yp pline 
	rf>xy 2dup 'yp !+ ! op ;  | punto
:a2 rf>xy pline ; | linea
:a3 swap >r rf>xy r@+ rf>xy pcurve r> ;  | curva
:a4 swap >r rf>xy r@+ rf>xy r@+ rf>xy pcurve3 r> ; | curva3

#gfont a0 a1 a2 a3 a4 0 0 0

:drawrf | 'rf --
	@+ rf>xy 2dup 'yp !+ ! op
	( @+ 1? )( dup $7 and 2 << 'gfont + @ exec ) 2drop
	xp yp pline
	poli ;

:wsizerf | c -- wsize
	2 << fontsize + @ ccw 14 *>> ;

:emitrf | c --
	2 << fontrom + @ drawrf ;

::rfont! | rom size --
	pxtext
	'fontrom ! 'fontsize !
	v>rfw ccw 3 >> + 'fycc ! |** ajuste a ojo
	v>rfw neg 'fxcc !
	'emitrf  'wsizerf setfont
	cch dup 2 >> - 'cch !	|** ajuste a ojo
	;

