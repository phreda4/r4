| Navegador de Archivos
| PHREDA 2006
|---------------------------------------
^r4/lib/btn.txt
^r4/lib/cursor.txt
^r4/lib/trace.txt

|---- dlgload
#buffname )( 256
#path
#wild
#name
#inif #act #maxf #maxl

#filen )( 8192
#filen> 'filen
#files )( 4096
#files> 'files
#nfiles 

:inifile
	0 'nfiles !
	'filen 'filen> !
	'files 'files> !
	'path "\\*" ffirst
	( 1? )(
		filen> files> !+ 'files> !
		44 + filen> strcpyl 'filen> !
		fnext ) drop
	files> 'files - 2 >> 'nfiles !
	;

:file | n -- ""
	nfiles >=? ( drop 0 ; )
	2 << 'files + @ ;

:clickf
	dup 'act !
	mark
	'buffname 'here !
	path ,s dup file ,s 0 ,c
	'buffname 'name !
	empty
	exit
	;

:actinpage
	act inif <? ( maxl neg 'inif +! )
	inif maxl 3 * >? ( maxl 'inif +! )
	drop ;

:showload
	inifile
	0 'act !
	0 'inif !
	scr home
	rows 3 - 2 3 */ 'maxl !
	show clrscr
		64 gc.top $00f gc.hfill
		scr home
		verde " Load FILE :" print
		blanco path " %s" print
		wild " (%s) " print cr cr2
		rojo sp 'exit dup >esc< "ESC-Cancel" link
		verde sp [ act clickf drop ; ] dup <enter> "ENTER-Ok" link cr cr
		chome!
		inif ( act =? ( verde )( negro )
			dup file 1? )(
			wild strwild 1? ( drop
				'clickf swap 16 sp btntf cr cr2
				allowchome
				)( 2drop )
			1+ ) drop 1- 'maxf !
		[ act maxf <? ( 1+ ) 'act ! ; ] <dn>
		[ act 1? ( 1- ) 'act ! ; ] <up>
		[ act maxl + maxf <=? ( dup 'act ! ) drop ; ] <ri>
		[ act maxl - 0 >=? ( dup 'act ! ) drop ; ] <le>
		[ inif maxl + maxf <? ( 'inif ! ) drop ; ] <pgup>
		[ inif maxl - 0 >=? ( 'inif ! ) drop ; ] <pgdn>

		cmano ;

| "path" "wild"
::dlgfileload | "path" "wild" -- "fullname"
	'wild !
	'path !
	0 'name !

|	trace
	showload
	name ;

::dlgfilename | -- "name"
	mark
	'buffname 'here !
	act file ,s 0 ,c
	'buffname 'name !
	empty name ;

