| Navegador de Archivos
| PHREDA 2006
|---------------------------------------
^r4/lib/btn.txt
^r4/lib/input.txt

^r4/lib/cursor.txt
^r4/lib/trace.txt

|---- dlgload
#buffname )( 1024
#path
#wild
#inif #act #maxf #maxl

#filen )( 8192
#filen> 'filen
#files )( 4096
#files> 'files
#nfiles

:inifile
	0 'nfiles !
	'filen 'filen> !
	'files 'files> !
	path "%s\\*.*" mprint
	ffirst drop fnext drop fnext | quita . y ..
	( 1? )(
		filen> files> !+ 'files> !
		44 + filen> strcpyl 'filen> !
		fnext ) drop
	files> 'files - 2 >> 'nfiles !
	;

:file | n -- ""
	nfiles >=? ( drop 0 ; )
	2 << 'files + @ ;

:clickf
	dup 'act !
	dup file 'buffname strcpy
	exit
	;

:actinpage
	act inif <? ( maxl neg 'inif +! )
	inif maxl 3 * >? ( maxl 'inif +! )
	drop ;

:showload
	oscurecetodo
	0 0 fpos 0.9 0.9 fdim
	gris gc.box
	4 'w +! 4 'h +!
	blanco gc.box
	>xfb
	inifile
	0 'act !
	0 'inif !
	rows 3 - 2 3 */ 'maxl !
	show xfb>scr

		0 0 fpos 0.9 0.9 fdim
		home cr2
		verde "  ARCHIVO :" print
		blanco 'buffname 64 input
		rojo sp [ 0 'buffname ! exit ; ] dup >esc< "ESC-X" link
		verde sp [ act clickf drop ; ] dup <enter> "ENTER-O" link
		cr cr
		chome!
		inif ( act =? ( verde )( negro )
			dup file 1? )(
			wild strwild 1? ( drop
				'clickf swap 16 sp sp btntf cr cr2
				allowchome
				)( 2drop )
			1+ ) drop 1- 'maxf !
		[ act maxf <? ( 1+ ) 'act ! ; ] <dn>
		[ act 1? ( 1- ) 'act ! ; ] <up>
		[ act maxl + maxf <=? ( dup 'act ! ) drop ; ] <ri>
		[ act maxl - 0 >=? ( dup 'act ! ) drop ; ] <le>
		[ inif maxl + maxf <? ( 'inif ! ) drop ; ] <pgup>
		[ inif maxl - 0 >=? ( 'inif ! ) drop ; ] <pgdn>

		cmano ;

| "path" "wild"
::dlgfileload | "path" "wild" -- "fullname"
	'wild ! 'path !
	showload
	'buffname ;

::dlgfilename | -- "name"
	'buffname ;

