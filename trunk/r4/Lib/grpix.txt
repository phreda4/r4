| new frame buffer
| PHREDA 2010
|-------------
^r4/lib/rand.txt

#rc #gc #bc
#sr #sg #sb

#copiadeg )( 24

::degrade! |  cant $000FFF --
    dup  $f00000 and dup 4 >> or
	over $000f00 and 8 << dup 4 << or
	dup 'rc ! - pick2 / 'sr !
	dup  $0f0000 and dup 4 << or
	over $0000f0 and 12 << dup 4 << or
	dup 'gc ! - pick2 / 'sg !
	dup  $00f000 and 4 << dup 4 << or
	swap $00000f and 16 << dup 4 << or
	dup 'bc ! - swap / 'sb ! ;

::degrade32! |  $ff $ff00 --
    over $ff0000 and
	over $ff0000 and
	dup 'rc ! - 8 >> 'sr !
    over $ff00 and 8 <<
	over $ff00 and 8 <<
	dup 'gc ! - 8 >> 'sg !
    $ff and 16 << swap
	$ff and 16 << swap
	dup 'bc ! - 8 >> 'sb ! ;


::deg@+ | -- color
	rc dup sr + 'rc ! $ff0000 and
	gc dup sg + 'gc ! 8 >> $ff00 and or
	bc dup sb + 'bc ! 16 >> $ff and or ;

::deg+!
	sr 'rc +!
	sg 'gc +!
	sb 'bc +! ;

::*deg | guarda
	'copiadeg 'rc 6 move ;

::deg* | restaura
	'rc 'copiadeg 6 move ;

::degnow | -- $fff000
	rc $ff0000 and
	gc 8 >> $ff00 and or
	bc 16 >> $ff and or ;

#xa #ya

::.at | x y --
	'ya ! 'xa ! ;

:.fline | w --
	xa ya setxy dup 'xa +!
	( 1? )( 1- ink@ px!+ ) drop ;

:.fdeg | w --
	xa ya setxy dup 'xa +!
	( 1? )( 1- deg@+ px!+ ) drop ;

::.fbox | w h --
	( 1? )( 1-
		xa ya setxy
		over ( 1? )( 1-
			ink@ px!+
			) drop
		1 'ya +!
		) 2drop ;

::.fboxdh | w h --
	( 1? )( 1-
		xa ya setxy
		deg@+ pick2
		( 1? )( 1-
			over px!+
			) 2drop
		1 'ya +!
		) 2drop ;

::.fboxdv | w h --
	*deg
	( 1? )( 1-
		deg*
		xa ya setxy
		over
		( 1? )( 1-
			deg@+ px!+
			) drop
		1 'ya +!
		) 2drop ;


::getpixel | x y -- c
	setxy px@ ;	|	vmem @ ;

::setpixel | c x y --
	setxy px!+ ;    |	vmem ! ;

::pix! | x y --
	setxy ink@ px!+ ;

|---- bitmap
::oscurecetodo
	0 0 setxy
	0 ( sh <? )( 1+
		0 ( sw <? )( 1+
			px@ 2/ $7f7f7f and | oscurece
			px!+
			) drop
		) drop
|	framev sw sh *  ( 1? )( 1- swap dup @ 2/ $7f7f7f and swap !+ swap )	2drop
	;

::bmnoise | ancho alto x y --
	( 2swap 1? )( 1-
		2over setxy
		over ( 1? )( 1- rand px!+ ) drop
		2swap 1+ )
	4drop ;

::bmnoiseb | ancho alto x y --
	setxy
	sw pick2 - rot rot | paso ancho alto
	( 1? )( 1-
		over ( 1? )( 1- rand8 px!+ ) drop
		pick2 px+!
		)
	3drop ;

::rect | ancho alto x y --
	setxy
	sw pick2 - rot rot | paso ancho alto
	( 1? )( 1-
		over ( 1? )( 1- ink@ px!+ ) drop
		pick2 px+!
		) 3drop ;


|--- mem video
|:vmem | x y -- addr
|	sw * +  2 << framev + ;
|	sw sh * <? ( +? ( 2 << framev + ; ) ) drop rdrop ;

::color50% |(uint c1, uint c2)
	$fefefe and swap $fefefe and + 2/ ;

::color25% |(uint c1, uint c2)
	dup color50% color50% ;

::color75% |(uint c1, uint c2)
	over color50% color50% ;

::mix50 | ancho alto x y --
	setxy
	sw pick2 - rot rot | paso ancho alto
	( 1? )( 1-
		over ( 1? )( 1- px@ ink@ color50% px!+ ) drop
		pick2 px+!
		) 3drop ;

#xi #yi
#hi #wi

:clip | w h x y -- x y
	pick2 over + sh >=? ( sh pick2 - 1- )( pick3 ) 'hi !
	drop rot drop
	-? ( dup 'hi +! neg 'yi ! 0 )( 0 'yi ! )
	swap | w y x
	pick2 over + sw >=? ( sw pick2 - 1- )( pick3 ) 'wi !
	drop rot drop
	-? ( dup 'wi +! neg 'xi ! 0 )( 0 'xi ! )
	swap ;

#colores $ffffff $bfbfbf

::drawalphagrid | w h x y --
	clip
	hi wi or -? ( 3drop ; ) drop
	setxy
	sw wi -
	hi ( 1? )( 1-
		wi ( 1? )( 1- |2dup xor dup 8 << or dup 8 << or
			2dup xor %100 and 'colores + @
			px!+ ) drop
		over px+!
		) 2drop ;