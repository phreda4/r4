| tex 1024x1024 raw graphic buffer
| PHREDA 2011
|--------------------------
#xa #xa1 #xs
#ya #ya1 #ys
#wi #hi

::tex.XY | xb yb --
	16 << 'ya1 ! 16 << 'xa1 ! ;

::tex.WH | wb hb --
	1024 16 <</ 'ys ! 1024 16 <</ 'xs ! ;

::tex.draw | adr x y w h --
	'hi ! 'wi !
	setxy
	ya1 'ya !
	sw wi -
	hi ( 1? )(
		xa1 'xa !
	 	pick2 ya 4 >> $3ff000 and +
		wi ( 1? )(
			over xa 14 >> $ffC and + @ px!+
			xs 'xa +!
			1- ) 2drop
		over px+!
		ys 'ya +!
		1- ) 
	3drop ;

::tex.scrshoot | adr --
	>r
	0 ( sh <? )( 
 		0 ( 1024 <? )( 
		dup pick2 setxy	px@ r!+
		1+ ) drop
	1+ ) drop
	rdrop
	;

::tex.drawraw | adr x y w h --
	'hi ! 'wi !
	setxy
	sw wi -
	hi ( 1? )(
		pick2 wi ( 1? )( swap @+ px!+ swap 1- ) 2drop
		over px+!
		rot 4096 + rot rot 1- ) 
	3drop ;

#iconshoot )( 16384 | 64x64x4

:clearicon
	'iconshoot 4096 ( 1? )( 1- 0 rot !+ swap ) 2drop ;

::icon.shoot | --
	clearicon
	'iconshoot
	0 ( sw <? )( 
		0 ( sh <? )( 
		2dup setxy px@ 
		pick3 
		pick3 64 sw */ 
		pick3 64 sh */ 6 << + 2 << +
		dup @ rot or swap !
		1+ ) drop
	1+ ) 2drop ;


::icon.show | x y -
	setxy
	'iconshoot
	sw 64 -
	64 ( 1? )( rot 64 ( 1? )( swap @+ px!+ swap 1- ) drop
		rot dup px+!
		rot 1- ) 
	3drop ;

	
::icon.attex | tex x y --
	10 << + 2 << + >r
	'iconshoot 
	64 ( 1? )( 
		swap 64 ( 1? )( swap @+ r!+ swap 1- ) drop
		3840 r+
		swap 1- ) 
	2drop rdrop ;
	