| r4color.txt
| Integrar...integrar..
| PHREDA 2011
|---------------------
^r4/lib/btn.txt
^r4/lib/input.txt
^r4/lib/dlg.txt
^r4/lib/graf.txt
^r4/lib/polygr.txt

#nombre )( 256
#color $ff00
#grosor 2

#rampag

#colora
#grosora
|--- dibujos
#dibujos )( 1024
#dibujo> 'dibujos
|--- nombres
#nombres )( 1024
#nombres> 'nombres
|--- colores
#colores )( 2048
#colores> 'colores

|---- memoria para nombres
#texto #texto> 'texto
|---- memoria vectorial
#todo  #todo> 'todo
|--- trazo de capa actual
#trazo  #trazo> 'trazo

|--- xfb refresh
:copybox | w h x y --
	sw * + 2 << >r | w h  R:a
	( 1? )( 1-
		xfb r + framev r + pick3 move
		sw 2 << r+ )
	2drop rdrop ;

|--- trazo actual
:trazo.op | xy
	$00000000 or trazo> !+ 'trazo> ! ;
:trazo.line | xy
	$10000000 or trazo> !+ 'trazo> ! ;
:trazo.color | color
	$20000000 or trazo> !+ 'trazo> ! ;
:trazo.grosor | grosor
	$30000000 or trazo> !+ 'trazo> ! ;

:trazo.clear
	trazo 'trazo> ! ;

|---
:t0	$fffffff and d>xy gop ;
:t1	$fffffff and d>xy gline ;
:t2	$ffffff and ink ;
:t3	$fff and linegg! ;

#acctr 't0 't1 't2 't3

:trazo.draw | last ini --
	( over <? )(
		@+ dup 26 >> $c and 'acctr + @ exec
		) 2drop
	grosor linegg! ;

:rehacexfb
	cls
	trazo> trazo trazo.draw
	>xfb
	;

|----- grosor de linea
:ingrosor
	[ xymouse pick2 - swap pick3 - min abs dup 'grosor ! linegg! ; ] guiMove ;

:btngrosor
	ccx ccy pick2 dup gc.xywh
	dup 2/ dup ccx + ccy rot +
	'ingrosor guiIn
	negro gc.fbox
    color ink grosor fcircle
	blanco grosor "%d" print
	'ccx +! ;

|----- trazo por colores
:indc+! | valor
	colores> !+ 'colores> ! ;

:indexcolor
	'colores 'colores> !
	trazo> trazo
	( over <? )(
		@+ 28 >>
		2 =? ( over 4 - indc+! )
		drop )
	2drop ;

:clickcolor
	;

:colormenu
	1 'colores ( colores> <? )(
		@+ @ $ffffff and ink
		swap
		'clickcolor over "%d" link
		1+ swap ) 2drop ;


|---- SAVEALL
:saver4c | "" --
	mark	| guarda texto
	trazo> trazo - ,
	trazo ( trazo> <? )( @+ , ) drop

	savemem empty ;

|---- LOADALL
:loadr4c | "" --
	mark
	here dup rot load 'here ! | -- iniload..here
	@+ trazo + 'trazo> !
	trazo ( trazo> <? )( swap @+ rot !+ ) 2drop

	empty
	;

|---- MODO DIBUJO
#xm #ym
:dnCursor
	color colora <>? ( dup trazo.color 'colora ! )( drop )
	grosor grosora <>? ( dup trazo.grosor 'grosora ! )( drop )
	xymouse
	2dup gop
	2dup 'ym ! 'xm !
	xy>d trazo.op ;

:copy0
	xm pick2 <? ( pick2 )( pick2 swap ) over - 2 +  | x w
	ym pick3 <? ( pick3 )( pick3 swap ) over - 2 +  | y h
	swap >r rot 1- r> 1- copybox
	'ym ! 'xm !
	;

:movCursor
	xymouse
	ym =? ( swap xm =? ( 2drop ; ) swap )
	2dup gline
	2dup xy>d trazo.line
	grosor 0? ( drop copy0 ; ) drop
	xm pick2 <? ( pick2 )( pick2 swap ) over - grosor 2* +  | x w
	ym pick3 <? ( pick3 )( pick3 swap ) over - grosor 2* +  | y h
	swap >r rot grosor - r> grosor - copybox
	'ym ! 'xm !
	;

:mododibujo
	color ink 'dnCursor 'movCursor guiDnMove
	[ trazo trazo> =? ( drop ; ) drop -4 'trazo> +! rehacexfb ; ] <del>
	;

|---- MODO SELECCION
#xa #ya #xb #yb
:clearsel
	-1 'xa ! ;

:modoseleccion
	[ xymouse 2dup 'yb ! 'xb ! 'ya ! 'xa ! ; ]
	[ xymouse 'yb ! 'xb ! ; ]
	[ ; ]
	guiMap
	xa -? ( drop ; ) ya xb yb caja
	;

|---- MODO POLIGONO
:modopoligono
	;

#modo 'mododibujo
|----- Main
:main
	rehacexfb
	grosor linegg!

	color 1+ 'colora !
	grosor 1+ 'grosora !
	color col.clapiz!

	fonti
	show xfb>scr

		modo exec

		32 'color btncolora
		32 btngrosor
|		cr2

|        blanco 'nombre printx

		0 rows 2 - gotoxy
		verde [ cls >xfb trazo.clear ; ] "CLS" sp btnt

		sp colormenu
		dup " (%d) " print
		trazo> trazo - 2 >> "T:(%d)" print
		[ 'mododibujo 'modo ! ; ] <f1>
		[ indexcolor 'modoseleccion 'modo ! ; ] <f2>
		[ 'modopoligono 'modo ! ; ] <f3>
		'exit >esc<
|	blink 1? ( blanco )( negro ) drop
		blanco
	 	cmira ;

|------ INI
:memlienzo
	mark
	here dup 'todo ! 'todo> !
	$3ffff 'here +!
	here dup 'texto ! 'texto> !
	$ffff 'here + !
	here dup 'trazo ! 'trazo> !
	$ffff 'here +!
	;

:
	memlienzo
    0 paper
    cls >XFB
	"test.r4c" loadr4c
	33 main
	"test.r4c" saver4c
	;

