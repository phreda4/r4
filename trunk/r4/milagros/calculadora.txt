| Calculadora parlante
| PHREDA 2013
|--------------------------------
^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/input.txt
^r4/lib/rfont.txt
^r4/lib/ricons.txt

^inc/ric/fontawesomewebfont.ric
^inc/rft/robotobold.rft

|--------------------------------------------------------------------------
|	ComponeFrase
#frase  )( 1024
#frase> 'frase
#fraseA 0

:frase,
	frase> !+ 'frase> ! ;

:clearfrase
	'frase 'frase> ! ;

:hablaMilagros
	-1 sinfo 1? ( drop ; ) drop
	fraseA 0? ( drop clearfrase ; )
	@+ 1? ( splay )( nip )
	'fraseA ! ;

:frase!
|	0 frase> !+ 'frase> !
	0 frase> !
	fraseA 0? ( 'frase 'fraseA ! ) drop ;

|--------------------------------------------------------------------------
#s0 #s1 #s2 #s3 #s4 #s5 #s6 #s7 #s8 #s9
#s11 #s12 #s13 #s14 #s15 #s16 #s17 #s18 #s19
#s10 #s20 #s30 #s40 #s50 #s60 #s70 #s80 #s90
#s100 #s200 #s300 #s400 #s500 #s600 #s700 #s800 #s900
#s10y #s20y #s30y #s40y #s50y #s60y #s70y #s80y #s90y
#sUN #sMillones #sUnMillon #sMil

#sstart #sexit #serror

#sigual #smas #smenos #spor #sdividido

:playU 2 << 's0 + @ frase, ;
:playDb 2 << 's11 + @ frase, ;
:playD 2 << 's10 + @ frase, ;
:playC 2 << 's100 + @ frase, ;
:playDe 2 << 's10y + @ frase, ;

:decena | nro decena -- nro
	0? ( drop ; )
	over 10 mod 0? ( drop 1- playD ; )
	swap 1 =? ( drop 1- playDb 0 nip ; )
	nip 1- playDe ;

:cien2texto | nro 0 --
	swap 0? ( 2drop ; )
	100 =? ( 0 nip playC drop ; )
	dup 100 / 10 mod
	1? ( 1- playC )( drop )
	dup 10 / 10 mod
	decena
	10 mod 0? ( 2drop ; )
	1 =? ( swap 0? ( sUN splay 2drop ; ) drop )( nip )
	playU ;

::nro2say | nro --
|	clearfrase
	0? ( dup playU frase, frase, ; )
	dup 1000000 / 1000 mod
	1 >? ( 0 cien2texto sMillones frase, )( 1 =? ( sUnMillon frase, ) drop )
	dup 1000 / 1000 mod
	1 >? ( 0 cien2texto sMil frase, )( 1 =? ( sMil frase, ) drop )
	1000 mod
	1 cien2texto
	|0 frase,
	;

:saynro | nro --
	nro2say frase! ;

|--------------------------------------------------------------------------
|--------------------------------------------------------------------------
#nro1 0
#nro2
#nop -1

#log )( 1024

:transf
	nop +? ( drop ; ) drop
	nro1 dup nro2say frase!
	'nro2 !
	0 'nro1 ! ;

:tmas
	transf
	1 'nop !
	smas frase, frase! ;
:tmenos
	transf
	2 'nop !
	smenos frase, frase! ;
:tpor
	transf
	3 'nop !
	spor frase, frase! ;
:tdiv
	transf
	4 'nop !
	sdividido frase, frase! ;

:opmas + ;
:opmenos - ;
:oppor * ;
:opdiv / ;

#lop 'opmas 'opmenos 'oppor 'opdiv

:tigual
	nop -? ( drop ; ) drop
	sigual frase, frase!
	nro2 nro1
	nop 1- 2 << 'lop + @ exec
	dup 'nro1 !
	0 'nro2 !
	nro2say frase!
	-1 'nop !
	;

:esnro | v -- v
	11 =? ( 11 - )( 1- )
	dup saynro
	nro1 10 * over + 'nro1 !
	;

:teclas
	'tmas <f1>
	'tmenos <f2>
	'tpor <f3>
	'tdiv <f4>
	key 1 >? ( 12 <? ( esnro ) ) drop
	'tigual <enter>
	[ nro1 10 / 'nro1 ! ; ] <back>
	[ 0 'nro1 ! ; ] <del>
	;

:teclado
	show clrscr
		hablaMilagros
		robotobold 48 rfont!
		verde " Calculadora de Milagros" print cr cr2
        robotobold 32 rfont!
		cyan
		"f1:+ f2:- f3:* f4:/" print cr

		teclas
		blanco
		nro1 " %d" print cr
		nro2 " %d" print cr
		'log print

		'exit >esc<
		cminiflecha
		;

:swait
	-1 ( sinfo 0? ) drop ;
:main
	sstart splay
	teclado
	sexit splay
	swait ;

:sonidos
	"media/milagros/0cero.ogg" sload 's0 !
	"media/milagros/1uno.ogg" sload 's1 !
	"media/milagros/2dos.ogg" sload 's2 !
	"media/milagros/3tres.ogg" sload 's3 !
	"media/milagros/4cuatro.ogg" sload 's4 !
	"media/milagros/5cinco.ogg" sload 's5 !
	"media/milagros/6seis.ogg" sload 's6 !
	"media/milagros/7siete.ogg" sload 's7 !
	"media/milagros/8ocho.ogg" sload 's8 !
	"media/milagros/9nueve.ogg" sload 's9 !
	"media/milagros/10diez.ogg" sload 's10 !
	"media/milagros/11once.ogg" sload 's11 !
	"media/milagros/12doce.ogg" sload 's12 !
	"media/milagros/13trece.ogg" sload 's13 !
	"media/milagros/14catorce.ogg" sload 's14 !
	"media/milagros/15quince.ogg" sload 's15 !
	"media/milagros/16dieciseis.ogg" sload 's16 !
	"media/milagros/17diecisiete.ogg" sload 's17 !
	"media/milagros/18dieciocho.ogg" sload 's18 !
	"media/milagros/19diecinueve.ogg" sload 's19 !
	"media/milagros/20veinte.ogg" sload 's20 !
	"media/milagros/30treinta.ogg" sload 's30 !
	"media/milagros/40cuarenta.ogg" sload 's40 !
	"media/milagros/50cincuenta.ogg" sload 's50 !
	"media/milagros/60sesenta.ogg" sload 's60 !
	"media/milagros/70setenta.ogg" sload 's70 !
	"media/milagros/80ochenta.ogg" sload 's80 !
	"media/milagros/90noventa.ogg" sload 's90 !
	"media/milagros/20y.ogg" sload 's20y !
	"media/milagros/30y.ogg" sload 's30y !
	"media/milagros/40y.ogg" sload 's40y !
	"media/milagros/50y.ogg" sload 's50y !
	"media/milagros/60y.ogg" sload 's60y !
	"media/milagros/70y.ogg" sload 's70y !
	"media/milagros/80y.ogg" sload 's80y !
	"media/milagros/90y.ogg" sload 's90y !

	"media/milagros/100ciento.ogg" sload 's100 !
	"media/milagros/200doscientos.ogg" sload 's200 !
	"media/milagros/300trescientos.ogg" sload 's300 !
	"media/milagros/400cuatrocientos.ogg" sload 's400 !
	"media/milagros/500quinientos.ogg" sload 's500 !
	"media/milagros/600seicientos.ogg" sload 's600 !
	"media/milagros/700setecientos.ogg" sload 's700 !
	"media/milagros/800ochocientos.ogg" sload 's800 !
	"media/milagros/900novecientos.ogg" sload 's900 !
	"media/milagros/un.ogg" sload 'sUN !
	"media/milagros/Millones.ogg" sload 'sMillones !
	"media/milagros/UnMillon.ogg" sload 'sUnMillon !
	"media/milagros/Mil.ogg" sload 'sMil !

	"media/milagros/start.ogg" sload 'sstart !
	"media/milagros/exit.ogg" sload 'sexit !
	"media/milagros/error.ogg" sload 'serror !

	"media/milagros/igual.ogg" sload 'sigual !
	"media/milagros/mas.ogg" sload 'smas !
	"media/milagros/menos.ogg" sload 'smenos !
	"media/milagros/por.ogg" sload 'spor !
	"media/milagros/dividido.ogg" sload 'sdividido !
	;

: sonidos main ;