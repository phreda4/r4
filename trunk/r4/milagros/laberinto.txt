^r4/lib/gui.txt
^r4/lib/trace.txt

^r4/milagros/dibujos.spr

#lab1 30 15 1 1
"##############################"
"#      1         ##        3##"
"################ ## ####### ##"
"#     ##    ####  2 ##   ## ##"
"##### ## ##   ######## #    ##"
"##### ## ####     #### #######"
"#     ###  ###### ####4     ##"
"# ########     ## ######### ##"
"# ############ ## ######### ##"
"#    ###    ## ## ####   ## ##"
"#### ### ## ## ##   ## # ## ##"
"#### ### ## ## #### ## # ## ##"
"### #### ## ## #### ## # ## ##"
"###      ##    ####F   #5   ##"
"##############################"

#lab2 30 15 1 1
"##############################"
"#                 ##    #   ##"
"################# ## ## # #  #"
"##    ###     ### ## ## # ## #"
"## ## ### ### ###    ##   ## #"
"## ## ### ### ############## #"
"## ## ### ###     ####       #"
"## ##     ####### #### #######"
"## ########    ## #### #######"
"## ######## ## ##    #     ###"
"##       ## ## ##### ##### ###"
"######## ## ##           #   #"
"##          ##### ###### ### #"
"##########        ###        #"
"##############################"

#lab3 30 15 1 1
"##############################"
"# #   ##       #   ###     ###"
"##############   ########## ##"
"#    ###     ##########     ##"
"# ## ### ### ##      ## ######"
"# ##     ### #######    ######"
"# ########## # ###############"
"#     ######## #####        ##"
"##### ##    ##       ###### ##"
"##   ### ##  #### ###    ##  #"
"## ##### ####  ## #   ## ### #"
"##############################"

#lab4 30 15 1 1
"##############################"
"# #   #   #   #   #   #   #  #"
"# # # # # # # # # # # # # # ##"
"# # # # # # # # # # # # # # ##"
"# # # # # # # # # # # # # # ##"
"# # # # # # # # # # # # # # ##"
"# # # # # # # # # # # # # # ##"
"# # # # # # # # # # # # # # ##"
"# # # # # # # # # # # # # # ##"
"# # # # # # # # # # # # # # ##"
"# # # # # # # # # # # # # # ##"
"# # # # # # # # # # # # # # ##"
"# # # # # # # # # # # # # # ##"
"#   #   #   #   #   #   #   ##"
"##############################"

#lab5 30 15 1 1
"##############################"
"#                            #"
"############################ #"
"#                            #"
"# ############################"
"#                            #"
"############################ #"
"#                            #"
"# ############################"
"#                            #"
"############################ #"
"#                            #"
"# ############################"
"#                            #"
"##############################"

#lab6 30 15 1 1
"##############################"
"#        ##        ##    ## ##"
"######## ## ###### ## ##    ##"
"######## ## ###### ## ########"
"##       ## ##     ##       ##"
"## ######## ## ############ ##"
"##       ## ## ###    #     ##"
"######## ## ## ### ## # ######"
"######## ## ## ### # ##     ##"
"#        ##  #     # ##### ###"
"# ########## ####### #   # ###"
"#        ###       # # # # ###"
"######## ######### # # # # ###"
"########           #   #   ###"
"##############################"

#lab7 30 15 1 1
"##############################"
"#   ###      ##     ###     ##"
"###     ####    ###     ### ##"
"########################### ##"
"###      ###       ###      ##"
"#   ####     #####     #######"
"# ######################    ##"
"#      ################# ## ##"
"###### #       ##      # ## ##"
"##     # ##### ## #### # ## ##"
"## ##### ##    ## ##  ## ## ##"
"##       ## ##### ## ### ## ##"
"##### #####       ##     ## ##"
"#####       ############    ##"
"##############################"

#lab8 30 15 1 1
"##############################"
"# #   #   #   #   #   #   ####"
"#   #   #   #   #   #   #   ##"
"########################### ##"
"#    #                ###   ##"
"# ## # ########### ##### #####"
"# ##   ###         #####     #"
"# ######## ################# #"
"# ###      #############     #"
"#   # ################## #####"
"### #                 ##    ##"
"#   ################# ##### ##"
"# ####        ##        ### ##"
"#      #######   ######     ##"
"##############################"

#lab9 30 15 1 1
"##############################"
"#                            #"
"############################ #"
"#                            #"
"# ############################"
"#                            #"
"############################ #"
"#                            #"
"# ############################"
"# #   #   #   #   #   #   #  #"
"# # # # # # # # # # # # # # ##"
"# # # # # # # # # # # # # # ##"
"# # # # # # # # # # # # # # ##"
"#   #   #   #   #   #   #   ##"
"##############################"

#lab10 30 15 1 1
"##############################"
"#I#     ##3      ####    #   #"
"# # ### ## #####      ## # # #"
"#   ### ##     #########   # #"
"####### ######   ########### #"
"####### #    ### #        4# #"
"###     # ## ### # #######   #"
"### #####  #     # #     #####"
"#1  #   ## ######  # ### #   #"
"# ### #  #      # ## #   # # #"
"#   # ## ###### # ## # ### # #"
"### #  # #   ## # ## # ##  # #"
"### ## # # # ## # ## # ## ## #"
"###    #   #2   #    #5   ##F#"
"##############################"

|*********** variables
#wl #hl
#lab

#xj #yj
#dj
#direcciones
1 0
0 1
-1 0
0 -1

#tiempo
#estado
#puntos

:reset
	0 'dj !
	0 'tiempo !
	0 'estado !
	0 'puntos !
	;

:setlab  | 'lab --
	@+ 'wl ! @+ 'hl !
	@+ 'xj ! @+ 'yj !
	'lab !
	reset
	;

:dgetxy | dir -- dx dy
    3 << 'direcciones + >r
	r@+ r> @ ;

:getb | x y -- b
	wl 1+ * + lab + c@
	;


|********* dibujo del juego

#pdj ">v<^"

:drawlab
	verde
	0 2 gotoxy
	0 ( hl <? )(
		0 ( wl <? )(
			2dup swap
			getb emit
			1+ ) drop
		cr
		1+ ) drop
	blanco
	xj yj 2 + gotoxy
	dj 'pdj + c@ emit
	;

:espared | dx dy -- 1/0
    yj + swap xj + swap getb
	$23 =? ( drop 1 ; ) drop 0 ;

:espareda | dx dy n -- 1/0
	>r
	dj dgetxy
	r * swap r> * swap
	rot + >r + r>
	espared ;

:dibujanivel | n --
	>r
	dj 1- 3 and dgetxy
	r espareda 1? ( 'izc )( 'iza ) nsprite drop
	dj 1+ 3 and dgetxy
	r espareda 1? ( 'dec )( 'dea ) nsprite drop
	0 0 r> 1+ espareda 0? ( drop ; ) drop
	'pared nsprite ;

:dibuja3d
	0 dup fpos
	1.2 vdim 'fondo nsprite
	0.0156 vdim 6 dibujanivel
	0.0313 vdim 5 dibujanivel
    0.0625 vdim 4 dibujanivel
    0.125 vdim 3 dibujanivel
    0.25 vdim 2 dibujanivel
    0.5 vdim 1 dibujanivel
   	1.0 vdim 0 dibujanivel
	;

|--------------------------------------------------------------------------
|*********  sonidos2 **************
#sndsin
#sndini
#sndl1
#sndl2
#sndl3
#sndl4
#sndl5
#sndfin

|*********  sonidos **************
#sndstop
#snddos
#sndizq
#sndder

#sndlista 0 'snddos 'snddos 'snddos 'sndstop
#sndpan 0 0 255 0 0


:loads
	mark
	"media/milagros/sin3.ogg" sload 'sndsin !
	"media/milagros/inicio.ogg" sload 'sndini !
	"media/milagros/lugar1.ogg" sload 'sndl1 !
	"media/milagros/lugar2.ogg" sload 'sndl2 !
	"media/milagros/lugar3.ogg" sload 'sndl3 !
	"media/milagros/lugar4.ogg" sload 'sndl4 !
	"media/milagros/lugar5.ogg" sload 'sndl5 !
	"media/milagros/salida.ogg" sload 'sndfin !


	"media/milagros/8802.ogg" sload 'sndstop !
	"media/milagros/4402.ogg" sload 'snddos !

	;


| PAN 0..7f(center)..ff
| VOL 0..ff
| FREC  muestreo
|---------------------------------------
#frec 44100

:explay | SND pan vol frec --
	pick3 sset splay ;

| ahora dist|pan
:sndplay | n --
	dup $ff and
	swap 8 >> 1- 14 <<
	frec +		| pan frec
	sndsin rot rot $ff swap explay ;

#frase  )( 256
#frase> 'frase
#fraseA 0

:frase,
	frase> !+ 'frase> ! ;

:clearfrase
	'frase 'frase> ! ;

:sonido
	-1 sinfo 1? ( drop ; ) drop
	fraseA 0? ( drop clearfrase ; )
	@+ 1? ( sndplay )( nip )
	'fraseA ! ;

|********* acciones ************
:mover
	yj + swap xj + swap
	2dup getb
	$23 =? ( 3drop ; )		| # pared
	$46 =? ( 1 'estado ! sndfin splay )	| F fin
	$2E =? ( 1 'puntos +! ) | . punto
	$31 =? ( sndl1 splay )
	$32 =? ( sndl2 splay )
	$33 =? ( sndl3 splay )
	$34 =? ( sndl4 splay )
	$35 =? ( sndl5 splay )
	drop
	'yj ! 'xj !
	;


:prof | direccion -- profundidad
	dgetxy	| dx dy
	1 pick2 pick2	| dx dy 1 accx accy
	( 2dup espared 0? )(
		drop rot 1+ rot pick4 + rot pick3 + )
		3drop
	nip nip
	;

:playsonar2
	clearfrase
	dj 1- $3 and prof 8 << 0 or frase,
	dj  $3 and prof 8 << $7f or frase,
	dj 1+ $3 and prof 8 << $ff or frase,
|	dj 2 + $3 and prof 8 << $7f or frase,
	'frase 'fraseA !
	;

:adelante
	dj dgetxy mover playsonar2 ;

:atras
	dj 2 + 3 and dgetxy mover playsonar2 ;

|********** principal ************
#vista

:main
    'lab1 setlab
	fonti
	33
	sndini splay
	show clrscr
		sonido

		vista
		1 and? ( dibuja3d )
		2 and? ( drawlab )
    	drop

		0 0 gotoxy blanco
		2dup "%d %d " print
		"Laberinto " print
		tiempo "t:%d seg." print


		'adelante <up>
		'atras <dn>
		[ dj 1+ 3 and 'dj ! playsonar2 ; ] <ri>
		[ dj 1- 3 and 'dj ! playsonar2 ; ] <le>
		[ vista 1 xor 'vista ! ; ] <f1>
		[ vista 2 xor 'vista ! ; ] <f2>


		'exit >esc<
		1000 .mseg .restart
		1 'tiempo +! ;

: loads main ;