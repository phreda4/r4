| Manuel Cornes
| :r4 clock

| These are includes, any words defined in these two files that are prefixed with ::
| are visible from this file
^r4/lib/gui.txt

| variables definitions
#secref #msecref

:initime | ( -- )
time 2drop 'secref ! 2drop msec 'msecref ! ;

:elapsedmsec | ( -- elmsec )
msec msecref - secref 1000 * + 60000 mod ;

:center | screen center coordinates ( -- xc yc )
sw 2/ sh 2/ ;

| used to get coordinates of point belonging to a circle centered on (xc,yc)
| of radius r, at angle 'angle'
:ccircle_ | ... r angle -- ... x y
center 2swap swap xy+polar ;

| draws a portion of a radius of a circle centered on xc, yc
:radseg | ( angle r1 r2 g -- )
>r pick2 >r r ccircle_ rot r> ccircle_ r> glineg drop ;

:lines | draws hour clock lines ( -- )
0 ( 12 <? )( dup $10000 12 / * 290 300 2 radseg 1 + ) drop ;

| gets angle value from (basis,val)
:angle | (... basis val | ... angle)
$10000 * swap / $10000 2/ swap - ;

:main
initime
show clrscr
blanco lines
|time
| seconds
elapsedmsec 60000 swap angle 0 180 2 radseg
| minutes
time rot drop swap
dup 60 swap angle 0 160 4 radseg
rojo
swap 60 * + 12 60 * swap angle 0 100 8 radseg
'exit >esc<
;

: main ;