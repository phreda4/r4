| MC 2013 - Clock
| Adapts better to different screen dimensions

^r4/lib/gui.txt
^r4/lib/polygr.txt

#tickmin 0 #ticksize 0  | clock ticks
#hlen 0 #mlen 0 #slen 0 | lengths of arms
#hg 0 #mg 0 #sg 0       | arms thickness

:center | ( -- xc yc ) screen center coordinates
	sw 2/ sh 2/ ;

:ccircle_ | ( r angle -- x y )
	center 2swap swap xy+polar ;

:angle | ( val basis -- angle)
	swap $10000 * swap / $10000 2/ swap - ;

:radseg | ( angle r1 r2 g -- ) draws radius circle (xc, yc)
	>r pick2 >r r ccircle_ rot r> ccircle_ r> glineg drop ;

:ticks | draw clock ticks
	0 ( 12 <? )( dup $10000 12 / * tickmin dup ticksize + sg radseg 1 + ) drop ;

:drawh time 12 mod 60 * + 60 12 * angle 0 hlen hg radseg drop ;

:drawm time drop 60 * + 3600 angle 0 mlen mg radseg ;

:draws time drop drop 60 angle 0 slen sg radseg ;

:drawticks blanco ticks ;

:drawhms drawh drawm rojo draws ;

:drawclock drawticks drawhms ;

:th | ( val x y -- (val*x/y )
    >r * r> / ;

:init sw sh min 2/
      dup 8 10 th 'tickmin !
      dup 1 20 th 'ticksize !
      dup 4 5 th 'slen !
      dup 2 3 th 'mlen !
      dup 1 3 th 'hlen !
      dup 1 200 th 'sg !
      dup 1 100 th 'mg !
          2 100 th 'hg ! ;

:debug dup "%d" print ;

:main init show clrscr debug drawclock cmano 'exit >esc< ;

: 33 main ;
