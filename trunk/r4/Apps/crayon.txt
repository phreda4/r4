| Crayon - dibujador simple
| PHREDA
|---------------------
^r4/lib/btn.txt
^r4/lib/dlg.txt
^r4/lib/graf.txt
^r4/lib/polygr.txt

#sizebmr
#wbmr
#hbmr
#rbrm

#nombre )( 256

:resetbmr
	sw dup 'wbmr !
	sh dup 'hbmr !
	* 'sizebmr !
	32 'rbrm !
	0 'nombre !
	;

|------------ Grabar simple RLE
#cnt
#colact

:scanlast | --
	colact cnt ( 255 >? )( over $ff000000 or , 255 - )
	24 << or , ;

:scanconv | adr -- adr+
	@+ $ffffff and | adr+ color
	colact =? ( 1 'cnt +! drop ; )
	scanlast
:scanini | color --
	$ffffff and 'colact ! 1 'cnt ! ;

:grabarpad
	mark	| guarda texto
	"BMR." ,s
	xfb
	@+ scanini
	sizebmr ( 1- 1? )( >r scanconv r> ) 2drop
	nombre 0? ( "media/bmr/crayonpad.bmr" )( 'nombre ) nip
	savemem
	empty
	;

|-------------- Cargar simple RLE
:cargarpad
	mark
	here dup
	nombre 0? ( "media/bmr/crayonpad.bmr" )( 'nombre ) nip
	load 'here !
	0 0 setxy
	4+
	( here <? )(
		@+
		dup $ffffff and swap 24 >> $ff and | color cant
		( 1? )( over px!+ 1- ) 2drop
		) drop
	empty
	>xfb
	;

|--- FILES
#cntf
#actf

:clickf
    cargarpad
	exit ;

:savefile
	usogui
	"media/bmr/" dir
	0 ( dup file 1? )( drop 1+ ) drop 'cntf !
	show xfb>scr
		scr fonti home cr2
		verde  [ "" dir exit ; ] dup "EDIT" sp btnt >esc<

		cr cr
		0 ( cntf <? )(
			actf =? ( cyan )( negro )
			'clickf
			over file "%s" mprint
			16 sp btntf cr cr
			bout? 1? ( chome cr2 ) drop
			1+ ) drop

	 	clapiz 	;

|---- nuevo archivo
:clickf
	;

:finnew
	exit
	'nombre c@ 0? ( drop ; ) drop
	'nombre "media/bmr/%s" mprint 'nombre strcpy
    grabarpad
	;

:newfile
	usogui
	show xfb>scr
		scr fonti home cr2
		verde  [ "" dir exit ; ] dup "BACK" sp btnt >esc<
		blanco
		" NEW FILE" print cr cr
		"media/bmr/" sp print
		'nombre 32 sp getline
		'finnew lostfoco
	 	clapiz 	;

|--- trazo
#trazo )( 2048
#trazo> 'trazo

:++trazo | px --
	trazo> 4 - @ =? ( drop ; )
:+trazo | px --
	trazo> !+ 'trazo> ! ;
:-trazo
	'trazo 'trazo> ! ;

:enlineas
	'trazo @+ 0? ( 2drop ; ) d>xy op
	( trazo> <? )( @+ d>xy line ) drop ;

:engrosor
	linegr 0? ( drop enlineas ; ) drop
	'trazo @+ 0? ( 2drop ; ) d>xy gop
	( trazo> <? )( @+ d>xy gline ) drop ;

:dibujalinea
	engrosor >xfb ;

|--- grosor
#winGr 1 0 0 80 200 "Line"
:dlgGr
	linegr
	0 ( 18 <? )(
		over =? ( azul )( cyan )
		[ dup linegg! ; ] over " %d " sp btnt cr cr2
		2 + ) drop
	blanco "= %d =" printc
	;

|----- Main
:main
	usogui
	0 linegg!

	$ffffff col!

	10 sh 230 - dlgColat!
	sh 230 - 200 'winGr 4+ !+ !

	show xfb>scr
		coloractual ink
		[ xymouse xy>d +trazo ; ]
		[ xymouse xy>d ++trazo ; ]
		[ dibujalinea -trazo ; ]
		guiMap
	    engrosor

		scr fonti home 	cr2
		rojo 'exit dup "EXIT" sp btnt >esc<
		verde
		[ cls >xfb ; ] "CLS" sp btnt
		azul
		'savefile "LOAD" sp btnt
		'newfile "NEW" sp btnt

		0 rows 2 - gotoxy
		coloractual ink
		sp dlg.color
		azul 'dlgGr 'winGr sp guiWin | grosor
	 	clapiz
		;

:
    0 paper
    cls >XFB
	mark
	resetbmr
	cargarpad
	main
	grabarpad
	;

