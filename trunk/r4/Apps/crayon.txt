| Crayon - dibujador simple
| PHREDA
|---------------------

^r4/lib/btn.txt
^r4/lib/dlg.txt
^r4/lib/graf.txt

#sizebm

|------------ Grabar simple RLE
#memc
#memc>

:,comp | v --
	memc> !+ 'memc> ! ;

#cnt
#colact

:scanlast | --
	colact cnt ( 255 >? )( over $ff000000 or ,comp 255 - )
	24 << or ,comp ;

:scanconv | adr -- adr+
	@+ $ffffff and | adr+ color
	colact =? ( 1 'cnt +! drop ; )
	scanlast
:scanini | color --
	$ffffff and 'colact ! 1 'cnt ! ;

:grabar
	mem @+ scanini
	sizebm 2dup 2 << + dup 'memc ! 'memc> !
	( 1- 1? )( >r scanconv r> ) 2drop
	scanlast
	memc memc> over - "bmr/test.bmr" save
	;

|-------------- Cargar simple RLE
:cargar
	mem sizebm 2 << + 'memc !
	memc "bmr/test.bmr" load 'memc> !
	mem >r
	memc ( memc> <? )( @+
		dup $ffffff and swap 24 >> $ff and | color cant
		( 1? )( over r!+ 1- ) 2drop
		) drop
	rdrop
	 ;

|----------------------------------------
#linegro 0

#trazo )( 2048
#trazo> 'trazo

:++trazo | px --
	trazo> 4 - @ =? ( drop ; )
:+trazo | px --
	trazo> !+ 'trazo> ! ;
:-trazo
	'trazo 'trazo> ! ;

:enlineas
	'trazo @+ 0? ( 2drop ; ) >xy op
	( trazo> <? )( @+ >xy line ) drop ;

:engrosor
	linegro 0? ( drop enlineas ; ) drop
	'trazo @+ 0? ( 2drop ; ) >xy gop
	( trazo> <? )( @+ >xy gline ) drop ;

:dibujalinea
	scr engrosor
	>XFB
	;


|--- grosor
:gr!
	dup 'linegro !
	dup gg gg
	;

#winGr 1 10 32 90 200 "GROSOR"
:dlgGr
	linegro
	0 ( 18 <? )(
		over =? ( azul )( cyan )
		[ dup gr! ; ] over "  %d  " sp btnt cr cr2
		2 + ) drop
	blanco "= %d =" printc
	;

|----- Main
:main
	usogui
	0 gr!
	$ffff col!
	sw 360 - sh 320 - dlgColat!

	show xfb>scr
		coloractual ink
		[ xymouse xy> +trazo ; ]
		[ xymouse xy> ++trazo ; ]
		[ dibujalinea -trazo ; ]
		guiMap
		coloractual ink

	    engrosor

		scr fonti home 	cr2
		rojo 'exit dup "EXIT" sp btnt >esc<
		verde
|		[ cls >xfb ; ] "CLS" sp btnt
|		0 "SAVE" sp btnt
|		0 "LOAD" sp btnt
		coloractual ink
		sp dlg.color
		cyan
		'dlgGr 'winGr guiWin | grosor

	 	clapiz
		;

:
    0 paper
    cls >XFB

	mark
	sw sh *	'sizebm !
	main ;

