| Crayon - dibujador simple
| PHREDA
|---------------------
^r4/lib/btn.txt
^r4/lib/dlg.txt
^r4/lib/graf.txt
^r4/lib/polygr.txt

#sizebm

|------------ Grabar simple RLE
#memc
#memc>

:,comp | v --
	memc> !+ 'memc> ! ;

#cnt
#colact

:scanlast | --
	colact cnt ( 255 >? )( over $ff000000 or ,comp 255 - )
	24 << or ,comp ;

:scanconv | adr -- adr+
	@+ $ffffff and | adr+ color
	colact =? ( 1 'cnt +! drop ; )
	scanlast
:scanini | color --
	$ffffff and 'colact ! 1 'cnt ! ;

:grabar
	mem @+ scanini
	sizebm 2dup 2 << + dup 'memc ! 'memc> !
	( 1- 1? )( >r scanconv r> ) 2drop
	scanlast
	memc memc> over - "bmr/test.bmr" save
	;

|-------------- Cargar simple RLE
:cargar
	mem sizebm 2 << + 'memc !
	memc "bmr/test.bmr" load 'memc> !
	mem >r
	memc ( memc> <? )( @+
		dup $ffffff and swap 24 >> $ff and | color cant
		( 1? )( over r!+ 1- ) 2drop
		) drop
	rdrop
	 ;
|--- FILES
#winFl 0 100 32 160 300 "FILES"
:dlgFl
	linegr
	0 ( 18 <? )(
		over =? ( azul )( cyan )
		[ dup linegg! ; ] over " %d " sp btnt cr cr2
		2 + ) drop
	blanco "= %d =" printc
	;

|--- trazo
#trazo )( 2048
#trazo> 'trazo

:++trazo | px --
	trazo> 4 - @ =? ( drop ; )
:+trazo | px --
	trazo> !+ 'trazo> ! ;
:-trazo
	'trazo 'trazo> ! ;

:enlineas
	'trazo @+ 0? ( 2drop ; ) d>xy op
	( trazo> <? )( @+ d>xy line ) drop ;

:engrosor
	linegr 0? ( drop enlineas ; ) drop
	'trazo @+ 0? ( 2drop ; ) d>xy gop
	( trazo> <? )( @+ d>xy gline ) drop ;

:dibujalinea
	engrosor >xfb ;

|--- grosor
#winGr 1 10 32 60 200 "GR"
:dlgGr
	linegr
	0 ( 18 <? )(
		over =? ( azul )( cyan )
		[ dup linegg! ; ] over " %d " sp btnt cr cr2
		2 + ) drop
	blanco "= %d =" printc
	;

|----- Main
:main
	usogui
	0 linegg!
	$ffffff col!

	10 sh 220 - dlgColat!

	show xfb>scr
		coloractual ink
		[ xymouse xy>d +trazo ; ]
		[ xymouse xy>d ++trazo ; ]
		[ dibujalinea -trazo ; ]
		guiMap
	    engrosor

		scr fonti home 	cr2
		rojo 'exit dup "EXIT" sp btnt >esc<
		verde
		[ cls >xfb ; ] "CLS" sp btnt
		'dlgFl 'winFl guiWin | grosor
|		0 "FILES" sp btnt
		coloractual ink
		sp dlg.color
		cyan
		'dlgGr 'winGr guiWin | grosor

	 	clapiz
		;

:
    0 paper
    cls >XFB

	mark
	sw sh *	'sizebm !
	main ;

