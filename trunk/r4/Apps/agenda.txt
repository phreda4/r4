| Agenda
| PHREDA 2011
|_____________
^r4/lib/gui.txt
^r4/lib/db2.txt
^r4/lib/formkit.txt

|--- tablas
#dbagenda 0 "db/agenda/agenda.db"
|--- formularios
#fkagenda 0 "db/agenda/agenda.fk"

|--- listados
#gragenda 0 0
$400 $201 $202 $203 $304 0
"Nombre|Móvil|Particular|Trabajo|e-mail"

|---- titulo
:titulo
	clrscr
	fonta-Arial14
	48 gc.top
	$00528A $10101 hfil
	blanco
|*** DEBUG
		dup "(%d)" printc
|****
	cr2 scr ;

:insertar
	exit
	'fkagenda fkserialice
	'dbagenda dbinsert ;
	;
:nueva
	'fkagenda fkclear
	show titulo
		rojo 'exit dup >esc< "X" sp sp 2 menur
		blanco " >> Nuevo Registro" print
		sp verde 'insertar "Agregar" 6 menur
		cr cr cr2 chome! sp sp chome negro
		'fkagenda fkedit cr
|		'fkagenda fkdump
		cminimano ;

:updregistro
	exit
	'fkagenda fkserialice
	gragenda 'dbagenda dbupdate ;

:delregistro
	exit
	gragenda 'dbagenda dbdelete ;

:editar
	'fkagenda fkclear
	gragenda 'dbagenda dbgetreg
	'fkagenda fkdeserialice
	show titulo
		rojo 'exit dup >esc< "X" sp sp 2 menur
		blanco " >> Registro  " print
		sp verde 'updregistro "Aceptar" 6 menur
		sp rojo 'delregistro "Borrar" 6 menuc

		cr cr cr2 chome! sp sp chome negro
		'fkagenda fkedit
		cminimano ;

|---- PRINCIPAL
#pad )( 64

:testreg | reg -- reg 1/0
	'pad over reg?i ;

:dofilter
	'pad c@ 1? ( 'testreg nip ) 'dbagenda dbfilterv ;

:main
	0 'pad !
	show titulo
		rojo 'exit "X" sp sp 2 menur
		blanco " > Agenda" print
		cr cr
		" >" print
		'pad 32 input 'dofilter lostfoco
		cr cr
		chome! verde
		'nueva "Nueva >>" sp 10 menur cr cr2
		'editar "Editar >>" sp 10 menur cr cr2
		chome blanco
		" Listado " print
		fonta-arial10
		22 'gragenda 'dbagenda dbgrid cr cr

		cminiflecha
		'exit >esc<
		;

:inicio
	$999999 paper
	mark
	'fkagenda fkload
	'dbagenda dbload
	;

: inicio 33 main ;
