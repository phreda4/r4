| rutinas graficas 4
| para compilador
| PHREDA 2012
|----------------------------

^r4/lib/gui.txt
^r4/lib/fontj.txt
^r4/lib/trace.txt

#col1 $ffffff
#col2 0
#tex
#mtx #mty
#ma #mb

#mex #mey

|::ink 'col1 ! ;
|::ink@ col1 ;

#px #py
#cx #cy
#dx

| lineas
#xmin #xmax
#ymin $ffff
#ymax -1
-1 -1 -1 -1
| lo borra el optimizador..por eso en ymax
| segmentos
| ymin x deltax ymax (cnt)
#segs )( $3fff
#segs> 'segs
| activos por linea
| @segs
#activos )( 2048
#activos> 'activos

:activos!+ | v --
	activos> !+ 'activos> ! ;
:-activos	| --
	'activos 'activos> ! ;

|---------- GRAFICOS ------------
:clamp0 | v -- v
	dup neg 31 >> and ;

:clampmax | v max -- v
	swap over - dup 31 >> and + ;

|----- mexcla de colores
:acpx!+ | alpha col --
	dup $ff00ff and				| alpha color colorand
	px@ dup $ff00ff and 		| alpha color colorand inkc inkcand
	pick2 - pick4 * 8 >> rot +	| alpha color inkc inkcandl
	$ff00ff and >r				| alpha color inkc
	swap $ff00 and 				| alpha px colorand
	swap $ff00 and 				| alpha colorand pxa
	over - rot * 8 >> + $ff00 and
	r> or px!+  ;

:gr_mix | a b al -- c (DWORD col,BYTE alpha)
	>r
	dup $ff00ff and pick2 $ff00ff and over - r * 8 >> + $ff00ff and
    rot rot
    $ff00 and swap $ff00 and over - r> * 8 >> + $ff00 and
	or ;

:mixcolor | niv -- color
    1 <? ( col2 nip ; )
    254 >? ( col1 nip ; )
    col1 col2 rot gr_mix ;

|----- linea horizontal
:hlinefill | x1 y x2 --    ; clip x1-x2
	-? ( 3drop ; ) sw >=? ( drop sw 1- rot sw >=? ( 3drop ; ) )( rot )
	clamp0 rot over swap 						| x2 x1 x1 y1
	setxy - 1+
	( 1? )( 1- col1 px!+ ) drop ;

|*******************************
::CP4 | xy --
	'cy ! 'cx ! ;

:linhor | x y --
	-? ( drop 'px ! ; )
	sh >? ( drop 'px ! ; )
	px pick2 <? ( swap pick2 hlinefill 'px ! )( pick2 'px ! hlinefill ) ;

:linizq | x y x1 y1 cnt dx --
	drop
	( 1? )( 1- >r 	| x y x1 y1
		swap dup dx + dup 16 >> rot 16 >>	| x y y1 x1+dx xd1 xd2
		pick3 swap hlinefill					| x y y1 x1+dx ( xd1 y1 xd2  _hline
		swap 1+ r> )
:endline | x y .. .. ..  --
	3drop

|*******************************
::OP4 | xy --
	'py ! 'px ! ;

:linder | x y x1 y1 cnt dx --
	drop
	( 1? )( 1- >r 	| x y x1 y1
		swap dup dx + dup 16 >> rot 16 >>	| x y y1 x1+dx xd1 xd2
		swap pick3 swap hlinefill				| x y y1 x1+dx ( xd2 y1 xd1  _hline
		swap 1+ r> )
	endline ;

|*******************************
::LINE4 | x y --
	py =? ( linhor ; )
	py over - px pick3 - 16 << over / 'dx !							| x y dy dx<<
	-? ( neg >r px 16 << $8000 + py )( >r over 16 << $8000 + over )	| x y x1 y1
	-? ( dup r+ neg dx * + 0 ) sh >? ( r> endline ; )
	r> -? ( endline ; )											| x y x1 y1 cnt
	2dup + sh >? ( sh - - )( drop )
	dx -? ( linizq ; ) linder ;

|------------------------------
:sp-dist | x y xe ye -- x y xe ye dd
	pick3 pick2 - abs pick3 pick2 - abs + ;

:sp-cal1 | x y _cx _cy px py -- x y xn yn
	rot 2* + pick3 + 2 >> >r			| x y cx px   r: ye
	swap 2* + pick2 + 2 >> r> ;			| x y xe ye

:sp-cal2 | cx cy px py -- x y
	rot + 2/ >r + 2/ r> ;

:spl | x y cx cy px py --
	2dup >r >r 2over >r >r sp-cal1 r> r> 			| x y nx ny cx cy --  r: px py
	sp-dist 6 <? ( 3drop rdrop rdrop line line ; ) drop
	>r >r 2over r> r> 2dup >r >r sp-cal2 2swap		| x y xa ya nx ny -- r: cx cy px py
	2dup r> r> r> r> 2dup >r >r sp-cal2 r> r> 		| x y xa ya nx ny nx ny xb yb px py
	spl spl ;

|*******************************
::CURVE4 | xy --
	cx cy px py spl ;

|----------- internas de pintado
#alphaini

|----------------- solido
:_setmixs
	2drop ;
:_inkmixs | -- c
	col1 ;
:_lines | cnt --
	( 1? )( 1- col1 px!+ ) drop ;

|----------------- degrade lineal
:_setmixl | y x -- ; 0<x>sw
	mtx - ma * swap mty - mb * - 'mex ! ;
:_inkmixl | -- c
    mex 8 >> ma 'mex +! mixcolor ;
:_linel | cnt --
	( 1? )( 1-
		mex 8 >> mixcolor px!+
		ma 'mex +! ) drop ;

|----------------- degrade radial
:distf | dx dy -- dis
	-? ( neg ) swap -? ( neg ) swap
	over <? ( swap ) | min max
	dup 8 << over 3 << + over 4 << - swap 2* -
	over 7 << + over 5 << - over 3 << + swap 2* - ;

:_inkmixr | -- c
    mex mey distf 16 >>
    ma 'mex +! mb 'mey +!
	mixcolor ;
:_liner	| cnt --
	( 1? )( 1-
	    mex mey distf 16 >> mixcolor px!+
	    ma 'mex +! mb 'mey +!
		) drop ;

:_setmixr | y x --
:_setmixt | y x --
    dup mtx - ma * pick2 mty - mb * - 'mex !
    mtx - mb * swap mty - ma * + 'mey ! ;

|------------------ textura
:_inkmixt | -- c
    mex 8 >> $ff and mey $ff00 and or 2 << tex + @
    ma 'mex +! mb 'mey +! ;
:_linet	| cnt --
	( 1? )( 1-
	    mex 8 >> $ff and mey $ff00 and or 2 << tex + @ px!+
	    ma 'mex +! mb 'mey +!
		) drop ;

|--------------------
#hmix_	'_setmixs
#hink_	'_inkmixs
#hlin_	'_lines

| hmix_ exec	| x y --
| hink_ exec	| -- c
| hlin_ exec	| cnt --
|*******************************
::FMAT4 'mb ! 'ma ! ;
::FCEN4 'mty ! 'mtx ! ;
::FCOL4 'col2 ! 'col1 ! ;

::SFILL4	'_setmixs 'hmix_ ! '_inkmixs 'hink_ ! '_lines 'hlin_ ! ;
::RFILL4	'_setmixr 'hmix_ ! '_inkmixr 'hink_ ! '_liner 'hlin_ ! ;
::LFILL4	'_setmixl 'hmix_ ! '_inkmixl 'hink_ ! '_linel 'hlin_ ! ;
::TFILL4	'tex ! '_setmixt 'hmix_ ! '_inkmixt 'hink_ ! '_linet 'hlin_ ! ;	| tex --

|---- agrega segmentos ordenados
:insseg | ymax deltax x ymin seg --
	16 + dup
	16 + over segs> over - 2 >> move>
	!+ !+ !+ !
	16 'segs> +! ;

:addseg | ymax deltax x ymin --
	pick3 >? ( 4drop ; )		| (*1) clip invierte
	segs> ( 'segs >=? )( 16 -
		dup @ pick2 <? ( drop insseg ; )	| ordena por ymin
		pick2 =? ( drop dup 8 + @			| ordena por deltax si son =
			pick4 <? ( drop insseg ; ) )
		drop ) drop
	segs> !+ !+ !+ !+ 'segs> ! ;

|************************************
::PLINE4 | x y --
	py =? ( drop 'px ! ; )
	over px - 16 << over py - 				| x y dx dy
	-? ( / py 2over )( / over px py )	| x y deltax ymax xmin ymin
	sh >? ( 4drop 'py ! 'px ! ; )
	-? ( neg pick3 16 *>> + 0 )
	ymin <? ( dup 'ymin ! )
	>r >r
	-? ( rdrop rdrop 2drop 'py ! 'px ! ; )
	ymax >? ( dup 'ymax ! )
	1-  | corta justo un pixel antes, genera (*1)
	swap r> 16 << r>
	addseg | ymax deltax xmin ymin --
	'py !
	xmin <? ( dup 'xmin ! )
	xmax >? ( dup 'xmax ! )
	'px ! ;

|---------------------------------------
|---------------------------------------
|---------------------------------------
#xb
:n@		xb neg px+! ;

:fout | x1 x2 --  x1<x2 x1<sw x2>0
|	dup pick2 - "O (%d) %d %d " print ;
	-? ( 2drop ; ) swap sw >=? ( 2drop ; ) swap
	dup pick2 - |-? ( 3drop ; )
	$ff00 swap / >r | x1 x2 r:delta
	$0 rot rot 					| alpha x1 x2
	sw 1- clampmax
	swap -? ( neg r * rot + swap 0 ) | alpha x2 x1
	2dup px+! 'xb !
	- r> rot rot 					| delta alphai cnt
	hink_ exec 'col1 !
	( 1? )( 1- >r
		dup 8 >> col1 acpx!+ over + r> ) 3drop
	n@ ;

:fin | x1 x2 --    x1<x2 x1<sw x2>0
|	dup pick2 - "I (%d) %d %d " print ;
	-? ( 2drop ; ) swap sw >=? ( 2drop ; ) swap
	dup pick2 - |-? ( trace 3drop ; )
	$ff00 swap / >r
	$ff00 rot rot		| al x1 x2
	sw 1- clampmax
	swap -? ( r * rot + swap 0 ) | alpha x2 x1
	2dup px+! 1+ 'xb !
	- r> rot rot
	hink_ exec 'col1 !
	1+ ( 1? )( 1- >r
		dup 8 >> col1 acpx!+ over - r> )
	3drop
	n@ ;

:fline | x1 x2 --
|	dup pick2 - "L (%d) %d %d " print ;
	over <? ( swap )
	-? ( 2drop ; ) swap sw >=? ( 2drop ; ) swap
	sw 1- clampmax
	swap clamp0 |>r pick4 over hmix_ exec r>
	2dup px+! 'xb !
	- hlin_ exec
	n@ ;


|------------------------------------------
:ttap | y x2 d2 x1 -- y x2 d2 x1.
	dup 16 >> -? ( nip ; ) sw >=? ( nip ; )
	pick3 over hmix_ exec
	dup px+!
	swap 8 >> $ff and ink@ acpx!+
	dup 1+ neg px+! 1+ ;

:ttA | y x2 d2 x1 d1+ -- y x2 d2 x1.
	1.0 <? ( drop ttap ; )
	over + 16 >> >r 16 >> r>  | x1 x2
    pick4 sh 1- =? ( 2drop 1+ ; ) drop
	over 1- swap
|	rojo
	sw px+! fin sw neg px+!
	1+ ;

:ttB | y x2 d2 x1 d1- -- y x2 d2 x1.
	-1.0 >? ( drop ttap ; )
	over + 16 >> swap 16 >> swap over
|	verde
	fin 1+ ;

:ttcp | x1. x2 d2+ --
	+ -? ( 2drop ; )
|	violeta
	dup 16 >> 1+ rot swap fline
	dup 16 >> 1+ sw >=? ( 2drop ; )
	dup px+! 1+ 'xb !
	8 >> not $ff and ink@ acpx!+
	n@
	;

:ttC | x1. x2 d2+ --
	1.0 <? ( ttcp ; )
	over + 16 >> 1+ | x1. x2 x2.
	swap 16 >> 1+ | x1. x2+. x2.
	swap rot pick2 | x2. x2+ x1. x2.
|	azul
	1+ fline fout ;

:ttD | x1. x2 d2- --
	-1.0 >? ( ttcp ; )
	over + 16 >> | x1. x2 x2+.
	swap 16 >> | x1. x2+. x2.
	rot over
|	amarillo
	fline
	pick2 sh 1- =? ( 3drop ; ) drop
	sw px+! fout sw neg px+!
	;

:ttF | x1+ x2 --
	|over clamp0 pick3 hmix_ exec
	16 >> 1+ fline ;

:fillact | y adr -- y adr
	>r r@+ r@+ 		| y p1 p2
	4+
	dup 8 + @ pick3 =? ( 0 r 4 - ! ) drop
	dup @+ swap @ rot over swap +!
	rot				| y x2 d2 p1
	4+
	dup 8 + @ pick4 =? ( 0 r 8 - ! ) drop
	dup @+ swap @ rot over swap +!
					| y x2 d2 x1 d1
	0? ( drop 16 >> )( +? ( ttA )( ttB ) )
	rot rot
	0? ( drop ttF )( +? ( ttC )( ttD ) )
	r> ;


|------------------------------------
:fillactp | y adr -- y adr
	>r r@+ r@+  	| y p1 p2
	4+
	dup 8 + @ pick3 =? ( 0 r 4 - ! ) drop
	dup @+ swap @ rot over swap +!
	rot				| y x2 d2 p1
	4+
	dup 8 + @ pick4 =? ( 0 r 8 - ! ) drop
	dup @+ swap @ rot over swap +!
	"%f %f %f %f " print
					| y x2 d2 x1 d1
	cr
	r> ;


|------------------------------------
:filline | y -- y
	'activos | primero ordena
	@+ 0? ( 2drop ; )
	4+ @ swap  			| x1 'act
	( activos> <? )( @+ 	| x1 'act 'ac2
		4+ @ rot 			| 'act x2 x1
		<? ( over 8 - dup @+ swap @ rot !+ !
				drop 12 - 'activos <? ( 4+ ) @+ 4+ @ )
		swap ) 2drop
	'activos | ahora dibuja
	( activos> <? )(
		fillact
		) drop
	'activos | borra
	( activos> <? )(
		@+ 0? ( drop 4 - activos> 4 - @ over ! -4 'activos> +! )( drop )
		) drop
	;

:enddraw
	'segs 'segs> !
	$ffff dup  'xmin ! 'ymin !
	-1 dup 'xmax ! 'ymax !
	;

|************************************
::POLI4
	xmin sw >? ( drop enddraw ; ) drop
	xmax -? ( drop enddraw ; ) drop

	sh ymax <? ( 'ymax ! )( drop )
	-1 segs> ! | marca ultimo
	-activos
	'segs dup @ ymin | 'seg y1 ymin
	0 over setxy
	( ymax <? )(
		( over =? )( nip
			swap dup activos!+ | agrega nuevos
			16 + dup @ rot )
		filline
		sw px+!
		1+ ) 3drop
	enddraw ;

|---------- zoom
:setpixel | y x color -- y x
	ink
	48 pick2 - 2 << 200 +
	48 pick2 - 2 << 200 +  |xr yr
	swap
	over 5 + over 4 +
	cajaf
	;

:zoom128
	0 0 setxy
	48 ( 1? )( 1-
		48 ( 1? )( 1-
        	px@ setpixel
			1 px+!
			) drop
		sw 48 - px+!
		) drop
	;


#puntos
5 1 8 6 9 4 1 5 -$ffffff

#puntos
10 5 4 9 19 15 16 10 -$ffffff

#puntos
5 5 20 5 20 20 5 20 -$ffffff

#puntos )( 1024
#nrop 16

:movep
	[ 1 'puntos nrop + +! ; ] <ri>
	[ -1 'puntos nrop + +! ; ] <le>
	[ 1 'puntos nrop 4+ + +! ; ] <dn>
	[ -1 'puntos nrop 4+ + +! ; ] <up>
	[ 8 'nrop +! ; ] <f1>
	[ -8 'nrop +! ; ] <f2>
	;

:ypol
	'puntos ( @+ -$ffffff <>? )(
		drop
		over over 4 - +!
		4+ ) 3drop ;

:xpol
	'puntos ( @+ -$ffffff <>? )(
		drop
		4+ over over 4 - +!
		) 3drop ;

:movea
	[ 1 xpol ; ] <dn>
	[ -1 xpol ; ] <up>
	[ 1 ypol ; ] <ri>
	[ -1 ypol ; ] <le>
	;

:pnt | x y --
	over 30 + over 10 +
	setxy ink@ px!+
	setxy ink@ px!+ ;

:test2
	'puntos >r
	r@+ r@+ 2dup op4
	( r@+ -$ffffff <>? )( r@+ pline4 ) drop rdrop
	pline4

   	azul $ff 'col1 !

	$ff $ff0000 FCOL4
	sw 2/ sh 2/ fcen4
	200 100
	over dup * over dup * + 0? ( 1 nip ) >r | gracias IQ
	swap neg r 16 <</ swap r> 16 <</ fmat4
	RFILL4
	poli4

	rojo
|	'puntos >r
|	r@+ 30 + r@+ 10 + 2dup op
|	( r@+ -$ffffff <>? )( 30 + r@+ 10 + pline ) drop rdrop
|	pline poli

	cyan
	blink 1? (
|	'puntos >r ( r@+ -$ffffff <>? )( r@+ pnt ) drop rdrop
	) drop

	blanco cr cr cr cr
	scr 2dup  "%d %d" print
	zoom128
|	movep
	movea
	;


:randpoli
	rand ink
	rand $1ff and
	rand $1ff and
	|xymouse
	2dup op4
	270 ( 1? )( 1-
		rand 7 >> $7ff and 100 -
		rand 7 >> $3ff and 100 -
		pline4 ) drop
	pline4 poli4 ;


:dibujapoli
|		msec 6 >> 'seed !
	5 ( 1? )( 1- randpoli ) drop
	;

:dumpseg
	'segs
	( segs> <? )(
		@+ "y:%d " print @+ "x:%d " print @+ "dx:%d " print @+ "ymax:%d " print cr
		) drop
	;

:makepoli
	'puntos >r
	20 ( 1? )( 1-
		rand 7 >> $1ff and r!+
		rand 7 >> $1ff and r!+
		) drop
	-$ffffff r> ! ;

|----------------------------
|----------------------------
:main
	makepoli
	show clrscr
		test2
		|dibujapoli
		cflecha
		'exit >esc<
		;

: 33 main ;