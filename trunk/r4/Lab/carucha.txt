| Carucha
| Falta corregir dimenciones
| PHREDA
|--------------------------------
^r4/lib/gui.txt
^r4/lib/sprites.txt
^inc/carucha.inc

#vacio 0

#pelos	'dibu1 'dibu2 'dibu3 'dibu4 'dibu5 'dibu6 'dibu7 'dibu8 0
#ojos	'dibu9 'dibua 'dibub 'dibuc 'dibud 'dibue 'dibuf 'dibu10 0
#caras	'dibu11 'dibu12 'dibu13 'dibu14 'dibu15 'dibu16 'dibu17 'dibu18 0
#marcas	'dibu19 'dibu1a 'dibu1b 'dibu1c 'dibu1d 'dibu1e 'dibu1f 'dibu20 0
#narizs	'dibu21 'dibu22 'dibu23 'dibu24 'dibu25 'dibu26 'dibu27 'dibu28 0
#bocas	'dibu29  'dibu2a 'dibu2b 'dibu2c 'dibu2d 'dibu2e 'dibu2f  'dibu30 0

#pelo
#ojo
#cara
#marca
#nariz
#boca

:reset
	pelos 'pelo ! ojos 'ojo ! caras 'cara ! marcas 'marca ! narizs  'nariz ! bocas 'boca ! ;

#medida

#tamojos 30
#altojos 100
#distojos 100
#altoboca 140
#tamboca 40
#altonariz 30
#tamnariz 40
#altopelo 80

#centrox 256
#centroy 384

#nrocara	0
#nromarca	0
#nroojo		0
#nronariz	0
#nroboca	0
#nropelo	0

:dibucara
	centrox centroy 2dup pos
	medida dup 2dup dim
	cara sprite
	marca sprite
	pick3 pick3 altoboca + pos

	medida tamboca 100 */ dup dim
	boca sprite

	pick3 pick3 altonariz + pos
	medida tamnariz 100 */ dup dim
	nariz sprite

	medida tamojos 100 */ dup neg swap dim
	pick3 distojos - pick3 altojos - pos
	ojo sprite

	medida tamojos 100 */ dup  dim
	pick3 distojos + pick3 altojos - pos
	ojo sprite

	dim altopelo - pos
	pelo sprite
	;


:otracara
	nrocara 1+ dup 'nrocara ! 2 << 'caras + @
	0? ( 'nrocara ! caras ) 'cara ! ;

:otramarca
	nromarca  1+ dup 'nromarca ! 2 << 'marcas + @
	0? ( 'nromarca ! marcas ) 'marca ! ;

:otrojo
	nroojo 1+ dup 'nroojo ! 2 << 'ojos + @
	0? ( 'nroojo ! ojos ) 'ojo ! ;

:otranariz
	nronariz 1+ dup 'nronariz ! 2 << 'narizs + @
	0? ( 'nronariz ! narizs ) 'nariz ! ;

:otraboca
	nroboca 1+ dup 'nroboca ! 2 << 'bocas + @
	0? ( 'nroboca ! bocas ) 'boca ! ;

:otropelo
	nropelo 1+ dup 'nropelo ! 2 << 'pelos + @
	0? ( 'nropelo ! pelos ) 'pelo ! ;

:main
	$ff paper
	sw 2/ 'centrox !
	sh 2/ 'centroy !
	sh 2/ 'medida !
	inigui
	'exit >esc<
	'otracara <f1>
	'otramarca <f2>
	'otrojo <f3>
	'otranariz <f4>
	'otraboca <f5>
	'otropelo <f6>
	show clrscr

		0.1 dup fdim 0.9 0.9 fpos
		rojo 'exit 'ifin btnd
		0.05 0.3 fdim
		-0.9 0.7 fpos
		 cyan
		'tamojos vslide gc>>
		'altojos vslide gc>>
		'distojos vslide gc>>
		-0.9 0 fpos
		rojo
		'altoboca vslide gc>>
		'tamboca vslide gc>>
		-0.9 -0.7 fpos
		amarillo
		'altonariz vslide gc>>
		'tamnariz vslide gc>>
		gris
		'altopelo vslide gc>>
|	"cara" 'otracara <f1>
|	"rasgo" 'otramarca dup <f2> .tbtn .>
|	"ojos" verde 'otrojo dup <f3> .tbtn .>
|	"mariz" amarillo 'otranariz dup <f4> .tbtn .>
|	"boca" rojo 'otraboca dup <f5> .tbtn .>
|	"pelo" gris 'otropelo dup <f6> .tbtn .>
		scr dibucara

		cmano
		;


:
|save-dibu
reset
main ;
