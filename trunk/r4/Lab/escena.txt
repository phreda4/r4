| escena
| PHREDA 2009
|------------------------------
^r4/lib/sprite.txt
^r4/lib/gui.txt
^inc/escena.inc

^r4/lib/trace.txt

#piedra.spr 0


#x -2.0
#y 2.4
#z

#vx
#vy

#salto

|**** mapa
#dibujos 0 'dib18 'dib19 'dib16 'dib17 'dib5 'dib6 'dib7
'dib8 'dib9 'diba 'dibb 'dibc 'dibd 'dibe 'dibf

#mapa )( 1024
#xref

:drawm
	$f and 2 << 'dibujos + @ 3dnsprite ;

:drawmapa
	'mapa >r
	mpush
	-8.5 xref - -7.5 0 mtransi
	-8.0 ( 8.0 <? )(
		-8.0 ( 8.0 <? )(
			r@+ 1? ( drawm )( drop )
			1.0 0.0 0.0 mtransi
			1.0 + ) drop
		-16.0 1.0 0.0 mtransi
		1.0 + ) drop
	mpop rdrop ;

:leftscroll
	'mapa dup 4+ 255 move ;

:softscroll | vel --
	xref +
    1.0 >? ( 1.0 - leftscroll )
	'xref ! ;

|*** linea de dibujos

#linea1 0 $1 $1 $1 $1 $1 $1 $1 $1 $1 $1 $1 $1 $1 $1 $1 $1
#linea2 0 $2 $2 $2 $2 $2 $2 $2 $2 $2 $2 $2 $2 $2 $2 $2 $2
#linea3 0 $2 $2 $2 $2 $2 $2 $2 $2 $2 $2 $2 $2 $2 $2 $2 $2
#linea4 0 $3 $3 $3 $3 $3 $3 $3 $3 $3 $3 $3 $3 $3 $3 $3 $3

:lin.draw | 'list
	>r
	mpush
	-7.0 r@+ - 0 0 mtransi
	16 ( 1? )( 1-
		r@+ 1? ( drawm )( drop )
		1.0 0 0 mtransi
		) drop rdrop
	mpop ;

:lin.fill | valor 'list --
	16 2 << + ! ;

:lin.scroll | getval 'list vel --
	over @ +
	1.0 >? ( 1.0 - over 4+ dup 4+ 15 move pick2 exec pick2 lin.fill
		)
	swap ! drop ;


|*** multisprite
:ms.draw | m --
	>r
	( r@+ 1? )(
		mpush
		r@+ r@+ 0 mtransi
		r@+ mrotzi
		3dnsprite
		mpop
		) drop rdrop
	;

:ms.move | x y 'ms --
	>r
	( r@+ 1? )( drop
		over  r @ + r!+
		dup r @ + r!+
		) drop
	rdrop ;

:s.move | x y 'ms nro --
	4 << + 4+
	>r swap
	r +! r> 4+ +! ;

:s.rot | r 'ms nro --
	4 << + 12 + +! ;

:s.ypos | y 'ms nro --
	4 << + 8 + ! ;

#auto
'dibD 0.0 0.0 0.0
'dibC 0.3 0.25 0.0
'dibC -0.3 0.25 0.0
0


|**** particulas
| x  y  s r  vx vy acc tl
|    4  8 12 16 20 24  28
|-28-24-20-16-12 -8 -4

:sxy@ | adrrl -- adrrl x y
	dup 28 - @+ swap @ ;

:svxy! | adrrl +vy +vx -- adrrl
	pick2 12 - !+ ! ;

:sz! | adrrl scale -- adrrl
	over 20 - ! ;

:sr! | adrrl rotacion -- adrrl
	over 16 - ! ;
:+sr! | adrrl +rotacion -- adrrl
	over 16 - +! ;
:sr@ | adrrl  -- adrrl rotacion
	dup 16 - @ ;

:draw2d | adr -- adr tl
	mpush
	>r
	r@+ r@+ 0 mtransi
	r@+ dup 0 mscalei
	r@+ mrotzi
	r@+ r 20 - +!
	r@+ r 20 - +!
	r> @+ exec | adrtl t1
	mpop ;

:delspr | 'lsp 'lsig 'act -- 'lsig
	dup 32 - over | nueva vieja
	pick3 3 << move | de sr cnt
	32 - ;

:+layer | tl 'acc vy vx r z y x  'layer --
	dup @+ | 'listfx 'dirini cantact
	>r r
	5 << + | 'listfx 'diractual
	swap >r | ... y x 'diractual
	!+ !+ !+ !+ !+ !+ !+ !
	r> r> | 'diract cantact
	1+ swap ! ;

:layerdraw | adr --
	dup @+ ( 1? )( 1- swap  | nexl cant lis'
		draw2d
        1? ( swap !+ )( drop 4+
				delspr |trace
				-1 pick3 +! 	| resta 1 a cantidad
				)
		swap )
	3drop ;

:hit? | s1 s2 -- 0/s1
	over =? ( 2drop 0 ; ) | s1=s2?
	>r
	dup @+ r@+ -	| s1 s1y dx
	swap @ r@+ -	| s1 dx dy
	distfast			| s1 dist
	over 8 + @ r@+ + | s1 dist dw
	pick2 12 + @ r> @ +
	distfast 2/ -		| s1 dd
	+? ( 2drop 0 ; )
	drop ;

:layerhit | adr adr -- adr'/0
	@+
	( 1? )( 1-
		swap pick2 hit? 1? ( nip nip nip ; ) drop
		swap )
	nip nip ;

#layerfx 0 )( 8192

:+layerfx | tl 'acc vy vx r z y x --
	'layerfx +layer ;

|--- rand
:r0.1 | -- r
	rand 0.1 mod ;

:r01 | -- r
	rand 1.0 mod ;

|----  ROCKS
:rocks
	0.01 +sr!
	sxy@
	abs 6.0 >? ( 0 nip nip ; ) drop
	abs 6.0 >? ( 0 nip ; ) drop
    -1
    'dib6 3dnsprite ;

| tl 'acc vy vx r z y x  'layer --
:+rocks
	-1 'rocks
	r0.1 r0.1
	r01 0.4 r01 +              | rot y tam
	r01 2* r01 2*
	+layerfx ;

|-------- disparos
:shoot
	dup @ 1- 'dib3 3dnsprite ;

| tl 'acc vy vx r z y x  'layer --
:+shoot
	60 'shoot
	0 0.2
	0 0.1
	y x
	+layerfx ;

| tl 'acc vy vx r z y x  'layer --
:+shootup
	60 'shoot
	-0.2 0
	0 0.1
	y x
	+layerfx ;

|-----------------------
::late | amp -- n
	msec 3 >> $1ff and 7 << *. ;

:pinpon | amp -- n
	msec 3 >> $3ff and $1ff >? ( $3ff swap - ) 7 << *. ;


:vehiculo
	mpush
	x y 0 mtransi
	'auto ms.draw
	mpop

	vx 'x +!
	vy 'y +!


	-0.0164	'auto
	2dup 1 s.rot 2 s.rot
	rand 0.02 mod 0.25 + 'auto 1 s.ypos
	rand 0.02 mod 0.25 + 'auto 2 s.ypos

	y 2.4 <? ( 0.01 'vy +! 0 'salto ! drop ; ) drop
	2.4 'y !
	0 'vy !
	salto 1? ( -0.18 'vy ! 0 'salto ! ) drop
	;

#disparo 0

:principal
	inigui
	'exit >esc<
	[ 0.1 'vx ! ; ] <ri>
	[ -0.1 'vx ! ; ] <le>
	[ 0 'vx ! ; ] dup >ri< >le<
	[ 1 'salto ! ; ] <up>
	[ 1 'disparo ! ; ] <dn>
	[ 0 'disparo ! ; ] >dn<
	show clrscr
		1.0 3dmode

	|***** camara
		0 0 8.0 mtrans

|		drawmapa
|		0.1 softscroll

	|*** fondo
		mpush
		0 0 0 mtrans
        2.0 2.0 1.0 mscale
		'linea1 lin.draw
		[ 1 ; ] 'linea1 0.005 lin.scroll
		0 2.8 0 mtrans
		0.5 0.5 1.0 mscale
		'linea2 lin.draw
		[ 2 ; ] 'linea2 0.02 lin.scroll
		0 1.2 0 mtrans
		0.9 0.9 1.0 mscale
		'linea3 lin.draw
		[ 2 ; ] 'linea3 0.04 lin.scroll
		0 0.9 0 mtrans
		'linea4 lin.draw
		[ 3 ; ] 'linea4 0.1 lin.scroll
		mpop

		'layerfx layerdraw
		vehiculo


	|*** debug
		scr fonti home verde
		x y z vx vy salto 	"salto:%d vy:%f vx:%f z:%f y:%f x:%f" print cr
		layerfx "layerfx: %d" print cr

		dup "%d" print
		cmano
		50 .mseg .restart
		disparo 1? ( +shoot +shootup ) drop ;

: 33 principal ;
