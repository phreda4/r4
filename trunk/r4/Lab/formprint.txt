| formprint.txt
| PHREDA 2011
|-----------------------

| res1: 4950x7000 ( a4? )
|
^r4/lib/btn.txt
^r4/lib/fonth.txt
^r4/lib/fontt.txt

^r4/lib/leebmp.txt

#docu0

|--- posicion y tam ventana
#xi 0 #yi 0
#wi 0 #hi 0

|--- tam impresora
#wpr #hpr

|--- tam ventana
#wven #hven

|--- miniatura
#wm #hm | total
#wc #hc | cursor en miniatura

|-- elemento de posicion
#ccx #ccy
#ccw #cch
#ncmp #nrep
#pad )( 64

|----- Conversiones
| de real a miniatura
:r2m | x y -- xm ym
	swap wm wpr */
	swap hm hpr */ ;

| de real a ventana
:r2v | x y -- xv yv
	swap wven wpr */
	swap hven hpr */ ;

| de ventana a real
:v2r | xv yv -- x y
	swap wpr wven */
	swap hpr hven */ ;

| de bmp a real
:b2r | x y -- xp yp
	swap wpr bmp.ancho */
	swap hpr bmp.alto */ ;

| de real a bmp
:r2b | x y -- xp yp
	swap bmp.ancho wpr */
	swap bmp.alto hpr */ ;

|-- Formulario
| x y | w h | info
#campos )( 8192
#campos> 'campos
#camaux )( 8192
#camaux> 'camaux

:,campos | val --
	campos> !+ 'campos> ! ;

:+campo | tipo dir --
	ccw cch v2r	16 << or ,campos
	ccx ccy v2r 16 << or ,campos
	nrep 16 << ncmp or ,campos
	8 << or ,campos ;

:campos.clear
	'campos 'campos> !
	'camaux 'camaux> !
	;

:campos.dellast
	campos> 'campos =? ( drop ; ) drop
	-16 'campos> +!
	; | ** falta ajustar cmaaux

:,aux | c --
	camaux> c!+ 'camaux> ! ;

:+str | "" -- index
	camaux> swap
	( c@+ 1? )( ,aux ) ,aux
	drop 'camaux - ;


|---- load &save
:loadform | "name" --
	mark
	here dup rot load 'here !
	here =? ( drop empty ; )
	@+ 'campos swap | adr 'campos cant
	( 1? )( 1- >r
		swap @+ rot !+
		r> ) drop 'campos> !
	@+ 'camaux swap
	( 1? )( 1- >r
		swap c@+ rot c!+
		r> ) drop 'camaux> !
	drop
	empty ;

:saveform | "name" --
	mark
	'campos campos> over - 2 >> ,
	( campos> <? )( @+ , ) drop
	'camaux camaux> over - ,
	( camaux> <? )( c@+ ,c ) drop
	savemem
	empty ;

:load.form
	"mem/formp.frm" loadform ;
:save.form
	"mem/formp.frm" saveform ;


|----- show bmp
:showdocu1 | x y -- ; 1X
	0 ( hi <? )(
		pick2 pick2 pick2 + setxy
		xi over yi + bmp.getm
		0 ( wi <? )(
			swap @+ px!+ | al sal anch
			swap 1+ ) 2drop
		1+ ) 3drop ;

:showdocu2 | x y -- ; 2X
	0 ( hi <? )(
		pick2 pick2 pick2 + setxy
		xi 2* over yi + 2* bmp.getm
		0 ( wi <? )(
			swap @+ px!+ | al sal anch
			4+ swap 1+ ) 2drop
		1+ ) 3drop ;

:showdocu4 | x y -- ; 4X
:showdocu8 | x y -- ; 8X
	2drop ;

#sd 0

:drawbmp
|	0 0 sd exec
	;

|---------
:clipxy | x y -- x y
	-? ( -1 nip )
	hi >? ( hi nip )
	swap
	-? ( -1 nip )
	wi >? ( wi nip )
	swap ;

:boxxclip | w h x y --
	2dup clipxy
	|over 4+ over 4+ atxy
	op
	pick3 pick2 + over clipxy line
	pick3 pick2 + pick3 pick2 + clipxy line
	over pick3 pick2 + clipxy line
	clipxy line 2drop ;


:drawcampos
	rojo home
	'campos
	( campos> <? )( >r
		r@+ >uv r2v
		r@+ >uv r2v
		swap xi -
		swap yi -
		boxxclip
		r@+ drop
		r@+ drop |printx
		r> ) drop
	;

|----------- mover cursor y cambiar limites
:cursormov | x y --
	mshift 1? ( drop 3 << )( drop ) 'ccy +!
	mshift 1? ( drop 3 << )( drop ) 'ccx +!

	ccx -? ( 0 'ccx ! )
	wpr ccw - swap <? ( 'ccx ! )( drop )
	ccy -? ( 0 'ccy ! )
	hpr cch - swap <? ( 'ccy ! )( drop )

	ccx xi <? ( dup 'xi ! )
	xi wi + swap ccw + <? ( ccx ccw + swap - 'xi +! )( drop )
	ccy yi <? ( dup 'yi ! )
	yi hi + swap cch + <? ( ccy cch + swap - 'yi +! )( drop )

	xi -? ( 0 'xi ! )
	wpr wi - swap <? ( 'xi ! )( drop )
	yi -? ( 0 'yi ! )
	hpr hi - swap <? ( 'yi ! )( drop )
	;

|------------- en impresora

|    case DOCMOVE: // x y --
|    case DOCLINE: // x y --
|    case DOCTEXT: // "tt" --
|    case DOCSIZE: // "tt" -- w h
|    case DOCFONT: // size angle "font" --

:boxprint | w h x y --
	2dup docat
	pick3 pick2 + over docline
	pick3 pick2 + pick3 pick2 + docline
	over pick3 pick2 + docline
	docline 2drop ;

:labelprint | x y w h
|	8 >> 'cursoraux +
	swap >uv
|	14 0
	"Arial" docfont
	doctext ;

:t0
:t1
:t2
:t3
	2drop
	boxprint ;
|	4drop ;

#tipocampo 't0 't1 't2 't3

:cadacampo
	dup $3 and 2 << 'tipocampo + @ exec ;

:drawcamposprint
	'campos
	( campos> <? )( >r
		r@+ >uv r@+ >uv
		r@+ r@+ cadacampo
		r> ) drop ;

:imprimir
	docini

|	10 0 "Arial" docfont
|	wpr 2/ hpr 2/ docat
|	"texto de prueba" doctext
	drawcamposprint
	docend
	;

|--------
:getpad
	0 'pad !
	usogui
	show
		scr
		"ingrese texto" print cr
		'pad 63 getline
		'exit >esc<
		;

:addline
	0 +campo ;
:addbox
	1 +campo ;
:addlabel
	2 "label1" +str +campo ;
:addcampo
	3 "campo1" +str +campo ; | 'pad

|----- zoom
:partemin
	wi wm wven */ 'wc ! hi hm hven */ 'hc !			| miniatura de pantalla
	;

:zoom1
	'showdocu1 'sd !
	wpr 'wven !
	hpr 'hven !
	partemin ;
:zoom2
	'showdocu2 'sd !
	wpr 2/ 'wven !
	hpr 2/ 'hven !
	partemin ;
:zoom4
	'showdocu4 'sd !
	wpr 2 >> 'wven !
	hpr 2 >> 'hven !
	partemin ;
:zoom8
	'showdocu8 'sd !
	wpr 3 >> 'wven !
	hpr 3 >> 'hven !
	partemin ;

|----- miniatura
:boxx | w h x y --
	2dup op
	pick3 pick2 + over line
	pick3 pick2 + pick3 pick2 + line
	over pick3 pick2 + line
	line 2drop ;

:drawcampominiatura
	rojo
	'campos
	( campos> <? )( >r
		r@+ >uv r2m
		r@+ >uv r2m
		swap wi +
		swap
		boxx
		r@+ drop
		r@+ drop
		r> ) drop
	;

:miniatura
	verde
	wm hm wi 0 boxx
	wc hc xi yi r2m swap wi + swap boxx
	blink 1? ( blanco )( azul ) drop
	ccw cch r2m ccx ccy r2m swap wi + swap boxx
	drawcampominiatura
	;


|----- CURSOR
:ondn
	xymouse
	wm <? ( | cursor
		; )
	| pantalla
	;
:onmove
	;

|--------------
|  EDITANDO
|--------------
:main
	usogui
	show clrscr

|	0 0 showdocu
		drawbmp
		blink 1? ( negro )( azul ) drop
		ccw cch ccx xi - ccy yi - boxx

		drawcampos
		miniatura

		azul fonti home
		dup "(%d)" print cr
		hpr wpr "prn %d %d " print cr

		yi xi "x:%d y:%d " print
		hi wi "wi:%d hi:%d " print cr cr

		ccx ccy "ccx:%d ccy:%d " print cr
		ccw cch "cch:%d cch:%d " print cr
		ncmp nrep "nrep:%d ncmp:%d " print cr

		scr
		[ xymouse yi + 'ccy ! xi + 'ccx ! 0 dup 'cch ! 'ccw ! ; ]	|onDn
		[ xymouse yi + ccy - 'cch ! xi + ccx - 'ccw ! ; ] 			|onMove
		0 guiMap

		48 gc.botton $00000f gc.hfill
		font-vard-8-bold home cr2
		rojo sp 'exit dup >esc< "esc-Exit" btnt
		verde
		sp 'addline dup <f1> "f1-Line" btnt
		sp 'addbox dup <f2> "f2-Box" btnt
		sp 'addlabel dup <f3> "f3-Label" btnt
		sp 'addcampo dup <f4> "f4-Campo" btnt

		amarillo
		sp 'load.form dup <f5> "f5-Load" btnt
		sp 'save.form dup <f6> "f6-Save" btnt
		sp 'imprimir dup <f7> "f7-Print" btnt
		rojo
		sp 'campos.clear dup <f9> "f9-Clear" btnt
		sp 'campos.dellast dup <del> "DEL-Last" btnt cr cr2
		cyan
		sp 'zoom1 "ZoomX1" btnt
		sp 'zoom2 "ZoomX2" btnt
		sp 'zoom4 "ZoomX4" btnt
		sp 'zoom8 "ZoomX8" btnt

		cminiflecha
		keypad
		[ 0 -8 cursormov ; ] <up>
		[ 0 8 cursormov ; ] <dn>
		[ -8 0 cursormov ; ] <le>
		[ 8 0 cursormov ; ] <ri>
		;

:initbmp
	campos.clear

	"db/dbgestoria/form01-a.bmp" bmp.load 'docu0 !

	docres 'hpr ! 'wpr !							| res. impresora
	sw 2/ dup 2/ + 'wi ! sh 32 - 'hi !  			| res. pantalla (px)

	sw wi - dup 'wm ! bmp.alto bmp.ancho */ 'hm !	| res. miniatura
	wi wm bmp.ancho */ 'wc ! hi hm bmp.alto */ 'hc ! | segun bmp

	0 'ccx ! 0 'ccy ! 200 'ccw ! 25 'cch ! 		| cursor
	;

:init
	campos.clear

	docres 'hpr ! 'wpr !							| tam. impresora
	sw 2/ dup 2/ + 'wi ! sh 32 - 'hi !  			| tam. pantalla (px)
	sw wi - dup 'wm ! hpr wpr */ 'hm !				| tam. miniatura

	zoom1

	0 'ccx ! 0 'ccy ! 200 'ccw ! 25 'cch ! 		| cursor

	"db/dbgestoria/form01-a.bmp" bmp.load 'docu0 !
	;

: mark init main ;