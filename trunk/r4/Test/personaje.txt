| personaje animado
| PHREDA
|----------------------------------
^r4/lib/gui.txt
^r4/lib/sprites.txt
^inc/personaje.inc

#x1 #y1 #vx1 #vy1
#s1 'dibu1
#accion
#tacc
#dir1

:salto | 
	y1 0.5 >? ( 'dibu4 )(	'dibu5 )
	's1 ! drop
	;

:golpe
	tacc
	4 <? ( 'dibu6 )( 
		8 <? ( 'dibu7 )( 
		0 'accion ! 'dibu1
		) ) 's1 ! 
	1+ 'tacc ! ;

:patea
	tacc
	4 <? ( 'dibu3 )( 
		8 <? ( 'dibu8  )(
		0 'accion ! 'dibu1
		) ) 's1 ! 
	1+ 'tacc ! ;

:caida
	tacc
	4 <? ( 'dibua )( 
		8 <? ( 'dibub  )(
		0 'accion ! 'dibub
		) ) 's1 ! 
	1+ 'tacc ! ;

:defensa
	tacc
	4 <? ( 'dibuc )( 
		8 <? ( 'dibud  )(
			12 <? ( 'dibuc )(
			0 'accion ! 'dibu1
		) ) ) 's1 ! 
	1+ 'tacc ! ;
	
:saltando
	;

:caminando
	;

#caminar 'dibu1 'dibu2 'dibu1 'dibu3

:camina
	vx1 0? ( drop ; ) drop
	tacc 1+ 7 >? ( 0 nip )
	dup 'tacc !
	1 >> 2 << 'caminar + @ 's1 !
	;

:+accion | acc --
	accion 1? ( 2drop ; )
	drop 'accion !
	0 'tacc ! 	;

:jugadores
	dir1 +? ( 0.3 )( -0.3 ) 0.3 fdim drop
	x1 y1 fpos
	s1 sprite

	vx1 'x1 +!
	vy1 'y1 +!
	y1 -0.5 >? ( -0.01 'vy1 +! salto )( 
		-0.5 <? ( 0 'vy1 ! -0.5 'y1 ! 'dibu1 's1 ! )( camina )
		) drop

	x1 1.0 >? ( 2.0 - ) -1.0 <? ( 2.0 + ) 'x1 !
	;

:posicioninicio
	0.2 'x1 ! -0.5 'y1 !
	0 dup dup 'vx1 ! 'vy1 ! 'tacc !
	;

#xl 1.0 
#yl 0.2 
#vxl 0.015
#vyl -0.04

:nuevol
	rand 1.0 mod 0.6 + 'yl !
	;

:ladrillazo
	0.1 dup fdim
	xl yl fpos
	'dibue xl 5 >>  rsprite
	xl vxl - -1.0 <? ( 1.0 nip nuevol ) 'xl !
	-0.01 'vyl +!
	yl vyl +
	-0.7 <? ( vyl neg 'vyl ! -0.7 nip ) 'yl !
	;

:juego
inigui
'exit >esc<
[ 0.02 dup 'dir1 ! 'vx1 ! ; ] <ri>
[ -0.02 dup 'dir1 ! 'vx1 ! ; ] <le>
[  0 'vx1 ! ; ] dup >ri< >le<
[ y1 -0.5 =? ( 0.12 'vy1 ! ) drop ; ] <up>
[ 'golpe +accion ; ] <spc>
[ 'patea +accion  ; ] <enter>
[ 'caida +accion ; ] <f1>
[ 'defensa +accion ; ] <f2>
[ 'dibu9 's1 ! ; ] <dn>
[ 'dibu1 's1 ! ; ] >dn<
posicioninicio
show clrscr
	jugadores
	accion exec
	ladrillazo
	16 font home blanco 1 0 at
	y1 x1 "%f %f - " print azul
	vy1 vx1 "%f %f" print
	dup " %d" print
	;

: 0 paper 4 juego ;