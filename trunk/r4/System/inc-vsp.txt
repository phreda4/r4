| Editor de includes en vsprite
| PHREDA 2010
|--------------------------------------------------
^r4/lib/btn.txt
^r4/lib/parse.txt
^r4/lib/vsprite.txt
^r4/lib/trace.txt

#nombre )( 256

#orden 0
#nrovsp 0
#nomvsp )( 32

#actual
#cntvar
#indexn )( 3200 | 100 nombres de 32
#index )( 400

|--- parse
:getnro | txt -- txt' 0 / nro
	( dup c@ 33 <? )( 0? ( ; ) drop 1+ ) drop
	dup ?numero 0? ( ; )
	drop rot drop ;

:cpynom | adr en -- adr''
	swap ( c@+ $ff and 32 >? )(
		rot c!+ swap )
	drop 0 rot c! ;

:cadadibujo | adr $23 -- adr'
	drop c@+ $3a <>? ( drop ; ) drop | #:
	cntvar 5 << 'indexn + cpynom
	here cntvar 2 << 'index + !
	( getnro 1? )( , ) ,
	1 'cntvar +!
	;

:parsefile | "" --
	here dup rot load 0 swap !+ 'here !
	0 'cntvar !
	( c@+ 1? )(
		$23 =? ( cadadibujo dup )
		drop
		) 2drop
	;

|--- write
:,codigo | adr --
	0 swap
	( @+ 1? )(
		" $%h" ,print
		swap 1+ 16 >? ( 0 nip ,nl ) swap
		) 3drop
	" 0" ,s ,nl ;

:writefile | "" --
	mark
	"| VSP file" ,s ,nl
	0 ( cntvar <? )(
		'indexn over 5 << + "#:%s" ,print ,nl
		dup 2 << 'index + @ ,codigo
		1+ ) drop
	savemem
	empty ;


|--------------------------------------------------
:editdib
	1 'orden !
	actual 'nrovsp !
	actual 5 << 'indexn + 'nomvsp strcpy
	'orden 40 "mem/inc-vspo.mem" save
	mark
	actual 2 << 'index + @
	( @+ 1? )( , ) , drop
	"mem/notepad.vsp" savemem
	empty
	"r4/system/edit-vsp.txt" run
	;

:newdib
	1 'orden !
	cntvar dup 'nrovsp !
	"new%d" mprint 'nomvsp strcpy
	'orden 40 "mem/inc-vspo.mem" save
	mark
	0 ,
	"mem/notepad.vsp" savemem
	empty
	"r4/system/edit-vsp.txt" run
	;


|----------------------------
:insertamodificado
	'orden "mem/inc-vspo.mem" load drop
	orden 1- 1? ( drop ; ) drop

	here
	dup nrovsp 2 << 'index + !
	"mem/notepad.vsp" load 0 swap !+ 'here !
	'nomvsp nrovsp 5 << 'indexn + strcpy

	nrovsp cntvar =? ( 1 'cntvar +! ) 'actual !
	'nombre writefile

	0 'orden !
	'orden 4 "mem/inc-vspo.mem" save
	;

|----------------------------
:actual! | nro
	-? ( 0 nip )
	cntvar >=? ( cntvar 1- nip )
	'actual !
	refreshfoco ;

:cadadib | n n -- n n
	pick2 cntvar >=? ( drop ; )

	-1.0 -1.0 0 project3d
	1.0 1.0 0 project3d      | x y x y
	pick3 pick2 + 2/ 'xc !
	pick2 over + 2/ 'yc !
	rot - abs >r - abs r> dim

	[ 0.11 ; ] [ 0.09 ; ] guiInOutPre dup fdim
	[ dup actual! ; ] guiBtn
	actual =? ( blanco gc.box )
	dup 2 << 'index + @ 1? ( vsprite )( drop )
	home verde
	5 << 'indexn + printc
	|1+ "%h" print
	rot 1+ rot rot
	;

:tabladib
	omode
	0 0 20.0 mtrans
	0
	-8.0 ( 8.0 <=? )(
		-8.0 ( 8.0 <=? )(
			mpush
			2dup swap  0 mtrans
			cadadib
			mpop
			2.0 + ) drop
		2.0 + )
	2drop ;

:main
	'nombre "mem/inc-vsp.mem" load drop
	'nombre parsefile
	insertamodificado
	usogui
	show clrscr
		32 gc.top $ccc gc.hfill
		home cr2
		verde dup " :R%d" print
		blanco " EDIT VSP " print
		'nombre print
		verde " : " print
		actual 5 << 'indexn + 31 getline

|		cr filetxt print
		26 gc.botton $ccc gc.hfill
		home cr2

	| acciones
		rojo
		sp 'exit dup >esc< "esc-Exit" btnt
		sp verde
		sp 'editdib dup <f1> "f1-Edit" btnt
		sp 'newdib dup <f2> "f2-New" btnt
|		violeta sp 'adddome "+Fold" btnt
|		rojo sp 'borractual "Del" btnt

		[ actual 1+ actual! ; ] <ri>
		[ actual 1- actual! ; ] <le>
		32 26 gc.vbetween
		tabladib

|	'copy-dibu <f2>
|	'ante-dibu <f3>
|	'post-dibu <f4>
|	'borra-dibu <f5>
|	'save-code <f6>
|	'eligearchivo <esp>
|	'editahoja <enter>

		cmano ;

|--------------------------------------------------
:	4 0 paper
	mark
	main
	;

