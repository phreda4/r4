| word analisis
| PHREDA 2010
|----------------------------
^r4/system/r4post.txt

| token(32) | accR(8) niva(8) niv(8) accD(8) |
#accD | DeltaPilaD Acumulado
#accR | DeltaPilaR Acumulado
#niv  | nivel de condicionales
#niva | nivel de anonimas

#:maxw 0				| cantidad de palabras
#:anaword )( 8192 		| 256 palabras max
#anaword> 'anaword

:+tok
	anaword> !+ !+ 'anaword> ! ;

#stacka )( 1024
#stacka> 'stacka

:>aux | a --
	stacka> !+ 'stacka> ! ;
:aux> | -- a
	-4 'stacka> +! stacka> @ ;

:everyword | token rtoken --
	13 =? ( 1 'niv +! )		| (
	15 =? ( -1 'niv +! )	| )
	16 =? ( accD 1+ >aux accR >aux 1 'niva +! )	| [
	17 =? ( aux> 'accR ! aux> 'accD ! -1 'niva +! )	| ]
	3 <? ( 2drop -1 'maxw +! ; )  | no guarda multiples :
	drop
	dup tok>udr
	accR + dup 'accR ! >r
	accD + dup 'accD ! $ff and
	nip
	niv 8 << or
	niva 16 << or
	r> 24 << or
	swap +tok ;

::makeanaword | nro --
	'anaword 'anaword> !
	0 'accD ! 0 'accR !
	0 'niv ! 0 'niva !
	nro>toklen dup 'maxw !
	( 1? )( 1-
		swap @+
		dup $ff and
		everyword
		swap ) 2drop ;

|--- dibuja movimiento pilas
#movp )( 64
#car

:n,car ( 1? )( 1- car ,c 1 'car +! ) drop ;

::movpilas | valor -- ""
	$61 'car ! | a
	here >r 'movp 'here !

	" " ,print

	dup 24 << 24 >> | usedD
	neg dup n,car
	" -- " ,print

	over 8 << 24 >> + | deltaD
	n,car

	24 >> | deltaR
	0 >? ( dup "  R: -- " ,print n,car )
	0 <? ( dup "  R: " ,print neg n,car " --" ,print )
	drop

	,eol
	r> 'here !
	'movp ;

|-- imprime indentado y agrupado (salta cuando consume)
#prevn 0
::printanaword
	'anaword ( anaword> <? )(
		@+ tokenprint sp
    	@+ dup 24 << 24 >> prevn <? ( cr over 8 >> $ff and 2* col )
		'prevn ! drop
		) drop
	;

::printcursoranaword | --
	'anaword ( anaword> <? )(
		@+ tokenprint sp
    	@+ dup 24 << 24 >> prevn <? ( cr over 8 >> $ff and 2* col )
		'prevn ! drop
		) 2drop
	;

