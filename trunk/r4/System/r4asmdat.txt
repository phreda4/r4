| R4asmdat.txt - PHREDA 2010
| Generacion de data para i86-FASM
|
|--------------------------------------------
^r4/lib/gui.txt
^r4/lib/parse.txt
^r4/lib/trace.txt

^r4/system/r4post.txt

:nro>dicn 8 >> "w%h" mprint ;			| numero de palabra

|----- pila de compilador
#pilac )( 256	#pilac> 'pilac

:>pilac pilac> ! 4 'pilac> +! ;
:<pilac -4 'pilac> +! pilac> @ ;
:pilac! 'pilac 'pilac> ! ;

|----- nro de str y label
#nstr	| nro de string

|------------ compila DATO
#d1 "dd "
#d2 "dw "
#d3	"db "
#d4 "rb "
#dtipo 'd1
#dini 0
#dcnt 0

:dfin	dini 1? ( dtipo )( "," ) ,s drop 0 'dini ! ;

:stringdd | cuando hay string dentro de otro tipo
	drop
	dtipo 'd3 =? ( drop "," ,s ; ) drop
	,cr
	"db " ,s ;

:dfins	dini 1? ( "db " )( stringdd ; ) ,s drop 0 'dini ! ;
:dfind	dini 1? ( "dd " )( "," ) ,s drop 0 'dini ! ;

:dtipoch dini 0? ( ,cr 1 'dini ! ) drop  ;

:cpycad | adr --
	( c@+ 1? )( 34 =? ( drop c@+ 34 <>? ( 2drop ; ) ) ,c ) 2drop ;

:,ddefw
:,ddefv drop ;
:,dlit  1 'dcnt +!
		dfin tok>cte
		99 >? ( "$" ,s ,h ; )
		-9999999 <? ( "$" ,s ,h ; )
		,d
		;

:,dlits	1 'dcnt +! dfins tok>str 34 ,c cpycad 34 ,c ",0" ,s ;
:,dwor	1 'dcnt +! dfind nro>dicn ,s ;
:,d;	drop ;
:,d(	drop dtipo >pilac 'd3 'dtipo ! dtipoch ;
:,d)(	drop 'd4 'dtipo ! dtipoch ;
:,d)	drop <pilac 'dtipo ! dtipoch ;
:,d[	drop dtipo >pilac 'd2 'dtipo ! dtipoch ;
:,d]	drop <pilac 'dtipo ! dtipoch ;

#coded 0
,ddefw ,ddefv ,dlit ,dlit ,dlit ,dlit ,dlits ,dwor ,dwor ,dwor ,dwor
,d; ,d( ,d)( ,d) ,d[ ,d]
|----------------------------------------------

|---- datos
:compvar | nropal cntlla -- nropal
	over nro>nom "; %w" ,print
	,cr
	drop dup "w%h " ,print
	'd1 'dtipo !
	1 'dini ! 0 'dcnt ! pilac!
	dup nro>tok
	( @+ dup $ff and
		2 >? )( 	 | dirv n c
		18 <? ( 2 << 'coded + @ exec )( 2drop )
		,sp )
	3drop
	dini 0? ( drop ,cr ; ) drop
	dcnt 1? ( drop ,cr ; ) drop
	dtipo ,s 0 ,d ,cr ;

:otrostr
	over
	1 'dini !
	nstr "s%h " ,print
	,dlits
	1 'nstr +!
	,cr ;

:compvarw | nropal cntlla -- nropal
	drop
	dup nro>toklen
	( 1? )( 1- swap
		@+ dup $ff and
		7 =? ( otrostr ) 2drop
		swap )
	2drop ;


::,comp.dat.asm
	0 'nstr !
    'compvar varmap
	"; *** STRINGS ***" ,print ,cr
    'compvarw wordmap ;
