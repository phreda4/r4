| R4i86o.txt - PHREDA 2010
| Generacion de codigo para i86-FASM
|
| .. [esp] - pila R
| .. [esi] eax - pila D
|
| ebx edi - Libres
| ecx - libre | sar sal
| edx - libre | * /
|
| ebp - VFRAME pointer
| MMX - auxiliares de almacenamiento
|
| nivel 0 -- solo macros,variable y direcciones
| nivel 1 -- llamada a palabras de nivel 0
| nivel 2 -- llamada a palabras de nivel 1 y 0
|--------------------------------------------
^r4/lib/gui.txt
^r4/lib/parse.txt

^r4/system/r4code.txt
^r4/system/vstack.txt

^r4/lib/trace.txt

|----- nro de str y label
#nstr	| nro de string
#nlabel | nro de etiqueta
#nanon	| nro de def anonima

| por palabras
#niva 		| nivel de anonimo
#compara?	| 0 si no esta comparado nos con tos
#jmp?       | 1 si salto en la anterior (para no hacer ret)

::ini.compila
	0 'nstr ! 0 'nlabel ! 0 'nanon ! ;

|----- pila de compilador
#pilac )( 256	#pilac> 'pilac

:>pilac pilac> !+ 'pilac> ! ;
:<pilac -4 'pilac> +! pilac> @ ;
:pilac! 'pilac 'pilac> ! ;

|:nro>dicn2   8 >> 5 << 'indicepal + @ ; | nombre
:nro>dicn   8 >> "w%h" mprint ;			| numero de palabra

:label	"_" ,s ,h 	;
:jmp,	"jmp " ,s label ,cr ;

|---------------------------
:+etiqueta	| -- nueva   // y agrega a pila
	nlabel 1+ dup 'nlabel ! dup >pilac ;

:salto? | adr++ xx xx -- adr++ xx xx 0/1
	pick2 8 - @ $ff and
	18 >? ( 31 <? ( 1 nip ; ) ) 0 nip ;

:nextret? |
	pick2 @ $ff and 12 =? ( 1 )( 0 ) nip ;

:,lit
	dup tok>cte d.pushCte ; |	dup tok>cte ,DUP "mov eax," ,s ,d ,cr ;
:,lits
	nstr d.pushStr 1 'nstr +! ; |	,DUP "mov eax,s" ,s nstr ,h ,cr 1 'nstr +! ;

:,wor
	d.resortstack
	nextret? 1? ( 1 'jmp? ! "jmp " )( "call " ) nip
	,s dup nro>dicn ,ln
	;

:,var
	dup 8 >> d.pushVar ; |	dup nro>dicn ,DUP "mov eax,[" ,s ,s "]" ,s ,cr ;
:,dwo
	dup 8 >> d.pushDcod ; |	dup nro>dicn ,DUP "mov eax," ,s ,s ,cr ;
:,dva
	dup 8 >> d.pushDvar ; |	dup nro>dicn ,DUP "mov eax," ,s ,s ,cr ;


:,[
	1 'niva +!
	nanon d.pushIcod 1 'nanon +! ; |,DUP +etiqueta +etiqueta "mov eax," ,s label ,cr jmp, <pilac label ":" ,ln ;
:,]
	-1 'niva +! ; |<pilac label ":" ,ln ;

|--------------------------------------------
:,AND
	NOSreg
	"and " ,s ,NOS ,, ,TOS ,cr *DROP ;

:,OR
	NOSreg
	"or " ,s ,NOS ,, ,TOS ,cr *DROP ;

:,XOR
	NOSreg
	"xor " ,s ,NOS ,, ,TOS ,cr *DROP ;

:,NOT
	TOSreg
	"not" ,s ,TOS ,cr ;

:,+
	NOSreg
	"add " ,s ,NOS ,, ,TOS ,cr *DROP ;

:,-
	NOSreg
	"sub " ,s ,NOS ,, ,TOS ,cr *DROP ;

:,*
	NOSreg
	"imul " ,s ,NOS ,, ,TOS ,cr *DROP ;

:,/
	NOSreg
	"idiv " ,s ,NOS ,, ,TOS ,cr *DROP ;

:,*/
	NOS2eax
	FREEedx
	"cdq" ,ln
	"imul " ,s ,NOS ,cr
	"idiv " ,s ,TOS ,cr
	*2DROP ;

:,*>>
	TOSecx
	NOS2eax
	FREEedx
	"cdq" ,ln
	"imul dword [esi]" ,ln
	"shrd eax,edx,cl" ,ln
	*2DROP ;

:,<</
	TOSecx
	NOS2eax
	"cdq" ,ln
    "shld eax,edx," ,s ,TOSbyte ,cr
	"idiv " ,s ,NOS ,cr
	*2DROP ;


:,/MOD
	NOSeax
	"cdq" ,ln
	"idiv " ,s ,TOS ,cr
	eaxNOS
	edxTOS ;

:,MOD
	NOSeax
	FREEedx
	"idiv " ,s ,TOS ,cr
	"mov eax,edx" ,ln
	*DROP ;

:,<<
	NOSreg
	"sal ",s ,NOS ,, ,TOS ,cr
	*DROP ;

:,>>
	NOSreg
	"sar ",s ,NOS ,, ,TOS ,cr
	*DROP ;

:,ABS
	"or " ,s ,TOS ,, ,TOS ,cr
	"jp @f" ,ln
	"neg " ,s ,TOS ,cr
	"@@:" ,ln ;

:,CLZ
	"bsr " ,s ,TOS ,, ,TOS ,cr
	"xor " ,s ,TOS ",31" ,ln
	;

:,NEG
	TOSreg
	"neg" ,s ,TOS ,cr ;

:,1+
	TOSreg
	"add " ,s ,TOS ",1" ,s ,cr ;

:,1-
	TOSreg
	"add " ,s ,TOS ",-1" ,s ,cr ;

:,4+
	TOSreg
	"add " ,s ,TOS ",4" ,s ,cr ;


:,2/
	TOSreg
	"sar " ,s ,TOS ",1" ,s ,cr ;

:,2*
	TOSreg
	"sal " ,s ,TOS ",1" ,s ,cr ;

|---------------------
:,EXEC
|	-1 d.plain
	"mov ecx," ,s ,TOS
   	"or ecx,ecx" ,ln
	"jz @f" ,ln
	"call ecx" ,ln
	"@@:" ,ln ;
	;

:,>R
	"push " ,s ,TOS ,cr *DROP ;
| "mov " ,s ,RTOS ,, ,TOS *DROP ;

:,R>
	*DUP "pop " ,s ,TOS ,cr ;
|** nada/mov/ push

:,R
	*DUP
	"mov " ,s ,TOS ,RTOS ,cr ;

:,RDROP
	*RDROP ;

:,R+
	"add [" ,s ,RTOS "]," ,s ,TOS ,cr
	*DROP ;

:,R!+
	"mov [" ,s ,RTOS "]," ,s ,TOS ,cr
	"add " ,s ,RTOS ",4" ,ln
	*DROP ;

:,R@+
	*DUP
	"mov " ,s ,TOS ",[" ,s ,RTOS "]" ,ln
	"add " ,s ,RTOS ",4" ,ln ;

:,@
	"mov " ,s ,TOS "dword [" ,s ,TOS "]" ,s ,cr ;

:,C@
	"movsx " ,s ,TOS "byte [" ,s ,TOS "]" ,s ,cr ;

:,W@
	"movsx " ,s ,TOS "word [" ,s ,TOS "]" ,s ,cr ;

:,!
	"mov [" ,s ,TOS "]," ,s ,NOS ,cr
	*2DROP ;

:,C!
	"mov byte [" ,s ,TOS "]," ,s ,NOSbyte ,cr
	*2DROP ;

:,W!
	"mov word [" ,s ,TOS "]," ,s ,NOSword ,cr
	*2DROP ;

:,+!
	"add [" ,s ,TOS "]," ,s ,NOS ,cr
	*2DROP ;

:,W+!
	"add word [" ,s ,TOS "]," ,s ,NOSword ,cr
	*2DROP ;

:,C+!
	"add byte [" ,s ,TOS "]," ,s ,NOSbyte ,cr
	*2DROP ;

:,!+
	"mov [" ,s ,TOS "]," ,s ,NOS ,cr
	"add " ,s ,TOS ",4" ,s ,cr
	*NIP ;

:,W!+
	"mov word [" ,s ,TOS "]," ,s ,NOSword ,cr
	"add " ,s ,TOS ",2" ,s ,cr
	*NIP ;

:,C!+
	"mov byte [" ,s ,TOS "]," ,s ,NOSbyte ,cr
	"add " ,s ,TOS ",1" ,s ,cr
	*NIP ;

:,@+
	*DUP
	"mov " ,s ,TOS ",[" ,s ,NOS "]" ,ln
	"add " ,s ,NOS ",4" ,ln ;

:,W@+
	*DUP
	"movsx " ,s ,TOS ",word [" ,s ,NOS "]" ,ln
	"add " ,s ,NOS ",2" ,ln ;


:,C@+
	*DUP
	"movsx " ,s ,TOS ",byte [" ,s ,NOS "]" ,ln
	"add " ,s ,NOS ",1" ,ln ;

|------------------------------------
:,0?
	"or " ,s ,TOS ,, ,TOS ,cr
	"jnz " ,s ;

:,+?
	"or " ,TOS ,, ,TOS ,cr
	"js " ,s ;

:,-?
	"or " ,s ,TOS ,, ,TOS ,cr
	"jns " ,s ;

:,1?
	"or " ,s ,TOS ,, ,TOS ,cr
	"jz " ,s ;

:,=?
	"cmp " ,s ,NOS ,, ,TOS ,cr *DROP
	"jnz " ,s ;

:,<?
	"cmp " ,s ,NOS ,, ,TOS ,cr *DROP
	"jge " ,s ;

:,>?
	"cmp " ,s ,NOS ,, ,TOS ,cr *DROP
	"jle " ,s ;

:,<=?
	"cmp " ,s ,NOS ,, ,TOS ,cr *DROP
	"jg " ,s ;

:,>=?
	"cmp " ,s ,NOS ,, ,TOS ,cr *DROP
	"jl " ,s ;

:,<>?
	"cmp " ,s ,NOS ,, ,TOS ,cr *DROP
	"jz " ,s ;

:,and?
	"test " ,s ,NOS ,, ,TOS ,cr *DROP
	"jz " ,s ;

:,nand?
	"test " ,s ,NOS ,, ,TOS ,cr *DROP
	"jnz " ,s ;

|--------- Sistema
:,DIR	"call SYSDIR" ,ln ;
:,FILE  "call SYSFILE" ,ln ;
:,FSIZE "call SYSFSIZE" ,ln ;
:,VOL   "call SYSVOL" ,ln ;
:,LOAD  "call SYSLOAD" ,ln ;
:,SAVE  "call SYSSAVE" ,ln ;
:,UPDATE "call SYSUPDATE" ,ln ;
:,MSEC	"call SYSMSEC" ,ln ;
:,TIME  "call SYSTIME" ,ln ;
:,DATE  "call SYSDATE" ,ln ;
:,END   "jmp SYSEND" ,ln ;
:,RUN   "call SYSRUN" ,ln ;

:,SW    ,DUP  "mov eax,[SYSW]" ,ln ;
:,SH    ,DUP  "mov eax,[SYSH]" ,ln ;
:,MEM   ,DUP  "mov eax,FREE_MEM" ,ln ;

:,PAPER		"mov [SYSPAPER],eax" ,ln ,DROP ;
:,CLS       "call SYSCLS" ,ln ;
:,REDRAW    "call SYSREDRAW" ,ln ;

:,FRAMEV    ,DUP  "mov eax,SYSFRAME" ,ln ;

:,XFB       ,DUP  "mov eax,XFB" ,ln ;
:,>XFB      "call SYSTOXFB" ,ln ;
:,XFB>      "call SYSXFBTO" ,ln ;

:,SETXY | x y --
	"imul eax,dword [SYSW]" ,ln
	"add eax,[esi]" ,ln
	"lea ebp,[SYSFRAME+eax*4]" ,ln
	,2DROP ;

:,PX+!	| s --
	"lea ebp,[ebp+eax*4]" ,ln
	,DROP ;
:,PX!+	| rgb --
	"mov [ebp],eax" ,ln
	"add ebp,4" ,ln
	,DROP ;
:,PX@	| -- rgb
	,DUP "mov eax,[ebp]" ,ln ;


:,XYMOUSE | -- x y
	,DUP
	"lea esi,[esi-4]" ,ln
	"mov eax,[SYSXYM]" ,ln
	"mov ecx,eax" ,ln
	"and ecx,$ffff" ,ln
	"shr eax,16" ,ln
	"mov [esi],ecx" ,ln ;

:,BMOUSE | -- c
	,DUP "mov eax,[SYSBM]" ,ln ;

:,KEY! | v --
	"mov [SYSKEY],eax" ,ln
	,DROP ;

:,KEY	| -- c
	,DUP "mov eax,[SYSKEY]" ,ln ;

|-----------definiciones en asmbase.txt ---------------
:,MOVE :,MOVE> :,CMOVE :,CMOVE>
:,SQRT
:,INK :,INK@ :,ALPHA :,OP :,CP :,LINE :,CURVE :,PLINE :,PCURVE
:,POLI :,FCOL :,FCEN :,FMAT :,SFILL :,LFILL :,RFILL :,TFILL

|-- falta definir
:,CNTJOY :,GETJOY
:,SLOAD :,SPLAY :,MLOAD :,MPLAY
:,OPENURL
:,DOCINI :,DOCEND :,DOCAT :,DOCLINE :,DOCTEXT :,DOCFONT :,DOCBIT
:,DOCRES :,DOCSIZE

:,SYSTEM ;
|-----------definiciones en asmbase.txt ---------------

:,defw :,defv ;

:,;
	jmp? 1? ( drop 0 'jmp? ! ; ) drop
	|d.resortstack | reacomoda pila
	"ret" ,ln ;
:,(
	+etiqueta salto? 1? ( swap label ,cr 1 )( swap label ":" ,ln 2 ) >pilac drop ;
:,)(
	<pilac salto? 0? ( drop
			1 =? ( <pilac +etiqueta jmp, label ":" ,ln 1 >pilac drop ; )
			)( drop
			2 =? ( <pilac +etiqueta label ,cr >pilac 3 >pilac drop ; ) )
	drop ;
:,)
	<pilac salto? 0? ( drop
			1 =? ( <pilac label ":" ,ln drop ; )
			2 =? ( <pilac jmp, drop ; )
			3 =? ( <pilac jmp, <pilac label ":" ,ln drop ; )
		)( drop
			2 =? ( <pilac label ,cr drop ; ) )
	drop ;

|---- nivel 0 - sin desordenar la pila
#nivel0 0
,defw ,defv ,lit ,lit ,lit ,lit ,lits ,wor ,var ,dwo ,dva
,; ,( ,)( ,) ,[ ,] ,EXEC
,0? ,+? ,-? ,1? ,=? ,<? ,>? ,<=? ,>=? ,<>? ,AND? ,NAND? |..30

*DUP *DROP *OVER *PICK2 *PICK3 *PICK4 *SWAP *NIP	|--- pila
*ROT *2DUP *2DROP *3DROP *4DROP *2OVER *2SWAP

,>R ,R> ,R ,R+ ,R@+ ,R!+ ,RDROP					|--- pila direcciones  52
,AND ,OR ,XOR ,NOT  								|--- logicas
,+ ,- ,* ,/ ,*/ ,*>> ,/MOD ,MOD ,ABS  			|--- aritmeticas
,SQRT ,CLZ ,<</
,NEG ,1+ ,4+ ,1- ,2/ ,2* ,<< ,>> | 73
,@ ,C@ ,W@ ,! ,C! ,W! ,+! ,C+! ,W+!  			|--- memoria
,@+ ,!+ ,C@+ ,C!+ ,W@+ ,W!+ | 88
,MOVE ,MOVE> ,CMOVE ,CMOVE>
,MEM ,DIR ,FILE ,FSIZE ,VOL ,LOAD ,SAVE			|--- memoria bloques
,UPDATE
,XYMOUSE ,BMOUSE
,KEY! ,KEY
,CNTJOY ,GETJOY
,MSEC ,TIME ,DATE ,END ,RUN 				|--- sistema
,SW ,SH ,CLS ,REDRAW ,FRAMEV  						|--- pantalla
,SETXY ,PX+! ,PX!+ ,PX@
,XFB ,>XFB ,XFB>
,PAPER ,INK ,INK@ ,ALPHA							|--- color
,OP ,CP ,LINE ,CURVE ,PLINE ,PCURVE ,POLI		|--- dibujo
,FCOL ,FCEN ,FMAT ,SFILL ,LFILL ,RFILL ,TFILL
,SLOAD ,SPLAY	,MLOAD ,MPLAY					|--- Sonido
,OPENURL
,DOCINI ,DOCEND ,DOCAT ,DOCLINE ,DOCTEXT ,DOCFONT ,DOCBIT ,DOCRES ,DOCSIZE  |-- impresora
,SYSTEM


:print.regused
	'regused
	'regname
	5 ( 1? )( rot @+ 0? ( " " )( ">" ) print drop
			rot @+ print cr
			rot 1- ) 3drop

	;

#nrcompila
#codehere
:printdebug
    fonti home verde
	"-----------------------" print cr
	dumpvstack
	"-----------------------" print cr
	print.regused
	"-----------------------" print cr
	dup
	11 >? ( 12 - macroname print  )( "%d" print ) cr cr
	0 here ! codehere print

	nrcompila printword
	;

:espera
	inigui
	'exit >esc<
	show clrscr
		printdebug cflecha ;

| compila nivel 0 de palabras
:,token0 | a -- a.
	reg.refresh
	@+ dup
	$ff and espera
	2 << 'nivel0 + @ exec
	drop
	;

:makevstack | nro --
	nro>mov @ 24 << 24 >> dup uquieto!
	d.ini
	0? ( drop ; )
	neg 1- ( 1? )( dup d.pushEsi 1- )
	d.pushReg
	reg.refresh ;

::,compword | nro --
	0 'niva !
	0 'compara? !
	0 'jmp? !
	dup 'nrcompila ! | solo para ver
	dup makevstack
	here 'codehere !
	nro>toklen
	( 1? )( 1- swap	,token0 swap )
	2drop

	espera | para ver el resultado
	;

