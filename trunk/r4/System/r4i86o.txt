| R4i86o.txt - PHREDA 2010
| Generacion de codigo para i86-FASM
|
| .. [esp] - pila R
| .. [esi] eax - pila D
|
| ebx edi - Libres
| ecx - libre | sar sal
| edx - libre | * /
|
| ebp - VFRAME pointer
| MMX - auxiliares de almacenamiento
|
| nivel 0 -- solo macros,variable y direcciones
| nivel 1 -- llamada a palabras de nivel 0
| nivel 2 -- llamada a palabras de nivel 1 y 0
|--------------------------------------------
^r4/lib/gui.txt
^r4/lib/parse.txt

^r4/system/r4code.txt
^r4/system/r4i86.txt
^r4/system/vtstack.txt

^r4/lib/trace.txt

#,TOS "n/a"
#,TOSb "n/a"
#,NOS "n/a"
#,NOS2 "n/a"

:,DUP :,DROP :,NIP
:,2DROP :,3DROP :,4DROP
:,OVER :,PICK2 :,PICK3 :,PICK4
:,SWAP :,ROT :,2DUP :,2OVER :,2SWAP
	drop nip ;

:,AND	drop nip ,NOS ,TOS "and %w,%w" ,print ;
:,OR	drop nip ,NOS ,TOS "or %w,%w" ,print ;
:,XOR	drop nip ,NOS ,TOS "xor %w,%w" ,print ;
:,NOT	drop nip ,TOS "not %w" ,print ;
:,+		drop nip ,NOS ,TOS "add %w,%w" ,print ;
:,-		drop nip ,TOS ,NOS "sub %w,%w" ,print ;
:,*		drop nip ,NOS ,TOS "imul %w,%w" ,print ;
:,/		drop nip ,NOS ,TOS "idiv %w,%w" ,print ;
:,<<	drop nip ,TOS ,NOS "sal %w,%w" ,print ;
:,>>	drop nip ,TOS ,NOS "sar %w,%w" ,print ;
:,NEG 	drop nip ,TOS "neg %w" ,print ;
:,1+	drop nip ,TOS "add %w,1" ,print ;
:,1-	drop nip ,TOS "add %w,-1" ,print ;
:,4+ 	drop nip ,TOS "add %w,4" ,print ;
:,2/ 	drop nip ,TOS "sar %w,1" ,print ;
:,2* 	drop nip ,TOS "sal %w,1" ,print ;

:,*/
	drop nip
	"cdq" ,ln
	,NOS "imul %w" ,print ,cr
	,TOS "idiv %w" ,print ;

:,*>>
	drop nip
	"cdq" ,ln
	,NOS "imul %w" ,ln
	,TOSb "shrd eax,edx,%w" ,print cr ;

:,<</
	drop nip
	"cdq" ,ln
    ,TOSb "shld eax,edx,%w" ,print cr
	,NOS "idiv %w" ,print ;

:,/MOD
	drop nip
	"cdq" ,ln ,TOS "idiv %w" ,print ;

:,MOD
	drop nip
	,TOS "idiv %w" ,print ,cr
	"mov eax,edx" ,ln ;

:,ABS
	drop nip
	"cdq" ,ln
	,NOS "add %w,edx" ,ln
	,NOS "xor %w,edx" ,ln ;

:,CLZ
	drop nip
	,TOS dup "bsr %w,%w" ,print ,cr
	,TOS "xor %w,31" ,print ;


:,>R	drop nip
		,TOS "push %w" ,print ;
:,>Rv	drop nip ,TOS ,NOS "mov %w,%w" ,print ;

:,R>
		drop nip
		,TOS "pop %w" ,print ;
:,R>v   drop nip ;

:,R 	drop nip ,TOS "mov %w,[esp]" ,print ;
:,Rv 	drop nip ,NOS ,TOS "mov %w,%w" ,print ;

:,R+    drop nip ,TOS "add [esp],%w" ,print ;
:,R+v   drop nip ,TOS ,NOS "add [%w],%w" ,print ;

:,R!+
	drop nip
	,TOS ,NOS "mov [%w],%w" ,print ,cr
	,NOS "add %w,4" ,print ;

:,R@+
	drop nip
	,NOS ,TOS "mov %w,[%w]" ,print ,cr
	,NOS "add %w,4" ,print ;

:,RDROP
	drop nip ;

:,@		drop nip ,TOS ,TOS "mov %w,dword [%w]" ,print ;
:,C@	drop nip ,TOS ,TOS "movsx %w,byte [%w]" ,print ;
:,W@    drop nip ,TOS ,TOS "movsx %w,word [%w]" ,print ;

:,@x1	"lea %w,[%w+%w]" ,print ;
:,@x2	"lea %w,[%w+%w*%w]" ,print ;
:,@x3	"lea %w,[%w+%w+%w*%w]" ,print ;

:,!ex	"mov [%w+%w*%w],%w" ,print ;

:,!		drop nip ,NOS ,TOS "mov [%w],%w" ,print ;
:,C!    drop nip ,NOS ,TOS "mov byte [%w],%w" ,print ;
:,W!    drop nip ,NOS ,TOS "mov word [%w],%w" ,print ;

:,+!    drop nip ,TOS ,NOS "add [%w],%w" ,print ;
:,W+!   drop nip ,TOS ,NOS "add word [%w],%w" ,print ;
:,C+!   drop nip ,TOS ,NOS "add byte [%w],%w" ,print ;

:,!+
	drop nip
	,NOS ,TOS "mov [%w],%w" ,print ,cr
	,TOS "add %w,4" ,print ;
:,W!+
	drop nip
	"mov word [%w],%w" ,print ,cr
	,TOS "add %w,2" ,print ;
:,C!+
	drop nip
	"mov byte [%w],%w" ,print ,cr
	,TOS "add %w,1" ,print ;

:,@+
	drop nip
	"mov %w,[%w]" ,print ,cr
	,TOS "add %w,4" ,print ;

:,W@+
	drop nip
	"movsx %w,word [%w]" ,print ,cr
	,TOS "add %w,2" ,print ;

:,C@+
	drop nip
	"movsx %w,byte [%w]" ,print ,cr
	,TOS "add %w,1" ,print ;

|------------------------------------
:,0?
	drop nip
	,TOS dup "or %w,%w" ,print ,cr
	"jnz " ,s ;
:,+?
	drop nip
	,TOS dup "or %w,%w" ,print ,cr
	"js " ,s ;
:,-?
	drop nip
	,TOS dup "or %w,%w" ,print ,cr
	"jns " ,s ;
:,1?
	drop nip
	,TOS dup "or %w,%w" ,print ,cr
	"jz " ,s ;

:,=?
	drop nip
	,TOS ,NOS "cmp %w,%w" ,print ,cr
	"jnz " ,s ;
:,<?
	drop nip
	,TOS ,NOS "cmp %w,%w" ,print ,cr
	"jge " ,s ;
:,>?
	drop nip
	,TOS ,NOS "cmp %w,%w" ,print ,cr
	"jle " ,s ;
:,<=?
	drop nip
	,TOS ,NOS "cmp %w,%w" ,print ,cr
	"jg " ,s ;
:,>=?
	drop nip
	,TOS ,NOS "cmp %w,%w" ,print ,cr
	"jl " ,s ;
:,<>?
	drop nip
	,TOS ,NOS "cmp %w,%w" ,print ,cr
	"jz " ,s ;
:,and?
	drop nip
	,TOS ,NOS "test %w,%w" ,print ,cr
	"jz " ,s ;
:,nand?
	drop nip
	,TOS ,NOS "test %w,%w" ,print ,cr
	"jnz " ,s ;

|--------- Sistema
:,END   	drop nip "jmp SYSEND" ,ln ;

:,DIR		drop nip "call SYSDIR" ,ln ;
:,FILE  	drop nip "call SYSFILE" ,ln ;
:,FSIZE 	drop nip "call SYSFSIZE" ,ln ;
:,VOL   	drop nip "call SYSVOL" ,ln ;
:,LOAD  	drop nip "call SYSLOAD" ,ln ;
:,SAVE  	drop nip "call SYSSAVE" ,ln ;
:,UPDATE 	drop nip "call SYSUPDATE" ,ln ;
:,MSEC		drop nip "call SYSMSEC" ,ln ;
:,TIME  	drop nip "call SYSTIME" ,ln ;
:,DATE  	drop nip "call SYSDATE" ,ln ;
:,RUN  		drop nip "call SYSRUN" ,ln ;
:,CLS       drop nip "call SYSCLS" ,ln ;
:,REDRAW    drop nip "call SYSREDRAW" ,ln ;
:,>XFB      drop nip "call SYSTOXFB" ,ln ;
:,XFB>      drop nip "call SYSXFBTO" ,ln ;

:,MEM   	drop nip ,TOS "mov %w,FREE_MEM" ,ln ;
:,FRAMEV    drop nip ,TOS "mov %w,SYSFRAME" ,ln ;
:,XFB       drop nip ,TOS "mov %w,XFB" ,ln ;

:,SW    	drop nip ,TOS "mov %w,[SYSW]" ,ln ;
:,SH    	drop nip ,TOS "mov %w,[SYSH]" ,ln ;
:,KEY		drop nip ,TOS "mov %w,[SYSKEY]" ,ln ;
:,BMOUSE 	drop nip ,TOS "mov %w,[SYSBM]" ,ln ;

:,KEY! 		drop nip "mov [SYSKEY],eax" ,ln ;
:,PAPER		drop nip "mov [SYSPAPER],eax" ,ln ;

:,SETXY | x y --
	drop nip
	,TOS "imul %w,dword [SYSW]" ,ln
	,NOS ,TOS "add %w,%w" ,print ,cr
	,TOS "lea ebp,[SYSFRAME+%w*4]" ,print ,cr ;

:,PX+!	| s --
	drop nip
	,TOS "lea ebp,[ebp+%w*4]" ,print ,cr ;

:,PX!+	| rgb --
	drop nip
	,TOS "mov [ebp],%w" ,print ,cr
	"add ebp,4" ,ln ;

:,PX@	| -- rgb
	drop nip
	,TOS "mov %w,[ebp]" ,print ,cr ;

:,XYMOUSE | -- x y
	drop nip
	"lea esi,[esi-4]" ,ln
	"mov eax,[SYSXYM]" ,ln
	"mov ecx,eax" ,ln
	"and ecx,$ffff" ,ln
	"shr eax,16" ,ln
	"mov [esi],ecx" ,ln ;

|-- falta definir
:,CNTJOY :,GETJOY
:,SLOAD :,SPLAY :,MLOAD :,MPLAY
:,OPENURL
:,DOCINI :,DOCEND :,DOCAT :,DOCLINE :,DOCTEXT :,DOCFONT :,DOCBIT
:,DOCRES :,DOCSIZE
:,SYSTEM ;

|-----------definiciones en asmbase.txt ---------------
:,MOVE :,MOVE> :,CMOVE :,CMOVE>
:,SQRT
:,INK :,INK@ :,ALPHA :,OP :,CP :,LINE :,CURVE :,PLINE :,PCURVE
:,POLI :,FCOL :,FCEN :,FMAT :,SFILL :,LFILL :,RFILL :,TFILL ;


#nstr 0
:nro>dicn   8 >> "w%h" mprint ;			| numero de palabra

|--- por ahora nada
:,defw :,defv :,;
:,lit :,lits :,var :,dwo :,dva
:,( :,)( :,) :,[ :,]
	drop nip ;

:,wor
	nro>dicn "jmp " ,s ,s ,cr nip ;

:,EXEC
	drop nip
	,TOS "mov ecx,%w" ,print
   	"or ecx,ecx" ,ln
	"jz @f" ,ln
	"call ecx" ,ln
	"@@:" ,ln ;
	;

:,EXECx1 | 2 >> + @ exec
	"mov ecx,[eax*4+ebx]" ,ln
	"call ecx" ,ln ;

|---- nivel 1 - usando registros
| info adr+ valor -- adr+
#nivel1 0
,defw ,defv ,lit ,lit ,lit ,lit ,lits ,wor ,var ,dwo ,dva | 11
,; ,( ,)( ,) ,[ ,] ,EXEC	| 18
,0? ,+? ,-? ,1? ,=? ,<? ,>? ,<=? ,>=? ,<>? ,AND? ,NAND? | 30
,DUP ,DROP ,OVER ,PICK2 ,PICK3 ,PICK4 ,SWAP ,NIP	|--- pila 38
,ROT ,2DUP ,2DROP ,3DROP ,4DROP ,2OVER ,2SWAP	| 45
,>R ,R> ,R ,R+ ,R@+ ,R!+ ,RDROP					|--- pila direcciones 52
,AND ,OR ,XOR ,NOT  								|--- logicas        56
,+ ,- ,* ,/ ,*/ ,*>> ,/MOD ,MOD ,ABS  			|--- aritmeticas | 65
,SQRT ,CLZ ,<</ ,NEG ,1+ ,4+ ,1- ,2/ ,2* ,<< ,>> | 76
,@ ,C@ ,W@ ,! ,C! ,W! ,+! ,C+! ,W+!  			|--- memoria 85
,@+ ,!+ ,C@+ ,C!+ ,W@+ ,W!+	| 91
,MOVE ,MOVE> ,CMOVE ,CMOVE> | 95
,MEM ,DIR ,FILE ,FSIZE ,VOL ,LOAD ,SAVE			|--- memoria bloques 102
,UPDATE
,XYMOUSE ,BMOUSE
,KEY! ,KEY
,CNTJOY ,GETJOY 							| 109
,MSEC ,TIME ,DATE ,END ,RUN 				|--- sistema 114
,SW ,SH ,CLS ,REDRAW ,FRAMEV  						|--- pantalla
,SETXY ,PX+! ,PX!+ ,PX@
,XFB ,>XFB ,XFB>
,PAPER ,INK ,INK@ ,ALPHA							|--- color
,OP ,CP ,LINE ,CURVE ,PLINE ,PCURVE ,POLI		|--- dibujo
,FCOL ,FCEN ,FMAT ,SFILL ,LFILL ,RFILL ,TFILL
,SLOAD ,SPLAY ,MLOAD ,MPLAY					|--- Sonido
,OPENURL
,DOCINI ,DOCEND ,DOCAT ,DOCLINE ,DOCTEXT ,DOCFONT ,DOCBIT ,DOCRES ,DOCSIZE  |-- impresora
,SYSTEM

|------------------------------
0 | para salto?
#bufftok )( 4096
#buff> 'bufftok

:+buff | tok --
	buff> !+ 'buff> ! ;
:-buff | --
	'bufftok 'buff> ! ;

:buffmap | exec token --
	'bufftok ( buff> <? )(
		@+ $ff and pick2 =? ( drop pick2 exec )( drop )
		) 3drop ;

|--- optimizadores
:calltojump | buff+4 -- buff+4
	dup 8 - @ $ff and
	8 =? ( 1 1 pick3 8 - 4096 + !+ ! ) | W; --> jmpW*****
	| r>; --> jmp
	| exec; -->jmp
	drop
	;

|-------------
:optimiza | nro --
	nro>mov @ 24 << 24 >> neg | uso
	buff> 'bufftok rot vtstack | analisis

|	'calltojump 12 buffmap	| transformo jmp

	;

|----- compila
:niv0 | adr adri+ info -- adri+ adr+
	drop swap
	,token0 ;

| extrae TOS NOS NOS2
:extraeREGS | info -- info
	"TOS" ',TOS !
	"TOSb" ',TOSb !
	"NOS" ',NOS !
	"NOS2" ',NOS2 !
	;

:niv1 | adr adri+ info -- adri+ adr+
	extraeREGS
	rot					| adri+ info adr
	@+ dup $ff and		| adri+ info adr+ valor token
|		trace
	2 << 'nivel1 + @ exec | adri+ adr+
	| ordena registros ?
	;

::,compwordopt | nro --
	| carga en buffer
	-buff
	dup nro>toklen
	( 1? )( 1- swap
		@+ +buff
		swap ) 2drop

	optimiza

	| genera codigo
	'buffinf
	'bufftok ( buff> <? )(
		swap @+
		0? ( niv0 )( niv1 )
		) 2drop
	;
