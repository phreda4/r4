| Editor de includes en sprites
| PHREDA 2010
|--------------------------------------------------
^r4/lib/btn.txt
^r4/lib/parse.txt
^r4/lib/sprite.txt
^r4/lib/trace.txt

#nombre )( 256

#orden 0
#nrospr 0
#nomspr )( 32

#actual
#cntvar
#indexn )( 3200 | 100 nombres de 32
#index )( 400

|--- parse
:getnro | txt -- txt' 0 / nro
	( dup c@ $ff and 33 <? )( 0? ( ; ) drop 1+ ) drop
	dup ?numero 0? ( ; )
	drop rot drop ;

:cpynom | adr en -- adr''
	swap ( c@+ $ff and 32 >? )(
		rot c!+ swap )
	drop 0 rot c!
	;
:cadadibujo | adr $23 -- adr'
	drop c@+ $3a <>? ( drop ; ) drop | #:
	cntvar 5 << 'indexn + cpynom
	here cntvar 2 << 'index + !
	( getnro 1? )( , ) ,
	1 'cntvar +!
	;

:parsefile | "" --
	here dup rot load 0 swap !+ 'here !
	0 'cntvar !
	( c@+ 1? )(
		$23 =? ( cadadibujo dup )
		drop
		) 2drop
	;

|--- write
:,codigo | adr --
	0 swap
	( @+ 1? )(
		" $%h" ,print
		swap 1+ 16 >? ( 0 nip ,nl ) swap
		) 3drop
	" 0" ,s ,nl ;

:writefile | "" --
	mark
	"| Nsprite file" ,s ,nl
	0 ( cntvar <? )(
		'indexn over 5 << + "#:%s" ,print ,nl
		dup 2 << 'index + @ ,codigo
		1+ ) drop
	savemem
	empty ;


|--------------------------------------------------
:editdib
	1 'orden !
	actual 'nrospr !
	actual 5 << 'indexn + 'nomspr strcpy
	'orden 40 "mem/inc-spro.mem" save
	mark
	actual 2 << 'index + @
	( @+ 1? )( , ) , drop
	"mem/notepad.spr" savemem
	empty
	"r4/system/edit-spr.txt" run
	;

:newdib
	1 'orden !
	cntvar dup 'nrospr !
	"new%d" mprint 'nomspr strcpy
	'orden 40 "mem/inc-spro.mem" save
	mark
	0 ,
	"mem/clipspr.mem" savemem
	empty
	"r4/system/edit-spr.txt" run
	;


|----------------------------
:insertamodificado
	'orden "mem/inc-spro.mem" load drop
	orden 1- 1? ( drop ; ) drop

	here
	dup nrospr 2 << 'index + !
	"mem/notepad.spr" load 0 swap !+ 'here !
	'nomspr nrospr 5 << 'indexn + strcpy

	nrospr cntvar =? ( 1 'cntvar +! ) 'actual !
	'nombre writefile

	0 'orden !
	'orden 4 "mem/inc-spro.mem" save
	;

|----------------------------
:actual! | nro
	-? ( 0 nip )
	cntvar >=? ( cntvar 1- nip )
	'actual !
	refreshfoco ;

:cadadib | n n -- n n
	[ 0.11 ; ] [ 0.09 ; ] guiInOutPre dup fdim
	pick2 cntvar >=? ( drop ; )
	[ dup actual! ; ] guiBtn
	actual =? ( blanco gc.box )
	dup 2 << 'index + @ 1? ( nsprite )( drop )
	home verde
	5 << 'indexn + printc
	|1+ "%h" print
	rot 1+ rot rot
	;

:tabladib
	0 0.7 ( -0.7 >=? )(
		-0.7 ( 0.7 <=? )(
			2dup swap fpos
			0.1 dup fdim
			cadadib
			0.2 + ) drop
		0.2 - )
	2drop ;

:main
	'nombre "mem/inc-spr.mem" load drop
	'nombre parsefile
	insertamodificado
	usogui
	show clrscr
		32 gc.top $ccc gc.hfill
		home cr2
		verde dup " :R%d" print
		blanco " EDIT SPR " print
		'nombre print
		verde " : " print
		actual 5 << 'indexn + 31 getline

		26 gc.botton $ccc gc.hfill
		home cr2
	| acciones
		rojo
		sp 'exit dup >esc< "esc-Exit" btnt
		sp verde
		sp 'editdib dup dup <enter> <f1> "f1-Edit" btnt
		sp 'newdib dup <f2> "f2-New" btnt
		[ actual 1+ actual! ; ] <ri>
		[ actual 1- actual! ; ] <le>

		32 26 gc.vbetween
		tabladib

|	'copy-dibu <f2>
|	'ante-dibu <f3>
|	'post-dibu <f4>
|	'borra-dibu <f5>
|	'save-code <f6>
|	'eligearchivo <esp>

		cmano ;

|--------------------------------------------------
:	4 0 paper
	mark
	main
	;

