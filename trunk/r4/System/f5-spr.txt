| Editor en codigo
| PHREDA 2010
|--------------------------------------------------
^r4/lib/gui.txt
^r4/lib/sprite.txt
^r4/lib/parse.txt
^r4/lib/trace.txt

^r4/system/edit-1spr.txt

#codetxt
#codestart
#codelast
#codefin

|--------- edit-code
#ncar
#nlin
#nombre )( 64
#lugar

:cargaeditornl
	'ncar "mem/edit-code.mem" load drop ;

:grabaeditornl
	'ncar 76 "mem/edit-code.mem" save ;

| code a dibujo
|--------------------------------------------------
|::?numero | str -- 0 / str' nro 1

:getnro | txt -- txt' 0 / nro
	( dup c@ 33 <? )( 0? ( ; ) drop 1+ ) drop
	dup ?numero 0? ( ; )
	drop rot drop ;

:findfirst | a -- a
	>>sp trim
	dup c@ $7c =? ( swap >>cr swap ) drop |	si es com pasar la linea
	;

:findlast | a -- a
	( c@+ 33 <? )( 0? ( drop 1- ; ) drop ) drop 1- ;

:load-code
	'dibujoed >r
	codestart
	findfirst
	( getnro 1? )(
		| falta comprobar color solido
		r!+ ) r !
	r> 'dibujoed> !
	findlast
	'codelast !
	;

| savecode
|--------------------------------------------------
:newline 13 ,c 10 ,c ; | m#@$#!%@$#

#compnombre )( 64

:compone | ".ext" "n" -- "n.ext"
	 'compnombre swap
	( c@+ 1? )( rot c!+ swap )  2drop
	swap
	( c@+ 1? )( rot c!+ swap ) rot c!
	drop 'compnombre ;

|--- quita los tokens innecesarios ( los degrade y la matriz )
#buffopt )( $4000
#buffopt> 'buffopt

:codedibu
	0 'buffopt
	buffopt> >=? ( 2drop ; )
	codestart "%w | spr ctrl-E" ,print newline
	( buffopt> <? )(
		@+ 0? ( " %d" )( " $%h" ) ,print
		swap 1+ 8 >? ( drop 0 newline )
		swap ) 2drop
	newline
	;

:procbuff | adr --
	'buffopt swap
	( @+ 1? )(
		dup $f and
	|	$d =? ( drop rot 12 - rot rot dup ) | quitar los que no se usan!!
		drop
		rot !+ swap
		)
	rot !+ 'buffopt> !
	drop
	;

:save-code
	codefin 'here !

	'dibujoed procbuff

	here codetxt codestart over - dup 'here +!
	cmove
	codedibu newline
	here codelast codefin over - dup 'here +!
	cmove
	codefin here over -
	'nombre
	save
	;

|--------------------------------------------------
:	33
	cargaeditornl
	mark
	here dup 'codetxt !
	'nombre load 'codefin !
	codetxt
	lugar ( 1? )( 1- swap c@+ 13 =? ( drop 1+ dup ) drop  swap ) drop
	'codestart !

	resetspr
	load-code
	editandospr
	save-code
	empty ;

