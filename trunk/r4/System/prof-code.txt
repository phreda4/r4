| prof-code.txt
| PHREDA 2010
|---------------------------------------
^r4/lib/gui.txt
^r4/lib/parse.txt
^r4/lib/codecolor.txt
^r4/lib/fontt.txt
^r4/lib/fonta.txt

^r4/lib/fontj.txt
^r4/lib/dlgfile.txt
^r4/lib/trace.txt

^r4/lib/sort.txt
^r4/lib/grid.txt

^r4/system/mem-ed.txt
^r4/system/r4code.txt
^r4/system/anaword.txt
^r4/system/vtstack.txt

#mensaje )( 64
#pagina
#actual

:mensaje! 'mensaje strcpy ;

|-------- VER PALABRA
#iniword 0
#actualw 0

:cadalinea | 'vector --
	iniword rows 1- ( 1? )( 1- swap
		maxw >=? ( 3drop ; )
		pick2 exec
		1+ swap ) 3drop
	;

:ltoken
	dup 3 << 'anaword +
	dup 4+ @ 8 >> $ff and 2* col
	@ tokenprint
    cr ;

#usow
:lcomme	| accR(8) niva(8) niv(8) accD(8) |
	dup 3 << 'anaword +
	4+
	@ dup 24 << 24 >>
	cyan

	dup usow - " %d " print
|	sp dup usow - ( 1? )( 1- "a" print ) drop 10 col

	gris
	" %d" print	| accD
	dup 24 >> " %d" print			| accR
	dup 8 << 24 >> " %d" print		| niva
	16 << 24 >> " %d" print			| niv
	cr ;

:gridword
	[ dup actualw =? ( blanco " %h<" )( verde " %h ") print 4 col cr ; ] cadalinea chome
	'ltoken cadalinea
|	chome gris
|	'lcomme cadalinea
	chome
	iniword sregdumpi
	chome blanco
	iniword oregdumpi
	chome amarillo
	iniword rregdumpi
	chome cyan
	dumpregs
	;

:gentxt
	;

|---------------------------------
:refrescaw
	actualw -? ( 0 nip )
	maxw >=? ( maxw 1- nip )
	iniword <? ( dup 'iniword ! )
	iniword rows 2 - +  >? ( dup rows 2 - - 'iniword ! )
	'actualw !
	;

:viewCode
	0 'iniword !
	0 'actualw !
	actual ind2nro
	dup makeanaword	| arma analisis
	dup vtstacknro     | analisis con tstack
	drop

	show clrscr
		60 gc.top $777f00 gc.hfill
		fonta-verdana14b home
		verde dup " :R%d" print
		blanco "wORD " print
		'ed.nombre "%s " blanco printr
		cr cr2
		actual ind2nro
		dup nro>nom " :%w" negro print
		nro>mov @ blanco
		dup movpilas " | %s  " print
		dup 16 << 24 >> " nivel:%d " print
		dup 24 << 24 >> dup 'usow ! "uso:%d " print
		dup 24 >> "dR:%d " print
		8 << 24 >> "dD:%d " print


		fonta-verdana10b
		rojo 'exit dup >esc< "esc-Exit" sp link
		verde 'gentxt dup <f1> "f1-txt" sp link
		60 0 gc.vbetween fonti home
		gridword
		keypad
		[ 1 'actualw +! refrescaw ; ] <dn>
		[ -1 'actualw +! refrescaw ; ] <up>
		[ 20 'actualw +! refrescaw ; ] <pgdn>
		[ -20 'actualw +! refrescaw ; ] <pgup>

		cmano ;

:value dup 4 - @ ;

:nombre value 8 >> nro>nom ;

:cadavalor
	7 =? ( drop value tok>str """%s"" " azul allowcr print ; )
	8 =? ( 10 nip ) 10 =? ( drop nombre "%w " verde allowcr print ; ) | 'code
	9 =? ( 11 nip ) 11 =? ( drop nombre "%w " amarillo allowcr  print ; ) | 'data
	11 >? ( 12 - ";(|)[]" + c@ blanco emit sp ; )
	drop amarillo
	value tok>cte "%d " allowcr print ;

:printvalor | nro --
	nro>tok
	@+ $ff and 2 =? ( 2drop "0" blanco print ; )
	( 2 >? )( cadavalor @+ $ff and ) 2drop
	;

:viewData
	show clrscr
		60 gc.top $777f0f gc.hfill
		fonta-verdana14b home
		verde dup " :R%d" print
		blanco "WORD " print
		'ed.nombre "%s " blanco printr
		cr cr2
		actual ind2nro nro>nom " #%w" violeta print
		fonta-verdana10b
		rojo 'exit dup >esc< "esc-Exit" sp btnt
		60 0 gc.vbetween fonti home
		actual ind2nro
		dup nro>nom " #%w " violeta print
		printvalor
		cmano ;

:viewWord
	actual ind2nro nro>var? 0? ( drop viewCode ; )
	drop viewData ;

|-------- VER DICCIONARIO
:tipomov
	dup 24 << 24 >> swap
	dup 16 << 24 >> swap
	dup 8 << 24 >> swap
	24 >> "R:%d D:%d N:%d U:%d" gcell
	;

:gridline2 | nro --
	5 << 'indicepal +
	indicepal> >? ( drop ; )
	dup 8 + @ 1 and? ( violeta "#" )( rojo ":" ) nip
	swap @+ rot
	"%s%w" gcell
	@+ "%h" gcell
	@+ "%h" gcell
	@+ "%h" gcell
	@+ "%d" gcellr
	@+ "%d" gcellr
	@+ tipomov |	@+ "%h" gcell
	@ "%w" gcell
	;

:refrescaind
	actual -? ( 0 nip )
	cntw >? ( cntw nip )
	pagina <? ( dup 'pagina ! )
	pagina 20 + >? ( dup 20 - 'pagina ! )
	'actual !
	;

#grid1 1 4 2 2 2 1 1 5 5
:gridindexwords
	'grid1 9 0.96 grid
	"Nro" gcellc
	"string" gcellc
	"token" gcellc
	"info" gcellc
	"mem" gcellc
	"len" gcellc
	"call" gcellc
	"dir" gcellc
	"group" gcellc
	cr
	pagina 21 ( 1? )( 1- swap
		cntw <=? ( rowg
			dup actual =? ( blanco ">%d" )( verde " %d" ) gcellc
			dup ind2nro gridline2 cr )
		1+ swap ) 2drop
	gris $grid
	;

:viewDicc
	show clrscr
		keypad
		'exit >esc<
		50 gc.top $77700f gc.hfill
		fonta-verdana14b home
		verde dup " :R%d" print
		blanco "dICC " print
		'mensaje print
		cntw " %d palabras" print
		'ed.nombre "%s " printr
		cr cr2 fonta-verdana10b
		rojo sp 'exit "esc-Exit" link
		verde sp 'viewWord "enter-viewWord" link
		amarillo

		54 0 gc.vbetween fonti home
		gridindexwords cr
		actual ind2nro dup printword cr
		blanco cr
		nro>info @
		$1 and? ( "var" )( "acc" ) print sp
		$2 and? ( "exp" print sp )
		$100 and? ( "inline" print sp )
		$1000 and? ( "adr" print sp )
		$10000 and? ( "R" print sp )
		$20000 and? ( ";;" print sp )
		$40000 and? ( "[]" print sp )
		$80000 and? ( "Rec" print sp )

		drop
		cr
		[ 1 'actual +! refrescaind ; ] <dn>
		[ -1 'actual +! refrescaind ; ] <up>
		[ 20 'actual +! refrescaind ; ] <pgdn>
		[ -20 'actual +! refrescaind ; ] <pgup>
		'viewWord <enter>

		cmano ;

|-----------------------------------
:clickvar
	dup 'actual !
	dup makeanaword ;

:cadavariable
	drop allowchome
	violeta actual <>? ( oscuro )
	'clickvar over 5 << 'indicepal + @ "#%w " link cr
	;

:clickpalabra
	dup 'actual !
	dup makeanaword ;

:cadapalabra
	drop allowchome
	actual <>? ( rojo oscuro oscuro )( rojo )
	'clickpalabra over 5 << 'indicepal + @ ":%w " link cr
	;

:listofwords
	'cadavariable varmap | 'vector --
	'cadapalabra wordmap | 'vector --
	chome
	actual printname cr verde
	printanaword
|	printcursoranaword
	;

|---------
:fullDicc
	actual makeanaword | arma analisis
	show clrscr
		keypad
		[ 1 'actual +! refrescaind ; ] <dn>
		[ -1 'actual +! refrescaind ; ] <up>
		[ 20 'actual +! refrescaind ; ] <pgdn>
		[ -20 'actual +! refrescaind ; ] <pgup>

		50 gc.top $f gc.hfill
		fonta-verdana14b
		home cr2
		verde dup " :R%d" print
		blanco "dICC " print
		'mensaje print
		cntw " %d palabras" print

		cr cr2 fonta-verdana10b
		rojo sp 'exit dup >esc< "esc-Exit" link
		verde sp 'viewWord dup <enter> "enter-viewWord" link
		cyan

		54 0 gc.vbetween |fonti
		home
		listofwords
		cmano ;

|----------------------------------
:main
	ed.load
	'ed.nombre tokencomp
	msg 1? ( drop saverror ; ) drop
	tokenpost
	msg 1? ( drop saverror ; ) drop
	viewDicc
|	fullDicc
	;

: 0 paper
	mark
	4 main
	empty ;
