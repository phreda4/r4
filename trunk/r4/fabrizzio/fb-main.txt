^r4/lib/math.txt
^r4/lib/gui.txt
^r4/lib/fonth.txt
^r4/lib/trace.txt
^r4/fabrizzio/fb-sprites.inc

|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

^r4/fabrizzio/fb-keyboard.txt

|||| Imported words:

| assignKeys		arguments: --		returns: --
| cleanKeys			arguments: --		returns: --
| checkPressed		arguments: 'key 	returns: Boolean
| checkReleased		arguments: 'key 	returns: Boolean
| checkJustPressed	arguments: 'key 	returns: Boolean
| downKey			arguments: --		returns: 'key
| rightKey			arguments: --		returns: 'key
| leftKey			arguments: --		returns: 'key
| upKey				arguments: --		returns: 'key
| jumpKey			arguments: --		returns: 'key
| fireKey			arguments: --		returns: 'key

|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

^r4/fabrizzio/fb-nodeVars.txt

|||| Imported vars:

| nodeBytes 64

|||| Node Vars
| x  | y  | scale | dir | hspeed | vspeed | friction | grav
|    | 4  |   8   | 12  |   16   |   20   |    24    |  28

| dirgrav | hmask  | vmask  | action/Sprite(for inanimates) | hitAction | flags | alpha | tl
|   32    |  36    |  40    |             44                |    48     |  52   |  56   | 60

|||| Flags:
| flagSolid		= collides or not with players
| flagVisible	= should draw or not sprite on screen
| flagInanimate	= determines whether to just draw sprite or execute action
| flagHostile	= harms player on contact
| flagSemiSolid	= determines if player can go through it from below (requires solid flag)
| flagFacing	= determines which way the particle is facing (0 = right)

|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

^r4/fabrizzio/fb-hitDetection.txt

|||| Imported words:

| moveContact			arguments: 'particle 'me direction				returns: --
| collisionPoint		arguments: x y 'particle						returns: Boolean
| cycleCollisionPoint	arguments: x y 'list listCnt 'me				returns: 'particle (0 if no collision)
| hit?					arguments: addr addr2							returns: Boolean
| cycleHits				arguments: 'listCnt 'list 'me(particle)			returns: --
| defaultHitAction		arguments: 'act 'me								returns: 'newMe

|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

^r4/fabrizzio/fb-auxiliar.txt

|||| Imported words:

| toInteger			arguments: float						returns: integer
| toFloat			arguments: integer						returns: float
| maxValue			arguments: value1 value2				returns: max
| maxValueExt		arguments: value 0 value 1 value 2 etc	returns: representation of the maximum
| minValue			arguments: value1 value2				returns: min
| minValueExt		arguments: value 0 value 1 value 2 etc	returns: representation of the minimum
| calcSpeeds		arguments: speed direction				returns: hspeed vspeed
| calcSpeed			arguments: hspeed vspeed				returns: speed
| calcDir			arguments: hspeed vspeed				returns: newDirection
| calcFriction		arguments: hspeed friction				returns: newHspeed
| pointDirection	arguments: x1 y1 x2 y2					returns: direction
| pointDistSquared	arguments: x1 y1 x2 y2					returns: distSquared
| addGravity		arguments: hspeed vspeed grav gravDir	returns: newHspeed newVspeed

|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

^r4/fabrizzio/fb-particleHandling.txt

|||| Imported vars:

| listfx
| listfxCnt
| listfxSize 256
| listen
| listenCnt
| listenSize 128
| listob
| listobCnt
| listobSize 1024
| player )( 64

|||| Imported words:

| +list				arguments: tl flags hitAction action Vmask Hmask
|								gravityDir gravity friction vspeed hspeed
|								direction scale y x 'list 'listCnt			returns: --
| GeneralUpdate		arguments: 'particle									returns: 'particle
| SimpleUpdate		arguments: 'particle									returns: 'particle
| delspr			arguments: 'listCnt 'list 'toErase						returns: 'newAct
| delList			arguments: 'listCnt										returns: --
| cycleLayer		arguments: 'listCnt 'list								returns: --

|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

^r4/fabrizzio/fb-graphics.txt

|||| Imported vars:

| xcam | ycam | zcam
| playerSprite
| trail
| trail>
| trailSize 64

|||| Imported words:

| ajustacam2		arguments: 'particle to follow			returns: --
| ajustacam			arguments: 'particle to follow			returns: --
| isInsideView		arguments: x y							returns: Boolean
| drawExt			arguments: 'sprite x y					returns: --
| prepareTrail		arguments: trailSize 'trail>			returns: --
| pushTrail			arguments: 'trail trail> 'particle		returns: --
| drawTrail			arguments: trail> 'trail				returns: --
| drawBackground	arguments: xcam							returns: --

|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

^r4/fabrizzio/fb-obstacles.txt

|||| Imported words:

| platformAction	arguments: 'particle			returns: newAddr
| CreatePlatform	arguments: x y					returns: --
| CreateFloor		arguments: x y cont interval	returns: --
| CreateWall		arguments: x y cont interval	returns: --

|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

^r4/fabrizzio/fb-effectParticles.txt

|||| Imported words:

| createStar	arguments:  impulse		returns: --
| createDebri	arguments: x y impulse	returns: --

|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

^r4/fabrizzio/fb-player.txt

|||| Imported words:

| playerAction		arguments: 'me				returns: 'me
| playerHitAction	arguments: 'other 'me		returns: 'newMe
| createPlayer		arguments:  --				returns: --
| createHat			arguments:  x y				returns: --

|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

^r4/fabrizzio/fb-levelHandling.txt

|||| Imported vars:

| currentLevel
| levels

|||| Imported words:

| setActions		arguments: 'list listCnt	returns: --
| loadLevel			arguments: level			returns: --
| nextLevel			arguments: --				returns: 'level (0 if there's no next level)
| gotoLevel			arguments: levelNumber		returns: --
| gotoNextLevel		arguments: --				returns: --

|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
|| Game Loop

#menu 1

| arguments: -- | returns: --
:executeLayers
	'player adjustCam2
	xcam ycam zcam mtrans
	drawBackground
	trail> 'trail drawTrail
	'listfxCnt 'listfx cycleLayer
	'player GeneralUpdate drop
	'listenCnt 'listen cycleLayer
	'listobCnt 'listob cycleLayer
	
	'trail trail> 'player pushTrail
	cleanKeys
	;

| arguments: -- | returns: --
:executeMenu
	xcam ycam zcam mtrans
	drawBackground
	scr home  
	$ff0000 $0 fcol
	5 bfont
	"Platformer Engine" cr printc cr
	menu 1 =? (
		15 bfont 0 8 at
		$ff0000 $0 fcol
		"Play" printc cr

		18 bfont 0 11 at
		$00 $ffffff fcol
		"Exit" printc cr
	)(
		18 bfont 0 10 at
		$00 $ffffff fcol
		"Play" printc cr

		15 bfont 0 9 at
		$ff0000 $0 fcol
		"Exit" printc cr
	)
	drop

	upKey CheckPressed
	1? ( drop 1 'menu ! )( drop	)

	downKey CheckPressed
	1? ( drop 2 'menu ! )( drop )

	fireKey CheckPressed
	1? (
		drop
		menu 2 =? (
			exit
		)(
			0 'menu !
		)
		drop
	)(
		drop
	)
	;

| arguments: -- | returns: --
:debug
	scr home fonti verde
	dup "Must be 33: %d" print cr
	currentLevel "current level: %d" print cr
	listfxCnt "cnt FX: %d" print cr
	listenCnt "cnt EN: %d" print cr
	listobCnt "cnt OB: %d" print cr

	'player @xy swap "pl.x: %f pl.y: %f" print cr
	'player @hvspeeds swap 'player @grav "pl.grav: %f pl.hspeed: %f pl.vspeed: %f" print cr
	;

| arguments: -- | returns: --
| Main loop of the game
:juego
	inigui
	assignKeys
	gotoNextLevel drop
	83.0 -20.0 CreateGoal
	| 3.0 -20.0 CreateGoal
	trailSize 'trail> prepareTrail

	show clrscr
		omode
		menu 1? (
			drop
			executeMenu
		)(
			drop
			executeLayers

			changeLevel 1? (
				drop
				0 'changeLevel !

				gotoNextLevel
				1? (
					83.0 -20.0 CreateGoal
					| 3.0 -20.0 CreateGoal
				)
				drop
			)(
				drop
			)

			debug
			| 100 .mseg .restart
			| 0 createStar
		)
	;

|||| BOOT
: 33
	mark
	juego
	;