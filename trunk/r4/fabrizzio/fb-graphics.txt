|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
|| Graphics

|||| Exported words:

| updateCamSmooth	arguments: 'particle to follow			returns: --
| updateCam			arguments: 'particle to follow			returns: --
| isInsideView		arguments: x y							returns: Boolean
| drawExt			arguments: 'sprite x y					returns: --
| prepareTrail		arguments: trailSize 'trail>			returns: --
| pushTrail			arguments: 'trail trail> 'particle		returns: --
| drawTrail			arguments: trail> 'trail				returns: --
| drawBackground	arguments: xcam							returns: --

| Guarda los ultimos 8 lugares donde has estado (12 bytes por lugar: x, y, facing)
#:trail )( 96
#:trail> 'trail
#:trailSize 96

#:xcam 0
#:ycam 0
#:zcam 10.0

#yback -6.0

#playerTrailSprite 'spr_star

| arguments: 'particle to follow | returns: --
| Smoothly adjusts the camera according to the player's position
::updateCamSmooth
		dup @x xcam neg - 4 >> neg	'xcam +!
		dup @y ycam neg - 4 >> neg 	'ycam +!
		
		@xy
		neg ycam 3.0 + >? ( 3.0 - 'ycam ! )(
			ycam 3.0 - <? ( 3.0 + 'ycam ! )( drop ) )
		neg xcam 3.0 + >? ( 3.0 - 'xcam ! )(
			xcam 3.0 - <? ( 3.0 + 'xcam ! )( drop ) )
	;

| arguments: 'particle to follow | returns: --
| Snaps the camera to the player's position
::updateCam
	@xy neg 'ycam ! neg 'xcam !
	;

| arguments: x y | returns: Boolean
::isInsideView
	ycam neg - abs		| x abs(y-ycam)
	8.0 <? (				| If the vertical position is within view
		drop			| x
		xcam neg - abs	| abs(x-xcam)
		10.0 <? (			| If the horizontal position is within view
			drop		| --
			1 ;
		)
		2drop 0 ;
	)
	3drop 0
	;

| arguments: 'sprite x y | returns: --
|Only draws the sprite if it's within view of the camera
::drawExt
	isInsideView		| 'sprite boolean
	1? (
		drop
		3dnsprite
	)(
		drop
	)
	;

| arguments: 'particle 'nodeSprite x y | returns: --
|Only draws the sprite if it's within view of the camera
::drawExtFrame
	isInsideView		| 'particle 'sprite boolean
	1? (
		drop			| 'particle 'sprite
		swap			| 'sprite 'particle
		@imageIndex		| 'sprite imageIndex
		2 << +			| 'sprite+(imageIndex*4)
		@ 3dnsprite
	)(
		3drop
	)
	;

| arguments: trailSize 'trail> | returns: --
::prepareTrail
	'trail over !		| 'trail> = 'trail
	dup >r @			| trailSize 'fin	| R: 'trail>
	swap				| 'fin trailSize
	( 0 >? )(
		swap			| cnt 'act
		'player @x swap !+		| cnt 'act+4		| act = x
		'player @y swap !+		| cnt 'act+8		| act = y
		0 swap !+				| cnt 'act+12		| act = 0 (facing)
		
		dup r !			| cnt 'act+4		| trail> = 'act+4
		swap 12 -		| 'act+12 cnt-12
	)
	2drop rdrop
	;

| arguments: 'trail trail> 'particle | returns: --
::pushTrail
	rot pick2		| 'fin 'particle 'trail 'fin
	swap dup 12 +	| 'fin 'particle 'fin 'trail 'trail+12
	rot				| 'fin 'particle 'trail 'trail+12 'fin
	over -			| 'fin 'particle 'trail 'trail+12 ('fin-'trail+12)
	2 >>			| 'fin 'particle 'trail 'trail+12 ('fin-'trail+12)/4
	move			| 'fin 'particle
	dup @flagFacing	| 'fin 'particle flagFacing
	pick2 4 - !		| 'fin 'particle				| 'fin-4 = facing
	@xy pick2		| 'fin x y 'fin
	8 - !			| 'fin x 						| 'fin-8 = y
	swap 12 - !		| --							| 'fin-12 = x
	;

| arguments: trail> 'trail | returns: --
::drawTrail
	20 alpha
	2dup - 12 /			| trail> 'trail (cant de mirrors)
	150 swap / 20		| trail> 'trail (cant de alpha incremental entre mirrors) 20
	>r >r				| trail> 'trail		| r: alpha cant
	
	( over <? )(		| 'fin 'act < 'fin
		mpush			| 'fin 'act
		@+ swap			| 'fin x 'act+4
		@+ rot			| 'fin 'act+8 y x
		swap 0 mtransi	| 'fin 'act+8
		@+				| 'fin facing 'act+12
		0? (
			drop
			1.0 1.0 0 mscalei
		)(
			drop
			-1.0 1.0 0 mscalei
		)
		0 mrotzi
		playerTrailSprite 3dnsprite
		mpop
		r> r>			| 'fin 'act cant alpha
		over + dup		| 'fin 'act cant alpha+cant alpha+cant
		alpha >r >r		| 'fin 'act							| r: alpha+cant cant
	)
	255 alpha
	2drop
	rdrop rdrop
	;

| arguments: -- | returns: --
::drawBackground
		| $0000ff paper
		mpush
		-6.0 'yback !
		yback 14.5 + ycam neg 6.0 + <? (
			ycam neg 6.0 + 14.5 - 'yback !
		)(
			yback 14.5 - ycam neg 6.0 - >? (
				ycam neg 6.0 - 14.5 + 'yback !
			)
			drop
		)
		drop
		xcam neg yback 0 mtransi
		
		7.0 14.0 0 mscalei
		'spr_fondo 3dnsprite
		mpop
	;