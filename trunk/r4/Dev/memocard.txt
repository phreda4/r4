| MEMOCARD
| PHREDA 2013
|--------------
^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/rfont.txt
^r4/lib/ricons.txt

^inc/ric/efon.ric
^inc/rft/gooddog.rft


#cartascnt 40
#cartas )( 1024
#cartase )( 1024

#carta1
#carta2

#riccnt 40 	| cantidad de dibujos
#cartasric
i.quotedbl i.percent i.ampersand i.quotesingle i.parenleft i.parenright i.asterisk i.plus
i.comma i.less i.equal i.greater i.question i.at i.A i.B i.C i.D i.E i.F i.G i.H i.I
i.L i.M i.N i.O i.P i.Q i.R i.S i.T i.U i.V i.W i.X i.Y i.Z i.bracketleft i.backslash

:3dop project3d op ;
:3dpline project3d pline ;

:cartaback
	gris oscuro
	0.9 1.0 0 3dop
	-0.9 1.0 0 3dpline
	-0.9 -1.0 0 3dpline
	0.9 -1.0 0 3dpline
	0.9 1.0 0 3dpline
	poli ;

:cartaf
	$888888 ink
	0.9 1.0 0 3dop
	-0.9 1.0 0 3dpline
	-0.9 -1.0 0 3dpline
	0.9 -1.0 0 3dpline
	0.9 1.0 0 3dpline
	poli ;

:cartafront | nro --
	cartaf
	2 << 'cartas + @
	dup 8 >> ink
	$ff and 2 << 'cartasric + @
	3ddrawric
	;

:elijecarta | n --
	carta1 -? ( drop 'carta1 ! ; ) drop
	carta2 -? ( drop 'carta2 ! ; ) drop
	drop ;

:clickCarta
	bmouse 0? ( drop ; ) drop
	xymouse inscreen
	rot - abs 40 >? ( 3drop ; )
	drop - abs 40 >? ( drop ; ) drop
	dup elijecarta
	;

:drawcartam | nro --
	clickCarta
	mpush
	dup 2 << 'cartase + @ dup mrotyi
	0.25 - oxyztransform nip atan2 + | correccion por vista
	$ffff and 0.5 <? ( 2drop cartaback )( drop cartafront )
	mpop ;


:printmazog
	-12.2 -4.0 0 mtransi
	0
	4 ( 1? )( 1-
		10 ( 1? )( 1-
			2.2 0 0 mtransi
			rot dup drawcartam 1+ rot rot
			) drop
		-22.0 2.2 0 mtransi
		) 2drop
	;

:llenacartas
	'cartas >r
	cartascnt ( 1? )( 1-
		dup
		rand 1.0 1.0 hsv2rgb
		8 << or
		r!+ ) drop
	rdrop ;

:freelook
	xymouse
	sh 2/ - 7 << swap
	sw 2/ - neg 7 << swap
	neg mrotx
	mroty ;

:giratodo
	'cartase >r
	cartascnt ( 1? )( 1-
		0.01  r +!
		4 r+ ) drop
	rdrop ;

:esconder
	'cartase >r
	cartascnt ( 1? )( 1-
		0.5  r +!
		4 r+ ) drop
	rdrop ;

:giracarta
	dup 2 << 'cartase + dup @
	0.01 + dup rot !
	drop
	;

:accionmazo
	carta1 +? ( giracarta ) drop
	carta2 +? ( giracarta ) drop
	;

:jugar
	-1 'carta1 !
	-1 'carta2 !
	llenacartas
|	esconder
	33
	show clrscr
		negro
		dup "%d" print cr
		omode
		0 0 14.0 mtrans

		printmazog
		accionmazo
		'exit >esc<
		cflecha
		;

:main
	show clrscr
		gooddog 64 rfont!
		cr
		Azul
		"MemoCard" printc
		cr cr
		verde
		sp sp 'jugar " Play " btnt
		rojo
		sp sp 'exit " Exit " btnt
		cr cr cr
		gooddog 32 rfont!
		negro ":r4 - PHREDA 2013" printc

		'exit >esc<
		cflecha
		;

: $ffffff paper
|main
jugar
0 paper ;