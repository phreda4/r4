| Convierte SVG font a spr
| PHREDA 2014
|---------------------------------------------
^r4/lib/gui.txt
^r4/lib/parse.txt
^r4/lib/trace.txt
^r4/lib/dlgfilea.txt

#$start
#$end

|------------------ PARSE
:=p | s1 s2 -- 1/0
	( c@+ 1? )( rot c@+ rot - 1? ( 3drop 0 ; ) drop swap ) 3drop 1 ;

:findl | adr "texto" -- adr'/0
	( 2dup =p 0? )( drop swap 1+ dup c@ 32 <? ( 3drop 0 ; ) drop swap ) 2drop ;

:findc | 13 adr -- adr'
	( c@+ 1? )( pick2 =? ( drop nip ; ) drop ) nip nip ;

:find | adr "texto" -- adr'
	( 2dup =p 0? )( drop swap 1+ dup c@ 0? ( 3drop 0 ; ) drop swap ) 2drop ;

:nextline | adr -- adr'
	( c@+ 32 >=? )( drop ) 0? ( nip ; ) drop ;

|------------------ NRO
:signo | str -- str signo
	dup c@
	$2c =? ( drop 1+ dup c@ )
	$2b =? ( drop 1+ 0 ; )	| + $2b
	$2d =? ( drop 1+ 1 ; )	| - $2d
	drop 0 ;

#f | fraccion

:getfrac | nro adr' char -- nro adr' char
	drop
	0 swap | nro 0 adr'
	( c@+ $2f >? )(
			$39 >? ( rot 'f ! ; )
			$30 - rot 10* + swap )
	rot 'f ! ;

:getnro | adr -- adr' nro
	0 'f !
	trim
	signo
	over c@ 33 <? ( 2drop 1- 0 ; ) | caso + y - solos
	swap  1? ( [ neg ; ] >r ) drop
	drop
	0 swap ( c@+ $2f >? )(	| 0 adr car
		$39 >? ( drop 1- swap ; )			| 0..9
		$30 - rot 10* + swap )
	$2e =? ( getfrac )
	drop 1- swap ;

:getfnro | adr -- adr fnro
	getnro 16 << $10000 f
	1 over ( 1 >? )( 10 / swap 10* swap ) drop
	*/ $ffff and or
	;

|-------------------------
#colsvg
#svgx #svgy

#$spr

:,spr | nro --
	 $spr !+ '$spr ! ;

:getcolor | adr --
	$23 swap findc 0? ( drop ; ) | #
	str$>nro 'colsvg !
	;

:xy>oo | x y -- d
	4 >> $3fff0 and swap 10 << $fffc0000 and or ;

#last

:,line | x y --
	xy>oo 3 or ,spr ;

:,op | x y --
	xy>oo dup 'last ! 1 or ,spr ;

:,cp | x y --
	xy>oo 2 or ,spr ;

:,curve | x y --
	xy>oo 4 or ,spr ;

:,cp2 | x y --
	xy>oo 9 or ,spr ;

:,curve2 | x y --
	xy>oo 10 or ,spr ;

:,last | --
	last 3 or ,spr ;

:,poly
	colsvg 4 << $d or ,spr ;

:getpoly | adr --
	$22 swap findc 0? ( drop ; ) | "
	getfnro 'svgx ! 1+ getfnro 'svgy ! trim
	svgx svgy ,op
	( dup c@ $22 <>? )( drop
		getfnro 'svgx ! 1+ getfnro 'svgy ! trim
		svgx svgy ,line
		) drop
	,last
	,poly
	;

|------------------------
#x1 #y1 #x2 #y2
:getvb | adr --
	getfnro 'x1 !
	getfnro 'y1 !
	getfnro 'x2 !
	getfnro 'y2 !
	x2 x1 - y2 y1 - dim
	x1 x2 + 2/ y1 y2 + 2/ pos
	;

|----------------------SVG path-----------------------
#sx $100 #sy $100
#x1 #y1
#x2 #y2
#x3 #y3

:moveto | adr -- adr' ; XY
	getfnro 'x1 !
	getfnro 'y1 !
	x1 y1 ,op ;
:movetor | adr -- adr' ; xy
	getfnro 'x1 +!
	getfnro 'y1 +!
	x1 y1 ,op ;

:lineto | adr -- adr' ; XY
	x1 y1 'y2 ! 'x2 !
	getfnro 'x1 !
	getfnro 'y1 !
	x1 y1 ,line ;
:linetor | adr -- adr' ; xy
	x1 y1 'y2 ! 'x2 !
	getfnro 'x1 +!
	getfnro 'y1 +!
	x1 y1 ,line ;
:hori | adr -- adr' ; X
	x1 y1 'y2 ! 'x2 !
	getfnro 'x1 !
	x1 y1 ,line ;
:horir | adr -- adr' ; X
	x1 y1 'y2 ! 'x2 !
	getfnro 'x1 +!
	x1 y1 ,line ;
:vert | adr -- adr' ; Y
	x1 y1 'y2 ! 'x2 !
	getfnro 'y1 !
	x1 y1 ,line ;
:vertr | adr -- adr' ; Y
	x1 y1 'y2 ! 'x2 !
	getfnro 'y1 +!
	x1 y1 ,line ;
:ccurve | adr -- adr' ; XYXYXY
	getfnro 'x3 !
	getfnro 'y3 !
	getfnro 'x2 !
	getfnro 'y2 !
	getfnro 'x1 !
	getfnro 'y1 !
	x1 y1 ,cp
	x2 y2 ,cp2
	x3 y3 ,curve2 ;
:ccurver | adr -- adr' ; XYXYXY
	getfnro x1 + 'x3 !
	getfnro y1 + 'y3 !
	getfnro x1 + 'x2 !
	getfnro y1 + 'y2 !
	getfnro x1 + 'x1 !
	getfnro y1 + 'y1 !
	x1 y1 ,cp
	x2 y2 ,cp2
	x3 y3 ,curve2 ;
:surve | adr -- adr' ; XYXY
	x1 2* x2 - 'x3 !
	y1 2* y2 - 'y3 !
	getfnro 'x2 !
	getfnro 'y2 !
	getfnro 'x1 !
	getfnro 'y1 !
	x1 y1 ,cp
	x2 y2 ,cp2
	x3 y3 ,curve2 ;
:surver | adr -- adr' ; XYXY
	x1 2* x2 - 'x3 !
	y1 2* y2 - 'y3 !
	getfnro x1 + 'x2 !
	getfnro y1 + 'y2 !
	getfnro x1 + 'x1 !
	getfnro y1 + 'y1 !
	x1 y1 ,cp
	x2 y2 ,cp2
	x3 y3 ,curve2 ;
:qurve | adr -- adr' ; XYXY
	getfnro 'x2 !
	getfnro 'y2 !
	getfnro 'x1 !
	getfnro 'y1 !
	x1 y1 ,cp
	x2 y2 ,curve ;
:qurver | adr -- adr' ; XYXY
	getfnro x1 + 'x2 !
	getfnro y1 + 'y2 !
	getfnro x1 + 'x1 !
	getfnro y1 + 'y1 !
	x1 y1 ,cp
	x2 y2 ,curve ;
:turve | adr -- adr' ; XY
	x1 2* x2 - 'x2 !
	y1 2* y2 - 'y2 !
	getfnro 'x1 !
	getfnro 'y1 !
	x1 y1 ,cp
	x2 y2 ,curve ;
:turver | adr -- adr' ; XY
	x1 2* x2 - 'x2 !
	y1 2* y2 - 'y2 !
	getfnro x1 + 'x1 !
	getfnro y1 + 'y1 !
	x1 y1 ,cp
	x2 y2 ,curve ;

:ellip | adr -- adr' ; XY
:ellipr
	getfnro 'x1 !
	getfnro 'y1 !
	getfnro 'x1 !
	getfnro 'y1 !
	getfnro 'x1 !
	getfnro 'y1 !
	;
:close | adr -- adr'
	,poly ;

:resolve | adr char -- adr'
	$4d =? ( drop moveto ; )	| M move
	$6d =? ( drop movetor ; )	| m move rel
	$4c =? ( drop lineto ; )	| L lineto
	$6c =? ( drop linetor ; )	| l lineto rel
	$48 =? ( drop hori ; )		| H
	$68 =? ( drop horir ; )		| h
	$56 =? ( drop vert ; )		| V
	$76 =? ( drop vertr ; )		| v
	$43 =? ( drop ccurve ; )	| C
	$63 =? ( drop ccurver ; )	| c
	$53 =? ( drop surve ; )		| S
	$73 =? ( drop surver ; )	| s
	$51 =? ( drop qurve ; ) 	| Q curve
	$71 =? ( drop qurver ; )	| q curve rel
	$54 =? ( drop turve ; )		| T
	$74 =? ( drop turver ; )	| t
	$41 =? ( drop ellip ; )		| A
	$61 =? ( drop ellipr ; )	| a
	$5a =? ( drop close ; )		| Z
	$7a =? ( drop close ; )		| z
	drop ;

:getpath | adr --
	3 +
|	dup memmap
	( trim
		c@+ 0? ( drop 1- ; )
		$22 <>? )( resolve ) drop  ;

|------------------------
:decodelimits | adr adrpoly -- adr
	dup "viewbox=" findl 1? ( getvb ) drop
	drop
	;

:decodepoly | adr adrpoly -- adr
	dup "fill=" findl 1? ( getcolor ) drop
	dup "points=" findl 1? ( getpoly ) drop
	drop
	;

:decodepath | adr adrpoly -- adr
	dup "fill=" findl 1? ( getcolor ) drop
	dup "d=" findl 1? ( getpath ) drop
	drop
	"/>" find
	;

:findline | adr -- adr
	dup "<svg " findl 1? ( decodelimits ; ) drop
	dup "<polygon " findl 1? ( decodepoly ; ) drop
	dup "<path " findl 1? ( decodepath ; ) drop
	;

:convertsvg | adr --
	$end '$spr !
	( 1? )( |dup memmap
		findline
|		dup memmap
		nextline
|		dup memmap
		) drop
	0 ,spr ;

|----------- stat & convert
#xmin #ymin #xmax #ymax

:movevert | adr val -- adr
	dup $f and $a >? ( 2drop ; ) drop	| no es color!!
	dup d>xy
	ymin + swap
	xmin + swap
	xy>d
	swap $f and or over 4 - !
	;

:inistat
	d>xy dup 'ymin ! 'ymax ! dup 'xmin ! 'xmax ! ;

:addstat
	dup $f and $a >? ( 2drop ; ) drop	| no es color!!
	d>xy
	ymin <? ( dup 'ymin ! ) ymax >? ( dup 'ymax ! ) drop
	xmin <? ( dup 'xmin ! ) xmax >? ( dup 'xmax ! ) drop
	;

:centersvg
	$end @+ inistat ( @+ 1? )( addstat ) 2drop
	xmin xmax + 2/ neg 'xmin !
	ymin ymax + 2/ neg 'ymin !
	$end ( @+ 1? )( movevert ) 2drop
	;

#yw #xw
:zoomvert
	dup $f and $a >? ( 2drop ; ) drop	| no es color!!
	dup d>xy
	$1fff yw */ swap
	$1fff xw */ swap
	xy>d
	swap $f and or over 4 - !
	;

:zoomsvg
	$end @+ inistat ( @+ 1? )( addstat ) 2drop
	xmax xmin - 'xw !
	ymax ymin - 'yw !
	$end ( @+ 1? )( zoomvert ) 2drop
	;


|------------- carga
:cargasvg
	"r4" dlgfileload 0? ( drop ; )
	$start swap load 0 swap !+ '$end !
	$start convertsvg
	centersvg
	;

|------------- muestra
:muestraspr
	0.8 %s dup dim 0 dup fpos
	gris 8 rgrilla
	$end nsprite
	;

:main
	33
	show clrscr
		dup "%d " print cr cr2

		cyan
		'cargasvg "SVG" sp btnt cr

		muestraspr

		'zoomsvg <f1>
		'exit >esc<
		cminiflecha ;

:me mark here dup '$start ! dup '$end ! '$spr ! 0 $spr ! ;

: me main ;
