|
^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/input.txt

^r4/lib/rfont.txt
^r4/lib/ricons.txt

^inc/ric/fontawesomewebfont.ric
^inc/rft/robotoregular.rft
^inc/rft/robotobold.rft

^r4/lib/trace.txt

^r4/dev/ide/tokenr4.txt
^r4/dev/ide/tokenrun.txt

#mok "Ok!"
#msg	'mok

#hist )( $ffff	| historia
#hist> 'hist
#pad )( 256		| pad entrada
#padh>

:paderror
    errormsg 'msg !
	padh> 0 over c! 'hist> !
	;

:padenter
	hist> 'padh> !
	'pad hist> strcpyl 1- 13 swap c!+ 0 over c! 'hist> !

	mark
	'hist tokeniza
	empty
	1? ( paderror ; ) drop
	'mok 'msg !
	0 'pad !

	|---- ejecuta codigo
	cls
	stackreset
	<<boot ( code> <? )( steptoken ) drop
    >XFB

	|-------------------
	refreshfoco
	;


|-----------------------------------------------
:f1 :f2 :f3 ;

:icono | "ESC" 'i.off 'exit --
	guiBtn
	2-gc gris 'oscuro guiO gc.fbox 2+gc
	4-gc negro drawric 4+gc
	blanco home 2 dup +atxy print
	;

:iconoi | 'i.off 'exit --
	guiBtn
	2-gc gc.fbox 2+gc
	negro drawric
	;

:botonera
	robotobold 24 rfont!
	32 qdim 20 sh 17 - pos
	"Esc" 'i.off 'exit dup >esc< icono gc>>
	"F1" 'i.bolt 'f1 dup <f1> icono gc>>
|	"F2" 'i.edit 'f2 dup <f2> icono gc>>
|	"F3" 'i.light_bulb 'f3 dup <f3> icono gc>>

|	cyan  'i.file 'f1 iconoi gc>>
|	amarillo 'i.time 'f1 iconoi gc>>
|	violeta 'i.trash 'f1 iconoi gc>>
	;
|-----------------------------------------------

:mensajer4
	msg 'mok =? ( verde )( rojo "! " printx amarillo ) printx
	;

:main
	4
	mark
	stackreset
	show xfb>scr
		|----------------------------------
|		fonti
		'hist print cr
		codeprint cr
		infodic cr
		dumpvstack

		|----------------------------------
		128 gc.bottom |$7081 gc.hfill
		home
		robotoregular 48 rfont! home
		verde
		dup ":r%d " print mensajer4
		cr
		blanco ">> " print
		'pad 255 inputcr
		'padenter <enter>

		botonera

		'exit >esc<
		cminiflecha ;

: main ;
