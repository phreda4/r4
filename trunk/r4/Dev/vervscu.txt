| verduras vs cucarachas
^r4/lib/gui.txt
^r4/lib/trace.txt
^r4/dev/grafico1.spr

|------ particulas de 8 valores
:pini | cantidad list --
	here dup rot !+ ! 5 << 'here +! ;

:pclear | list --
	dup 4+ @ swap ! ;

:p!+ | 7 6 5 4 3 2 1 'acc list --
	dup >r @ !+ !+ !+ !+ !+ !+ !+ !+ r> ! ;

:pmap | list --
	@+ swap @
	( over <? )(
		dup @+ exec
		32 + ) 2drop ;

:delp | list end now -- list end- now-
	swap 32 - 2dup 8 move
	dup pick3 !
	swap 32 - ;

:pmap0 | list --
	dup @+ swap @
	( over <? )(
		dup @+ exec 0? ( drop delp )
		32 + ) 3drop ;

:pdel | adr list --
	>r r @ 32 - 8 move r> dup @ 32 - swap ! ;

|.......
:isin | x y 'end 'now -- x y end now/ x y now 0
	dup 4+ @ pick4 - abs 0.06 >? ( drop ; ) drop
	dup 8 + @ pick3 - abs 0.06 >? ( drop ; ) drop
	nip 0 ; | 'now 0

:pin | x y 'list -- x y 'how/x y 0
	@+ swap @
	( over <? )(
		isin 0? ( drop ; )
		32 + )
	2drop 0 ;

|----------------------------------------


#seccuca cuca1 cuca2
#seczana zanaoria1 zanaoria2 zanaoria3 zanaoria4 zanaoria5

#cucarachas 0 0
#verduras 0 0
#disparos 0 0

:memoria
	mark
	100 'cucarachas pini
	100 'verduras pini
	100 'disparos pini
	;


:haceverduras 	'verduras pmap ;
:hacecucarachas 'cucarachas pmap ;
:hacedisparos	'disparos pmap0 ;

:cucar | a --
	>r
	r@+ r@+
	r@+ r 12 - +!
	r@+ r 12 - +!
	rdrop
	0.6 >? ( pick2 'cucarachas pdel )
	fpos
	200 dup dim
	msec 5 >> $4 and 'seccuca + @ nsprite
	;

:dispa | a --
	>r
	r@+ r@+
	over sw 2/ - abs
	over sh 2/ - abs +
	20000 >? ( drop 0 rot rot )( drop )
	fpos
	200 dup dim
	'espora r 4 - @ rnsprite
	r@+ r 12 - +!
	r@+ r 12 - +!
	rdrop
	;

:verdu | a --
	>r
	200 dup dim
	r@+ r@+ fpos
	msec 5 >> $c and 'seczana + @ nsprite
	r@+ r 12 - +!
	r@+ r 12 - +!
	rdrop
	;

:r.01 rand 0.01 mod ;

:+cuca
	0 0 0 r.01 r.01 0 0 'cucar 'cucarachas p!+ ;

:+disp
	0 0 0 r.01 r.01 0 0 'dispa 'disparos p!+ ;

:+verd
	0 0 0 r.01 r.01 0 0 'verdu 'verduras p!+ ;

:inicio
	33
	show clrscr
		dup "%d" print
		haceverduras
		hacecucarachas
		hacedisparos
		'exit >esc<
		'+cuca <f1>
		'+disp <f2>
		'+verd <f3>
		;

: memoria inicio ;