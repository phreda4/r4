| mask buffer
| PHREDA 2015
|-------------------------
^r4/lib/gui.txt
^r4/lib/trace.txt

|--- maskbuffer | 1024*sh
#maskb

:mask.adr | x y -- m d
	1 pick2 $1f and << rot 5 >> 2 << rot 7 << + maskb + ;

::maskb.get | x y -- b
	mask.adr @ and ;

::maskb.set | x y --
	mask.adr dup @ rot not and swap ! ;

:gmask1
	$1f and 1 swap << dup 1- or ;

:gmask2
	$1f and -1 swap << ;

:filline | x1 y1 x2 y2 a --
	2 << pick3 7 << + maskb + >r
	rot - | x1 x2 cnt
	rot gmask2 rot gmask1 and not
	swap 1+
	( 1? )( 1-
		r @ pick2 and r !
		128 r+
		) 2drop
	rdrop ;

::maskb.rset | x1 y1 x2 y2 --
	pick3 5 >>
	pick2 5 >> =? ( filline ; )
	2 << pick3 7 << + maskb + >r
	rot - 							| x1 x2 cnty
	over 5 >> pick3 5 >> - 1- swap	| x1 x2 cntx cnty
	2swap gmask1 not swap gmask2 not
	2swap 1+						| maska maskb cntx cnty
	( 1? )( 1- r @
		pick3 and r !+ 				| maska maskb cntx cnty adr
		pick2 ( 1? )( 1- 0 rot !+ swap ) drop
		pick4 over @ and swap !
		128 r+
		) 4drop
	rdrop ;


:rgetline | x1 y1 x2 y2 a -- b
	2 << pick3 7 << + maskb + >r
	rot - | x1 x2 cnt
	rot gmask2 rot gmask1 and
	swap 1+	| mask cnt
	negro
	( 1? )( 1-
		r @ pick2 and? ( 3drop rdrop 1 ; ) drop
		128 r+
		) 2drop
	rdrop 0 ;

::maskb.rget | x1 y1 x2 y2 -- b
	pick3 5 >>
	pick2 5 >> =? ( rgetline ; )
	2 << pick3 7 << + maskb + >r
	rot - 							| x1 x2 cnty
	over 5 >> pick3 5 >> - 1- swap	| x1 x2 cntx cnty
	2swap gmask1 swap gmask2
	2swap 1+						| maska maskb cntx cnty
	( 1? )( 1- r @+
		pick4 and? ( 4drop 2drop rdrop 1 ; ) drop	| maska maskb cntx cnty adr
		pick2 ( 1? )( 1- swap
			@+ -1 <>? ( 4drop 3drop rdrop 1 ; ) drop
			swap ) drop
		@ pick4 and? ( 4drop drop rdrop 1 ; ) drop
		128 r+
		) 4drop
	rdrop 0 ;

::maskb.clear
	maskb >r
	sh 5 << ( 1? )( -1 r!+ 1- ) drop
	rdrop ;

::maskb.ini
	here dup 'maskb !
	sh 7 << + 'here ! ;

|-------------------------------
:fpbox | color x2 y2 x1 y1 --
	2dup setxy
	rot swap - 1+ >r - 1+
	sw over - r> | cntx sum cnty
	( 1? )(
		pick2 ( 1? )( 1- pick4 px!+ ) drop
		over px+! 1- ) 4drop ;

:fpx | color xmin ymin xmax --
	pick2 - 1+ >r
	setxy r>
	( 1? )( over px!+ 1- ) 2drop ;

:fpy | color xmin ymin ymax --
	over - 1+ >r
	setxy sw 1- r>
	( 1? )( pick2 px!+ over px+! 1- ) 3drop ;

:drawpoint | oct x y --
|	-? ( 3drop ; ) sh >=? ( 3drop ; )	| CLIP
	2dup maskb.get 0? ( 4drop ; ) drop
	2dup maskb.set
	setxy px!+ ;

|------------------------------------
::maskpxy | color x y --
	2dup mask.adr swap over @
	nand? ( nip 4drop ; )
	not over @ and swap !
	setxy px!+ ;


:proc8 | mask -- mask
	$ff nand? ( 8 px+! ; )	| todos 0
	$01 and? ( ink@ px!+ )( 1 px+! )
	$02 and? ( ink@ px!+ )( 1 px+! )
	$04 and? ( ink@ px!+ )( 1 px+! )
	$08 and? ( ink@ px!+ )( 1 px+! )
	$10 and? ( ink@ px!+ )( 1 px+! )
	$20 and? ( ink@ px!+ )( 1 px+! )
	$40 and? ( ink@ px!+ )( 1 px+! )
	$80 and? ( ink@ px!+ )( 1 px+! )
	;

:oneword | nro --
	0? ( 32 px+! drop ; )
	-1 =? ( 32 ( 1? )( ink@ px!+ 1- ) 2drop ; )
	proc8 8 >> proc8 8 >> proc8 8 >> proc8 drop ;

:maskfpxl | x1 y x2 --
	pick2 $ffffffe0 and pick2 setxy 		| x y x2
	pick2 5 >> 2 << rot 7 << + maskb + >r	| x x2
	gmask1 swap gmask2 and
	r @ 2dup and oneword swap not and r> ! ;

::maskfpx | x y x --
	dup 5 >> pick3 5 >> -
	0? ( drop maskfpxl ; ) 1- >r				| x1 y x2 r:cntx
	pick2 $ffffffe0 and pick2 setxy 		| x y x2   r:cntx
	pick2 5 >> 2 << rot 7 << + maskb + 		| x1 x2 adr
	r> swap >r                              | x1 x2 cntx
	rot gmask2 r @ 2dup and oneword swap not and r!+			| x2 cntx
	( 1? )( r @ oneword 0 r!+ 1- ) drop
	gmask1 r @ 2dup and oneword swap not and r> !
	;


::maskfpy | x y1 y2 --
	pick2 pick2 setxy 		| x y1 y2
	pick2 5 >> 2 << pick2 7 << + maskb + >r	| x y1 y2
	swap - 1 rot $1f and << | cnty mask
	swap
	( 1? )( 1- | mask cnty
		r @ pick2 and? ( ink@ px!+ )( 1 px+! )
		pick2 not and r!+
		sw 1- px+! 124 r+
		) 2drop rdrop ;


:maskfpxyl | x1 y1 x2 y2 --
	pick3 $ffffffe0 and pick3 setxy
	pick3 5 >> 2 << pick3 7 << + maskb + >r
	rot - 						| x1 x2 cnty
	rot gmask2 rot gmask1 and	| cntx mask
	swap ( 1? )(
		r @ pick2 over and oneword pick2 not and r!+
		sw 32 - px+!
		124 r+
		1- ) 2drop rdrop ;

::maskfpxy | x1 y1 x2 y2 --
	over 5 >> pick4 5 >> -
	0? ( drop maskfpxyl ; ) 1- >r	| x1 y1 x2 y2 r:cntx
	pick3 $ffffffe0 and pick3 setxy
	pick3 5 >> 2 << pick3 7 << + maskb +
	r> swap >r >r					| x1 y1 x2 y2   r:cntx adr
	rot -							| x1 x2 cnty
	rot gmask2 rot gmask1 			| cnty m1 m2
	swap rot
	r> swap				| m2 m1 cntx cnty
	( 1? )(
		pick2 r @ 2dup and oneword swap not and r!+			| x2 cntx
		over ( 1? )( r @ oneword 0 r!+ 1- ) drop
		pick3 r @ 2dup and oneword swap not and r!+			| x2 cntx
		32 pick2 2 + - 2 << r+
		sw pick2 2 + 5 << - px+!
		1- ) 4drop rdrop ;

|-------------test mask
:maskb.draw
	maskb >r
	0 0 setxy
	sh ( 1? )( 1-
		32 ( 1? )( 1-
			r@+ 32 ( 1? )( 1- swap
				1 and? ( $ffffff )( 0 ) px!+
				2/
				swap ) 2drop
			) drop
		) drop
	rdrop ;

:dbox | x1 y1 x2 y2 --
	2over op
	pick3 over line
	2dup line
	over pick3 line
	2drop line ;

:livetest
	40 40 over 48 + over 48 +
	maskb.rget 1? ( rojo )( verde ) drop
	40 40 over 48 + over 48 + dbox
	100 40 maskb.get 1? ( rojo )( verde ) drop
	100 40 2dup op line
	;

:boxytest | x y color --
	ink
	xymouse
	2swap pick2 +
	swap pick3 + swap
	2over 2over dbox maskb.rset ;

#test $f0f0f0a1
:main
	maskb.ini
	maskb.clear
	cls
	33
	show clrscr
		dup "%d " print cr

		livetest

		rand xymouse maskpxy

		[ rojo 20 ( 40 <? )( 34 over 74 maskfpx 1+ ) drop ; ] <f1>
		[ verde 30 ( 50 <? )( 40 over 184 maskfpx 1+ ) drop ; ] <f2>
		[ amarillo 50 ( 90 <? )( dup 10 200 maskfpy 1+ ) drop ; ] <f3>
		[ azul 35 34 56 48 maskfpxy ; ] <f4>
		[ violeta 40 40 190 120 maskfpxy ; ] <f5>
		'exit >esc<
		;

: mark main ;

