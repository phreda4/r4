| iso test 1
| PHREDA 2014
|---------------------
^r4/lib/gui.txt
^r4/lib/dlgfile.txt

^r4/lib/morton.txt
^r4/lib/trace.txt

|#xcam 0 #ycam 0 #zcam 4096
#xcam 000 #ycam 0000 #zcam 8192

#octre
#pixels
#octre>

#dmask

|---------------------------------
| vectores para rotar puntos

#level

#zsz #zsy #zsx
#ysz #ysy #ysx
#xsz #xsy #xsx
#xcc #ycc
#rotsum )( 64

:fillrotvec | xyz--
	0 0 2048 transform 'zsz ! 'zsy ! 'zsx !
	0 2048 0 transform 'ysz ! 'ysy ! 'ysx !
	2048 0 0 transform 'xsz ! 'xsy ! 'xsx !

	pick2 pick2 pick2 transform project 'ycc ! 'xcc !

	'rotsum >r
	xsx ysx zsx + + neg pick3 +
	xsy ysy zsy + + neg pick3 +
	xsz ysz zsz + + neg pick3 +
	project ycc - r!+ xcc - r!+	| 111
	xsx ysx zsx + - pick3 +
	xsy ysy zsy + - pick3 +
	xsz ysz zsz + - pick3 +
	project ycc - r!+ xcc - r!+	| 110
	ysx zsx xsx + - pick3 +
	ysy zsy xsy + - pick3 +
	ysz zsz xsz + - pick3 +
	project ycc - r!+ xcc - r!+	| 101
	xsx ysx zsx - + pick3 +
	xsy ysy zsy - + pick3 +
	xsz ysz zsz - + pick3 +
	project ycc - r!+ xcc - r!+	| 100
	zsx ysx xsx + - pick3 +
	zsy ysy xsy + - pick3 +
	zsz ysz xsz + - pick3 +
	project ycc - r!+ xcc - r!+	| 011
	xsx zsx ysx - + pick3 +
	xsy zsy ysy - + pick3 +
	xsz zsz ysz - + pick3 +
	project ycc - r!+ xcc - r!+	| 010
	ysx zsx xsx - + pick3 +
	ysy zsy xsy - + pick3 +
	ysz zsz xsz - + pick3 +
	project ycc - r!+ xcc - r!+	| 001
	xsx ysx zsx + + pick3 +
	xsy ysy zsy + + pick3 +
	xsz ysz zsz + + pick3 +
	project ycc - r!+ xcc - r!+	| 000
	3drop
	rdrop ;

:idsum | x y id -- x y
	$7 and 3 << 'rotsum + >r
	r@+ level >> + swap
	r> @ level >> + swap ;


|-------------- orden segun vista
#maskc
$4152637	$15043726	$26370415	$37261504	$40516273	$51407362	$62734051	$73625140
$4216537	$15307426	$26034715	$37152604	$40625173	$51743062	$62470351	$73516240
$1234567	$10352476	$23016745	$32107654	$45670123	$54761032	$67452301	$76543210
$1452367	$10534276	$23670145	$32716054	$45016723	$54107632	$67234501	$76325410
$2461357	$13570246	$20634175	$31725064	$46052713	$57134602	$64207531	$75316420
$2134657	$13025746	$20316475	$31207564	$46570213	$57461302	$64725031	$75643120

|-------
:maskabs | ax ay az -- mask
	| ax ay az
	pick2 <? ( over <? ( | x<z && y<z
			drop <? ( 0 )( 8 )
			nip ; ) )
	rot | ay az ax
	pick2 <? ( over <? ( | y<x && z<x
			drop <? ( 16 )( 24 )
			nip ; ) )
	<? ( 32 )( 40 )
	nip nip ;

:calcmask
	matinv
	0 0 1.0 transform
	rot +? ( 1 )( 0 ) >r abs
	rot +? ( 2 r+ ) abs
	rot +? ( 4 r+ ) abs
	maskabs r> or
	2 << 'maskc + @ 'dmask !
	;

:drawpixels
	0 0 setxy
	pixels ( octre> <? )( @+ px!+ ) drop ;

:drawpoint	| x y nodo --
	>r setxy r> octre - pixels + @ px!+ ;

:viewchildm | x y node bit nro -- x y node bit
	$7 xor	| de atras para adelante
	dup place pick2 nand? ( 2drop ; ) | x y z node bit bitc
	1- pick2 and popcnt 2 << pick3 +
	>r >r pick3 pick3 r> idsum
	r>

	| ;; recursion !!
	level 7 >? ( drop drawpoint ; ) drop
|	pixels >? ( drawpoint ; )

:viewom | x y node --

	1 'level +!
	@ dup 8 >> 2 << octre + swap $ff and | x y adr bitmask

	dmask $7 and viewchildm
	dmask 4 >> $7 and viewchildm
	dmask 8 >> $7 and viewchildm
	dmask 12 >> $7 and viewchildm
	dmask 16 >> $7 and viewchildm
	dmask 20 >> $7 and viewchildm
	dmask 24 >> $7 and viewchildm
	dmask 28 >> $7 and viewchildm

	4drop
	-1 'level +! ;

::drawiso | xc yc zc octre --
	>r
	fillrotvec
	calcmask
	0 'level !
	xcc ycc r>
	viewom
	;

|-------------
:freelook
	xymouse
	sh 2/ - 7 << swap
	sw 2/ - neg 7 << swap
	neg mrotx mroty ;

|------------------------------
:load3do | "" --
	here 'octre !
	octre swap load 'octre> !
	octre> 4 - @ octre + 'pixels !
|	octre> 'here !
	;

:loadoctree
	mark
	"media/3do" dlgfileload 0? ( drop empty ; )
	load3do
	empty ;

|-------------
:main
	"media/3do/luigi.3do" load3do

	33
	show clrscr
|		drawpixels

		verde 
		dup "%d " print cr
|		pick2 pick2 pick2 "%d %d %d" print cr
		omode
		freelook
|		msec 3 << mrotxi
|		msec 4 << mrotyi

		xcam ycam zcam octre drawiso


		cr2 fonti
		'loadoctree dup <f1> "F1-Load" sp btnt cr

		[ -0.025 'zcam +! ; ] <up>
		[ 0.025 'zcam +! ; ] <dn>
		[ -0.025 'xcam +! ; ] <le>
		[ 0.025 'xcam +! ; ] <ri>
		[ -0.025 'ycam +! ; ] <pgup>
		[ 0.025 'ycam +! ; ] <pgdn>

		'exit >esc< cminiflecha ;

: mark main ;

