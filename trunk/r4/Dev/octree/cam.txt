| cam
| PHREDA 2014
|-----------------------------
^r4/lib/gui.txt
^r4/lib/trace.txt
^r4/lib/sort.txt
^r4/lib/morton.txt



|----------- graficacion
:freelook
	xymouse
	sh 2/ - 7 << swap
	sw 2/ - neg 7 << swap
	neg mrotx
	mroty ;

:drawp | x y z ---
	project3d |2dup op line ;
	-? ( 2drop ; ) sh >=? ( 2drop ; )
	swap -? ( 2drop ; ) sw >=? ( 2drop ; ) swap
	setxy
	ink@ px!+
	;

:3dop project3d op ;
:3dline project3d line ;

|---- caja en normalizado
:drawboxz | z --
	-0.5 -0.5 pick2 3dop
	0.5 -0.5 pick2 3dline
	0.5 0.5 pick2 3dline
	-0.5 0.5 pick2 3dline
	-0.5 -0.5 rot 3dline ;

:drawlinez | x1 x2 --
	2dup -0.5 3dop 0.5 3dline ;

:draw3dnorm
	-0.5 drawboxz
	0.5 drawboxz
	-0.5 -0.5 drawlinez
	0.5 -0.5 drawlinez
	0.5 0.5 drawlinez
	-0.5 0.5 drawlinez ;

|--------------------------------

#xcam 0 #ycam 0 #zcam 5.0

#camox 0 #camoy 0 #camoz 0	| origen
#camdx 0 #camdy 0 #camdz 0	| direccion
#camux 0 #camuy 0 #camuz 0	| up
#camvx 0 #camvy 0 #camvz 0	| left

#sumu 0.02
#sumv 0.02

:pointray | x y --
	2dup 0
	0 ( 32 <? )( 1+ >r
		pick2 pick2 pick2 drawp
		rot pick4 4 >> +
		rot pick3 4 >> +
		rot 0.01 +
		r> )
	4drop ;

#ax #ay #az	| actual
#sx #sy #sz	| suma ux
#vx #vy #vz	| suma vx
#nx #ny #nz | normal actual
#nsx #nsy #nsz | suma normal ux
#nvx #nvy #nvz | suma normal vx

:pointray | dx dy x y z --
|	pick2 nx + pick2 ny + pick2 nz +
	pick2 camdx + pick2 camdy + pick2 camdz +
	3dop 3dline ;

:pointcamo
	camox camux - camvx - 'ax !
	camoy camuy - camvy - 'ay !
	camoz camuz - camvz - 'az !
	camux 4 >> 'sx !	| 16 es la resolucion/2
	camuy 4 >> 'sy !
	camuz 4 >> 'sz !
	camux 2* neg camvx 4 >> + 'vx ! | 16 es la resolucion/2
	camuy 2* neg camvy 4 >> + 'vy ! | resta para retroceder el raster
	camuz 2* neg camvz 4 >> + 'vz !

	camdx 'nx !
	camdy 'ny !
	camdz 'nz !


	-16 ( 16 <? )( 1+
		-16 ( 16 <? )( 1+
			|ax ay az drawp
			ax ay az pointray
			sx 'ax +! sy 'ay +! sz 'az +!
			) drop
		vx 'ax +! vy 'ay +! vz 'az +!
		) drop ;

:drawcam
	rojo
	camox camoy camoz pick2 camdx + pick2 camdy + pick2 camdz + 3dop 3dline
	azul
	camox camoy camoz pick2 camux + pick2 camuy + pick2 camuz + 3dop 3dline
	cyan
	camox camoy camoz pick2 camvx + pick2 camvy + pick2 camvz + 3dop 3dline
	pointcamo
	;

:ubicam
	msec 2 <<
	dup sin 2* 'camoy ! cos 2* 'camox !
	msec 2 << sin 'camoz !

	camox neg 'camdx !
	camoy neg 'camdy !
	camoz neg 'camdz !

	| normaliza
	camdx dup *. camdy dup *. + camdz dup *. + sqrt. 1.0 swap /.
	camdx over *. 'camdx !
	camdy over *. 'camdy !
	camdz swap *. 'camdz !

	camdx neg 'camuy !
	camdy 'camux !
	|camdz
	0 'camuz !

	camdy camuz *. camuy camdz *. - 'camvx !
	camdz camux *. camuz camdx *. - 'camvy !
	camdx camuy *. camux camdy *. - 'camvz !

	;

|--------------------------
:main
	4
	show clrscr verde
		dup ":r%d " print

		ubicam
		omode
		freelook
		xcam ycam zcam mtrans

		cyan draw3dnorm

        drawcam

		[ -0.01 'zcam +! ; ] <up>
		[ 0.01 'zcam +! ; ] <dn>

		'exit >esc<
		cflecha ;

: mark main ;
