| WireLib
| PHREDA 2015
|-----------------------------------------
^r4/lib/3dmat.txt
^r4/dev/octree/iso_6_3.txt

:3dop transform 3dproject op ;
:3dline transform 3dproject line ;

|---- grillas
::grillaxy | step lim z -
	>r dup neg
	( over <=? )(
		dup pick2 neg r 3dop
		dup pick2 r 3dline
		over neg over r 3dop
		over over r 3dline
		pick2 + )
	3drop rdrop ;

::grillayz | step lim x --
	>r dup neg
	( over <=? )(
		r over pick3 neg 3dop
		r over pick3 3dline
		r pick2 neg pick2 3dop
		r pick2 pick2 3dline
		pick2 + )
	3drop rdrop ;

::grillaxz | step lim y --
	>r dup neg
	( over <=? )(
		dup r pick3 neg 3dop
		dup r pick3 3dline
		over neg r pick2 3dop
		over r pick2 3dline
		pick2 + )
	3drop rdrop ;

#qsize
#vert )( 64

:to3d
	transform 3dproject swap ;

:nver
	3 << 'vert + @+ swap @ ;

::xyzcubo | x y z size --
	'qsize !
	'vert >r
	pick2 pick2 pick2 to3d r!+ r!+
	pick2 qsize + pick2 pick2 to3d r!+ r!+
	pick2 pick2 qsize + pick2 to3d r!+ r!+
	pick2 qsize + pick2 qsize + pick2 to3d r!+ r!+
	qsize +
	pick2 pick2 pick2 to3d r!+ r!+
	pick2 qsize + pick2 pick2 to3d r!+ r!+
	pick2 pick2 qsize + pick2 to3d r!+ r!+
	pick2 qsize + pick2 qsize + pick2 to3d r!+ r> !
	3drop
	0 nver 2dup op 1 nver line 3 nver line 2 nver line line
	4 nver 2dup op 5 nver line 7 nver line 6 nver line line
	0 nver op 4 nver line
	1 nver op 5 nver line
	2 nver op 6 nver line
	3 nver op 7 nver line
	;

::voxcursor | x y z --
	1 deepvox 1- << >r
	r swap - 1- 16 deepvox - <<
	r rot - 1- 16 deepvox - <<
	rot r> swap - 1- 16 deepvox - <<
	1.0 deepvox >> | size
	xyzcubo ;

|--------- grilla
#nx #ny #nz

:dotprod2 | x y z -- dp
	nz * swap ny * + swap nx * + ;

::draw3dgrid
	0 0 0 transform 'nz ! 'ny ! 'nx !

	gris
|	$aa0000 ink
	0 0 $ff transform
	rot nx - rot ny - rot nz -
	dotprod2 +? ( 0.5 )( -0.5 ) nip
	0.25 0.5 rot grillaxy

|	$aa00 ink
	0 $ff 0 transform
	rot nx - rot ny - rot nz -
	dotprod2 +? ( 0.5 )( -0.5 ) nip
	0.25 0.5 rot grillaxz

|	$aa ink
	$ff 0 0 transform
	rot nx - rot ny - rot nz -
	dotprod2 +? ( 0.5 )( -0.5 ) nip
	0.25 0.5 rot grillayz
	;

|---------- bresenham
#:ox #:oy #:oz	| origen
#:cx #:cy #:cz	| actual
#:dx #:dy #:dz	| direccion
#ex #ey #ez
#sx #sy #sz

#paso

:pasox
	ey +? ( sy 'cy +! dx 2* - ) dy 2* + 'ey !
	ez +? ( sz 'cz +! dx 2* - ) dz 2* + 'ez !
	sx 'cx +! ;
:pasoy
	ex +? ( sx 'cx +! dy 2* - ) dx 2* + 'ex !
	ez +? ( sz 'cz +! dy 2* - ) dz 2* + 'ez !
	sy 'cy +! ;
:pasoz
	ey +? ( sy 'cy +! dz 2* - ) dy 2* + 'ey !
	ex +? ( sx 'cx +! dz 2* - ) dx 2* + 'ex !
	sz 'cz +! ;

::voxLine | xf yf zf --
	rot ox over - +? ( 1 )( -1 ) 'sx ! abs 'dx ! 'cx !
	swap oy over - +? ( 1 )( -1 ) 'sy ! abs 'dy ! 'cy !
	oz over - +? ( 1 )( -1 ) 'sz ! abs 'dz ! 'cz !
	dx dy >=? (
		dz >=? ( drop
			dy 2* dx - 'ey ! dz 2* dx - 'ez ! 'pasox 'paso ! ; )
		drop
		dx 2* dz - 'ex ! dy 2* dz - 'ey ! 'pasoz 'paso ! ; )
	drop
	dy dz >=? (
		drop
		dx 2* dy - 'ex ! dz 2* dy - 'ez ! 'pasoy 'paso ! ; )
	drop
	dx 2* dz - 'ex ! dy 2* dz - 'ey ! 'pasoz 'paso ! ;

::voxPaso
	paso exec ;

|-----------
::mouseInVox | -- 1/0
	0 0 0 transform 'oz ! 'oy ! 'ox !
	xymouse swap sw 2/ - swap sh 2/ - 512
	normInt2Fix transform

	neg oz + 1? ( 1.0 swap /. )
	-0.5 oz - over *. 0.5 oz - rot *.
	over <? ( swap )
	'ez ! 'sz !

	neg oy + 1? ( 1.0 swap /. )
	-0.5 oy - over *. 0.5 oy - rot *.
	over <? ( swap )
	'ey ! 'sy !

	neg ox + 1? ( 1.0 swap /. )
	-0.5 ox - over *. 0.5 ox - rot *.
	over <? ( swap )
	'ex ! 'sx !

	sx sy max sz max
	ex ey min ez min
	>? ( 0 )( 1 ) nip ;

|---------- mapeo
#vector
#zposnow

:3dchi | adr bitm nro n --  adr bitm adrc
	zposnow $fffffff8 and or 'zposnow !
	1- over and popcnt 2 << pick2 + ;  | node bitm node'

:3drec | oct --
	$pixels	>=? ( octcolor zposnow vector exec ; )
	oct++
	zposnow 3 << 'zposnow !
	1 over nand? ( drop )( 0 3dchi 3drec )
	2 over nand? ( drop )( 1 3dchi 3drec )
	4 over nand? ( drop )( 2 3dchi 3drec )
	8 over nand? ( drop )( 3 3dchi 3drec )
	16 over nand? ( drop )( 4 3dchi 3drec )
	32 over nand? ( drop )( 5 3dchi 3drec )
	64 over nand? ( drop )( 6 3dchi 3drec )
	128 over nand? ( drop )( 7 3dchi 3drec )
	2drop
	zposnow 3 >> 'zposnow !
	;

::3domap | vector 3d0 --
	adjustmem
	'vector !
	0 'zposnow !
	$octree 3drec ;

|------------ point in cuad
#xt #yt #inside

:testline | x1 y1 x2 y2 -- x2 y2
	2swap
	pick2 yt - over yt - xor +? ( 3drop ; ) drop  	 | yt entre y1..y2
	pick3 pick2 - yt pick2 - pick4 pick3 - 0? ( nip 4drop ; ) */
	nip + xt >=? ( 1 'inside +! ) drop ;

:testini | x y --
	'yt ! 'xt ! 0 'inside ! ;

:top
	0 'inside !
	transform 3dproject 2dup ;
:tline
	transform 3dproject testline ;
:tinside
	testline 2drop inside 1 and ;

:planexy | z --
	>r
	-0.5 -0.5 r top
	 0.5 -0.5 r tline
	 0.5  0.5 r tline
	-0.5  0.5 r> tline
	tinside ;

:planeyz | x --
	>r
	r -0.5 -0.5 top
	r  0.5 -0.5 tline
	r 0.5  0.5 tline
	r> -0.5  0.5 tline
	tinside ;

:planexz | y --
	>r
	-0.5 r -0.5 top
	 0.5 r -0.5 tline
	 0.5 r 0.5  tline
	-0.5 r> 0.5  tline
	tinside ;

#:hx0 #:hy0 #:hz0
#:hx1 #:hy1 #:hz1

#vertex )( 2048
#vertex>

:quadinside | -- in
	vertex> 64 - >r
	r@+ r@+ r@+ top
	r@+ r@+ r@+ tline
	r@+ r@+ r@+
	r@+ r@+ r> @
	tline tline tinside ;

:quadchild | id --
	vertex> dup >r 64 -
	swap 2 << dup 2* + | 12*
	over + swap >r >r | prev pivot
	r@+ r@+ r> @ r> | x y z pri
	@+ pick4 + 2/ r!+ @+ pick3 + 2/ r!+ @+ pick2 + 2/ r!+
	@+ pick4 + 2/ r!+ @+ pick3 + 2/ r!+ @+ pick2 + 2/ r!+
	@+ pick4 + 2/ r!+ @+ pick3 + 2/ r!+ @+ pick2 + 2/ r!+
	@+ pick4 + 2/ r!+ @+ pick3 + 2/ r!+ @ + 2/ r> !+ 'vertex> !
	2drop ;
:remchid
	-64 'vertex> ! ;

:testchild | -- z
	0 quadchild quadinside 1? ( drop 0 ; ) drop remchid
	1 quadchild quadinside 1? ( drop 1 ; ) drop remchid
	2 quadchild quadinside 1? ( drop 2 ; ) drop remchid
	3
|	3 quadchild quadinside 1? ( drop 3 ; ) drop remchid
	;

:zsearch | zorder2d
	0 deepvox ( 1? )( 1-
		testchild rot 2 << or
		swap ) drop ;

:quadinixy | z --
	'vertex >r
	-0.5 r!+ -0.5 r!+ dup r!+
	 0.5 r!+ -0.5 r!+ dup r!+
	-0.5 r!+  0.5 r!+ dup r!+
	 0.5 r!+  0.5 r!+ r> !+ 'vertex> ! ;
:quadxy | z -- x y
	quadinixy zsearch invmorton2d ;

:quadiniyz | z --
	'vertex >r
	dup r!+ -0.5 r!+ -0.5 r!+
	dup r!+  0.5 r!+ -0.5 r!+
	dup r!+ -0.5 r!+  0.5 r!+
	r!+      0.5 r!+  0.5 r> !+ 'vertex> ! ;
:quadyz | x -- y z
	quadiniyz zsearch invmorton2d ;

:quadinixz | z --
	'vertex >r
	-0.5 r!+ dup r!+ -0.5 r!+
	 0.5 r!+ dup r!+ -0.5 r!+
	-0.5 r!+ dup r!+  0.5 r!+
	 0.5 r!+ r!+      0.5 r> !+ 'vertex> ! ;
:quadxz | y -- x z
	quadiniyz zsearch invmorton2d ;

:p! | x y z
	hx0 -? ( drop 'hz0 ! 'hy0 ! 'hx0 ! ; ) drop
	'hz1 ! 'hy1 ! 'hx1 ! ;

#hit
::testbox | x y -- 1/0
	'yt ! 'xt !
	0 'hit !
	-1 'hx0 !
	-0.5 planexy 1? ( 1 'hit +! -0.5 quadxy 0 p! ) drop
	 0.5 planexy 1? ( 2 'hit +!  0.5 quadxy qsize p! ) drop
	-0.5 planeyz 1? ( 4 'hit +! -0.5 quadyz 0 rot rot p! ) drop
	 0.5 planeyz 1? ( 8 'hit +!  0.5 quadyz qsize rot rot p! ) drop
	-0.5 planexz 1? ( 16 'hit +! -0.5 quadxz 0 swap p! ) drop
	 0.5 planexz 1? ( 32 'hit +!  0.5 quadxz qsize swap p! ) drop
	hit ;


