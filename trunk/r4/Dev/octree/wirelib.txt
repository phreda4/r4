| WireLib
| PHREDA 2015
|-----------------------------------------

^r4/lib/3dmat.txt

|----------- graficacion
:3dop transform 3dproject op ;
:3dline transform 3dproject line ;

|------------------------------------------
::grillaxy | step lim z -
	>r dup neg
	( over <=? )(
		dup pick2 neg r 3dop
		dup pick2 r 3dline
		over neg over r 3dop
		over over r 3dline
		pick2 + )
	3drop rdrop ;

::grillayz | step lim x --
	>r dup neg
	( over <=? )(
		r over pick3 neg 3dop
		r over pick3 3dline
		r pick2 neg pick2 3dop
		r pick2 pick2 3dline
		pick2 + )
	3drop rdrop ;

::grillaxz | step lim y --
	>r dup neg
	( over <=? )(
		dup r pick3 neg 3dop
		dup r pick3 3dline
		over neg r pick2 3dop
		over r pick2 3dline
		pick2 + )
	3drop rdrop ;

#qsize
#vert )( 64

:to3d
	transform 3dproject swap ;

:nver
	3 << 'vert + @+ swap @ ;

::xyzcubo | x y z size --
	dup 2/ >r
	'qsize !
	rot r - rot r - rot r> -
	'vert >r
	pick2 pick2 pick2 to3d r!+ r!+
	pick2 qsize + pick2 pick2 to3d r!+ r!+
	pick2 pick2 qsize + pick2 to3d r!+ r!+
	pick2 qsize + pick2 qsize + pick2 to3d r!+ r!+
	qsize +
	pick2 pick2 pick2 to3d r!+ r!+
	pick2 qsize + pick2 pick2 to3d r!+ r!+
	pick2 pick2 qsize + pick2 to3d r!+ r!+
	pick2 qsize + pick2 qsize + pick2 to3d r!+ r> !
	3drop
	0 nver 2dup op 1 nver line 3 nver line 2 nver line line
	4 nver 2dup op 5 nver line 7 nver line 6 nver line line
	0 nver op 4 nver line
	1 nver op 5 nver line
	2 nver op 6 nver line
	3 nver op 7 nver line
	;

|--------- grilla
#nx #ny #nz

:dotprod2 | x y z -- dp
	nz * swap ny * + swap nx * + ;

::draw3dgrid
	0 0 0 transform
	'nz ! 'ny ! 'nx !

	gris
|	$aa0000 ink
	0 0 $ff transform
	rot nx - rot ny - rot nz -
	dotprod2 +? ( 0.5 )( -0.5 )
	0.25 0.5 rot grillaxy
	drop |"%d " print

|	$aa00 ink
	0 $ff 0 transform
	rot nx - rot ny - rot nz -
	dotprod2 +? ( 0.5 )( -0.5 )
	0.25 0.5 rot grillaxz
	drop |"%d " print

|	$aa ink
	$ff 0 0 transform
	rot nx - rot ny - rot nz -
	dotprod2 +? ( 0.5 )( -0.5 )
	0.25 0.5 rot grillayz
	drop |"%d " print
	;