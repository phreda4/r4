| WireLib
| PHREDA 2015
|-----------------------------------------

^r4/lib/3dmat.txt

|----------- graficacion
:3dop transform 3dproject op ;
:3dline transform 3dproject line ;

#octsize
|---- dibujos de grillas
:drawboxz | z --
	octsize neg octsize neg pick2 3dop
	octsize octsize neg pick2 3dline
	octsize octsize pick2 3dline
	octsize neg octsize pick2 3dline
	octsize neg octsize neg rot 3dline ;

:drawboxy | y --
	octsize neg over octsize neg 3dop
	octsize over octsize neg 3dline
	octsize over octsize 3dline
	octsize neg over octsize 3dline
	octsize neg swap octsize neg 3dline ;

:drawboxx | x --
	dup octsize neg octsize neg 3dop
	dup octsize octsize neg 3dline
	dup octsize octsize 3dline
	dup octsize neg octsize 3dline
	octsize neg octsize neg 3dline ;

:drawlinez | x1 x2 --
	2dup octsize neg 3dop octsize 3dline ;

:draw3dbox | size
	octsize neg drawboxz
	octsize drawboxz
	octsize neg octsize neg drawlinez
	octsize octsize neg drawlinez
	octsize octsize drawlinez
	octsize neg octsize drawlinez ;

|------------------------------------------
::grillaxy | step lim z -
	>r dup neg
	( over <=? )(
		dup pick2 neg r 3dop
		dup pick2 r 3dline
		over neg over r 3dop
		over over r 3dline
		pick2 + )
	3drop rdrop ;

::grillayz | step lim x --
	>r dup neg
	( over <=? )(
		r over pick3 neg 3dop
		r over pick3 3dline
		r pick2 neg pick2 3dop
		r pick2 pick2 3dline
		pick2 + )
	3drop rdrop ;

::grillaxz | step lim y --
	>r dup neg
	( over <=? )(
		dup r pick3 neg 3dop
		dup r pick3 3dline
		over neg r pick2 3dop
		over r pick2 3dline
		pick2 + )
	3drop rdrop ;

#qsize
#vert )( 64

:to3d
	transform 3dproject swap ;

:nver
	3 << 'vert + @+ swap @ ;

::xyzcubo | x y z size --
	dup 2/ >r
	'qsize !
	rot r - rot r - rot r> -
	'vert >r
	pick2 pick2 pick2 to3d r!+ r!+
	pick2 qsize + pick2 pick2 to3d r!+ r!+
	pick2 pick2 qsize + pick2 to3d r!+ r!+
	pick2 qsize + pick2 qsize + pick2 to3d r!+ r!+
	qsize +
	pick2 pick2 pick2 to3d r!+ r!+
	pick2 qsize + pick2 pick2 to3d r!+ r!+
	pick2 pick2 qsize + pick2 to3d r!+ r!+
	pick2 qsize + pick2 qsize + pick2 to3d r!+ r> !
	3drop
	0 nver 2dup op 1 nver line 3 nver line 2 nver line line
	4 nver 2dup op 5 nver line 7 nver line 6 nver line line
	0 nver op 4 nver line
	1 nver op 5 nver line
	2 nver op 6 nver line
	3 nver op 7 nver line
	;
