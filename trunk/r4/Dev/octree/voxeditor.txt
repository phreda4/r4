| Editor de Voxels
| PHREDA 2014
|-----------------------------
^r4/lib/gui.txt
^r4/lib/sort.txt
^r4/lib/bsearch.txt
^r4/lib/morton.txt

^r4/dev/octree/buildoctree.txt
^r4/dev/octree/iso_5.txt

^r4/lib/trace.txt

#filename )( 1024

#octsize 0.05

#xcam 0 #ycam 0 #zcam 0.2
#rx #ry

#curx #cury #curz

|----------------
:clearvox
	memvox 'memvox> ! ;

:vox-cnt-mem | -- cnt mem
	memvox memvox> over - 3 >> 1+ swap ;

:vox! | c x y z --
	mortonlut memvox> !+ !+ 'memvox> ! ;

:vox@ | x y z -- c
	mortonlut vox-cnt-mem binsearch
	0? ( -1 nip ; ) 4+ @ ;

:sortvox
	vox-cnt-mem shellsort ;


|----------- graficacion
:3dop transform 3dproject op ;
:3dline transform 3dproject line ;

|---- dibujos de grillas
:drawboxz | z --
	octsize neg octsize neg pick2 3dop
	octsize octsize neg pick2 3dline
	octsize octsize pick2 3dline
	octsize neg octsize pick2 3dline
	octsize neg octsize neg rot 3dline ;

:drawboxy | y --
	octsize neg over octsize neg 3dop
	octsize over octsize neg 3dline
	octsize over octsize 3dline
	octsize neg over octsize 3dline
	octsize neg swap octsize neg 3dline ;

:drawboxx | x --
	dup octsize neg octsize neg 3dop
	dup octsize octsize neg 3dline
	dup octsize octsize 3dline
	dup octsize neg octsize 3dline
	octsize neg octsize neg 3dline ;

:drawlinez | x1 x2 --
	2dup octsize neg 3dop octsize 3dline ;

:draw3dbox
	$707070 neg ink
	octsize neg drawboxz
	octsize drawboxz
	octsize neg octsize neg drawlinez
	octsize octsize neg drawlinez
	octsize octsize drawlinez
	octsize neg octsize drawlinez ;


|------------- load import
:loadmodel
	;

:testmodel
	qsize ( 1? )( 1-
		$ff 0 0 pick3 vox!
		$ff00 0 pick2 0 vox!
		$ff0000 over 0 0 vox!
		) drop
	$ffffff 0 0 0 vox!
	;


:randvox
	$ff00 0 0 0 vox!
	$ff00 0 0 1 vox!
	$ff 1 0 0 vox!
	$ff 1 0 1 vox!
	$ff0000 0 1 0 vox!
	$ff0000 0 1 1 vox!
	;

|------ vista
#xm #ym
:dnlook
	xymouse 'ym ! 'xm ! ;

:movelook
	xymouse
	ym over 'ym ! - 7 << 'rx +!
	xm over 'xm ! - neg 7 << neg 'ry +!  ;



|-----------------
:main
|	randvox
	testmodel
	buildoctree

|	qsize 1+ 5 << 'zcam !
	4
	show clrscr verde
		dup ":r%d " print cr
		qsize deepvox "profundidad:%d lado:%d " print
		memvox> memvox - 3 >> "puntos: %d " print
		octre> octre - 2 >> "nodos: %d " print
		cr

		curz cury curx "%d %d %d" print cr

		3dini
		rx mrotx ry mroty
		xcam ycam zcam mtrans
		qsize 2/ neg dup dup mtransi

		draw3dbox
		maskb.clear
		$inifile drawoctree

		'dnlook 'movelook guiDnMove

		[ 4 'curx +! ; ] <up>
		[ -4 'curx +! ; ] <dn>

		[ 1 'cury +! ; ] <le>
		[ -1 'cury +! ; ] <ri>

		[ 1 'curz +! ; ] <pgup>
		[ -1 'curz +! ; ] <pgdn>

		'exit >esc<
		cminiflecha ;

|--------------------------
:memory
	mark
	maskb.ini
	here dup 'memvox ! 'memvox> !
	;

: memory main ;
