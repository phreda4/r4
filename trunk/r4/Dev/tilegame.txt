^r4/lib/gui.txt
^r4/lib/bmr.txt
^r4/dev/tiles.bmr

#xp 64
#yp 64
#vxp
#vyp

#puntos

#estado

#quieto 0
#caminando 1
#saltando 2
#cayendo 3
#escalando 4

|------- animaciones
#amarioq mq0 mq0 mq0 mq0 mq0
#amariod mds1 md1 md2 md3 md2
#amarioi mis1 mi1 mi2 mi3 mi2
#amarioe me1 me2 me1 me3 me1

#acoin coin1 coin2 coin3 coin4 coin4 coin3 coin2 coin1

|------- sonidos
#smoneda

|-------- mapas
#escala 32
#escalap 5
#anchomapa 	32
#altomapa	18

#bloques 0 'coin1 'escalera 'piso 'tierra 'piedra2 'piedra2 'ladrillo 'ladrillo2 'pasto1 'tnt

#mapa1
6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6
6 0 0 0 0 0 6 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 6
6 0 0 0 0 0 6 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 6
6 6 6 0 0 0 6 0 0 0 0 6 2 6 6 6 6 0 0 0 0 0 0 0 0 0 0 0 0 0 0 6
6 0 0 0 0 0 6 0 0 0 0 0 2 0 0 0 0 0 0 0 0 0 0 0 0 5 5 5 5 5 5 6
6 0 0 0 0 0 6 0 0 0 0 0 2 0 0 0 0 0 0 0 7 7 0 0 0 5 5 0 0 0 0 6
6 0 0 0 0 0 0 0 0 0 0 0 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 6
6 4 4 4 0 0 0 0 0 0 0 0 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 6
6 4 4 4 0 0 0 0 0 0 0 0 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 6
6 0 0 0 0 0 0 0 6 6 6 6 6 0 0 0 0 0 0 0 1 0 0 0 0 0 0 0 0 0 0 6
6 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 6 6 6 6 6 0 0 0 0 0 0 0 0 6
6 1 1 1 1 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 6
6 6 6 6 6 6 0 0 0 0 0 0 0 1 1 1 1 1 0 0 0 0 0 0 0 0 0 0 0 0 1 6
6 0 0 0 0 0 0 0 0 0 0 0 0 4 4 4 4 4 0 0 0 0 0 0 0 0 0 0 0 0 8 6
6 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 8 6
6 0 0 0 0 0 0 6 8 8 8 6 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 8 6
6 0 0 0 0 0 6 3 3 3 3 3 6 7 7 7 7 7 7 0 0 0 0 0 0 0 0 0 0 0 8 6
6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6 6

#mapa )( 8192

:copiamapa
	'mapa >r
	'mapa1
	anchomapa altomapa *
	( 1? )(
		swap @+ r!+ swap
		1- )
	2drop rdrop
	;


:[mapa]	| x y -- dir
	anchomapa * + 2 << 'mapa + ;

:mapaba
	xp escala 2/ + escalap >> yp escala + escalap >> [mapa] @ ;

:maparr
	xp escala 2/ + escalap >> yp 1- escalap >> [mapa] @ ;

:mapder
	xp escala + escalap >> yp escala 2/ + escalap >> [mapa] @ ;

:mapizq
	xp escalap >> yp escala 2/ + escalap >> [mapa] @ ;

:mapain
	xp escala 2/ + escalap >> yp escala 2/ + escalap >> [mapa] @ ;

:mapain! | v --
	xp escala 2/ + escalap >> yp escala 2/ + escalap >> [mapa] ! ;

:nivelar
	yp escala 1- not and 'yp !
	quieto 'estado ! ;

:gsalto
	vyp 1+
	escala >? ( drop escala )
    +? ( cayendo 'estado ! )
	'vyp !
	;

:gravedad
	estado
	saltando =? ( drop gsalto ; )
	drop
    mapaba
	2 <? ( gsalto )( 0 'vyp ! nivelar )
	drop
	;

:anima | -- bmr
	estado escalando =? ( drop vyp 1? ( 'amarioe msec 5 >> $c and + )( 'amarioe ) nip @ ; ) drop
	vxp 0? ( 'amarioq )( +? ( 'amariod )( 'amarioi ) ) nip
	vyp 0? ( drop msec 4 >> $c and + 4+ )( drop )
	@
	;

:dibujopersonaje
	xp yp anima
	escala dup bmr.drawscale
	|bmr.draw
	vxp
	+? ( mapder )( mapizq )
	2 >? ( 2drop 0 )( drop )
	'xp +!
	vyp
	+? ( mapaba )( maparr 2 >? ( 0 'vyp ! ) )
	2 >? ( 2drop 0 )( drop )
	'yp +!

	estado
	escalando =? ( drop mapain 2 <>? ( mapaba 2 <>? ( quieto 'estado ! 0 'vyp ! ) drop ) drop ; )
	drop
	mapain
	1 =? ( 1 'puntos +! 0 mapain! smoneda splay )
	2 =? ( drop escalando 'estado ! 0 'vyp ! ; )

	drop

	gravedad
	;

:dibujotile | n -- dib
	1 =? ( drop 'acoin msec 5 >> $1c and + ; )
	$ff and 2 << 'bloques + ;

:dibujomapa
	'mapa >r
	0 ( altomapa <? )(
		0 ( anchomapa <? )(
			r@+ 0? ( drop )(
			    over escalap <<
			    pick3 escalap <<
				rot dibujotile
|				@ escala dup bmr.drawscale
				@ bmr.draw
				)
		1+ ) drop
	1+ ) drop
	rdrop
	;

:saltar
	estado
	escalando =? ( drop -4 'vyp ! ; )
	saltando =? ( drop ; )
	cayendo =? ( drop ; )
	drop
	-15 'vyp ! saltando 'estado ! ;

:bajar
	mapaba 2 =? ( escalando 'estado ! ) drop
	estado escalando =? ( 4 'vyp ! ) drop
	;

:debug
	xp "xp =%d" print cr
	yp "yp =%d" print cr
	vxp "vxp =%d" print cr
	vyp "vyp =%d" print cr
	estado "estado=%d" print cr
|mapaba "%d " print
	;


:juego
	copiamapa
	show30 clrscr
		dibujomapa
		debug
		dibujopersonaje

		'saltar <up>
		'bajar <dn>
		[ estado escalando =? ( 0 'vyp ! ) drop ; ] dup >up< >dn<
	    [ 4 'vxp ! ; ] <ri>
		[ -4 'vxp ! ; ] <le>
		[ 0 'vxp ! ; ] dup >ri< >le<
		'exit >esc<
		;

:cargasonidos
	mark
	"media/milagros/start.ogg" sload 'smoneda !
	;


: cargasonidos juego ;