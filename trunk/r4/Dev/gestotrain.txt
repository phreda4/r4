| $p point cloud recognizer
| basado en http://depts.washington.edu/aimgroup/proj/dollar/pdollar.js
| PREDA 2013
|--------------------
^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/input.txt

^r4/lib/trace.txt

#numPoints 32

#nsimbol 0
#simbols )( 1024 | 256 simbolos
#infosim )( 1024 | 256 info sim
#lensim )( 1024 | 256 info sim

#memsim
#memsim>

#pc2d )( $3fff
#pc2d> 'pc2d
#id 1
#aux )( $3fff
#aux> 'aux


| formato de reconocedor
| 32 puntos por signo
| + 1 de info ( 1 o multiples solo)..
#signon 0		| cantidad
#signoi )( 1024 | 256 signos
#signod )( $7fff |


| formato de trazo
|  X    Y   ID
| $fff fff ff

:xy>n | x y -- n
	$fff and 8 << swap $fff and 20 <<  or id or ;
:n>xy	 | n -- x y
	dup 20 >> swap 12 << 20 >> ; | con signo

:ngc>xy	 | n -- x y ;gc
	dup 20 >> w 12 *>> xc + swap 12 << 20 >> h 12 *>> yc + ; | con signo

:xygc>n	 | x y -- n ;gc
	yc - h 12 <</ $fff and 8 << swap xc - w  12 <</ $fff and 20 << or id or ;


:++pc2d | px --
	pc2d> 4 - @ =? ( drop ; )
:+pc2d | px
	pc2d> !+ 'pc2d> ! ;

:pc2dreset
	'pc2d 'pc2d> ! 1 'id ! ;

:pc2ddraw
	0 >r
	'pc2d ( pc2d> <? )( @+
		dup $ff and r <>? ( drop n>xy op 1 r+ )( drop n>xy line )
		) drop rdrop ;

:gcpc2ddraw
	0 >r
	'pc2d ( pc2d> <? )( @+
		dup $ff and r <>? ( drop ngc>xy op 1 r+ )( drop ngc>xy line )
		) drop rdrop ;

:pc2dpoint
	'pc2d ( pc2d> <? )( @+
		n>xy 2 box
		) drop ;


:pc2dcopy | adr -- adr'
	'pc2d ( pc2d> <? )(
		@+ rot !+ swap ) drop
	0 swap !+ ;

:pntdist | p1 p2 -- len
	n>xy rot n>xy	| x1 y1 x2 y2
	rot - dup * >r
	- dup * r> + sqrt ;

:pc2dlen | -- length
	0 'pc2d
	( pc2d> <? )( @+
		over 4+ @	| adr' p1 p2
		over $ff and over $ff and - | adr' p1 p2 id
		0? ( drop pntdist rot + swap )( 3drop )
		) drop
	sqrt ;

:pc2dlen2 | -- length
	0 'pc2d
	( pc2d> <? )( @+
		over 4+ @	| adr' p1 p2
		over $ff and over $ff and - | adr' p1 p2 id
		0? ( drop pntdist rot + swap )( 3drop )
		) drop ;

|------------------------------------
#cx #cy #minx #maxx #miny #maxy
#size

:normalize
	'pc2d
	@+ n>xy
	dup 'miny ! 'maxy !
	dup 'minx ! 'maxx !
	( pc2d> <? )( @+ n>xy
		miny <? ( dup 'miny ! )
		maxy >? ( dup 'maxy ! )
		drop
		minx <? ( dup 'minx ! )
		maxx >? ( dup 'maxx ! )
		drop
		) drop
	maxx minx - maxy miny - max 'size !
	minx maxx + 2/ neg 'cx !
	miny maxy + 2/ neg 'cy !
	'pc2d ( pc2d> <? )( dup @
		dup $ff and >r
		n>xy
		cy + $7ff size */
		swap
		cx + $7ff size */
		swap
		xy>n $ffffff00 and r> or
		swap !+ ) drop ;

|------ debug
:drawresample
	chome! verde
	'aux ( aux> <? )(
		@+ n>xy swap "%d %d |" print
		@+ n>xy swap "%d %d |" print
		@+ "%d |" print cr
		allowchome
		) drop
	;


:dumptest
	show clrscr
		size "size:%d " print cr
		over "sumaa %d " print cr
		drawresample
		cr
		cyan
		'exit >esc<
		;

|------------ version 1
:calcxy	| p1 p2 n -- nxy
	>r n>xy rot n>xy	| x2 y2 x1 y1
	rot 				| x2 x1 y1 y2
	over - r 16 *>> +	| x2 x1 yn
    $fff and 8 <<
	swap rot
	over - r> 16 *>> +	| yn xn
	$fff and 20 << or ;

:interpola | adr value adr -- adr value
	@+ swap @+ swap @		| value p1 p2 sumalast
	pick3 swap -			| value p1 p2 x1
	size over - 			| value p1 p2 x1 size-x1
	dup >r
	pick4 rot - 16 <</		| value p1 p2 t
	calcxy	| adr value npx

	nip     		| adr npx
	swap 12 -
	r> neg over 8 + +! | disminuye distancia
	2dup ! | npx adr. ; guarda nuevo punto
	swap

	+pc2d
	0 ;

:resample
	'aux >r
	0 'pc2d
	( pc2d> 4 - <? )( @+
		over @	| adr' p1 p2
		over $ff and over $ff and - | adr' p1 p2 id
		0? ( drop 2dup swap r!+ r!+ pntdist dup r!+ rot + swap )( 3drop )
		) drop
	numPoints 1- / 'size !
	r> 'aux> !
	pc2dreset
	'aux @+ +pc2d
	0 swap ( aux> <? )(
		4+ @+ 	| acc adr dist
		rot +	| adr acc+
		size >=? ( over 12 - interpola  )
		swap 4+ ) 2drop

|	pc2d> 'pc2d - 2 >> | cantica de puntos
|	32 >? ( dup 32 - 2 << neg 'pc2d> +! )
|	( 32 <? )( aux> 8 - @ +pc2d 1+ ) 2drop

|	pc2d> 'pc2d - 2 >> nsimbol 2 << 'lensim + ! |

	0 pc2d> !
	;


|------------------------------------
:dnc
	xymouse xy>n +pc2d ;
:moc
	xymouse xy>n ++pc2d ;
:upc
	1 'id +! ;

:dorec
	;

:drawgesto
	0.8 %s qdim
	0 0 fpos
	blanco gc.box
	'dnc 'moc 'upc guiMap
|	pc2ddraw
	pc2dpoint
	;


|------------------------------------
:select.move
	gc.popush
	xymouse xygc>n
	over 4 - ! ;

:editbox | x y box --
	gc.push
	qdim pos gc.box
	0 'select.move guiDnMoveA | 'dn 'move --
	gc.pop
	;

:gceditdraw
	( @+ 1? )(
		ngc>xy 5 editbox
		) 2drop ;

#editn
#pad

:editgesto
	dup 2 << 'simbols + @ 'editn !
	show clrscr
		dup "%d " print cr
    	'pad 4 input cr
		blanco
		0.8 %s qdim 0 0 fpos
		gc.box
		editn gceditdraw

		cminiflecha
		'exit >esc< ;

:gcdraw | adr --
	( @+ 1? )(
		ngc>xy 1 box
		) 2drop ;

:drawnsimbol | n -- n
	nsimbol >=? ( ; )
	'editGesto guiBtn
	gc.box
	dup 2 << 'simbols + @
	gcdraw
	home
	dup "%d" print
	dup 2 << 'infosim + @ "%d" print cr
	dup 2 << 'lensim + @ "%d" print
	;


#primero 0

:drawsim
	verde
	0.1 %s qdim
	primero
	-0.9 ( 0.9 <? )(
		dup -0.9 fpos
		swap drawnsimbol
		1+ swap 0.2 + ) 2drop ;

:sigsim
	primero 10 +
	nsimbol >? ( drop ; )
	'primero ! ;

:presim
	primero 10 -
	0 <? ( drop ; )
	'primero ! ;


|------------------------------------
#actual

:getxy1 | nro -- xy
	2 << actual 2 << 'simbols + @ + @ ngc>xy ;

:getxy2 | nro -- xy
	2 << 'pc2d + @ ngc>xy ;

:drawmatch
	0 ( 32 <? )(
		dup getxy1 op
		dup getxy2 line
		1+ ) drop
	;

|--------------------------------
:calcfit | nro -- nro suma
	dup 2 << 'simbols + @ >r	| nro   r:simbolo
	'pc2d dup 128 +		| nro act last
	0 rot
	( pick2 <? )( | nro last suma act
		@+ n>xy r@+ n>xy
		rot - rot rot - distfast	| nro last suma act delta
		rot + swap )				| nro last suma act
	drop nip rdrop ;

#best
#nbest

:fitness
	$7fffffff 'best !
	0 ( nsimbol <? )(
		calcfit dup pick2 2 << 'infosim + !
		best <? ( 'best ! dup 'nbest ! )( drop )
		1+ ) drop ;


:searchar
	resample
	normalize
	fitness
	nbest 'actual !
	show clrscr
		dup "%d " print cr
		0.5 %s qdim
		0 dup fpos
		azul gc.box
		drawmatch

		verde actual 2 << 'simbols + @ gcdraw
		cyan 'pc2d gcdraw

		drawsim

		[ pc2dreset exit ; ] >esc< ;

:addchar
	pc2d> 'pc2d - 2 >> 2 <? ( drop pc2dreset ; ) drop
	normalize
	resample
	memsim> dup pc2dcopy 'memsim> !
	nsimbol 2 << 'simbols + !
	1 'nsimbol +!
	pc2dreset
	;

|-------------------------

:drawnorm
	;

:generaysave
	nsimbol 'signon !
	'signod
	0 ( nsimbol <? )(
		dup 2 << 'simbols + @ >r	| nro   r:simbolo
        swap
		32 ( 1? )( 1- r@+ rot !+ swap ) drop
		swap 1+ ) 2drop
	;

:reconoce
|	resample
|	normalize
|	fitness

	;

:gestobox
	0.8 %s qdim
	0 0 fpos
	blanco gc.box
	[ xymouse xygc>n +pc2d ; ]
	[ xymouse xygc>n ++pc2d ; ]
	[ reconoce ; ]
	guiMap
	gcpc2ddraw
	;


:writetest
	pc2dreset
	show clrscr
		gestobox
		cminiflecha
		'exit >esc<
		;
|------------------------------------
:main
	mark
	here dup 'memsim ! 'memsim> !

|	'signon "inc\gesto\gesto.mem" load drop

	pc2dreset
	33
	show clrscr
		drawgesto
		drawsim
		scr home verde
		dup "%d) " print
		nsimbol "%d simbolos " print
		cr cr
		rojo
		'pc2dreset dup <f10> "clear" sp btnt
		verde
		'addchar dup <f1> "+simbolo" sp btnt
		'searchar dup <f2> "?search" sp btnt
		'resample dup <f3> "resample" sp btnt cr cr

		cyan
		'generaysave dup <f4> "SAVE" sp btnt
		'writetest dup <f5> "WRITE" sp btnt

		cminiflecha
		'exit >esc< ;

: main ;