| Ray Marching
| http://iquilezles.org/www/articles/terrainmarching/terrainmarching.htm
|
^r4/lib/gui.txt
^r4/lib/trace.txt

#rox 0 #roy 0 #roz 0 | origen
#rdx 0 #rdy 0 #rdz 0 | direccion

#px #py #pz
#nx #ny #nz

#sun 0.0 1.0 0
#eps 0.0001

:f(xy) | x y -- r
	sin swap sin *. ;

:getNormal
	px eps - pz f(xy)
	px eps + pz f(xy) - 'nx !
	eps 2* 'ny !
	px pz eps - f(xy)
	px pz eps + f(xy) - 'nz !
	'nx v3nor
	;

:skycolor
	drop $ff0000
	;

:terraincolor
	drop
	getnormal
	'nx 'sun v3ddot
	;

:generateRay | y x --
	'rdx >r
	dup sw 2/ - 1.0 sw */ r!+
	over sh 2/ - 1.0 sh */ r!+
	1.0 r> !
	;

:calcpf | t -- t ;ro +rd*t
	rdx over *. rox + 'px !
	rdy over *. roy + 'py !
	rdz over *. roz + 'pz !
	px pz f(xy)
	;

:castRay
	0.01
	0.001 ( 10.0 <? )(
		calcpf
		over >? (
			3drop
			1 ;	)
		drop
		over + )
	2drop 0 ;

:generaimagen
	0 0 setxy
	0 ( sh <? )(
		0 ( sw <? )(
			generateRay
			castRay
			0? ( skycolor )( terraincolor )
			px!+
			1+ ) drop
		redraw
		1+ ) drop
	;

:v3print | v
	@+ "%f "  print
	@+ "%f "  print
	@ "%f "  print cr ;

:main
	generaimagen
	show clrscr
		dup "%d" print cr
		xymouse swap generateRay
		'exit >esc<
		cminiflecha ;

: 33 main ;
