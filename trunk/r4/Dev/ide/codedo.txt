| CODEdo (ide)
| PHREDA 2013
|----------------------
^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/input.txt

^r4/lib/rfont.txt
^r4/lib/ricons.txt
^inc/ric/fontawesomewebfont.ric
^inc/rft/dejavusans.rft

^r4/dev/ide/tokenr4.txt

#error
#pad )( 4096
#ypad
#sizepad

#strerror
"Ok"
"Palabra no definida"
"Palabra sin direccion"
"Desborde de Pila"
"Escases de Pila"
"Pila Incompatible"
"Falta ;"
"Bloque mal cerrado"
"No existe Libreria"
0

#strerrorEN
"Ok"
"Undefined Word"
"No Word Adress"
"Stack Overflow"
"Stack Underflow"
"Stack not match"
"Need ;"
"Bad Structure"
"Library not exist"
0

:resetpad
	sh sizepad - 'ypad ! ;

#newline ( 13 0 )

:morepad
	sizepad neg 'ypad +!
	'pad 'newline strcat
	refreshfoco
	;

:errorpad
	-? ( drop morepad ; ) | negativo
	'strerror swap ( 1? )( swap >>0 swap 1- ) drop
	'error !
	;

#tokenpad

:ejecuta
	tokencod> 'tokenpad !
	'pad str2token
	1? ( errorpad ; ) drop
	tokenpad exectoken
	tokenpad 'tokencod> !

	resetpad
	'pad stackdstr
	refreshfoco ;

:showpad
	'ejecuta <enter>

	verde oscuro
	0 ypad sw 1- over op line
	verde
	dejavusans sizepad rfont!
	4 ypad 4+ atxy
	'pad 64 input
	;

:msgerror
	error 0? ( drop ; )
	blink 1? ( blanco )( rojo ) drop
	print ;


:main
	show clrscr
		fonti
		verde "r4: " print msgerror cr
		amarillo dumptok cr
		verde dumpdic cr

		showpad

|		24 sh 24 - pos 48 qdim
|		'exit rojo btne negro 'i.off drawric
|		gc>> exit verde btne blanco 'i.play drawric

		[ 4 'sizepad +! resetpad ; ] <f1>
		[ -4 'sizepad +! resetpad ; ] <f2>

		cminiflecha
		'exit >esc< ;

:m
	mark
	64 'sizepad !
	resetpad
	0 paper
	;

: m main ;