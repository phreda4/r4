| R4i86o3.txt - PHREDA 2015
| Generacion de codigo para i86-FASM
|
| .. [esp] - pila R
| .. [esi] eax - pila D
|
| ebx edi - Libres
| ecx - libre | sar sal
| edx - libre | * /
|
| ebp - VFRAME pointer
| MMX - auxiliares de almacenamiento
|
|--------------------------------------------
^r4/lib/gui.txt
^r4/lib/parse.txt

^r4/dev/ide/r4-stack.txt

^r4/lib/trace.txt

|----- nro de str y label
#nstr | nro de string
#nlabel | nro de etiqueta

::ini.compila
	0 'nstr ! 0 'nlabel ! ;

|----- pila de compilador
#pilac )( 256	#pilac> 'pilac

:>pilac pilac> ! 4 'pilac> +! ;
:<pilac -4 'pilac> +! pilac> @ ;
:-pilac 'pilac 'pilac> ! ;

|------------ compila CODIGO
:nro>dicname   8 >> 4 << 'indicepal + @ ; | nombre
:nro>dicn   8 >> "w%h" mprint ;			| numero de palabra
:toknow		dup 4 - @ ;

:,defw
	| palabras continuadas *********
:,defv ; | no hay definiciones en optimizado (investigar como)
:,[
	| llamada a anonimas *********
:,] ; | no hay anonimas en optimizado

#strpending
#lastdircode

:,wor
	vpila2normal
	dup @ $ff and 			| word ;
	$10 =? ( "jmp " )( "call " ) nip ,s
	toknow tok>name ,s ,cr
	toknow 8 >> vpila2callV
	strpending neg vpila2v
	0 'strpending !
	;

:,callsys | usapila? --
	1? ( vpila2normal ) drop
	dup @ $ff and | word ;
	$10 =? ( "jmp " )( "call " ) nip
	,s ;

:,END		"jmp SYSEND" ,ln ;
:,FFIRST 	1 ,callsys "SYSFFIRST" ,ln ;
:,FNEXT		1 ,callsys "SYSFNEXT" ,ln 1 vpila2v ;
:,LOAD		1 ,callsys "SYSLOAD" ,ln -1 vpila2v ;
:,SAVE		1 ,callsys "SYSSAVE" ,ln -3 vpila2v ;
:,APPEND	1 ,callsys "SYSAPPEND" ,ln -3 vpila2v ;
:,UPDATE	1 ,callsys "SYSUPDATE" ,ln ;
:,MSEC		1 ,callsys "SYSMSEC" ,ln 1 vpila2v ;
:,TIME		1 ,callsys "SYSTIME" ,ln 3 vpila2v ;
:,DATE		1 ,callsys "SYSDATE" ,ln 3 vpila2v ;
:,RUN		1 ,callsys "SYSRUN" ,ln ;
:,CLS		0 ,callsys "SYSCLS" ,ln ;
:,REDRAW	0 ,callsys "SYSREDRAW" ,ln ;
:,>XFB  	0 ,callsys "SYSTOXFB" ,ln ;
:,XFB>  	0 ,callsys "SYSXFBTO" ,ln ;

:,;
	dup 8 - @ $ff and
	$c =? ( drop ; ) drop | word ;
	vpila2normal
	"ret" ,ln ;

:,lit	toknow tok>cte pushNRO ;
:,lits
		toknow tok>mem strusestack
		'strpending ! drop
		nstr pushSTR 1 'nstr +! ;
:,dwo	toknow 8 >> dup 'lastdircode ! pushCOD ;
:,dva   toknow 8 >> dup 'lastdircode ! pushDAT ;
:,var	toknow 8 >> dup 'lastdircode ! pushVar ;

|-------- CONDICIONALES
#jmpstr 0
#contype

:consimple
	d.tos cell2REG
	d.tos cellADR? 1? ( drop
		"cmp " ,s ,TOS ",0" ,ln ; ) drop
	"or " ,s ,TOS "," ,s ,TOS ,cr
	;

:,simple?
	'jmpstr ! 'consimple 'contype ! ;

:,0? "jnz " ,simple? ;
:,+? "js "  ,simple? ;
:,-? "jns " ,simple? ;
:,1? "jz "  ,simple? ;

:condoble
	d.nos cell2REG
|	d.tos cellADR? 1? ( drop
|		d.nos cellADR? 1? ( drop
|			newReg 6 >? ( drop freelastreg newReg )
|			"mov " ,s dup ,REG "," ,s ,TOS ,cr
|			"cmp " ,s ,NOS "," ,s ,REG ,cr
|			d.drop ;
|			)( drop ) )( drop )
    "cmp " ,s ,NOS "," ,s ,TOS ,cr
	d.drop ;

:,doble?
	'jmpstr ! 'condoble 'contype ! ;

:,=? "jnz " ,doble?	;
:,<? "jge " ,doble?	;
:,>? "jle " ,doble?	;
:,<=? "jg " ,doble?	;
:,>=? "jl " ,doble?	;
:,<>? "jz " ,doble?	;

:conbit
	d.nos cell2REG
|	d.tos cellADR? 1? ( drop
|		d.nos cellADR? 1? ( drop
|			newReg 6 >? ( drop freelastreg newReg )
|			"mov " ,s dup ,REG "," ,s ,TOS ,cr
|			"test " ,s ,NOS "," ,s ,REG ,cr
|			d.drop ;
|			) )( drop )
    "test " ,s ,NOS "," ,s ,TOS ,cr
	d.drop ;

:,bit?
	'jmpstr ! 'conbit 'contype ! ;

:,and?  "jz " ,bit?	;
:,nand? "jnz " ,bit? ;

|----- auxiliares bloques
:label		"_" ,s ,h ;
:jmp,		"jmp " ,s label ,cr ;
:,etiqueta	label ": " ,s ;
:,jmpcond   jmpstr ,s label ,cr ;

:+etiqueta	| -- nueva   // y agrega a pila
	nlabel 1+ dup 'nlabel ! dup >pilac ;

|----- BLOQUES
:vpilad
	vpila2normal

|	deepnow blockde ";vpila2reg << %d >> %d " ,print printddvtack
|	blockde vpila2reg
|	deepnow blockde ";vpila2reg << %d >> %d " ,print printddvtack
	;

:,cond
	d.cnt "; COND cnt: %d " ,print
|	blockde "; blockde: %d " ,print
	inivpcnt "; inivpcnt: %d " ,print
	printddvtack

	contype
|	blockt $10 nand? ( drop
|		'consimple =? ( blockde vpila2fix )( blockde vpila2fix2 )
|		)( drop )
	exec

	d.cnt "; COND cnt: %d " ,print
|	blockde "; blockde: %d " ,print
	inivpcnt "; inivpcnt: %d " ,print
	printddvtack
	;

:,bloque
	";----- BLOQUE ----" ,s ,cr
|	d.cnt "; cantidad: %d " ,print blockde "; blode: %d " ,print inivpcnt "; incnt: %d " ,print ,cr
|	d.cnt inivpcnt - vpila2reg

|	"; " ,s printddvtack
	;

:vppush
	;
:vppop
	;
:vppick
	;
:vpdrop
	;
:vpnip
	;
:vpFitAll
	;

:,(
	block.new
	blockt block.now "; BLK %d %d (( " ,print printddvtack
|	"r4asm/cod.asm" savemem

	blockt $f and
	1 <=? ( ,cond vppush +etiqueta ,jmpcond )	| IF/ELSE
	2 >=? ( vpilad vppush +etiqueta ,etiqueta )	| WHILE | UNTIL | REPEAT
	drop

|	deepnow blockde "; << %d >> %d " ,print printddvtack
	;

:,)(
	block.now ";BLK %d )( " ,print printddvtack
|	"r4asm/cod.asm" savemem

	blockt $f and
	1 =? ( ,bloque <pilac +etiqueta jmp, ,etiqueta vppush vppick vpnip )	| ELSE
	2 =? ( ,cond <pilac +etiqueta ,jmpcond >pilac vpdrop vppush )	| WHILE
	drop

|	deepnow blockde "; << %d >> %d " ,print printddvtack
|	"r4asm/cod.asm" savemem

	;

:,)
	block.now ";BLK %d )) " ,print printddvtack
|	"r4asm/cod.asm" savemem
	blockt $f and
	1 <=? (
			over 8 - @ $ff and $10 <>? ( vpFitAll ) drop
			<pilac ,etiqueta				| IF ELSE
			)
	2 =? ( <pilac jmp, <pilac ,etiqueta )	| WHILE
	3 =? ( ,cond <pilac ,jmpcond )	| UNTIL
	4 =? ( <pilac jmp, )					| REPEAT
	drop

	vpPop
|	deepnow blockde "; << %d >> %d " ,print printddvtack

	block.end ;

|------- EXEC
:,execr
	dup @ $ff and 			| word ;
	$10 =? ( "jmp ecx" )( "call ecx" ) nip
	,s ,cr
	lastdircode vpila2callV ;

:,EXECPP | v --
	"push " ,s ,TOS ,cr
	d.drop

|	vpilanormal?
|	"; normal? %d " ,print ,cr

	vpila2normal

	"pop ecx" ,ln
	,execr ;

:,EXEC | v --
	d.tos cellVAR -? ( 2drop ,EXECPP ; ) 2drop
	d.tos d.drop vpila2normal
	"mov ecx," ,s ,CELL ,cr
	,execr ;

|------- PILAR

:,>R
	toknow 8 >> 1? ( drop d.pop r.push ; ) drop
	"push " ,s ,TOS ,cr d.drop ;

:,R>
	toknow 8 >> 1? ( drop r.pop d.push ; ) drop
	d.dup d.tos cell2REG "pop " ,s ,TOS ,cr ;

:,R
	toknow 8 >> 1? ( drop r.tos d.push ; ) drop
	d.dup d.tos cell2REG "mov " ,s ,TOS ",dword [esp]" ,ln ;

:,RDROP
	toknow 8 >> 1? ( drop r.drop ; ) drop
	"lea esp,[esp+4]" ,ln ;

:,R+a
	r.tos cell2REG
	"add " ,s ,RTOS "," ,s ,TOS ,cr
	d.drop ;

:,R+
	toknow 8 >> 1? ( drop ,R+a ; ) drop
	d.tos cell2NMEM "add dword [esp]," ,s ,TOS ,cr d.drop ;

:,R!+a
	r.tos cell2REG
	"mov dword [" ,s ,RTOS "]," ,s ,TOS ,cr
	"add " ,s ,RTOS ",4" ,ln
	d.drop ;

:,R!+
	toknow 8 >> 1? ( drop ,R!+a ; ) drop
	d.tos cell2NMEM
	newReg |6 >? ( drop freelastreg newReg )
	"mov " ,s dup ,REG ",dword [esp]" ,ln
	"mov dword [" ,s ,REG "]," ,s ,TOS ,cr
	"add dword [esp],4" ,ln
	d.drop ;

:,R@+a
	d.dup d.tos cell2REG
	r.tos cell2NMEM
	"mov " ,s ,TOS ",dword [" ,s ,RTOS "]" ,ln
	"add " ,s ,RTOS ",4" ,ln
|	4 r.tos Cell+ | add 4
	;

:,R@+
	toknow 8 >> 1? ( drop ,R@+a ; ) drop
	d.dup d.tos cell2REG
	"mov " ,s ,TOS ",[esp]" ,ln
	"mov " ,s ,TOS ",[" ,s ,TOS "]" ,ln
	"add dword [esp],4" ,ln ;

|------- ARITMETICAS LOGICAS
:oper21con
	d.nos cellREG? 0 >=? ( drop ; ) drop
	d.tos cellREG? 0 >=? ( drop d.swap ; ) drop
	d.nos cell2REG ;

:,AND
	oper21con
	"and " ,s ,NOS "," ,s ,TOS ,cr
	d.drop ;
:,OR
	oper21con
	"or " ,s ,NOS "," ,s ,TOS ,cr
	d.drop ;
:,XOR
	oper21con
	"xor " ,s ,NOS "," ,s ,TOS ,cr
	d.drop ;
:,+
	oper21con
	"add " ,s ,NOS "," ,s ,TOS ,cr
	d.drop ;
:,-
	d.nos cell2REG
	"sub " ,s ,NOS "," ,s ,TOS ,cr
	d.drop ;
:,*
	oper21con
	| si d.tos es pow2..
	"imul " ,s ,NOS "," ,s ,TOS ,cr
	d.drop ;

:,/
	| si d.tos es pow2..
	nosEAXtosREGMEMfreeEDX

	"cdq" ,ln
	"idiv " ,s ,TOS ,cr
	d.drop ;

:,/MOD
	| si d.tos es pow2..
	nosEAXtosREGMEMfreeEDX

	"cdq" ,ln
	"idiv " ,s ,TOS ,cr
	d.nos setEAX
	d.tos setEDX ;

:,MOD
	| si d.tos es pow2..
	nosEAXtosREGMEMfreeEDX

	"cdq" ,ln
	"idiv " ,s ,TOS ,cr
	d.drop
	d.tos setEDX ;

:,*/
	pk2EAXtosnosREGMEMfreeEDX

	"cdq" ,ln
	"imul " ,s ,NOS ,cr
	"idiv " ,s ,TOS ,cr
	d.2drop
	d.tos setEAX ;

:,*>>
	| si d.nos es pow2..

	usoEDX
	d.pk2 needEAX
	d.nos needREGorMEM
	d.tos cteorECX

	"cdq" ,ln
	"imul " ,s ,NOS ,cr
	"shrd eax,edx," ,s ,TOSb ,cr
|	"shr edx," ,s ,TOSb ,cr | no hace falta!!
	d.2DROP ;

:,<</
	d.pk2 needEAX
	usoEDX
	d.nos needREGorMEM
	d.tos cteorECX

	"cdq" ,ln
    "shld edx,eax," ,s ,TOSb ,cr
	"shl eax," ,s ,TOSb ,cr
	"idiv " ,s ,NOS ,cr
	d.2DROP
	d.tos setEAX ;

:,<<
	d.tos cteorECX
	d.nos cell2REG
	"sal " ,s ,NOS "," ,s ,TOSb ,cr
	d.drop ;

:,>>
	d.tos cteorECX
	d.nos cell2REG
	"sar " ,s ,NOS "," ,s ,TOSb ,cr
	d.drop ;

:,ABS
	usoEDX
	d.tos cell2REG
	d.tos cellREG? 0? ( | EAX
		"cdq" ,ln
		)(
		"mov edx," ,s ,TOS ,cr
		"sar edx,31" ,ln
		) drop
	"add " ,s ,TOS ",edx" ,ln
	"xor " ,s ,TOS ",edx" ,ln ;

:,CLZ
	d.tos cell2REG
	"bsr " ,s ,TOS  "," ,s ,TOS ,cr
	"xor " ,s ,TOS ",31" ,ln ;
:,NOT
	d.tos cell2REG
	"not " ,s ,TOS ,cr ;
:,NEG
	d.tos cell2REG
	"neg " ,s ,TOS ,cr ;
:,1+
	d.tos cell2REG
	"add " ,s ,TOS ",1" ,ln ;
:,4+
	d.tos cell2REG
	"add " ,s ,TOS ",4" ,ln ;
:,1-
	d.tos cell2REG
	"sub " ,s ,TOS ",1" ,ln ;
:,2/
	d.tos cell2REG
	"sar " ,s ,TOS ",1" ,ln ;
:,2*
	d.tos cell2REG
	"sal " ,s ,TOS ",1" ,ln ;

|------ MEMORIA
:,@
	d.tos cell2REG
	"mov " ,s ,TOS ",dword [" ,s ,TOS "]" ,ln ;
:,C@
	d.tos cell2REG
	"movsx " ,s ,TOS ",byte [" ,s ,TOS "]" ,ln ;
:,W@
	d.tos cell2REG
	"movsx " ,s ,TOS ",word [" ,s ,TOS "]" ,ln ;

:,!
	d.tos cell2NMEM
	d.nos cell2NMEM
	"mov dword [" ,s ,TOS "]," ,s ,NOS ,cr
	d.2DROP ;
:,C!
	d.tos cell2NMEM
	d.nos cell2NMEM
	"mov byte [" ,s ,TOS "]," ,s ,NOSb ,cr
	d.2DROP ;
:,W!
	d.tos cell2NMEM
	d.nos cell2NMEM
	"mov word [" ,s ,TOS "]," ,s ,NOSw ,cr
	d.2DROP ;

:,+!
	d.nos cell2NMEM
	d.tos cell2NMEM
	"add dword [" ,s ,TOS "]," ,s ,NOS ,cr
	d.2DROP ;
:,W+!
	d.nos cell2NMEM
	d.tos cell2NMEM
	"add word [" ,s ,TOS "]," ,s ,NOSw ,cr
	d.2DROP ;
:,C+!
	d.nos cell2NMEM
	d.tos cell2NMEM
	"add byte [" ,s ,TOS "]," ,s ,NOSb ,cr
	d.2DROP ;

:,!+
    d.nos cell2NMEM
	d.tos cell2REG
	"mov dword [" ,s ,TOS "]," ,s ,NOS ,cr
	"add " ,s ,TOS ",4" ,ln
	d.NIP ;
:,W!+
    d.nos cell2NMEM
	d.tos cell2REG
	"mov word [" ,s ,TOS "]," ,s ,NOSw ,cr
	"add " ,s ,TOS ",2" ,ln
	d.NIP ;
:,C!+
    d.nos cell2NMEM
	d.tos cell2REG
	"mov byte [" ,s ,TOS "]," ,s ,NOSb ,cr
	"add " ,s ,TOS ",1" ,ln
	d.NIP ;

:,@+
	d.tos cell2REG
	d.dup d.tos cell2REG
	"mov " ,s ,TOS ",dword [" ,s ,NOS "]" ,ln
	"add " ,s ,NOS ",4" ,ln ;
:,W@+
	d.tos cell2REG
	d.dup d.tos cell2REG
	"movsx " ,s ,TOS ",word [" ,s ,NOS "]" ,ln
	"add " ,s ,NOS ",2" ,ln ;
:,C@+
	d.tos cell2REG
	d.dup d.tos cell2REG
	"movsx " ,s ,TOS ",byte [" ,s ,NOS "]" ,ln
	"add " ,s ,NOS ",1" ,ln ;

|--------- Sistema
|#syscons "XRES" "YRES" "FREE_MEM" "SYSFRAME" "XFB" | [] | "SYSPAPER" "SYSXYM" "SYSBM" "SYSKEY"

:,SW 		0 pushCTE ;
:,SH		1 pushCTE ;

:,MEM		|2 pushCTE ;
		2 pushCTE d.tos cellMEM! ;

:,FRAMEV	3 pushCTE ;
:,XFB		4 pushCTE ;

:,BMOUSE | -- c
	7 pushCTE d.tos cellMEM! ;

:,KEY	| -- c
	8 pushCTE d.tos cellMEM! ;

:,PAPER | n --
	d.tos cell2NMEM | no memoria []
	"mov [SYSPAPER]," ,s ,TOS ,cr
	d.drop ;

:,KEY! | v --
	d.tos cell2NMEM | no memoria []
	"mov [SYSKEY]," ,s ,TOS ,cr
	d.drop ;

:,SETXY | x y --
	d.tos cellNRO? 1? ( drop
		d.nos cellNRO? 1? ( drop
			"lea ebp,[SYSFRAME+(" ,s ,TOS "*XRES+" ,s ,NOS ")*4]" ,ln
			d.2drop ; ) drop ) drop

	d.tos cell2REG

|	"imul " ,s ,TOS ",XRES" ,ln	| cualquier ancho
	"shl " ,s ,TOS ",10" ,ln	| 1024 de ancho

	d.nos cell0? 0? ( "add " ,s ,TOS "," ,s ,NOS ,cr ) drop
	"lea ebp,[SYSFRAME+" ,s ,TOS "*4]" ,ln
	d.2DROP ;

:,PX+!	| s --
	d.tos cell2NMEM
	"lea ebp,[ebp+" ,s ,TOS "*4]" ,ln
	d.drop ;

:,PX!+	| rgb --
	d.tos cell2NMEM
	"mov dword [ebp]," ,s ,TOS ,cr
	"add ebp,4" ,ln
	d.drop ;

:,PX@	| -- rgb
	d.dup d.tos cell2REG
	"mov " ,s ,TOS ",dword [ebp]" ,ln ;

:,XYMOUSE | -- x y
	d.dup d.tos cell2REG
	d.dup d.tos cell2REG
	"mov " ,s ,TOS ",[SYSXYM]" ,ln
	"mov " ,s ,NOS "," ,s ,TOS ,cr
	"and " ,s ,NOS ",$ffff" ,ln
	"shr " ,s ,TOS ",16" ,ln
	;

:,CNTJOY
	d.DUP "xor eax,eax" ,ln ;
:,GETJOY ;
:,SLOAD ;
:,SPLAY  d.DROP ;
:,SINFO ;
:,SSET  d.DROP d.DROP d.DROP d.DROP ;

:,DOCINI :,DOCEND
	;
:,OPENURL	| url header buff -- buff/0
	d.2drop ;
:,DOCAT     | x y --
	d.2drop ;
:,DOCLINE	| x y --
	d.2drop ;
:,DOCTEXT	| "tt" --
	d.drop ;
:,DOCFONT	| size angle "font" --
	d.3drop ;
:,DOCBIT	| bitmap x y --
	d.3drop ;
:,DOCRES	| -- xmax ymax
	d.2dup ;
:,DOCSIZE	| "tt" -- w h
	d.dup ;
:,SYSTEM	| "sys" --
	d.drop ;

|----------- en asmbase.txt
:,SQRT
:,MOVE :,MOVE> :,CMOVE :,CMOVE>
:,INK :,INK@ :,ALPHA
:,OP :,LINE :,CURVE :,CURVE3 :,PLINE :,PCURVE :,PCURVE3 :,POLI
:,FCOL :,FCEN :,FMAT :,SFILL :,LFILL :,RFILL :,TFILL ;

:,0 ;

#acompila ,0
,defw ,defw ,defv ,defv ,0 ,0
,lit ,lit ,lit ,lit ,lits ,wor ,var ,dwo ,dva | 11
,; ,( ,)( ,) ,[ ,] ,EXEC	| 18
,0? ,+? ,-? ,1? ,=? ,<? ,>? ,<=? ,>=? ,<>? ,AND? ,NAND? | 30

d.DUP d.DROP d.OVER d.PICK2 d.PICK3 d.PICK4 d.SWAP d.NIP	|--- pila 38
d.ROT d.2DUP d.2DROP d.3DROP d.4DROP d.2OVER d.2SWAP	| 45

,>R ,R> ,R ,R+ ,R@+ ,R!+ ,RDROP					|--- pila direcciones 52
,AND ,OR ,XOR ,NOT  								|--- logicas        56
,+ ,- ,* ,/ ,*/ ,*>> ,/MOD ,MOD ,ABS  			|--- aritmeticas | 65
,SQRT ,CLZ ,<</ ,NEG ,1+ ,4+ ,1- ,2/ ,2* ,<< ,>> | 76
,@ ,C@ ,W@ ,! ,C! ,W! ,+! ,C+! ,W+!  			|--- memoria 85
,@+ ,!+ ,C@+ ,C!+ ,W@+ ,W!+	| 91
,MOVE ,MOVE> ,CMOVE ,CMOVE> | 95
,MEM ,FFIRST ,FNEXT
,LOAD ,SAVE ,APPEND		|--- memoria bloques 102
,UPDATE
,XYMOUSE ,BMOUSE
,KEY! ,KEY
,CNTJOY ,GETJOY 							| 109
,MSEC ,TIME ,DATE ,END ,RUN 				|--- sistema 114
,SW ,SH ,CLS ,REDRAW ,FRAMEV  						|--- pantalla
,SETXY ,PX+! ,PX!+ ,PX@
,XFB ,>XFB ,XFB>
,PAPER ,INK ,INK@ ,ALPHA							|--- color
,OP ,LINE ,CURVE ,CURVE3 ,PLINE ,PCURVE ,PCURVE3 ,POLI		|--- dibujo
,FCOL ,FCEN ,FMAT ,SFILL ,LFILL ,RFILL ,TFILL
,SLOAD ,SPLAY ,SINFO ,SSET					|--- Sonido
,OPENURL
,DOCINI ,DOCEND ,DOCAT ,DOCLINE ,DOCTEXT ,DOCFONT ,DOCBIT ,DOCRES ,DOCSIZE  |-- impresora
,SYSTEM

|------------------------------
:imprimedebug | token -- token
	" ; " ,s dup tokenstrA ,print
	" : " ,s printddvtack
	"r4asm/cod.asm" savemem
	;

::compilatok | bufend bufini --
	2dup getusedstack neg inivpila
	0 'strpending !
	( over <? )(
		@+
		imprimedebug
		$ff and 2 << 'acompila + @ exec
		) 2drop
	;
