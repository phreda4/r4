| memoria + diccionario r4
| PHREDA 2013
|----------------------------------

|---- includes
| 'string|'mem|first|last
|  0      4    8     12
#:indiceinc )( $7fff
#:indiceinc> 'indiceinc

|---- diccionario
| str|token|info|???   16 bytes por palabra
| 0      4     8   12
#:indicepal )( $fffff
#:indicepal> 'indicepal
#:indicepal< 'indicepal

|---- memoria token
#:tokencod )( $ffff
#:tokencod> 'tokencod
#:tokencte )( $ffff
#:tokencte> 'tokencte
#:tokenmem )( $ffff
#:tokenmem> 'tokenmem

::dic>str	4 << 'indicepal + ;			| n -- %a
::dic>tok	4 << 'indicepal + 4+ ;		| n -- %a
::dic>mem	4 << 'indicepal + 8 + ; 	| n -- m
::dic>inf	4 << 'indicepal + 12 + ;	| n -- %a

::diccreset
	'indicepal dup
	'indicepal> ! 'indicepal< !
	'indiceinc 'indiceinc> !
::padreset
	'tokencod 'tokencod> !
	'tokencte 'tokencte> !
	'tokenmem 'tokenmem> !
	;

::adr>dic
	'indicepal - 4 >> ;

::?palabra | str -- str dir / str 0
	indicepal> 16 -	|---largo
	( 'indicepal >=? )(
		dup @						| str ind pal
		pick2
		=s 0? ( drop )( drop
			dup 8 + @
			%10 and? ( drop ; )( drop indicepal< >=? ( ; ) )
			)
		16 - ) drop
	0 ;

::?include | str -- adr/0
	'indiceinc ( indiceinc> <? )(
		@+ pick2 =s 1? ( drop nip 4 - ; ) drop
		12 + ) 2drop 0 ;

::word+! | tipo --
	indicepal> >r
	tokenmem> r!+	| str del nombre
	tokencod> r!+	| token de comienzo
	r!+				| info de palabra
	0 r!+			| mem?
	r> 0 over ! 'indicepal> ! ;

::dumpdic
|	"includes" print cr
|	'indiceinc ( indiceinc> <? )(
|		@+ allowcr "%w" print sp
|		12 + ) drop cr
	"words" print cr
	'indicepal ( indicepal> <? )(
		@+ print sp
		@+ "%h " print
		@+ "%h " print
		@+ "%h " print cr
		) drop cr
	;
