| font mono convert
| PHREDA 2014
|------------------------
^r4/lib/gui.txt
^r4/lib/dlgfilea.txt
^r4/lib/leebmp.txt

#bmp
#fuente
#fuente>
#txt

#sx #sy

#accline

:,fuente
	accline fuente> !+ 'fuente> ! ;

#ax #ay

:loadchar | y x -- y x
	2dup sx * 'ax ! sy * 'ay !
	0 ( sy <? )(
		0 'accline !
		0 ( sx <? )(
			2dup ax + swap ay + bmp.getm @
			22 >>	| escala de grises
|			$ffffff =? ( 3 )( 0 ) nip
    		over 2* << accline or 'accline !
			1+ ) drop
		,fuente
		1+ ) drop ;

:loadfont
	"r4/dev/fontmono" dlgfileload 0? ( drop ; )
	bmp.load 'bmp !
	here dup 'fuente ! 'fuente> !
	bmp.alto 4 >> 'sy !
	bmp.ancho 4 >> 'sx !
	0 ( 16 <? )(
		0 ( 16 <? )(
			loadchar
			1+ ) drop
		1+ ) drop
	fuente> dup 'here ! 'txt !
	mark
	fuente >r
	"#:font",s 'dlgfilename ,s
	sx sy " %d %d " ,print ,nl
	0 ( 256 <? )(
		sy ( 1? )( 1-
			r@+ "$%h " ,print
			) drop
		,nl
		1+ ) drop rdrop
	,nl
	'dlgfilename "r4/dev/fontmono/%s.txt" mprint  savemem
	empty
	;

:main
	33
	show clrscr
		cr2 verde 'loadfont " LOAD FONT " btnt
		dup "%d" print cr
		bmp.alto bmp.ancho "%dx%d" print cr
		0 64 bmp.xyreal
		'exit >esc<
		cminiflecha ;

:memo
	mark
	here 'fuente !
	;

: memo main ;