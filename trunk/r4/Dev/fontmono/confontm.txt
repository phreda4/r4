| consola fontm
| PHREDA 2014
|-------------------
^r4/lib/gui.txt
^r4/lib/fontm.txt
^r4/dev/fontmono/sourcecode13.txt
^r4/dev/fontmono/inconsolata13.txt
^r4/dev/fontmono/ubuntumon13.txt
^r4/dev/fontmono/droidsand13.txt
^r4/lib/trace.txt


#wcon #hcon
#advcha
#advlin
#memcon
#ccur>
#clast>
#ccolor $f0

:fullconsola
|	'fontInconsolata13 fontm
	'fontdroidsand13 fontm
|	'fontUbuntumon13 fontm
	sw ccw / 'wcon !
	sh cch / 'hcon !
	sw cch * neg ccw + 'advcha !
	sw cch * ccw wcon * - 'advlin !
	here 'memcon !
	wcon hcon * 2 << + 'here +!
	here 4 - 'clast> !
	memcon 'ccur> !
	;

:iniconsola | w h --
|	'fontInconsolata13 fontm
	'fontdroidsand13 fontm
|	'fontUbuntumon13 fontm
	'hcon ! 'wcon !
	sw cch * neg ccw + 'advcha !
	sw cch * ccw wcon * - 'advlin !
	here 'memcon !
	wcon hcon * 2 << + 'here +!
	here 4 - 'clast> !
	memcon 'ccur> !
	;

:cuscroll
	memcon wcon 2 << over + wcon hcon * move
	clast> wcon 1- 2 << - wcon ( 1? )( 1- 0 rot !+ swap ) 2drop ;

:setcur | adr --
	clast> >? ( cuscroll clast> wcon 1- 2 << - nip ) 'ccur> ! ;
:cemit | c --
	ccolor 8 << or ccur> !+ setcur ;
:cat | x y --
	wcon * + 2 << memcon + setcur ;
:chome
	memcon 'ccur> ! ;
:ccr ccur> memcon - 2 >> wcon / 1+ wcon * 2 << memcon + setcur ;
:cup ccur> wcon 2 << - memcon <? ( drop ; ) 'ccur> ! ;
:cdn ccur> wcon 2 << + setcur ;
:cle ccur> 4 - memcon <? ( memcon nip ) 'ccur> ! ;
:cri ccur> 4+ setcur ;

:cprint | "" --
	mprint ( c@+ 1? )( cemit ) 2drop ;

:ccls
	memcon dup 'ccur> ! >r wcon hcon * ( 1? )( 1- ccolor 8 << r!+ ) drop rdrop ;

:catr | col --
	'ccolor ! ;

:showconsola | x y -
	setxy
    blink
	1? ( ccur> @ $100000 xor ccur> ! )
	memcon >r
	hcon ( 1? )( 1-
		wcon ( 1? )( 1-
			r@+ dup 8 >> ink12
			$100000 and? ( charmtn )( charmt )
			advcha px+!
			) drop
		advlin px+!
		) drop rdrop
	1? ( ccur> @ $100000 xor ccur> ! )
	drop ;

:main
	sw ccw / sh cch / 2 - iniconsola
	ccls
	show clrscr

		showconsola

        [ key toasc cemit ; ] <visible>
        [ ccr ; ] <enter>
		'cle <le>
		'cri <ri>
		'cdn <dn>
		'cup <up>

        [ rand catr 65 cemit ; ] <f1>
        [ "Hola a todos" cprint ; ] <f2>
		[ ccr ; ] <f3>

		'exit >esc<
		cminiflecha ;

: mark main ;