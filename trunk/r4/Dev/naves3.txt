| simple alien shot III
| PHREDA 2012

^r4/lib/gui.txt
^r4/dev/simple.spr

|------ particulas de 8 valores
:pini | cantidad list --
	here dup rot !+ ! 5 << 'here +! ;

:pclear | list --
	dup 4+ @ swap ! ;

:p!+ | 7 6 5 4 3 2 1 'acc list --
	dup >r @ !+ !+ !+ !+ !+ !+ !+ !+ r> ! ;

:pmap | list --
	@+ swap @
	( over <? )(
		dup @+ exec
		32 + ) 2drop ;

:delp | list end now -- list end- now-
	swap 32 - 2dup 8 move
	dup pick3 !
	swap 32 - ;

:pmap0 | list --
	dup @+ swap @
	( over <? )(
		dup @+ exec 0? ( drop delp )
		32 + ) 3drop ;

:pdel | adr list --
	>r r @ 32 - 8 move r> dup @ 32 - swap ! ;

|.......
:isin | x y 'end 'now -- x y end now/ x y now 0
	dup 4+ @ pick4 - abs 0.06 >? ( drop ; ) drop
	dup 8 + @ pick3 - abs 0.06 >? ( drop ; ) drop
	nip 0 ; | 'now 0

:pin | x y 'list -- x y 'how/x y 0
	@+ swap @
	( over <? )(
		isin 0? ( drop ; )
		32 + )
	2drop 0 ;

|----------------------------------------
#puntos 0
#vidas 3

#xn 0.0 #yn -0.9
#vxn 0.0 #vyn 0.0

#fxs 0 0
#aliens 0 0
#disparos 0 0

:resetgame
	3 'vidas !
	0 'puntos !
	'aliens pclear
	'disparos pclear
	'fxs pclear
	;

|-----alien 1
:alienp
 	>r
	r@+ dup abs 1.4 >? ( r 4+ dup @ neg swap ! ) drop
	r@+ |dup abs 1.0 >? ( r 4+ dup @ neg swap ! ) drop

	over sin 2 >> +
|	-0.8 <? ( rdrop 2drop 0 ; ) | llego abajo
	-0.8 <? ( r 4+ dup @ neg swap ! )

	1.5 >? ( r 4+ dup @ neg swap ! )

  	fpos
	r@+ r 12 - +!
	r@+ r 12 - +!
	rdrop
	0.1 dup fdim
	'alien nsprite
	;

:nuevoalien
	0 0 200 -0.001 0.01 1.0 1.3 'alienp 'aliens p!+ ;

|---------
:fragmento
 	>r
	r@+ r@+ fpos
	r@+ r 12 - +!
	r @ 0.0001 - r ! | gravedad
	r@+ r 12 - +!
	r @ 0? ( rdrop ; ) 1- dup r> ! | tiempo de vida
	2 << alpha
	rand $1fff and 0.01 + dup fdim
	'frag rand rnsprite
	255 alpha
	;

:r.01 rand 0.01 mod ;

:frags | x y n -- x y n
	pick2 pick2 >r >r 0 0 64 r.01 r.01 r> r> swap 'fragmento 'fxs p!+ ;

|---------
| disparo

:crash | x y howalien -- 0
	'aliens pdel
	5 ( 1? )( 1- frags ) 3drop
	1 'puntos +!
	0 ;

:fireshot | adr -- adr/adr 0 delete
|	'aliens collisionbox 1? ( 2drop crash ; )( drop ) | 'fire 'aliens collisionbox
 	>r
	r@+ |dup abs 1.0 >? ( r 4+ dup @ neg swap ! ) drop
	r@+ dup abs 1.0 >? ( rdrop 3drop 0 ; ) drop

	'aliens pin 1? ( rdrop crash ; )( drop )
	fpos
	r@+ r 12 - +!
	r@+ r 12 - +!
	rdrop
	0.08 dup fdim
	'disparo nsprite
	;

:disparo
	0 0 0 0.02 0 yn xn 'fireshot 'disparos p!+ ;

|---------
:dibujaymuevenave
	scr home blanco
	dup "%d " print
	puntos "%d" print

	0.05 dup fdim
	xn yn fpos
	'nave nsprite

	xn vxn +
	1.0 >? ( 1.0 nip )
	-1.0 <? ( -1.0 nip )
	'xn ! ;

:teclas
	rand 7 >> $ff and 7 <? ( nuevoalien ) drop

	[ -0.01 'vxn ! ; ] <le>
	[ 0.01 'vxn ! ; ] <ri>
	[ 0 'vxn ! ; ] dup >le< >ri<

	'disparo <spc>
	'exit >esc< ;

:main
	resetgame
	show clrscr
		'disparos pmap0
		'aliens pmap0
		'fxs pmap0
		dibujaymuevenave
		teclas ;

:inicio
	mark
	100 'fxs pini
	100 'aliens pini
	100 'disparos pini
	;

: inicio 33 main ;

