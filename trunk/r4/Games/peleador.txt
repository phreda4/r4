| concurso enero
| Single Boss ADVA
| peleador
| PHREDA 2008
|------------------------------
^r4/lib/gui.txt
^r4/lib/sprite.txt
^inc/pelea.inc

#tiempo_pelea

#sjug1 )( 1024
#sjug2 )( 1024

|------ x y w h r alpha 'sprite

#sfondo  0.0  0.0  2.0 2.0 0.0 $ff 'dib7
#sbase	 0.0  0.7  1.0 1.0  0.0 $ff 'dib1
#sj1 	-0.2  0.65  0.08 0.08 0.0 $ff 'sjug1
#sj2 	 0.2  0.65 -0.08 0.08 0.0 $ff 'sjug2
#golpe1  0.0  0.0  0.02 0.02 0.0 0 'dib2
#golpe2  0.0  0.0  0.02 0.02 0.0 0 'dib2
#viento1 0.0  0.0  0.02 0.02 0.0 0 'dib5
#viento2 0.0  0.0  0.02 0.02 0.0 0 'dib5
#polvo1  0.0  0.0  0.02 0.02 0.0 0 'dib3
#polvo2  0.0  0.0  0.02 0.02 0.0 0 'dib3

|-------- escena
#juego
	sj1		sj2
	golpe1	golpe2
	viento1 viento2
	polvo1  polvo2
	0
#fondo
	sfondo sbase 0
#nnubes
	)( 256

|#sndgolpe
|#sndpatada

#zcam 10.0
#xcam 0.0
#ycam 0.0
#zruido 0.0
#vzruido 0.0

|----- orientacion del jugador 0?izq 1?derecha
:oj1	| -- orientacion1
	'sj1 8 + @ -? ( 0 nip ) ;
:oj1!
	0? ( -0.08 )( 0.08 ) nip 'sj1 8 + ! ;
:oj2	| --
	'sj2 8 + @ -? ( 0 nip ) ;
:oj2!
	0? ( -0.08 )( 0.08 ) nip 'sj2 8 + ! ;

:seguno1 | valor -- valorc
	'sj1 8 + @ +? ( drop ; ) drop neg ;

:seguno2 | valor -- valorc
	'sj2 8 + @ +? ( drop ; ) drop neg ;

|-------------- jugadores
|             4   8     12   16     20  24
|        v1  sv1 jla   jlb   jspr   e	e0
#movjug1 1.0 0.1 'dib9 'dib9 sjug1  0	0
#movjug2 1.0 0.1 'dib8 'dib8 sjug2  0	0

#dirxj1	#diryj1 #rotj1
#dirxj2 #diryj2 #rotj2
#pnt1 0
#pnt2 0

|---------------- estados
| tiempo dibujo / 0 sig->estado
#tquieto	0.1 'dibe	0 'tquieto
#tcamina	0.2 'dibb	0.2 'dibc	0.2 'dibd	0.2 'dibc	0 'tcamina
#tsalta		0.2 'dibF
#evsalta 	0.05 'dib11 0.01 'dib11 0 'evsalta
#tsaltader	0.2 'dib1b 0 'tsaltader
#tsaltaizq	0.2 'dib1c 0 'tsaltaizq
#tpiso		0.2 'dib10 0.2 'dibe 0 'tquieto
#tespera	0.1 'dib9	0.1 'diba	0 'tespera
#tgolpe		0.1 'dib12	0.2 'dib13
#evgolpe 	0.2 'dib12	0 'tquieto
#tpatea		0.1 'dib10	0.2 'dib1d
#evpatea	0.1 'dibf	0 'tquieto
#tabajo		0.1 'dibF	0 'tabajo
#tcae		0.1 'dib16	0 'tquieto
#ttirado	0.1	'dib18	0 'ttirado
#tcaeatraz	0.1 'dib17	0.1 'dib18	0 'ttirado
#tacostado	0.1 'diba	0 'tacostado
#tcaeade 	0.1 'dib19	0.1 'diba	0 'tacostado
#tagacha 	0.1 'dib16 0 'tagacha
#tsalto		0.1 'dibF 0.05 'dib11 0.01 'dib11 0 'tsalto
#tgiro 		0.2 'dib16 0 'tgiro

| cambio de frame en el movimiento

:nextframe | jug -- jug
	dup 12 + @ over 8 + !
	dup 20 + @
	@+ 0? ( drop @ over 20 + ! nextframe ; )
	1.0 =? ( drop @ exec nextframe ; ) | ejecuto evento
	pick2 4+ !
	@+ pick2 12 + !
	over 20 + !
	0 over ! ;

| avance de movimiento

:nextmov | jug --
	dup @ over 4+ @ + 1.0 >? ( drop nextframe 0.0 ) dup pick2 !
	swap 8 + >r r@+ r@+ r> @ nSprInter ;

:accj1!	| acc --
	'movjug1
:acc! | acc jug --
	2dup 20 + !
	nextframe
	24 + ! ;

:accj1@ | -- acc
	'movjug1
:acc@ | jug -- acc
	24 + @ ;

:raccj1@ | -- acc
	'movjug1
:racc@ | jug -- acc
	20 + @ 8 - ;

:accj2!	'movjug2 acc! ;
:accj2@ 'movjug2 acc@ ;
:raccj2@ 'movjug2 racc@ ;

|-------- mover nubes de fondo
:muevenubesn
	0.001 >r
	'nnubes ( @+ 1? )(
		dup @ r +
		1.5 >? ( -1.5 nip )
		swap !
		0.0001 r+
		) 2drop
	rdrop ;


|-------- mover efectos
:anima | adr --
	20 +
	dup @ 0? ( 2drop ; )
	16 - -? ( 0 nip ) swap !+
	16 - >r
	0.002 r +! 4 r+
	0.002 r +! 4 r+
	r> 0.0002 swap +!	| gira
	;

:animaefectos
	'golpe1 anima
	'golpe2 anima ;


|-------- dibujo de escena
:ajustacam12
	'sj1 @ 'sj2 @ 2dup + 2/ neg 'xcam ! - abs
	'sj1 4+ @ 'sj2 4+ @ 2dup + 2/ neg 0.01 + 'ycam ! - abs
	max 0.2 + zruido + 'zcam ! ;

:escenaini
	omode
	xcam ycam zcam mtrans ;

:escenadraw | adr --
	( @+ 1? )(
		mpush
		>r
		r@+ r@+ 0.0 mtransi
		r@+ r@+ 0.0 mscalei
		r@+ 1? ( mrotzi )( drop )
		r@+ 1? ( alpha r> @ 3dnsprite )( drop rdrop )
		mpop
		) 2drop
	255 alpha ;

|---------- direccion de los jugadores
#nombres
"Tu Soberbia"	"Sabiduria"
"Tu Pereza"		"Voluntad"
"Tu Envidia"	"Paz"
"Tu Avaricia"	"Valor"
"Tu Ira"		"Autocontrol"
"Tu Gula"      	"Justicia"
"Tu Lujuria"   	"Templanza"
#nombres> 'nombres


:>>nombres
	nombres> 'nombres>  =? ( 'nombres nip 'nombres> ! ; )
	>>0
	>>0
	'nombres> ! ;

#msgt
#tmsgt 0

#mensaje1
#mensaje2
#mensaje3

#otro

:perdiohum
	"Perdiste" 'mensaje1 !
	"Te consume" 'mensaje2 !
	nombres> 'mensaje3 !
	0 'otro !
	exit ;

:perdiocpu
	nombres> 'mensaje1 !
	"murio, ganas" 'mensaje2 !
	nombres> >>0 'mensaje3 !
	1 'otro !
	exit ;

:fintiempo
	"Tiempo" 'mensaje1 !
	"Fuera" 'mensaje2 !
	0 'otro !
	exit ;

:pisohumano
	0 'diryj1 !
	accj1@
	'tsalta =? ( 'tpiso accj1! )
	'tcaeatraz =? ( 'tpiso accj1! 0 'dirxj1 ! )
	'tcaeade =? ( 'tpiso accj1! 0 'dirxj1 ! )
	'tsaltader =? ( 'tpiso accj1! )
	'tsaltaizq =? ( 'tpiso accj1! )
	drop ;

:pisocpu
	0 'diryj2 !	| piso
	accj2@
	'tsalta =? ( 'tpiso accj2! )
	'tcaeatraz =? ( 'tpiso accj2! 0 'dirxj2 ! )
	'tcaeade =? ( 'tpiso accj2! 0 'dirxj2 ! )
	'tsaltader =? ( 'tpiso accj2! )
	'tsaltaizq =? ( 'tpiso accj2! )
	drop ;

:humano&cpu
	|-------- jugador 1
	'sj1 >r
	r @ dirxj1 +
	dup r!+
	abs 0.75 >? ( 0.0001 'diryj1 +!  )(
         r @ 0.65 >? ( 0.65 r ! pisohumano )
		drop ) drop
	r @ diryj1 1? ( 0.002 'diryj1 +! ) + | gravedad
	1.1 >? ( perdiohum )
	r!+
	rotj1 r> 8 + +!
	|-------- jugador 2
	'sj2 >r
	r @ dirxj2 +
	dup r!+
	abs 0.75 >? ( 0.0001 'diryj2 +! )(
         r @ 0.65 >? ( 0.65 r ! pisocpu )
		drop ) drop
	r @ diryj2 1? ( 0.002 'diryj2 +! ) + | gravedad
	1.1 >? ( perdiocpu )
	r!+
	rotj2 r> 8 + +!
	;

:setocan1?;
    'sj1 @+ 'sj2 @+ rot -
	oj1 0? ( drop neg )( drop )
	-? ( 3drop rdrop ; )
	0.06 >? ( 3drop rdrop ; )
	drop
	@ swap @ - abs
	0.06 >? ( drop rdrop ; ) drop
	;

:setocan2?;
    'sj1 @+ 'sj2 @+ rot -
	oj2 1? ( drop neg )( drop )
	-? ( 3drop rdrop ; )
	0.06 >? ( 3drop rdrop ; )
	drop
	@ swap @ - abs
	0.06 >? ( drop rdrop ; ) drop
	;

:golpe12
	setocan1?;
	accj2@
	'tcaeade =? ( drop ; )
	'tcaeatraz =? ( drop ; )
	drop
	0.008 pnt1 + seguno1 'dirxj2 +!
	-0.009 'diryj2 !
	oj1 oj2 0? ( drop not )( drop )
	1? ( 'tcaeade )( 'tcaeatraz ) nip
	accj2!

 	'golpe1 'sj1 @+ 0.03 seguno1 + | x
	rot !+ swap @
	accj1@ 'tpatea =? ( |sndpatada SPLAY 
		0.03 )( |sndgolpe SPLAY
		-0.02 ) nip + | y
	swap !+ | copia x y
	0.01 dup rot !+ !
	$ff 'golpe1 20 + !
	0.002 'pnt1 +!
	;

:golpe21
	setocan2?;
	accj1@
	'tcaeade =? ( drop ; )
	'tcaeatraz =? ( drop ; )
	drop
	0.008 pnt2 + seguno2 'dirxj1 +!
	-0.009 'diryj1 !
	oj1 oj2 0? ( drop not )( drop )
	1? ( 'tcaeade )( 'tcaeatraz ) nip
	accj1!

 	'golpe2 'sj2 @+ 0.03 seguno2 +
	rot !+ swap @
	accj2@ 'tpatea =? ( |sndpatada SPLAY
		0.03 )( |sndgolpe SPLAY
		-0.02 ) nip + | y
	swap !+ | copia x y
	0.01 dup rot !+ !
	$ff 'golpe2 20 + !
	0.002 'pnt2 +!
	;

| tabla de transiciones
:reglasgenerales
	accj1@
	'tpiso =? ( raccj1@
		'tquieto =? ( dirxj1 0? ( 'tquieto )( 'tcamina ) accj1! drop )
		drop )
	'tsalta =? ( raccj1@
		'evsalta =? ( -0.019 'diryj1 ! )
		'tquieto =? ( dirxj1 0? ( 'tquieto )( 'tcamina ) accj1! drop )
		drop )
	'tgolpe =? ( raccj1@
		'evgolpe =? ( golpe12 )
		'tquieto =? ( 'tquieto accj1! )
		drop )
	'tpatea =? ( raccj1@
		'evpatea =? ( golpe12 )
		'tquieto =? ( 'tquieto accj1! )
		drop )
	drop
	accj2@
	'tpiso =? (  raccj2@
		'tquieto =? ( dirxj2 0? ( 'tquieto )( 'tcamina ) accj2! drop )
		drop )
	'tsalta =? ( raccj2@
		'evsalta =? ( -0.019 'diryj2 ! )
		'tquieto =? ( dirxj1 0? ( 'tquieto )( 'tcamina ) accj2! drop )
		drop )
	'tgolpe =? ( raccj2@
		'evgolpe =? ( golpe21 )
		'tquieto =? ( 'tquieto accj2! )
		drop )
	'tpatea =? ( raccj2@
		'evpatea =? ( golpe21 )
		'tquieto =? ( 'tquieto accj2! )
		drop )
	drop

	animaefectos

|	setocan?; | se tienen que empujar
|	'sj1 'sj2 over @ over @ + dup rot !+ swap rot !+
|	over @ over @ + dup rot ! swap !
	;

:derj1 | valor --
	'dirxj1 ! 1 oj1! ;
:izqj1 | valor --
	neg 'dirxj1 ! 0 oj1! ;
:derj2 | valor --
	'dirxj2 ! 1 oj2! ;
:izqj2 | valor --
	neg 'dirxj2 ! 0 oj2! ;

|--- tecla derecha
:hitder
	accj1@
	'tquieto =? ( 'tcamina accj1! 0.0048 derj1 ) | quieto
	'tsalta =? ( oj1 1? ( 'tsaltader )( 'tsaltaizq ) nip accj1! 0.0048 derj1 ) | saltando
	drop ;

:upder
	0.0 derj1
	accj1@
	'tcamina =? ( 'tquieto accj1!  ) | quieto
	drop ;

|--- tecla izquierda
:hitizq
	accj1@
	'tquieto =? ( 'tcamina accj1! 0.0048 izqj1 ) | quieto
	'tsalta =? ( oj1 0? ( 'tsaltader )( 'tsaltaizq ) nip accj1! 0.0048 izqj1 ) | saltando
	drop ;

:upizq
	0.0 izqj1
	accj1@
	'tcamina =? ( 'tquieto accj1! ) | quieto
	drop ;

|--- flecha arriba
:hitjmp
	[ 'tsalta accj1! ; ]
	accj1@
	'tquieto =? ( over exec )
	'tcamina =? ( over exec )
	2drop ;

:upjmp
	;

|--- flecha abajo
:hitdo
    diryj1 0? ( 'tabajo accj1! )  drop ;
:updo
	diryj1 0? ( 'tquieto accj1! ) drop ;

|--- tecla A
:hitA
	[ 'tgolpe accj1! ; ] raccj1@
	'tquieto =? ( over exec )
	'tcamina =? ( over exec )
	'tabajo =? ( 'tpatea accj1! )
	2drop
	0 'dirxj1 ! 4.33 'vzruido ! ;

:upA
	;

|------------------TECLAS CPU
:der2
	accj2@
	'tquieto =? ( 'tcamina accj2! 0.0048 derj2 ) | quieto
	'tsalta =? ( oj2 1? ( 'tsaltader )( 'tsaltaizq ) nip accj2! 0.0048 derj2 ) | saltando
	drop ;

:izq2
	accj2@
	'tquieto =? ( 'tcamina accj2! 0.0048 izqj2 ) | quieto
	'tsalta =? ( oj2 0? ( 'tsaltader )( 'tsaltaizq ) nip accj2! 0.0048 izqj2 ) | saltando
	drop ;

:uderizq2
	0.0 'dirxj2 !
	accj2@
	'tcamina =? ( 'tquieto accj2! ) | quieto
	drop ;


:arr2
	[ 'tsalta accj2! ; ] accj2@
	'tquieto =? ( over exec )
	'tcamina =? ( over exec )
	2drop ;

:aba2
	diryj2 0? ( 'tabajo accj2! )  drop ;
	;
:uaba2
	diryj2 0? ( 'tquieto accj2! ) drop ;

:hit2
	[ 'tgolpe accj2! ; ] raccj2@
	'tquieto =? ( over exec )
	'tcamina =? ( over exec )
	'tabajo =? ( 'tpatea accj2! )
	2drop
	0 'dirxj2 ! 4.33 'vzruido ! ;

|------------- IA?
#ttink

:iacpu
	sj1 sj2 <? ( izq2 )( der2 ) drop | siempre mira bien

	ttink 1- +? ( 'ttink ! ; ) drop
    rand 12 >> $3f and 'ttink !
	sj1 sj2 - abs 0.1 <? ( hit2 drop ; ) drop

	sj1 sj2 <? ( 'izq2 )( 'der2 ) nip
	|  sale de los bordes ---
	sj2 abs 0.70 >? ( sj2 -? ( 'der2 )( 'izq2 ) >r 3drop r> dup ) drop

	rand 15 >> $7 and
	5 <? ( nip 'arr2 swap )
	5 >? ( nip 'uderizq2 swap )
	drop
	accj2@
	'tquieto =? ( over exec	)
	'tcamina =? ( uderizq2 over exec )
	2drop
	;

|-------- JUEGO principal
:pelea
	"Comienzo" 'msgt !
	3 'tmsgt !
	inigui
	[ "Adios" 'mensaje2 ! "" dup 'mensaje1 'mensaje3 ! 0 'otro ! exit ; ] >esc<
	'hitder <ri>		'upder >ri<
	'hitizq <le>   	'upizq >le<
	'hitjmp <up>       'upjmp >up<
	'hitdo <dn>        'updo >dn<
	'hitA <spc>         'upA >spc<
	show clrscr | pantalla 1/30 veces por segundo
| ..................... DIBUJO
		ajustacam12
		escenaini
		'fondo escenadraw
		'juego escenadraw
		'nnubes escenadraw
| ..................... CONTROL
		humano&cpu
		reglasgenerales
		iacpu
| ..................... MOVIMIENTO
		'movjug1 nextmov
		'movjug2 nextmov
		muevenubesn
| ..................... CARTELES
		scr 8 bfont home

		sw 2/ sh dup 4 >> swap 3 >> + fcen | centro del degrade
		0 0.02 fmat 				| vector del degrade
		$4400 $ff00 fcol			| color del degrade

		home cr2 tiempo_pelea "%d" printc

|		$ff0000 $ffff00 fcol
|		home cr2 pnt1 " %d" print
|		home cr2 pnt2 "%d " printr

		16 font 0 15 at
		tmsgt 1? ( msgt amarillo printc ) drop
| ..................... DEBUG
|		24 sfont
|		dup "%d" blanco print cr

|		accj1@ adr>nombre raccj1@ adr>nombre "j1  r:%s %s " print cr
|		accj2@ adr>nombre raccj2@ adr>nombre "j2  r:%s %s " print cr

| ..................... TIMER
		1 .segs .restart
		tiempo_pelea 1- 0 <=? ( fintiempo ) 'tiempo_pelea !
		tmsgt 1? ( 1- 'tmsgt ! dup ) drop
		;

|---------- Pantallas secundarias
:comienzo
	escenaini
	1.5 'zruido !
	inigui
	show clrscr home
		ajustacam12
		escenaini
		'fondo escenadraw
		'nnubes escenadraw
		'juego escenadraw
		muevenubesn
		'movjug1 nextmov
		'movjug2 nextmov

		8 bfont
		sw 2 >> sh 2/ fcen 0 0.005 fmat
		$0 $ff0000 fcol
|		0 2 at "Atento ! Ready" printc

		zruido 0.05 -
		-? ( exit ) 'zruido !
		;


:avisopelea
	inigui
	'exit >esc<
	show clrscr home
| ..................... DIBUJO
		ajustacam12
		escenaini
		'fondo escenadraw
		'juego escenadraw
		'nnubes escenadraw
| ..................... MOVIMIENTO
		muevenubesn
| ..................... CARTELES
		scr 8 bfont
		sw 2/ sh dup 4 >> swap 3 >> + fcen
		0 0.02 fmat
		$0 $ff00 fcol
		home cr2
		tiempo_pelea "%d" printc

		8 bfont
		sw 2/ sh 2/ fcen
		0 0.001 fmat
		$ffffff $ff0000 fcol

		0 2 at
		mensaje1 printc cr
		mensaje2 printc cr
		mensaje3 printc cr
		4 .segs exit
		;

:resetjuego
	'sj1 >r
	-0.1 r!+ 0.65 r!+ 0.08 r!+ 0.08 r!+ 0.0 r!+ $ff r!+ 'sjug1 r!+
	0.1  r!+ 0.65 r!+ -0.08 r!+ 0.08 r!+ 0.0 r!+ $ff r!+ 'sjug2 r> !
	'movjug1 >r
	1.0 r!+ 0.1 r!+ 'dib9 r!+ 'dib9 r!+ 'sjug1 r!+ 'tquieto dup r!+ r!+
	1.0 r!+ 0.1 r!+ 'dib9 r!+ 'dib9 r!+ 'sjug2 r!+ 'tquieto dup r!+ r> !
	'dirxj1	>r	| ojo con el orden de las variables en la definicion
	0 r!+ 0 r!+ 0 r!+
	0 r!+ 0 r!+ 0 r> !

	'nnubes >r
	mem 16 | cantidad de nubes
	( 1? )( 1- swap
		dup r!+ >r
    	rand 5 >> 1.5 mod r!+ |x
    	rand 5 >> 0.2 mod 0.3 + r!+ |y
		rand 4 >> 0.3 mod 0.4 + r!+
		rand 4 >> 0.1 mod 0.15 + r!+
		rand 6 >> r!+ $3f r!+ 'dib4 r!+ r>
		swap ) 2drop 0 r> !
	'tquieto accj1!
	'tquieto accj2!
	'movjug1 nextmov
	'movjug2 nextmov

	0 'pnt1 !
	0 'pnt2 !
	60 'tiempo_pelea !
	;

:pelear
	resetjuego
    comienzo
	'nombres 'nombres> !
	(
		"Comienza la pelea" 'mensaje1 !
		"Vos vs" 'mensaje2 !
		nombres> 'mensaje3 !
		resetjuego
		avisopelea pelea avisopelea
		otro 1? )( drop
		>>nombres resetjuego ) drop ;


:principal
|	"golpe.wav" SLOAD 'sndgolpe !
|	"patada.wav" SLOAD 'sndpatada !
|	0 splay
	resetjuego
	1.5 'zruido !
	ajustacam12
	inigui
	'exit >esc<
	'pelear <enter>
	show clrscr home
		escenaini
		'fondo escenadraw
		'juego escenadraw
		'nnubes escenadraw
		muevenubesn
		scr
		sw 2/ sh fcen 0 0.003 fmat
		$ff0000 $0 fcol
		4 bfont
		"Single Voss" cr printc
		16 bfont 0 8 at
		$00 $ffffff fcol
		"Eterna lucha contra uno mismo" printc cr
		"Eternal fight against oneself" printc cr
		cr cr
        "ENTER - Jugar/Play" printc cr
		"ESC -  Salir/Exit" printc cr cr
		$ff $ff fcol
		"phreda - 2008 - Concurso ADVA Single Boss" printc
|		25 sfont dup "%d" print

		;

: 33 principal ;
