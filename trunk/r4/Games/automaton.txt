| cellular automaton - rule 110 starting with a single black pixel
| for more details : http://en.wikipedia.org/wiki/Rule_110

^r4/lib/gui.txt

#rule 110 | <- the rule number can be changed here
#rulebits )( 32
#y 1

:calcbit | n --
	>r rule 1 r << and
	0? ( drop $ffffff )( drop 0 )
	'rulebits r> 4 * + !
	;

:precalc | produces rulebits data from the value of rule
	0 ( 8 <? )( dup calcbit 1+ ) drop ;

:seed 0 framev sw 2* + ! ; | single black pixel

:above | x -- adress of pixel directly above (x,y)
	y 1- sw * + 4 * framev + ;

:draw | x -- 
	0 >r
	above
	dup 4 - @ 0? ( 4 r+ ) drop
	dup     @ 0? ( 2 r+ ) drop
	dup 4 + @ 0? ( 1 r+ ) drop
	sw 4 * +                     | current pixel
	r> 4 * 'rulebits + @ swap ! ;

:procy	y 2* 1+ sw >? ( drop ; ) drop 
	1 ( sw <? )( dup draw 1+ ) drop 1 'y +! ;

:main
    precalc
    $FFFFFF paper
    usogui clrscr
    negro seed
    show
	procy
	'exit >esc< ;

: main ;
