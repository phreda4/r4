|--------------------------------------------------------------------
| click-o-tron - 2008-2013
| Rounds of 10sec
| Click as many blue disks as you can
| Get 1 point per correct click, -2 points per incorrect click
|--------------------------------------------------------------------
^r4/lib/gui.txt

#score 0 #hiscore 0 #x #y #r_ #mousedown #clock

:bounds over -
:randbtw | ( min max -- v )
       over - rand abs swap mod + ;

:smin sw sh min ;

:rand_circ smin dup
	   10 / dup dup
	   sw bounds 'x !
	   sh bounds 'y !
	   swap 20 /  | min max
	   swap
	   randbtw 'r_ ! ;

:bonus mousedown 1 =? ( 1 'score +! ) drop ;

#timer 0
:set.timer msec + 'timer ! ;

:malus mousedown 1 =? ( score 2 - 'score ! 50 set.timer ) drop ;

:elapsed msec clock - ;

:tosec 1000 / ;

:countdown 10 swap - ;

:roundstart msec 'clock ! ;

:updatehiscore score hiscore >? ( 'hiscore ! )( drop ) ;

:timeleft elapsed tosec countdown ;

:stats timeleft score hiscore
       "Clickotron%.Hiscore : %d%.Score : %d%.Time Left : %d" ;

:mousepixel xymouse getpixel ;

:debug dup "%d" print cr ;

:init rand_circ roundstart ;

:smallfont 40 font ;

:bigfont 20 font ;

:messages smallfont rojo debug bigfont amarillo stats print ;

:display messages azul x y r_ fcircle ;

:button.up? bmouse 0? ( 0 'mousedown ! ) drop ;

:over.circle? mousepixel $0000FF =? ( blanco x y r_ 1 + circle ) drop ;

:clicked mousepixel $0000FF =? ( bonus rand_circ )( malus ) drop ;

:click? bmouse 1? ( 1 'mousedown +! clicked ) drop ;

:interact button.up? over.circle? click? ;

:scores timeleft 0 <=? ( updatehiscore 0 'score ! roundstart ) drop ;

:bg.paper | set background color
	  msec timer >? ( 0 )( $ffffff ) paper drop ;

:go
   init
   show bg.paper
	clrscr
	display interact scores
	'exit >esc< 
	cmano ;

: 33 go ;
