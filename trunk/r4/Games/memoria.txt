| juego de memoria
| PHREDA
|-------------------------
^lib/reda4.txt
^lib/gui.txt
^lib/sprites.txt

|^inc/jonael.inc
^inc/kamart.inc

#cntcartas 8
#cntacierta
#cartas )( 1024
#ele1 #ele2
#testime

#dibujos
'dibu1 'dibu2 'dibu3 'dibu4 'dibu5 'dibu6 'dibu7 'dibu8 'dibu9 'dibua 'dibub 'dibuc 'dibud 'dibue 'dibuf

|------ cartas
:llenar | --
 'cartas cntcartas ( 1? )( 1- >r
  r dup rot c!+ c!+
  r> ) 2drop
  cntcartas 'cntacierta ! ;

:cambia | fd  --
 'cartas + dup c@ rot 'cartas + dup c@
 rot rot c! swap c! ;
 
:mexclar | --
 cntcartas 2* dup 2* ( 1? )( 1- >r
  rand over mod abs rand pick2 mod abs cambia
  r> ) 2drop 
  ;

:quita | val --
 'cartas cntcartas 2* ( 1? )( 1- >r
  dup c@ pick2 =? ( drop -1 over c! )( drop ) 1+
  r> ) 2drop 
  -1 'cntacierta +! ;
  
:conte | nro -- val  
 'cartas + c@ ;

|------
:testea
	ele1 conte ele2 conte =? ( quita ) drop
:iniele
  -1 dup 'ele1 ! 'ele2 ! 0 'testime ! ;

|------ interaccion
:dcarta | dir --
  'cartas + c@ 2* 2* 'dibujos + @ sprite ;

#cntc

:cclick | --
  ele2 1+ +? ( 2drop ; ) drop
  dup 'cartas + c@ -? ( 2drop ; ) drop
  ele1 -? ( drop 'ele1 ! ; ) drop
  ele1 =? ( drop ; ) 'ele2 !
  20 'testime ! ;

:cdrawcclick | --
	cntc 1+ dup 'cntc !
	[ dup cclick ; ] onClick
	dup 'cartas + c@ -? ( 2drop ; )
	-2 dup +dim gris gc.fbox
	drop
	ele1 =? ( dcarta ; )
	ele2 =? ( dcarta ; ) drop ;

|--- pantallas
:gano?
	cntacierta 1? ( drop ; ) drop
	inigui
	'exit >esc<
	show clrscr
		6 font
		rojo
		0 1 at "Ganaste" printc
		verde oscuro
		2 4 at 'exit "Otro" btnt
		;

:table | row col 'acc --
	>r
	1.0 pick2 / | row col sw
	1.0 pick2 / | row col sw sh
	0 ( pick4 <? )(
		 0 ( pick4 <? )( | row col sw sh x y
			gc.push
			2over		| row col sw sh x y sw sh
			pick3 pick2 * pick2 2/ + 0.5 -
			pick3 pick2 * pick2 2/ + 0.5 -
			gc.conv | w h xc yc --
			r exec
			gc.pop
			1+ ) drop
		1+ ) drop
	4drop rdrop ;

:play
	llenar mexclar iniele
	inigui
	'exit >esc<
	show clrscr
		16 font
		|   blanco 0 0 at dup ele1 ele2 "%d %d %d" print
		testime 1? ( 1- 0? ( testea ) 'testime ! )( drop )
		cntacierta 0? ( exit ) drop
		0.8 dup fdim 0 dup fpos
		0 'cntc !
		cntcartas 2/ dup 'cdrawcclick table
		cmano
		;

:presenta
    rerand
	inigui
	'exit >esc<
	[ play gano? ; ] >esp<
	show clrscr
|		4 font
		fonti
		 home cr
		rojo "Memoria" printc cr cr
		sp verde [ play gano? ; ] "JUGAR" btnt cr
		sp rojo 'exit " SALIR " btnt cr
		cmano ;

: 0 paper presenta ;
