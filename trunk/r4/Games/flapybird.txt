| FlapyBird
| Bava/Zubillaga/Reda
| 2014

^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/rfont.txt
^inc/rft/gooddog.rft
^r4/games/flapi.spr

#yb
#vy
#xt1 1.1
#yt1
#xt2 -1.1
#yt2
#punto
#anib 'Bird1

:reset
	0 'yb !
	0 'vy !
	1.1 'xt1 !
	-1.1 'xt2 !
	0 'punto !
	;

:pajaro
	0.2 0.2 fdim
	0 yb fpos
 	anib vy neg 2* rnsprite
	;

:pasto
	1.0 dup fdim
	0.0 -0.05 fpos
	'pasto1 nsprite
	;

:dibujotub
	0.2 2.0 fdim
	xt1 yt1 0.2 - fpos
	'cosodeabajo nsprite
	xt1 yt1 0.2 + fpos
	'cosodearriba nsprite
	0.2 2.0 fdim
	xt2 yt2 0.2 - fpos
	'cosodeabajo nsprite
	xt2 yt2 0.2 + fpos
	'cosodearriba nsprite
	;

:calcu-pajaro
	vy 'yb +!
	-0.0009 'vy +!
	yb
	-1.0 <? ( -1.0 'yb ! vy neg 'vy ! exit 	)
	drop
	;

:bird
	pajaro
	calcu-pajaro
	[ 0.018 'vy ! 'bird2 'anib ! ; ] <spc>
	[ 'bird1 'anib ! ; ] >spc<
	;

:perdio
	show clrscr
		pasto
		dibujotub
		pajaro
		-0.02 'yb +!
        -0.01 'vy +!
		yb -1.0 <? ( exit ) drop
	;

:calcu-tub
 	-0.01 'xt1 +!
	-0.01 'xt2 +!
 	xt1
	0.1 <? (
		yt1 yb - abs 0.2 >? ( perdio exit ) drop
		)
	-0.2 <? (
		xt1 'xt2 !
		yt1 'yt2 !
		1.1 'xt1 !
 		rand 0.8 mod 'yt1 !
		1 'punto +!
		)
	drop
	;

:turberia
	dibujotub
	calcu-tub
	;


:game
	reset
	show clrscr
		amarillo punto "%d" printc
		bird
		pasto
		turberia
		'exit >esc<
		;

:inicio
	show clrscr
		gooddog 0.2 %s rfont!
		Verde cr
		"Flapy Coco" printc
		cr cr cr
		Rojo
		gooddog 0.1 %s rfont!
		sp 'game dup >spc< "Comenzar" btnt
		cr cr
		sp 'exit dup >esc< "Salir" btnt
		cminiflecha
		;

: inicio ;