^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/anima.txt
^r4/lib/trace.txt
^r4/lib/3d-escena.txt
^r4/edu/media12/pruebarienda/graficos.spr

|---------------------------------------------
:r0001 rand 0.001 mod ;
:r001 rand 0.01 mod ;
:r01 rand 0.1 mod ;
:r1 rand 1.0 mod ;
:r10 rand 10.0 mod ;
:r100 rand 100.0 mod ;

|---------------------------------------------
| posicion y camara
#xcam  #ycam 2.0 #zcam 20.0
#vdir 0
#dircaballo 0
#vvel 0
#velcaballo

#cosas 0 0
#tiempo 0

|----------- arboles
:arbolupd
	esc.z+ ;

:arboldraw
	mpush
	esc.pos
	dircaballo mrotyi | correccion por vista
	dup 44 + @
	over 40 + @
	1.0 mscalei
	dup 48 + @  3dnsprite
	mpop ;

:arbolxy | x y --
	'cosas esc. >r
	'arbolupd r!+
	r!+					| x
	rand 2.0 mod 10.0 +	| alto
	-2.0 over 2/ - r!+	| y
	swap r!+ 			| z
	0 r!+ 0 r!+ 0 r!+ 	| rx ry rz
	2.0 r!+ 1.0 r!+
	'arboldraw r!+
	r!+						| alto
	rand 2.0 mod 6.0 + r!+	| ancho
	'arboleda1 r!+          | dibujo
	rdrop ;

|------------ TACHO
#distx #disty
#dist
:tachoupd
	mpush
	esc.pos
	oxyztransform | x y z
	nip distfast
	1.0 <? ( 'tachocaido pick2 40 + ! ) | disminuir velocidad
	drop
	mpop
	esc.z+ ;

:tachodraw
	mpush
	esc.pos
	dircaballo mrotyi | correccion por vista
	2.0 2.0 2.0 mscalei
	dup 40 + @ 3dnsprite
	mpop ;

:tachoxy | x y --
	'cosas esc. >r
	'tachoupd r!+ |'cosas esc!+ >r
	r!+ 0 r!+ r!+ | x y z
	0 r!+ 0 r!+ 0 r!+ | rx ry rz
	2.0 r!+ 1.0 r!+ 'tachodraw r!+
	'tacho r!+
	rdrop ;

|---------------------------------------------
:fijoupd
	esc.z+ ;

:fijodraw
	mpush esc.pos dup 40 + @ 3dnsprite mpop ;

:fijo+ | 'spr x y z --
	'cosas esc. >r
	'fijoupd r!+ |'cosas esc!+ >r
	rot r!+ swap r!+ r!+ | x y z
	0 r!+ 0 r!+ 0 r!+ | rx ry rz
	0 r!+ 0 r!+ 'fijodraw r!+
	r!+ rdrop ;


:dibujafondo
	sw 2* sh 16 + dim
	'fondo1 nsprite

	;

#seqcaballo 0 0 'caballo1 13 'caballo2 21 0

:caballo
	scr
	velcaballo 5 >> msec * sin 5 >> 0.1 - 0 swap fpos
	'seqcaballo seqAnima nsprite
	[ 0.01 'vvel ! ; ] <up>
	[ -0.03 'vvel ! ; ] <dn>
	[ 0 'vvel ! ; ] dup >up< >dn<

	[ 0.0015 'vdir ! ; ] <le>
	[ -0.0015 'vdir ! ; ] <ri>
	[ 0 'vdir ! ; ] dup >le< >ri<

|	ay 'ycam +!	| arriba/abajo

	vdir 'dircaballo +!
	velcaballo 5 >> msec * sin 3 >> 2.0 + 'ycam ! | galope

	velcaballo
	vvel
	+
	0.1 >? ( 0.1 nip )
	-0.01 <? ( -0.01 nip )
	'velcaballo !

	dircaballo 0.5 + velcaballo polar 'zcam +! neg 'xcam +!
	;

|-----------------------------------
:freelook
	xymouse
	sh 2/ - 7 << swap
	sw 2/ - 7 << swap
	neg mrotx mroty ;

|------
:armaescena
	| pone tachos
	8 ( 1? )( 1-
		dup 10.0 * 0 tachoxy
		dup 10.0 * 30.0 tachoxy
		) drop
	| borde con arboles
	-12 ( 12 <? )(
		dup 10.0 * -100.0 arbolxy
		dup 10.0 * 150.0 arbolxy
		1+ ) drop
	-10 ( 15 <? )(
		-140.0 over 10.0 * arbolxy
		140.0 over 10.0 * arbolxy
		1+ ) drop
	;

:main
	armaescena
	fonti2
	show clrscr
		omode
		|--------- vista de caballo
		xcam ycam zcam mtrans
		dircaballo mroty
		| --------- dibujo de escena
		dibujafondo
		blanco velcaballo " %f " print
		tiempo 60 /mod swap "%d:%d" printr cr
		zcam xcam "%f %f " print cr
		disty distx dist "%f x%f y%f" print

		esc.zclear
		'cosas esc.draw
		esc.zdraw
		caballo

		'exit >esc<
		1 .segs .restart
		1 'tiempo +!
|		cminiflecha
		;

:inimem
	mark $fffff 'cosas esc.create ;

: inimem main ;