|A JUNTAR MONEDAS - Ramos constanza
|ADAPTADO A LA NUEVA DISTRIBUCIÓN
|-------------------------------------------

^r4/lib/gui.txt
|^r4/lib/lists.txt
^r4/lib/sprite.txt
^r4/lib/btn.txt
^r4/lib/sprites.txt
^r4/lib/trace.txt

^inc/chancho.inc

| ---------------- variables ----------------

#x 0.5
#y 0.5
#vx 0.008

#vi
#vd

#chancho 'dib1
#moneda 'dib2

#puntos 0
#tiempo 850 |850 ver
#meta 100
#nivel 1

#monedas )( 1024
#monedas> 'monedas

| ------------------------pantalla fin -------------------------

:fin
usogui
show
	cls
	scr 8 font verde cr
    	2 2 gotoxy
	"FIN DE JUEGO" print cr
	scr 10 font verde cr
    	3 5 gotoxy scr
	"AHORRASTE:" print cr
	4 7 gotoxy scr
	puntos "%d MONEDAS" print
	3 .segs exit
	'exit >esc<
;

| ------------- Jugar  -----------------------

:dibchancho2

x 0.9 >? ( 0.9 'x ! )
-0.9 <? ( -0.9 'x ! )
-0.93 fpos
vi -? ( -0.99 )( 0.99 ) nip 0.99 fdim
chancho nsprite
vd vi + 'x +!
;

:borramoneda | dir -- dir++
	4 - dup | dirc dirc --> tengo la direccion de comienzo dos veces
	monedas> 8 - 2    | dirc dirc fun cant
	move  | dirc
	-8 'monedas> +!
	;


:acumulamoneda
borramoneda
10 'puntos +!
;


:dibmoneda | 'dir -- 'dir++

>r
r@+  r@+ fpos
0.055 0.055 fdim
'dib2 nsprite
r>
4 - dup @
0.02 -
-0.9 <?
	( drop borramoneda ; )
-0.75 <?
    ( over 4 - @  | diry y  x |obtengo x anterior de la moneda
          x - abs     | diry y  abs | le resto la x del chancho y saco abs
          0.14 <? | diry y  abs
               ( 2drop acumulamoneda ; )
          drop
    )
swap !+
;

:+monedas
	monedas> !+ !+ 'monedas> !
	;


:dibtodas
'monedas ( monedas> <? )(
	dibmoneda
) drop
;


#paso 0
#vmon 1.0
#tmon 500

:jugar
    show

		'exit >esc<
		[ 0.015 'vd ! ; ] <ri>
    	[ -0.015 'vi ! ; ] <le>
    	[ 0.0 'vd ! ; ] >ri<
    	[ 0.0 'vi ! ; ] >le<

	cls  blanco DUP " %d" print
	-1 'tiempo +!
	tiempo 0? ( exit ) drop
	meta puntos <=? ( 1 'paso ! exit ) drop
	scr
	0 -0.2 fpos
	'dib3 nsprite | pared
	-0.001 -0.52 fpos
	'dib4 nsprite | pasto
	0.70 0.68 fpos  | tacho
	0.25 0.25 fdim
	'dib5 nsprite
	dibchancho2
   	dibtodas

	scr 25 font rojo
	37 4 gotoxy
	puntos "%d" print
    	36 2 gotoxy
	"AHORRO" print
	scr 18 font rojo
	5 1 gotoxy
	meta "META: %d MONEDAS" print
	5 3 gotoxy
	nivel "NIVEL %d " print

|    	5 4 at
|	tiempo "T %d " print
|	monedas> 'monedas - 3 >>
 |	scr fonti verde
|    	x "%f" print

    	tmon .mseg .restart
    	vmon rand 0.99 mod +monedas
    ;

| ---------------------- niveles ----------------------

:pantallanivel
show
	cls
	scr 7 font verde cr
	2 1 gotoxy
	"FELICITACIONES!" print cr
	scr 5 font verde cr
	2 3 gotoxy scr
	nivel "NIVEL  %d" print cr
	'exit >esc<
	3 .segs exit
	;

:pasanivel
	0 'paso !
	0 'puntos !
	100 'meta +!
	500 'tiempo +!
	200 'tmon +!
	0.5 'vmon +!
	1 'nivel +!
|	nivel 3 >? ( fin  exit )
	pantallanivel
;

:pre
	( jugar paso 1? )( drop pasanivel ) drop ;


| --------------------------Pantalla inicio ---------------------

:inviertex | invierte velocidad en x
   vx neg 'vx !
;


:dibchancho1

x  1  fpos
vx -? ( -0.99 )( 0.99 ) nip 0.99 fdim
chancho nsprite
vx 'x +! | a x le sumo la velocidad en x
x 0.5 >? | si x = 0.5
    ( inviertex )
-0.5 <?
    ( inviertex )
drop

;


:bucle
	usogui
	azul 33 paper
	show clrscr
		9 font
		amarillo home
		"A JUNTAR MODEDAS!!" cr printc cr cr
		8 font
		3 5 gotoxy
		verde 'pre dup >enter< "Jugar" sp .btnt
        sp rojo 'exit dup >esc< "Salir" .btnt
		dibchancho1
		cmano
		'jugar >spc<
		'exit >esc<
		;


: 4 bucle  ;