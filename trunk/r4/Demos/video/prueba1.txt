^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/demos/video/sprites.spr

#points 0

| coord for shoot
#xs #ys 2.0
| coord and vel for player
#xp 0
#vxp 0
| coord and vel for alien
#xa 0 #ya 0
#vxa 0 #vya 0

:shoot
	ys 2.0 =? ( drop ; ) drop
	200 200 dim
	xs ys fpos
	'bull nsprite

	0.04 'ys +!
	
	ys 1.2 >? ( 2.0 'ys ! )  drop
	;

:player
	200 200 dim
	xp -0.6 fpos
	'ship nsprite
	
	vxp 'xp +!

	[ 0.01 'vxp ! ; ] <ri> | press ->
	[ -0.01 'vxp ! ; ] <le> 
	[ 0 'vxp ! ; ] dup >ri< >le< | release

	[ xp 'xs ! -0.6 'ys ! ; ] <spc>
	;

:explosion 
	show clrscr
	rand 400 mod dup dim
	xa ya fpos
	'explo nsprite
	500 .mseg | wait msec for next words
	exit
	0 'xa ! 0 'ya !
	;

:crash
	2.0 'ys ! | hide bullet
	1 'points +! 
	explosion
	;

:alien
	200 200 dim
	xa ya fpos
	'salien nsprite
	
	vxa 'xa +!
	vya 'ya +!

	rand 0.05 mod 'vxa !
	rand 0.05 mod 'vya !

	| colision !!
	xa xs - abs 0.05 <? (
		ya ys - abs 0.05 <? (
			crash
			) drop
		) drop
	;


:play
	show clrscr
		blanco fonti2
		points "points: %d" print
		player
		alien
		shoot		
		'exit >esc<
	;

:main
	fonti2
	show clrscr
		blanco | white in spanish
		"SpaceShoot" printc cr cr cr
		verde | green..
		sp sp 'play "Play" btnt cr cr
		rojo
		sp sp 'exit "Exit" btnt 
		'exit >esc<
		cflecha
	;

: main ;
| yes is very basic.. but only 99 lines !!! bye

