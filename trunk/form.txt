^lib/fontt.txt
^lib/gui.txt
^lib/dbtabla.txt

#focovar
#focopar1

:lstget | lista nro -- adr
	( 1? )( 1- swap >>f swap ) drop ;

:lstcnt | list -- cnt
	dup c@ 0? ( nip ; ) drop
	1 swap ( c@+ 1? )(
		$7c =? ( drop swap 1+ swap )( drop )
		) 2drop ;

|------------- lista de elecciones
:keys-choice | 'var xx --  ; boton con foco
	over 'focovar !
	pick2 lstcnt 'focopar1 !
	0 <enter>
	0 <visible>
	0 <ins> 0 <back> 0 <del>
	[ focovar @ 1+ focopar1 >=? ( 0 nip ) focovar ! ; ] <esp>
	'ktab <tab>
	'nextfoco <dn> 'prevfoco <up>
	;

#col1
:draw-choice
	$cccc00 'col1 ! ;

::choice | lista 'var --
	$cc00 'col1 !
	'draw-choice 'keys-choice w/foco
|	@ lstget "%a" print ;
    dup @ rot 0 swap ( dup c@ 1? )( drop
		dup " %a " mprint print2gc
		>r >r
		[ dup pick3 ! ; ] onClick
		over =? ( col1 $222222 vbtn ) 1+
		r> r>
		blanco printx >>f ) 4drop
	drop
	;

|--------- radio btn
:draw-choice
	$cccc00 'col1 ! ;

::rbtn | lista 'var --
	$cc00 'col1 !
	'draw-choice 'keys-choice w/foco
	2dup @ lstget
	" %a " mprint print2gc
	col1 $222222 vbtn | lista 'var sop
	[ over @ 1+ pick3 lstcnt >=? ( 0 nip ) pick2 ! ; ] onClick
	blanco printx
	2drop ;



|---------------- Grilla
#colums
#ancho
#factor
#xi
#tabcols )( 256
#tabcols>
#xf

#yi
#tabrows )( 256
#tabrows>
#yf

#col>

#xxi

::.table | --
	ccx dup dup 'tx1 ! 'tx2 ! 'tabcols !+ 'tabcols> !
	ccy dup dup 'ty1 ! 'ty2 ! 'tabrows !+ 'tabrows> !
::.row | --
	2 'ccy +!
	0 'col> ! ;

::.col | "titulo" cols --
	dup ccw * ccx +
	tabcols> !+ 'tabcols> !
	boxprintc
	col> 1+ dup 'col> !
	2 << 'tabcols + @ 'ccx !
	tx2 ccx max 'tx2 !
	ty2 ccy cch + max 'ty2 !
	;

::.cell | "texto" --
	4 'ccx +!
	print
	col> 1+ dup 'col> !
	2 << 'tabcols + @ 'ccx !
	tx2 ccx max 'tx2 !
	ty2 ccy cch + max 'ty2 !
	;

::.grid | --
	'tabcols
	( tabcols> <? )( @+ dup ty1 op ty2 line )
	drop
	ty1 ( ty2 <=? )(
		tx1 over op tx2 over line
		cch 2 + + ) drop
	;

::rowfill | color --
	ink@ swap ink
	tx1 ccy 2dup 1- op cch + pline
	tx2 ccy 2dup cch + pline 1- pline
	poli ink ;

|-------- nueva
::grid | grids cols ancho% --
	tx2 tx1 - 16 *>> 'ancho !
	tx2 tx1 - 2/ ancho 2/ - tx1 + 'xi !

	over >r
	0 over ( 1? )( 1- r@+ rot + swap )
	drop rdrop
	ancho 4 << swap / 'factor !
	dup 'colums !

	swap >r
	'tabcols xi rot		| grid xi cols
	( 1? )( 1- 			| adr xi cnt
		r@+ swap >r		| adr xi act
		factor * 4 >> +
		dup rot !+ 		| tab xi -- xi tab
		swap r> )
	drop nip 'xf !
	rdrop
	ccy 'yi !
	'tabrows 'tabrows> !
::rowg | --
	0 'col> !
	xi 'xxi !
	2 'ccy +!
	ccy cch + dup 'yf ! tabrows> !+ 'tabrows> !
	;

|-------------------
| #lista Act Pag c1 c2c3 .. 0
| cx $TLLFF
::grid2 | cols ancho --
	tx2 tx1 - 16 *>> 'ancho !
	tx2 tx1 - 2/ ancho 2/ - tx1 + 'xi !

	0 'colums !

	0 over 8 + ( @+ 1? )( | 0 dd aa
		8 >> $ff and rot + swap | 1 dd
		1 'colums +!
		) 2drop

	ancho 4 << swap / 'factor !

	'tabcols >r
	xi swap 8 + | xi cols
	( @+ 1? )( | i dd aa
		8 >> $ff and factor * 4 >> rot +
		dup r!+
		swap ) 2drop rdrop
	'xf !
	ccy 'yi !
	'tabrows 'tabrows> !
	rowg ;

|-------------------
::gfill | color --
	ink@ swap ink
	xi ccy 2dup 1- op cch + pline
	xf ccy 2dup cch + pline 1- pline
	poli ink ;

:xxf | -- xf
	col> dup 1+ 'col> !
	2 << 'tabcols + @ ;

:centerprint | "" x2 x1 --
	'ccx ! 'xxi !
	mprint sizeprint 
	xxi ccx - swap - 2/ 'ccx +!
	printx ;

:rigthprint | "" x2 x1 --
	drop 'xxi !
	mprint sizeprint
	xxi swap - 'ccx !
	printx ;

:leftprint | "" x2 x1 --
	'ccx ! 'xxi !
	print ;

::gcell | "texto" --
	xxf xxi 4+ leftprint ;

::gcellc | "texto" --
	xxf xxi centerprint ;

::gcellr | "texto" --
	xxf xxi 4 - rigthprint ;

::$grid | --
	'xi colums ( 1? )( 1- swap
		@+ dup yi op yf line
		swap ) 2drop
	'yi ( tabrows> <? )(
		@+ xi over op xf swap line
		) drop ;


| 'filas -> 4 9 8 0
|			[ dup pick3 ! ; ] onLineMove | 'vec --
|			pick2 @ =? ( $ff gfill blanco )( $ffffff gfill negro )

::strgrilla | "" 'filas ancho--
	tx2 tx1 - 16 *>> 'ancho !
	tx2 tx1 - 2/ ancho 2/ - tx1 + 'xi !

	0 over 1 >r  | "" 'filas 0 'filas
	( @+ 1? )( rot + swap r> 1+ >r ) 2drop | cnt filas
	ancho 4 << swap / 'factor !
	r> 'colums !

	xi over 'tabcols >r | "" 'filas xi 'filas
	( @+ 1? )(
		factor * 4 >> rot +
		dup r!+
		swap ) drop rdrop
	swap 'xf !
	ccy 'yi !
	'tabrows 'tabrows> !
	rowg
	$cccccc gfill negro
	( dup c@ 1? )( drop
			dup gcellc
			( c@+ 1? )( drop ) drop
			) 3drop cr
	dup c@ 0? ( 2drop $grid ; ) drop
	rowg
	dup ( c@+ 1? )(
		$7c =? ( pick2 "%a" mprint gcellc  rot drop over swap cr rowg )
		$5f  =? ( pick2 "%a" mprint gcellc  rot drop over swap )
		drop ) 2drop
	"%a" mprint gcellc
	$grid 	;



|---------------------------------
::dlgprinter
	oscurecetodo
	>xfb
	inigui
	show xfb>scr
		0 0 fpos 0.6 0.5 fdim
		$ff $11 vbtn
|		0.15 cubo -0.2 -0.3 fpos
		0.5 0.4 fdim 0 0 fpos
|		8 hfont
	font-vard-12-quad-out
		home cr cr
		"Imprimiendo..." printc cr cr
		"Espere un momento..." printc
		1 .segs exit ;

|---------------------------------
:t0	4 >> ( 1? )( cr 1- ) drop ; | cr's
:t1 4 >> dbprint sp sp ;
:t2 4 >> dup dbtitle cr dbedit cr cr2 ;
:t3
:t4
:t5 drop ;
:t6 4 >> dup dbtitle cr dup dbedit sp ink@ >r 
	verde [ date "%d/%d/%d" mprint over dbset ; ] " Hoy " btnt drop cr cr2 r> ink ;
:t7
:t8
:t9
:ta
:tb
:tc
:td
:te
:tf drop ;

|		( 3 <? )( dup dbprint sp sp 1+ ) cr cr
|		( cntflds <? )( dup dbtitle cr
|				dup dbedit cr cr2
|				1+ )
|		drop
#accf t0 t1 t2 t3 t4 t5 t6 t7 t8 t9 ta tb tc td te tf

::form | 'form --
	( @+ 1? )(
    	dup $f and 2 << 'accf + @ exec
		) 2drop ;

|---------------------- Mas botones
::btnex | 'acc "leyenda" "titulo" --
	gc.push ink@ dup >r $222222
	[ swap ; ] [ 0.95 fzoom ; ] onInOut
	vbtn
	gc2win home
	font-vard-12-bold-out
	cr2 printc cr cr2
	font-vard-8

	blanco linefill
	1 col negro print

	onClick
	r> ink gc.pop ;

::btnee | 'acc DB --
	gc.push ink@ dup >r $222222
	[ swap ; ] [ 0.95 fzoom ; ] onInOut
	vbtn
	gc2win home
	font-vard-12-quad-out
	cr2 linefill
	dup "%a" printc cr cr2
	font-vard-8-bold-out
	>>f
	( dup c@ 1? )( drop
|		blanco linefill negro
		1 col dup "* %a" print >>s cr
		) 2drop
	gc.pop
	onClick
	r> ink ;

|--------- reloj
:aguja | ang largo --
	polar
	xc pick2 3 >> - yc pick2 3 >> + op
	xc rot + yc rot - line ;

::reloj | --
	blanco
	w h min 2/
	0 ( 1.0 <? )(
		dup pick2 polar
		swap xc + swap yc + op
		dup pick2 dup 4 >> - polar
		swap xc + swap yc + line
		0.0834 + ) drop
	4 - >r
	time | s m h --
	rojo 1.0 12 */ r 2/ aguja
	verde 1.0 60 */ r dup 2 >> - aguja
	blanco 1.0 60 */ r> aguja ;
