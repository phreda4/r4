|	MAIN NUEVO
|	PHREDA 2013
|----------------------------------
^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/btn.txt
^r4/lib/sort.txt

^r4/system/mem-ed.txt

^r4/lib/rfont.txt
^r4/lib/ricons.txt

|^r4/lib/dlgfilea.txt
|^r4/lib/vkeyboard.txt

|^r4/lib/trace.txt

^inc/ric/fontawesomewebfont.ric
^inc/rft/robotoregular.rft
^inc/rft/robotobold.rft

#pagina
#actual
#path )( 1024

#nfiles
#files )( 1024
#files> 'files
#infos )( 1024
#infos> 'infos

#sortf )( 2048

#filen )( 8192
#filen> 'filen

#fileactual )( 32

#iconcolor $7f7f00 $3f00 $7f7f $ff
#iconinfo 'i.folder_close_alt 'i.cogs 'i.picture 'i.file

|----------------------------------------
:FNAME | adr -- adrname
|WIN|	44 +
|AND|	19 +
	;

:FDIR? | adr -- 1/0
|WIN|	@ $10
|AND|	18 + c@ 4
	=? ( 1 )( 0 ) nip ;

:FINFO | adr -- adr info
	dup FDIR? 1? ( 0 nip ; ) drop
	dup FNAME
	".txt" =pos 1? ( 2drop 1 ; ) drop
	".spr" =pos 1? ( 2drop 2 ; ) drop
	3 nip ;

:rebuild
	0 'nfiles !
	'filen 'filen> !
	'files 'files> !
	'infos 'infos> !
	'path ffirst drop fnext drop | quita . y ..
	( fnext 1? )(
		filen> files> !+ 'files> !
		FINFO infos> !+ 'infos> !
		FNAME filen> strcpyl 'filen> !
		) drop
	files> 'files - 2 >> 'nfiles !
	'sortf >r
	0 ( nfiles <? )(
		dup 2 << 'infos + @ 24 <<
|		over 2 << 'files +
|		c@+ swap c@+ swap c@
|		rot 16 << or swap 8 << or or
		r!+
		dup r!+
		1+ ) drop rdrop
	nfiles 1+ 'sortf shellsort
	;

:getname | nro -- ""
	3 << 'sortf + 4+ @ 2 << 'files + @ ;

:getinfo | nro -- info
	3 << 'sortf + 4+ @ 2 << 'infos + @ ;

#namesolo )( 64
:getnamesolo | nro -- ""
	dup getinfo 0? ( drop getname ; ) drop
	getname 'namesolo strcpyl
	0 swap 5 - c! | quita ".txt"
	'namesolo ;

:addpath | nro -- nro
	 "/" 'path strcat
    dup getname 'path  strcat
	rebuild ;

:clearact
	-1 'actual ! "" 'fileactual strcpy ;

:rempath
	'path ( c@+ 1? )( drop ) drop 1-
	( 'path >? )(
		dup c@ $2f =? ( drop 0 swap ! rebuild clearact ; )
		drop 1- ) drop
	rebuild clearact ;


|----------------------------------------
:clearall
	0 'pagina !
	clearact
	"r4" 'path strcpy ;

:loadm
	'pagina "mem/main.mem" load
	'pagina =? ( clearall ) drop
	rebuild
	actual -? ( drop ; )
	getnamesolo 'fileactual strcpy
	;

:savem
	'pagina 8 1024 +
	"mem/main.mem" save ;

:config
	show clrscr
		robotoregular 64 rfont!
		verde dup " :R%d " print
		blanco "Info" printc cr
		robotoregular 40 rfont!

		cyan
|WIN|	"Windows"
|AND|	"Android"
		" Version " print print cr
		sh sw " Res: %dx%d" print cr
		xymouse swap " MOUSE: %d %d" print cr
		bmouse " BMOUSE: %d" print cr
		key " KEY: %d" print
		48 qdim sw 32 - sh 32 - pos
		verde 'exit btn blanco 'i.ok drawric

		'exit >esc<
		cflecha
		;
|--------------------------------------
:runfile
    savem
	getname 'path "%s/%s" mprint run
	;

:editfile
	savem
	actual -? ( drop ; )
	getname 'path "%s/%s" mprint 'ed.nombre strcpy
	0 0 'ed.ncar ! 'ed.nlin !
	ed.save
	"r4/system/edit-code.txt" run
	;

:clickbtn | nro -- nro
	dup getinfo
	0? ( drop clearact addpath ; )
	drop
	actual =? ( runfile ; )
	dup 'actual !
	dup getnamesolo 'fileactual strcpy
	;

:botonapp | nro -- nro
	nfiles >=? ( ; )
	'clickbtn btnb
	dup getinfo
	2 << dup 'iconcolor + @ ink
	'iconinfo + @ drawric
	home blanco
	dup getnamesolo cr cr printc
	actual =? ( gc.box )
	;

#fcnt #fsize
#ccnt #csize

:prevp
	pagina fcnt - -? ( fcnt + ) 'pagina ! ;
:nextp
	pagina fcnt + nfiles >=? ( fcnt - ) 'pagina ! ;

:menubtn
	robotobold 24 rfont!
	80 qdim
|	fonti

	sw 80 / 2.0 over / 'fsize ! 2 - 'fcnt !
	sh 80 / 2.0 over / 'csize ! 2 - 'ccnt !

	fcnt 1- fsize * 2/
	ccnt 1- csize * 2/

	pagina >r
	0 ( ccnt <? )(
		0 ( fcnt <? )(
			dup	fsize * pick4 -
			pick2 csize * pick4 -
			neg fpos
			r> botonapp 1+ >r
			1+ ) drop
		1+ ) drop
	2drop rdrop
	;

:main
	4
	show clrscr
		robotoregular 64 rfont! home
		amarillo 'rempath ".." link
		verde dup ":R%d " printr
		blanco 'path "%s " printc
		0 rows 1- gotoxy
		'fileactual sp print

		menubtn

		48 qdim
		sw 32 - sh 32 - pos
		rojo 'exit btnb 'i.off drawric
		cyan
		gc<< 'config btnb 'i.info_sign drawric
		azul
		gc<< 'nextp btnb 'i.chevron_down drawric
		gc<< 'prevp btnb 'i.chevron_up drawric
		gc<<
		verde
		gc<< 'editfile dup <f2> btnb 'i.edit drawric

		[ actual -? ( drop ; ) runfile ; ] <f1>
		'exit >esc<
		cflecha
		;

: 0 paper mark loadm main ;