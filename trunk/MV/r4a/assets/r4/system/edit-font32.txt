| edit-font32.txt
| editor fuente 32x32 2colores
| PHREDA 2009
|----------------------------------
^r4/lib/gui.txt
^r4/lib/font.txt
^r4/lib/font32.txt
^r4/lib/fontj.txt
^r4/lib/sprite.txt

^r4/lib/dlgfile.txt

^inc/punteros.inc

#nombre )( 64
#fuente )( 32768 | 32kb
#cursorf 'fuente
#cursorn 0
#hojaini 0

|-----------------------------------
:loadfnt
	"./f32/" dir
	'fuente 'nombre load drop
	'fuente 'cursorf !
	0 'cursorn !
	"./" dir
	;

:savefnt
	"./f32/" dir
	'fuente 32768 'nombre save
	"./" dir
	;

:creainc
	mark
	"#:fuente32a " ,s
	'fuente ( 'cursorf <? )(
		dup 'fuente - 2 >> $f and 0? ( ,cr ) drop
		@+ "$%h " ,print
		) drop
	"inc/fnt32font.inc" savemem
	empty
	;

|------------ de viejo gui3
:table | row col 'acc --
	>r
	1.0 pick2 / | row col sw
	1.0 pick2 / | row col sw sh
	0 ( pick4 <? )(
		 0 ( pick4 <? )( | row col sw sh x y
			gc.push
			2over		| row col sw sh x y sw sh
			pick3 pick2 * pick2 2/ + 0.5 -
			pick3 pick2 * pick2 2/ + 0.5 -
			gc.conv | w h xc yc --
			r exec
			gc.pop
			1+ ) drop
		1+ ) drop
	4drop rdrop ;

:hlist | cnt 'acc --
	>r
	1.0 over / | cnt sw
	0 ( pick2 <? )( | cnt sw a
		gc.push
		over 1.0 2over * pick2 2/ + 0.5 - 0
		gc.conv | w h xc yc --
		r exec
		gc.pop 1+ )
	3drop rdrop ;
	;

::vlist | cnt 'acc --
	>r
	1.0 over / | cnt sw
	0 ( pick2 <? )( | cnt sw a
		gc.push
		1.0 pick2 2over * over 2/ + 0.5 - 0 swap
		gc.conv | w h xc yc --
		r exec
		gc.pop 1+ )
	3drop rdrop ;

|------------ de viejo gui3

|-----------------------------------
:pinta		over @ over or pick2 ! ;
:borra		over @ over not and pick2 ! ;
:invierte	over @ over xor pick2 ! ;

#mododibujo 'pinta

:led | x y -- x y
	dup 2 << cursorf +
	1 pick3 $1f and <<
	mododibujo guiMove
	swap @ and? ( blanco )( negro )
	0.9 fzoom gc.fbox drop ;

:bitmap
	verde gc.box
	32 32 'led table ;

#cursor
#nroestado

:lapiz
	0 'nroestado !
	'clapiz 'cursor !
	'pinta 'mododibujo ! ;
:goma
	1 'nroestado !
	'cgoma 'cursor !
	'borra 'mododibujo ! ;
:mano
	2 'nroestado !
	'cmano 'cursor !
	'invierte 'mododibujo ! ;

#acciones 'lapiz 'goma 'mano
#botones 'dib3 'dib4 'dib2

|-----------------------------------
:botoncontrol
|	0.9 [ 1.0 nip ; ] onMove fzoom
	[ dup 2 << 'acciones + @ exec ; ] guiBtn
	nroestado =? ( 0.4 pinpon 1.2 + fzoom )
	w 2 >> h neg 2 >> +pos
|	1.4 rzoom
	dup 2 << 'botones + @ $1ff rnsprite | quieto rotado

|	nroestado =? ( msec 2 >> )( 0 )		| gira dibujo
|	over 2 << 'botones + @ swap rnsprite
	;


|-------------- acciones en editar caracter
:borracar
	cursorf 32 ( 1? )( 1- 0 rot !+ swap ) 2drop
	;

:editando
	lapiz
	show clrscr
		0.8 0.8 fdim 0.0 0.0 fpos
		bitmap

		0.2 0.1 fdim -0.8 -0.9 fpos
		$ff 0 vbtn
		2 'botoncontrol hlist

		0.1 0.1 fdim -0.9 0.9 fpos
		rojo
		'exit 'ifin btnd gc>>
		'borracar 'iborrar btnd

		10 sfont naranja "32x32" printc
		fonti blanco cr cr cr cr cursorn emit

|		8 font dup ":r%d" verde print
		cursor exec
	'exit >esc<
		;


|------------------------------------------------

:borrar
	;
:copia
	;
:izq
	;
:der
	;

|------------- Acciones en todos los caracteres
:cadacar
	[ 1.1 fzoom verde gc.box ; ] guiInPre
	2dup 3 << + hojaini 64 * +
	cursorn =? ( 0.1 pinpon 0.9 + fzoom blanco gc.frod )( 0.9 fzoom )
	[ dup 'cursorn ! dup 7 << 'fuente + 'cursorf ! ; ] guiBtn
	dup gris
	xc w 2 >> - 'ccx ! yc h 2 >> - 'ccy !
	7 << 'fuente + char32i
	gc2win home
	fonti dup "%d " print emit
	;

:btnpag
	[ dup 'hojaini ! ; ] guiBtn
	hojaini =? ( amarillo )( gris 0.8 fzoom )
	'irec v8draw
	;

|------------------------------------------------
:elijearchivo
	savefnt
	"./f32/" dir
	'nombre dlgfile
	"./" dir
	loadfnt
	;

:accnombre
	'elijearchivo guiBtn
	$ff00 0 vbtn
	gc2win home
	blanco 1 font
	'nombre printc
	;

|------------------------------------------------

:hojas
	show clrscr
		0.8 0.8 fdim 0.0 0.0 fpos
		8 8 'cadacar table

		0.05 0.4 fdim -0.9 0 fpos 
		4 'btnpag vlist

		0.1 0.1 fdim -0.9 0.9 fpos
		rojo
		'exit 'ifin btnd gc>>
		verde
		'editando 'idibuja btnd gc>>
		cyan
		'copia 'icopia btnd gc>>
		'izq 'iizq btnd gc>>
		'der 'ider btnd gc>>
		rojo
		'borrar 'iborrar btnd gc>>
		verde
		'creainc 'irecicla btnd gc>>

		0.8 0.1 fdim 0.0 -0.9 fpos 
		accnombre

|		10 sfont
|		blanco 0 0 at
|		cursorn "%d" print
|		8 font dup ":r%d" verde print
		cmano  
	'exit >esc<
	'editando <f1>
	'copia <f2>
	'izq <f3>
	'der <f4>
	'borrar <f5>
;

|------------------------------------------------
:main
	'nombre "./nom/edfnt32.nom" load drop
	'nombre c@ 0? ( elijearchivo ) drop
	'nombre c@ 0? ( drop ; ) drop
	loadfnt
	hojas
	savefnt
	'nombre count "./nom/edfnt32.nom" save
	;

|****** BOOT ******
:	0 paper
	4 main
	;

