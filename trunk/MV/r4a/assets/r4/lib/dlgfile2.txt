| Navegador de Archivos
| PHREDA 2006
|---------------------------------------
^r4/lib/btn.txt
^r4/lib/input.txt
^r4/lib/fontt.txt
^r4/lib/cursor.txt

#cntlineas 0
#inifile 0
#fileact 0
#pnombre 0
#entrada )( 64
#sizep 0 | controls display of file size

|---------------------------------------------------------
:sumafile | nro --
	fileact + dup file 0? ( 2drop ; ) 'entrada strcpy  'fileact !
:ubicar
	rows 1- 'cntlineas !
	inifile cntlineas +  ( fileact <? )( 1+ ) cntlineas - 'inifile !
	inifile ( fileact >? )(  1- ) 'inifile !
	;

:buscafile
	'entrada c@ 0? ( drop ; ) drop
	0 ( dup file 1? )(  | nro file
		'entrada =pre 1? ( drop 'fileact ! ubicar ; ) drop 1+ )
	2drop ;

|---------------------------------------------------------
:printsize | ( filen -- )
	sizep 1? ( drop rojo fsize " - %d bytes" print  )( 2drop ) ;

:cadafile | n -- n
	blanco
	dup inifile +
	[ dup 'fileact ! dup file 1? ( 'entrada strcpy )( drop ) ; ] onLineMove |onClick
	fileact =? ( $0 $ffffff )( $fffff 0 )
	ink linefill ink
	dup file 1? ( sp print printsize )( 2drop ) cr ;

:eligeactual
	fileact
	file 1? ( pnombre strcpy exit ; )
	drop ;

:nuevonombre
	'entrada c@ 0? ( drop ; ) drop
	'entrada pnombre strcpy exit
	;

|---------------------------------------------------------
::dlgfile | nn --
	'pnombre !
	0 'fileact !
    pnombre 'entrada strcpy
	show clrscr
		32 gc.top $f gc.hfill
		font-vard-12-bold home cr2
		blanco pnombre " : %s : " print
|		dup "(%d)" print
		sp verde 'nuevonombre "Nuevo" btnt
		sp amarillo 'eligeactual "Actual" btnt
		sp azul [ 1 sizep - 'sizep ! ; ] "File size" btnt
		sp rojo [ "" pnombre strcpy exit ; ] "Cancelar" btnt
		blanco " : " print 'entrada 64 input

		32 32 gc.vbetween home

|		verde
|		0 ( 20 <? )(
|			dup "%d " print dup fsize over  file "%s %d kb." print cr
|			1+ ) drop

		0 ( rows <? )(
			cadafile
			1+ ) drop

		buscafile
	[ 1 sumafile ; ] <dn>
	[ -1 sumafile ; ] <up>

	[ cntlineas sumafile ; ] <pgdn>
	[ cntlineas neg sumafile ; ] <pgup>
	'eligeactual <enter>
	'exit  >esc<

		cmano ;

| test
: 33
	"./db/dbgestoria/forms" dir
	"" dlgfile ;