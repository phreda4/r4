| MEMOCARD
| PHREDA 2013
|--------------
^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/rfont.txt
^r4/lib/ricons.txt

^r4/lib/trace.txt

^inc/ric/efon.ric
^inc/rft/gooddog.rft

#cartascnt 20
#cartas )( 1024
#cartase )( 1024

:]nro 2 << 'cartas + ;
:]est 2 << 'cartase + ;

#carta1
#carta2
#estado
#puntos
#intentos

#filas
#columnas
#zcam
#pxcarta

#riccnt 40 	| cantidad de dibujos
#cartasric
i.quotedbl i.percent i.ampersand i.quotesingle i.parenleft i.parenright i.asterisk i.plus
i.comma i.less i.equal i.greater i.question i.at i.A i.B i.C i.D i.E i.F i.G i.H i.I
i.L i.M i.N i.O i.P i.Q i.R i.S i.T i.U i.V i.W i.X i.Y i.Z i.bracketleft i.backslash

:3dop project3d op ;
:3dpline project3d pline ;

:3dbox | size --
	dup dup 0 project3d 2dup op
	rot
	dup neg over 0 3dpline
	dup neg dup 0  3dpline
	dup swap neg 0 3dpline
	pline poli ;

:cartaback
	azul 1.05 3dbox
	;

:cartafront | nro --
	negro 1.05 3dbox
	blanco 0.98 3dbox

	]nro @
	dup 8 >> ink
	$ff and 2 << 'cartasric + @
	3ddrawric
	;

:elijecarta | n --
	carta1 -? ( drop 'carta1 ! ; ) drop
	carta1 =? ( drop ; )
	carta2 -? ( drop 'carta2 ! ; ) drop
	drop ;

:clickCarta
	bmouse 0? ( drop ; ) drop
	xymouse inscreen
	rot - abs pxcarta >? ( 3drop ; )
	drop - abs pxcarta >? ( drop ; ) drop
	dup elijecarta ;

:drawcartam | nro --
	dup ]nro @ $ff and $ff =? ( 2drop ; ) drop
	mpush
	clickCarta
	dup ]est @ dup mrotyi
	0.25 - oxyztransform nip atan2 + | correccion por vista
	$ffff and 0.5 <? ( 2drop cartaback )( drop cartafront )
	mpop ;



:printmazog
	columnas 2.2 * 2/ 1.1 + neg
	filas 2.2 * 2/ 1.1 - neg
	0 mtransi
	0
	filas ( 1? )( 1-
		columnas ( 1? )( 1-
			2.2 0 0 mtransi
			rot dup drawcartam 1+ rot rot
			) drop
		columnas 2.2 * neg 2.2 0 mtransi
		) 2drop
	;

|------- cartas
:resetelije
	-1 dup 'carta1 ! 'carta2 ! 0 'estado ! ;

:llenacartas
	'cartas >r
	cartascnt 2/
	( 1? )( 1-
		dup rand 1.0 1.0 hsv2rgb 8 << or
		dup r!+ r!+
		) drop
	rdrop ;

:swapcarta
	]nro dup @ rot ]nro rot over  @ swap ! ! ;

:mexclacartas
	msec 'seed !
	100 ( 1? )( 1-
		rand cartascnt mod abs
		rand cartascnt mod abs swapcarta
		) drop
	;

:mezclagiro
	'cartase >r
	cartascnt ( 1? )( 1-
		rand r!+ ) drop
	rdrop ;

:cartasgiro
	'cartase >r
	cartascnt ( 1? )( 1-
		0.01 r +! 4 r+ ) drop
	rdrop ;

:esconder
	'cartase >r
	cartascnt ( 1? )( 1-
		0.5 r!+ ) drop
	rdrop ;

:giracarta | nro -- nro
	]est dup @
	0 >? ( 0.02 - )( 0 nip 1 'estado +! ) swap ! ;
	;

:descubriendo
	0 'estado !
	carta1 0 >=? ( giracarta )( drop )
	carta2 0 >=? ( giracarta )( drop )
	;

:vuelvecarta | nro -- nro
	]est dup @
	0.5 <? ( 0.02 + )( 0.5 nip 1 'estado +! ) swap ! ;

:encubriendo
	500 'estado !
	carta1 vuelvecarta
	carta2 vuelvecarta
	estado 502 <? ( drop ; )
	resetelije ;

:coincidencia
	$ff carta1 ]nro !
	$ff carta2 ]nro !
	2 'puntos +!
	puntos cartascnt =? ( exit ) drop
	resetelije ;

:check
	1 'estado +!
	carta1 ]nro @
	carta2 ]nro @
	=? ( coincidencia ) drop
	1 'intentos +!
	;

:accionmazo
	estado
	3 <? ( descubriendo )
	1 >? ( 1 'estado +! )
	20 =? ( check )
	20 >? ( encubriendo )
	drop
	;

:calcdim
	cartascnt sqrt dup 'filas !
	dup * cartascnt swap -
	filas / filas + 'columnas !
	filas columnas max 2.4 * 'zcam !
	omode
	0 0 zcam mtrans
	1.0 0 0 projectdim drop 'pxcarta !
	;

:accion
	llenacartas
	mexclacartas
	esconder
	0 'puntos !
	0 'intentos !
	resetelije
	show clrscr
|		dup "%d" print cr
|		pxcarta "%d" print cr
|		carta1 carta2 "%d %d" print cr
|		filas columnas "%d %d " print

		omode
		0 0 zcam mtrans
		printmazog
		accionmazo

		rojo
		gooddog 0.15 %s rfont!
		intentos puntos 2/ " %d/%d" print

		'exit >esc<
|		cflecha
		;

:final
	llenacartas
	mexclacartas
	mezclagiro
	show clrscr
		'exit guiBtn
		omode
		0 0 zcam mtrans
		printmazog
		cartasgiro
		gooddog 0.2 %s rfont!
		cr
		rojo "Fin de juego" printc
		cr cr
		verde
		intentos puntos 2/  "Aciertos: %d/%d" printc cr cr
		'exit >esc<
		;

:jugar | cartas --
	'cartascnt !
	calcdim
	accion
	final
	;


:main
	show clrscr

		gooddog 0.2 %s rfont!
		Azul
		"Memoria" printc cr
		gooddog 0.05 %s rfont!
		cr cr
		verde
		gooddog 0.1 %s rfont!
		sp sp [ 16 jugar ; ] "16" btnt
		sp [ 32 jugar ; ] "32" btnt
		sp [ 48 jugar ; ] "48" btnt
		cr cr
		rojo
		sp sp 'exit " Exit " btnt
		cr cr
		negro ":R4 - PHREDA 2013" printc

		'exit >esc<
|		cflecha
		;

: $ffffff paper
main
0 paper ;