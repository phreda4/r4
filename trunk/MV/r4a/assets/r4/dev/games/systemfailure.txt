| system failure
| experimental gameplay project
| PHReda - 2009 - http://www.reda4.org
| http://code.google.com/p/reda4/
|-------------------------------------

^r4/lib/gui.txt
^r4/lib/fonth.txt
^r4/lib/trace.txt
^inc/systemfailure.spr

#xcam 0
#ycam 0
#zcam 10.0

|||| DRAW 2D
| x  y  s r  vx vy acc tl
|    4  8 12 16 20 24  28
|-28-24-20-16-12 -8 -4

:sxy@ | adrrl -- adrrl x y
	dup 28 - @+ swap @ ;

:svxy! | adrrl +vy +vx -- adrrl
	pick2 12 - !+ ! ;

:ss! | adrrl scale -- adrrl
	over 20 - ! ;

:sr! | adrrl rotacion -- adrrl
	over 16 - ! ;
:+sr! | adrrl +rotacion -- adrrl
	over 16 - +! ;
:sr@ | adrrl  -- adrrl rotacion
	dup 16 - @ ;

||||||||||||||||||||||||||||||||||
:draw2d | adr -- adr tl
	mpush
	>r
	r@+ r@+ 0 mtransi
	r@+ dup 0 mscalei
	r@+ mrotzi
	r@+ r 20 - +!
	r@+ r 20 - +!
	r> @+ exec | adrtl t1
	mpop ;


:hit? | s1 s2 -- 0/s1
	over =? ( 2drop 0 ; ) | s1=s2?
	>r
	dup @+ r@+ -	| s1 s1y dx
	swap @ r@+ -	| s1 dx dy
	distfast			| s1 dist
	over 8 + @ r@+ + | s1 dist dw
	pick2 12 + @ r> @ +
	distfast 2/ -		| s1 dd
	+? ( 2drop 0 ; )
	drop ;

:simplesprite | adr -- adr'
	@+ draw2d 2drop ;

|
| 2 listfx
| 1 <-lsig
| listfx ... 'borr <-act

:delspr | 'lsp 'lsig 'act -- 'lsig
	dup 32 - over | nueva vieja
	pick3 3 << move | de sr cnt
	32 - ;


:lsprite | adr -- adr'
	@+
	@+ ( 1? )( 1- swap  | nexl cant lis'
		draw2d
        1? ( swap !+ )( drop 4+
				delspr
				-1 pick3 4 - @ +! 	| resta 1 a cantidad
				)
		swap ) 2drop
	;

:lspritehit | 'spr 'adr --  0/'otro
	@+ @+
	( 1? )( 1- swap
        hit? 1? ( nip nip ; )
		swap )
	3drop 0 ;

:msprite
:noop
	@+ drop
	;

#accdib 'simplesprite 'lsprite 'msprite 'noop 'noop 'noop 'noop

:drawlayer | layer --
	( @+ 1? )(
		1- $3 and 2 << 'accdib + @ exec
		) 2drop ;

:+layer | tl 'acc vy vx r s y x  'lista --
	dup @+ | 'listfx 'dirini cantact
	>r r
	5 << + | 'listfx 'diractual
	swap >r | ... y x 'diractual
	!+ !+ !+ !+ !+ !+ !+ !
	r> r> | 'diract cantact
	1+ swap ! ;

||||||||||||||||||||||||||||||||||
:r0.1 | -- r
	rand 0.1 mod ;

:r01 | -- r
	rand 1.0 mod ;

||||||||||||||||||||||||||||||||||
|||| GOAL
:accgoal | adr -- adr t1
	'dib5 3dnsprite
	0.8 pinpon 0.8 + ss!
	-1 ;

#spr-goal 2.0 2.0 0.5 0 0 0 'accgoal -1

:resetgoal
	'accgoal 0 0 0 0.5 2.0 2.0 'spr-goal !+ !+ !+ !+ !+ !+ ! ;

|||| NAVE
| x y s r vx vy acc tl
#vrnave 0
#arnave 0
#vanave 0
#aanave 0

:accnave | adrtl -- adrtl tl
	'dib1 3dnsprite
	arnave 'vrnave +!
	vrnave +sr!	| rotacion
	aanave 'vanave +!
	sr@ sincos
	vanave neg *. swap vanave neg *. svxy! | velocidad
	-1 ;

#spr-nave 0 0 0.5 0 0 0 'accnave -1

:resetnave
	'accnave 0 0 0 0.5 0 0
	'spr-nave !+ !+ !+ !+ !+ !+ !
	0 0 0 0 'vrnave !+ !+ !+ !
	;

:posnave
	'spr-nave @+ swap @ swap ;

#listafx 0 )( 8192

#layer1
2	'listafx
|1	'spr-goal
1	'spr-nave
0


|||| SPRITES

|||| ROCKS
:rocks
	0.01 +sr!
	sxy@
	abs 6.0 >? ( 0 nip nip ; ) drop
	abs 6.0 >? ( 0 nip ; ) drop
    -1
    'dib6 3dnsprite
	;

:+rocks
	-1 'rocks
	r0.1 r0.1
	r01 0.4 r01 +              | rot y tam
	r01 2* r01 2*
	'listafx +layer
	;

|||| FIRE
:fire
	dup @ 1-
	'diba 3dnsprite ;

:+fire
	100 'fire
	'spr-nave 12 + @ dup sincos
	-0.2 *. swap -0.2 *. | velocidad
	rot 0.1              | rot y tam
	posnave
	'listafx +layer
	;

|||| SMOKE
#humo

:mismoke
	0.02 +sr!
	dup @ 1-
	dup 3 << alpha
	'dib2 3dnsprite
	255 alpha
	;

:+smoke
	30 'mismoke
	'spr-nave 12 + @ sincos
	humo *. swap humo *. | velocidad
	r01 0.4              | rot y tam
	posnave
	'listafx +layer
	;

|||| KEYS
:kder
	-0.001 'arnave ! ;
:ukder
	0 'arnave !
	vrnave 2/ 'vrnave ! ;
:kizq
	0.001 'arnave !	;
:ukizq
	0 'arnave !
	vrnave 2/ 'vrnave ! ;
:kup
	0.001 'aanave !
	0.05 'humo ! ;
:ukup
	0 'aanave !
	0 'humo ! ;
:kdn
	-0.001 'aanave !
	-0.05 'humo ! ;
:ukdn
	0 'aanave !
	0 'humo ! ;


|||| GAME LOOP
:debug
	scr home fonti verde
	dup "%d " print
	listafx "cnt:%d " print cr
	vrnave arnave "%f %f - " print vanave aanave "%f %f " print
	;

:ajustacam
	posnave
	neg xcam 3.0 + >? ( 3.0 - 'xcam ! )(
		xcam 3.0 - <? ( 3.0 + 'xcam ! )( drop ) )
	neg ycam 3.0 + >? ( 3.0 - 'ycam ! )(
		ycam 3.0 - <? ( 3.0 + 'ycam ! )( drop ) )
	;

#firek 0
:juego
	resetnave
	resetgoal
	show30 clrscr

		'exit >esc<
		'kder <ri> 'ukder >ri<
		'kizq <le>	'ukizq >le<
		'kup <up> 'ukup >up<
		'kdn <dn> 'ukdn >dn<
		[ 1 'firek ! ; ] <spc>
		[ 0 'firek ! ; ] >spc<

		omode
		ajustacam
		xcam ycam zcam  mtrans

		'layer1 drawlayer

		debug
|		cmano
		10 .mseg
		firek 1? ( +fire ) drop
		100 .mseg .restart
		humo 1? ( +smoke ) drop
		+rocks
		;



|||| BOOT
: 33
	mark
	juego
	;


