^r4/lib/gui.txt
^r4/lib/leebmp.txt


#map
#cm

#px 300.0
#py 300.0
#pa 0

#rx #ry #rz
#hc
#hr
#rsin #rcos

:getcol
	rx 16 >> $ff and
	ry 8 >> $ff00 and or
	map + c@ 9 << ;

:vline | x y1 y2 --
	pick2 swap op line ;

:drawfloor
	hr hc <? ( drop ; )
	getcol
|	$ff
	pick2 3 << - ink

	pick3 sh hc - sh hr - vline
	'hc ! ;

:getmap
	rcos 'rx +! rsin 'ry +!
	rx 16 >> $ff and ry 8 >> $ff00 and or
	map + c@ ;

:renderframe
	pa 0.15 -
	0 ( sw <? )( swap
		0.001 +
		dup sincos 'rcos ! 'rsin !
		px 'rx ! py 'ry !
		0 'hc !
		0 ( 120 <? )( 1+
			getmap 2 << over / 'hr !
|			rz 32 pick2 */ 300 + 'hr !
			drawfloor
			) drop
		swap 1+ ) 2drop ;

:fillmaps
	map >r
	255 ( 1? )( 1-
		256 ( 1? )( 1-
			2dup * r> c!+ >r
			) drop
		) drop
	rdrop
	;

:main
	here dup 'map !
	$ffff + dup 'cm !
	$ffff + 'here !
	fillmaps
	show clrscr
		renderframe
		[ pa sincos 2/ 'px +! 2/ 'py +! ; ] <up>
		[ pa sincos 2/ neg 'px +! 2/ neg 'py +! ; ] <dn>
		[ 0.01 'pa +! ; ] <le>
		[ -0.01 'pa +! ; ] <ri>
		'exit >esc<
	;

: mark main ;