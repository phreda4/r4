| edit-code.txt
|
| PHREDA 2007..
|---------------------------------------
^reda4.txt
^libgui.txt
^parse.txt

^fonth.txt
^dlgfile.txt
^codecolor.txt

#lugar
#nombre )( 64

#fuente0 0
#fuente )( $3ffff | 256k de fuente editable
#fuente> 'fuente	| cursor
#textofin> 'fuente

#helptxt )( $3fff | 16k help file

#inisel 0 | inicio seleccion
#finsel 0 | fin seleccion

#clipboard )( $ffff
#clipboard> 'clipboard

#cntlineas

#error )( 32
#nrolin )( 32
#ncar 0
#nlin 0

#pantaini> | comienzo de pantalla
#pantafin>
#prilinea
#actlinea

#mensaje )( 64

:lins  | c --
	fuente> dup 1- textofin> over - 1+ cmove>
	1 'textofin> +!
:lover | c --
	fuente> c!+ dup 'fuente> !
	textofin> >? ( dup 'textofin> ! ) drop
:0lin | --
	0 textofin> ! ;

#modo 'lins

:del
	fuente> textofin> >=? ( drop ; ) drop
	1 'fuente> +!
:back
	fuente> 'fuente <=? ( drop ; )
	dup 1- swap textofin> over - 1+ cmove -1 'textofin> +! -1 'fuente> +!
	;

:<<13 | a -- a
	( 'fuente >? )( dup c@ 13 =? ( drop ; ) drop 1- ) ;

:>>13 | a -- a
	( textofin> <? )( dup c@ 13 =? ( drop 1- ; ) drop 1+ ) drop textofin> 2 - ;

#1sel #2sel

:selecc	| agrega a la seleccion
	mshift 0? ( dup 'inisel ! 'finsel ! ; ) drop
	inisel 0? ( fuente> '1sel ! ) drop
	fuente> dup '2sel !
	1sel over <? ( swap )
	'finsel ! 'inisel !
	;

:khome
	selecc
	fuente> 1- <<13 1+ 'fuente> !
	selecc ;

:kend
	selecc
	fuente> >>13  1+ 'fuente> !
	selecc ;

:scrollup | 'fuente -- 'fuente
	pantaini> 1- <<13 1- <<13  1+ 'pantaini> !
	prilinea 1? ( 1- ) 'prilinea !
	selecc ;

:scrolldw
	pantaini> >>13 2 + 'pantaini> !
	pantafin> >>13 2 + 'pantafin> !
	1 'prilinea +!
	selecc ;

:colcur
	fuente> 1- <<13 swap - ;

:karriba
	fuente> 'fuente =? ( drop ; )
	selecc
	dup 1- <<13		| cur inili
	swap over - swap	| cnt cur
	dup 1- <<13			| cnt cur cura
	swap over - 		| cnt cura cur-cura
	rot min +
	'fuente> !
	selecc ;

:kabajo
	fuente> textofin> >=? ( drop ; )
	selecc
	dup 1- <<13 | cur inilinea
	over swap - swap | cnt cursor
	>>13 1+    | cnt cura
	dup 1+ >>13 1+ 	| cnt cura curb
	over -
	rot min +
	'fuente> !
	selecc ;

:kder
	selecc
	fuente> textofin> <?
	( 1+ 'fuente> ! selecc ; ) drop
	;

:kizq
	selecc
	fuente> 'fuente >?
	( 1- 'fuente> ! selecc ; ) drop
	;

:kpgup
	selecc
	cntlineas ( 1? )( 1- karriba ) drop
	selecc ;

:kpgdn
	selecc
	cntlineas ( 1? )( 1- kabajo ) drop
	selecc ;

|---------------   mover fuente
:e.clear
	'fuente dup 'pantaini> ! dup 'fuente> ! 'textofin> !
	0lin ;

|------------------------------------------------
:loadtxt | -- cargar texto
	clear mem 'nombre load 0 swap c!
	'fuente dup 'pantaini> ! 'fuente> !

	|-- si no hay cr y si ln reemplazar
	0 mem ( c@+ 1? )( 13 =? ( rot 1+ rot rot ) drop ) 2drop
	0? ( mem ( c@+ 1? )( 10 =? ( 13 pick2 1- c! ) drop ) 2drop )
	drop
	|--quitar 10
	'fuente mem ( c@+ 1? )(
		10 =? ( drop swap fuente> <? ( -1 'fuente> +!  ) swap )( rot c!+ swap )
		) 2drop 'textofin> ! 0lin

	nlin 0 >? ( ( 1- 1? )( fuente> >>13 2 + 'fuente> ! ) ) drop
	ncar +? ( dup 'fuente> +! ) drop

	;

:savetxt
	clear
	'fuente ( c@+ 1? )( 13 =? ( ,c 10 ) ,c ) 2drop
	mem here over - 'nombre save
	nlin -1 =? ( 'nombre count "./nom/editor.nom" save )( "" 1 "debug.err" save -1 'nlin ! ) drop
	;

|-------------------------------------------
:copysel
	inisel 0? ( drop ; )
	'clipboard swap
	finsel over - pick2 over + 'clipboard> !
	cmove
	;

:borrasel
	inisel finsel textofin> finsel - 4+ cmove
	finsel inisel - neg 'textofin> +!
	fuente> inisel >=? ( finsel <=? ( inisel 'fuente> ! )(  finsel inisel - over swap - 'fuente> ! ) )  drop
	0 dup 'inisel ! 'finsel ! ;

:kdel
	inisel 0? ( del )( borrasel ) drop ;

:kback
	inisel 0? ( back )( borrasel ) drop ;

|-------------------------------------------
:eligearchivo
	savetxt
	".//" dir
	'nombre dlgfile |explore
	'nombre c@ 1? ( loadtxt ) drop
	;

|-------------------------------------------
:drawcursor
	blink 0? ( drop ; ) drop
	modo 'lins =? ( gris )( rojo ) drop
	pantaini>
	home
	( fuente> <? )( c@+ gemit )
	printcur drop
	cntcr prilinea + 'actlinea ! ;

:drawselect
	inisel 0? ( drop ; ) drop
	$77 ink
	pantaini>
	( inisel <? )( c@+ gemit )
	sel>>
	( finsel <? )( c@+ gemit )
	sel<<
	drop
	;

:drawcode
	fonti  	|25 fonth
	drawselect
	drawcursor
	home
	pantaini> code.lines
	cntcr fils dup 'cntlineas !
	<? ( drop 1+ )( drop 1- )
	'pantafin> !
	fuente>
	( pantafin> >? )( scrolldw )
	( pantaini> <? )( scrollup )
	drop ;

:drawnum
	fonti 	| 25 fonth
	fils 0
	( over <? )(
		dup prilinea +
		actlinea =? ( blanco )( gris )
		1+ "%d" printr
		cr 1+ )
	2drop ;


|-------------------- tecla control
:desinstalarcontrol
	"ok" 'mensaje strcpy
	key.pop ;

:controlc | copy
	copysel ;

:controlx | move
	controlc
	borrasel ;

:controlv | paste
	'clipboard clipboard> over - 0? ( 3drop ; ) | clip cnt
	fuente> dup pick2  + swap | clip cnt 'f+ 'f
	textofin> over - 1+ cmove>	| clip cnt
	fuente> rot rot | f clip cnt
	dup 'textofin> +!
	cmove
	clipboard> 'clipboard - 'fuente> +!
	;

:instalcontrol
	"control" 'mensaje strcpy
	key.push
	inikey
	'controlx 45 >key
	'controlc 46 >key
	'controlv 47 >key
	'desinstalarcontrol 29 >ukey
	'desinstalarcontrol 56 >ukey
	;

|----------------------------
:evaluando
	savetxt	"r4eval.txt" run ;

:tokenizado
	savetxt "tokenizar.txt" run ;

:compilando
	savetxt	"compilar.txt" run ;

|----------------------------
:lineastatus
|	$3300 ink gc.fbox
|	2 sfont verde
	fonti blanco
	'mensaje print
|	cr
|	modo 'lins =? ( "INS" )( "OVR" ) nip
|	" %s | " print
|	finsel 0? ( drop "0" )( 'fuente - inisel 'fuente - "%d..%d" ) print
|	textofin> 'fuente -
|	actlinea
|	" | %d" print cr
|	 clipboard> 'clipboard <>? ( " | *" print ) drop
	;

:ayudainclude
	"include" 'mensaje strcpy
	;
:ayudadef
	"definicion" 'mensaje strcpy
	;
:ayudavar
	"variable" 'mensaje strcpy
	;
:ayudadir
	"direccion" 'mensaje strcpy
	;
:ayudanum
	"numero" 'mensaje strcpy
	;

|--------------------
:editasprite
	savetxt
	fuente> ( dup 1- c@ 32 >? )( drop 1- ) drop | busca comienzo
	dup c@
	$23 <>? ( 2drop ; ) | no es #
	drop
	'fuente - 'lugar !
	'lugar 68 "./nom/1spr.nom" save
	"edit-1spr.txt" run
	;

|--------------------
:buscawhelp | str -- str l/0
	'helptxt
	( 2dup =w 1? ( drop nip ; ) drop
		( c@+ 13 >? )( drop )
		0? ( nip nip ; )
		drop )
|	2drop "imposible" ;

:buscacom
	fuente>
	( dup c@ $7f <>? )(
			32 <? ( 2drop ; )
			drop 1- ) 2drop
	"comentario" 'mensaje strcpy
	;

:buscacad
	fuente>
	( dup c@ $22 <>? )(
			32 <? ( 2drop ; )
			drop 1- ) 2drop
	"cadena" 'mensaje strcpy
	;

|------------------- HELPCODE
:getword | adr -- adr car ; palabra que apunta el cursor
	( dup c@ 32 >? )( 0? ( ; ) drop 1- ) drop
    dup c@ 0? ( ; )
	33 <? ( drop 1+ dup c@ )
	;

:helpcode
	"" 'mensaje strcpy
	fuente>
	dup c@ 33 <? ( 2drop ; ) drop
	getword 0? ( 2drop ; ) 		| -> inicio de palabra
	$5e =? ( ayudainclude ; )	| $5e ^  Include
	$3A =? ( ayudadef ; )		| $3a :  Definicion
	$23 =? ( ayudavar ; )		| $23 #  Variable
	$27 =? ( ayudadir ; )		| $27 ' direccio
	drop
|	dup ?numero  1? ( ayudanum ; ) drop | numero
|	dup ?fnumero  1? ( drop rot drop -1 'basen ! esnumero ; ) drop | numero
|	dup ?macro 1? ( rot drop esmacro ; ) drop		| macro
|	dup	?palabra  1? ( nip espalabra ; ) drop		| palabra
	buscawhelp 1? ( 'mensaje strcpyln drop ; ) drop	| en ayuda?

	drop
	buscacad
	buscacom
	;

|-------------------- mirar libreria
#guiseelib
0

:seelib |
	inikey
	show cls scr
		'guiseelib gui ;

:directrun
	savetxt 'nombre run  ;

|--------------------
#accedit 5
$ff0000 'ifin	'exit
$ffff00	'ipreg	'helpcode
$00ff00	'iplay	'evaluando
$00ff00	'iff	'directrun
$0000ff	'icopia	'compilando

:botonera
	'accedit escf1.. ;

:titulo
	$333333 $aaaaaa
	[ swap ; ] onIn
	vbtn
	1 sfont negro "r4Ed:" print
	blanco 'nombre printc
	'eligearchivo onUp
	;

#guieditor
1.0 0.1 0.0 -0.45 'titulo
0.5 0.1 -0.25 0.45 'botonera
0.5 0.1 0.25 0.45 'lineastatus
0.05 0.8 -0.48 0 'drawnum
0.95 0.8 0.025 0 'drawcode
0


:editor
	inikey
	'exit  >esc<
	'kback	<back>		'kdel	<del>
	'karriba <arr>		'kabajo	<aba>
	'kder	<der>		'kizq	<izq>
	'khome	<home>		'kend	<end>
	'kpgup	<pgup>		'kpgdn	<pgdn>
	'instalcontrol 29 >key | control

	[ key toasc modo exec ; ] <visible>
	[ modo 'lins =? ( 'lover )( 'lins ) 'modo ! drop  ; ] <ins>
	[ 13 modo exec ; ] <enter>
	[ 9 modo exec ; ] <tab>

	'helpcode <f1>
	'evaluando  <f2>
	'directrun <f3>
	'compilando <f4>
	'editasprite <f5>

	show cls scr
		'guieditor gui
|		textofin> 'fuente - "%d" print
|		fuente> 'fuente - " %d" print
		cflecha
		;

:buscaerror
	"" 'error !  -1  'nlin ! 0 'ncar !
	mem "debug.err" load mem 4+ <? ( drop  ; ) 0 swap !
	mem
	'nombre swap ( c@+ $7c <>? )( rot c!+ swap ) drop 0 rot c!
	'nrolin swap ( c@+ $7c <>? )( rot c!+ swap ) drop 0 rot c!
	'nrolin ?numero 1? ( drop nip )  'nlin !
	'nrolin swap ( c@+ $7c <>? )( rot c!+ swap ) drop 0 rot c!
	'nrolin ?numero 1? ( drop nip )  'ncar !
	'error swap ( c@+ 1? )( rot c!+ swap ) rot c! drop
	nlin -1 =? ( drop ; )
	'error " %s en linea %d " mprint 'mensaje strcpy
	;

|----------- principal
:main
	'helptxt "helpr4.txt" load drop

	buscaerror
	nlin -1 =? ( 'nombre "./nom/editor.nom" load drop ) drop

	loadtxt
	editor
	savetxt
	;

: 0 paper 4 main "main.txt" run  ;

