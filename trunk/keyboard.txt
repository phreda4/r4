| Teclado, Memoria, Conversion, Basicos
| PHREDA
| :r4 system library
|-------------------------
^key-es1.txt	| Distribucion de teclado - Scancode to Ascii

#tablact 'k2asc
#:mshift 0

|--- mapa de teclado 256 keys (Up and Dn)
#:keymap )( 1024

:keydo
	key 2 << 'keymap + @ exec ;

::keyold
	'keydo ikey! ;

|--- Eventos del teclado
::>key 2 << 'keymap + ! ;
::>ukey 2 << 512 + 'keymap + ! ;

::clearkey
	'keymap 256 ( 1? )( 1- 0 rot !+ swap ) 2drop ;

::toasc | scancode -- ascii
	tablact + c@ ;

::inikey
	keyold
	clearkey
	[ 'k2ascs 'tablact ! 1 'mshift ! ; ] dup $2a >key $36 >key | teclas shift
	[ 'k2asc 'tablact ! 0 'mshift ! ; ] dup $2a >ukey $36 >ukey | levanta shift
	[ 'k2alt 'tablact ! ; ] $38 >key 								| abajo alt
	[ mshift 1? ( 'k2ascs )( 'k2asc ) nip 'tablact ! ; ] $38 >ukey	| arriba alt
	;

::<todas> | vec --
	'keymap 128 ( 1? )( 1-
		pick2  rot !+ swap ) 3drop ;
::>todas< | vec --
	'keymap 512 + 128 ( 1? )( 1-
		pick2  rot !+ swap ) 3drop ;

::<visible> | vec --
	>r 'k2asc ( c@+ 1? )( $ff and 32 >? ( over 1- 'k2asc - r swap >key ) drop ) 2drop
	r> $39 >key ; | y espacio


::<esc> 1 >key ;	::>esc< 1 >ukey ;
::<esp> $39 >key ;	::>esp< $39 >ukey ;
::<back> $e >key ;
::<tab> $f >key ;
::<enter> $1c >key ;	::>enter< $1c >ukey ;
::<ctrl> 29 >key ;		::>ctrl< 29 >ukey ;
::<alt> $38 >key ;		::>alt< $38 >ukey ;

::<home> $57 >key ;	::<end> $5f >key ;
::<arr> $58 >key ;	::<aba> $60 >key ;
::<der> $5d >key ;	::<izq> $5b >key ;
::<ins> $62 >key ;	::<del> $63 >key ;
::<pgup> $59 >key ; ::<pgdn> $61 >key ;

::>home< $67 >ukey ; ::>end< $5f >ukey ;
::>arr< $58 >ukey ;	::>aba< $60 >ukey ;
::>der< $5d >ukey ;	::>izq< $5b >ukey ;
::>ins< $62 >ukey ;	::>del< $63 >ukey ;
::>pgup< $59 >key ; ::>pgdn< $61 >key ;

::<f1> $3b >key ;	::<f2> $3c >key ;
::<f3> $3d >key ; 	::<f4> $3e >key ;
::<f5> $3f >key ; 	::<f6> $40 >key ;
::<f7> $41 >key ; 	::<f8> $42 >key ;
::<f9> $43 >key ; 	::<f10> $44 >key ;
::<f11> $57 >key ;	::<f12> $58 >key ;

|---- apila teclado
#teclados )( 4096 | guarda teclados
#teclados> 'teclados

::key.push
	teclados> 'keymap 256 move 1024 'teclados> +! ;

::key.pop
	-1024 'teclados> +! 'keymap teclados> 256 move ;

