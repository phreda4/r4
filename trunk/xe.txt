| Xenofobia Extraterreste
| PHReda 2006
| Dibujos Fidel 2008
|------------------------
^lib/reda4.txt
^lib/gui.txt
^lib/sprites.txt
^inc/fidel.inc

#xdir 0 #ydir 0
#fx #fy #xn #yn #px #py #ex #ey
#paso #minx #maxx
#puntaje 0
#sizenave 180

#aliens 'dibua 'dibua 'dibua 'dibua
#aliena 'dibua

:calibracion
	12 'paso ! 100 'minx !	sw 100 - 'maxx !
	sw 2/ 'xn ! sh 80 - 'yn ! ;

:choque
	1 'puntaje +! -40 'ex +! -20 'ey +!	20 'px +! ;

:disparo
	py -? ( drop ; ) 20 - 'py !
	px py pos 40 dup dim 'dibub rand rsprite	;

:iniene
	rand $ff and 40 + 'ey ! -100 'ex !
	rand $3 and 2* 2* 'aliens + @ 'aliena !
	;

:enemigo
	rand $f and 'ex +! rand $3 and 1- 'ey +!
	ex sw 80 + >? ( iniene ) drop
	ex ey pos 140 140 dim aliena sprite
	px py whin 1? ( choque ) drop ;

:rndf
	rand $7 and 0? ( -1 )( 1 ) nip ;

:fondoc
	sw 2/ 'fx ! sh 2/ 'fy ! ;

:play
	inigui
	'exit >esc<
	[ py -? ( xn 'px ! yn 'py ! )( drop ) ; ] $39 >key
	[ paso 'xdir ! ; ] $4d >key
	[ paso neg 'xdir ! ; ] $4b >key
	[ 0 'xdir ! ; ] dup $4d >ukey $4b >ukey
	iniene
	fondoc -1 'py ! iniene
	show clrscr
		sw 40 + sh 100 + dim fx fy pos 'dibu8 sprite
		16 font
		blanco puntaje "G:%d " print
		xn yn pos sizenave dup dim 'dibu9 sprite
		xdir 'xn +!
		minx xn >? ( 'xn ! )( drop )
		maxx xn <? ( 'xn ! )( drop )
		enemigo disparo ;

#fade 0
#swarning

:presenta
	inigui
	'exit >esc<
	'play >esp<
	show clrscr
		fade $1010101 + dup ink 'fade !
		2 font ":R4%.Games" print
		16 font verde
		0 7 at "Tirale piedras a lo desconocido" printc
		8 font
		rojo 0 1 at
         "Xenofobia" printc cr
		"Extraterrestre" printc cr cr cr cr 
		sp sp sp rojo 'exit " SALIR " btnt
		sp verde 'play " JUGAR " btnt
		cmano
		;

: 0 paper
calibracion
presenta
;