| INVASION K! de Gonzalo Orellano
| adaptado para la distribucion
|-----------------------------------
^reda4.txt
^gui.txt
^sprites.txt
^inc/invasion.inc

#vx #vy
#ax #ay        | velocidad de aliens

|	    x    y   w   h   r    'dibujo
#svaca     0.0 -0.9 0.1 0.1 0.0 'dibu2

#sfargo    0.6 -0.6 0.1 0.1 0.0 'dibu1
#sfargo2   0.0 -0.6 0.1 0.1 0.0 'dibu1
#sfargo3   -0.6 -0.6 0.1 0.1 0.0 'dibu1

#sfila41   0.4  0.8 0.1 0.1 0.0 'dibu3
#sfila42   0.2  0.8 0.1 0.1 0.0 'dibu3
#sfila43   0.0  0.8 0.1 0.1 0.0 'dibu3
#sfila44   -0.2 0.8 0.1 0.1 0.0 'dibu3
#sfila45   -0.4 0.8 0.1 0.1 0.0 'dibu3
#sfila31   0.4  0.6 0.1 0.1 0.0 'dibu4
#sfila32   0.2  0.6 0.1 0.1 0.0 'dibu4
#sfila33   0.0  0.6 0.1 0.1 0.0 'dibu4
#sfila34   -0.2 0.6 0.1 0.1 0.0 'dibu4
#sfila35   -0.4 0.6 0.1 0.1 0.0 'dibu4
#sfila21   0.4  0.4 0.1 0.1 0.0 'dibu3
#sfila22   0.2  0.4 0.1 0.1 0.0 'dibu3
#sfila23   0.0  0.4 0.1 0.1 0.0 'dibu3
#sfila24   -0.2 0.4 0.1 0.1 0.0 'dibu3
#sfila25   -0.4 0.4 0.1 0.1 0.0 'dibu3
#sfila11   0.4  0.2 0.1 0.1 0.0 'dibu4
#sfila12   0.2  0.2 0.1 0.1 0.0 'dibu4
#sfila13   0.0  0.2 0.1 0.1 0.0 'dibu4
#sfila14   -0.2 0.2 0.1 0.1 0.0 'dibu4
#sfila15   -0.4 0.2 0.1 0.1 0.0 'dibu4

#sdisp0    0.0 0.0 0.08 0.1 0.0 'dibu5
#sdisp1    0.0 0.0 0.1 0.1 0.0 'dibu5
#sdisp2    0.0 0.0 0.1 0.1 0.0 'dibu5
#sdisp3    0.0 0.0 0.1 0.1 0.0 'dibu5
#sdisp4    0.0 0.0 0.1 0.1 0.0 'dibu5
#sdisp5    0.0 0.0 0.1 0.1 0.0 'dibu5
#sdisp6    0.0 0.0 0.1 0.1 0.0 'dibu5
#sdisp7    0.0 0.0 0.1 0.1 0.0 'dibu5

#todosDisparos
sDisp0 sDisp1 sDisp2 sDisp3 sDisp4 sDisp5 sDisp6 sDisp7 0

#todosAliens
sfila41 sfila42 sfila43 sfila44 sfila45
sfila31 sfila32 sfila33 sfila34 sfila35
sfila21 sfila22 sfila23 sfila24 sfila25
sfila11 sfila12 sfila13 sfila14 sfila15
0

#slayerObstaculos
sfargo sfargo2 sfargo3
0

#slayerReal
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
#slayerDisparos
0 0 0 0 0 0 0 0 0 0

#nuevoDisparo 0
#puntaje 0
#xlim #ylim 0

:vacaX
      svaca
;

:vacaY
      'svaca 4+ @
;

:haceDisparo
	nuevoDisparo 0 >? ( drop ; )
	dup 1+ 'nuevoDisparo !
	2 << 'todosDisparos + @
	dup 'sLayerDisparos sAddLayer
	>r vacaY vacaX r> spos!
;

:nuevaOleada | agrega todos los aliens al layer real
	'todosAliens >r
	0.8 ( 0.2 >=? )(
		0.4 ( -0.4 >=? )(
			2dup r@+ sPos!
			0.2 - ) drop
		0.2 - ) drop
	'slayerReal 'todosAliens ( @+ 1? )( rot !+ swap ) rot ! drop
	0 'nuevoDisparo !
	0 'sLayerDisparos !
;

|..............por cada sprite en listas

:cadaDisparo | disparo --
           dup 'slayerReal sHitLayer        | si toco un alien
           1? ( 'sLayerReal sDropLayer
              'sLayerDisparos sDropLayer
              -1 'nuevoDisparo +!
              1 'puntaje +! ; ) drop

           dup 'slayerObstaculos sHitLayer   | si toco un obstaculo
           1? ( drop 'sLayerDisparos sDroplayer
              -1 'nuevoDisparo +! ; ) drop

           0.0 0.02 pick2 smove!
           dup 4+ @ 1.0 >? ( over 'sLayerDisparos sDropLayer -1 'nuevoDisparo +! ) 2drop
;

:cadaAlien | alien --
        dup @ abs xlim max 'xlim ! | mayor en x
        dup 4+ @ ylim min 'ylim ! | menor en y
        ay ax rot smove!
;

:bajaAlien | alien --
        ay 2* -0.1 rot smove!
;

:perdiste
	.page
	'exit >esc<
	.show cls
		6 font
		verde 0 1 at "GAME OVER" printc
		3 .segs exit
;

:jugar
	.page
	'exit >esc<
	[ 0.01 'vx ! ; ] <der>
	[ -0.01 'vx ! ; ] <izq>
	[ 0.0 'vx ! ; ] dup >izq< >der<
	[ hacedisparo ; ] $39 >key
	nuevaOleada
	0.0 'ax !
	0.005 'ay !
	.show cls
		0.0 0.0 fpos
		1.0 1.0 fdim
|		'dibu25 sprite 	      		| fondo de pantalla

		16 font
		blanco puntaje " Puntaje: %d" print cr

		'slayerReal sdrawlayer
		'slayerDisparos sdrawlayer 	|dibujo disparo
		'slayerObstaculos sdrawlayer	 |dibujo obstaculos
		'svaca sdraw 			|dibujo vaca

		vx vy 'svaca smove!
		'cadaDisparo 'sLayerDisparos sMapLayer

		0 'xlim ! 1.0 'ylim !
		'cadaAlien 'sLayerReal sMapLayer
		xlim 0.9 >? ( ay neg 'ay ! 'bajaAlien 'sLayerReal sMapLayer ) drop
		ylim -0.6 <? ( perdiste exit ) drop
;

:presenta
	.page
	12 12 .table
	1 9 .at 4 2 .to
	verde "JUGAR" [ jugar ; ] dup >esp< .tbtn
	.> .> .> .> .> .>
	rojo "SALIR" 'exit dup >esc< .tbtn
	.show cls scr
		6 font
		verde 0 1 at "INVASION" printc
		;

: 33 presenta "main.txt" run ;

