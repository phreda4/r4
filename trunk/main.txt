| main.txt
| menu principal
| PHREDA 2009
|----------------------------------
^lib/reda4.txt
^lib/gui3.txt
^lib/fontt.txt

|^lib/fonth.txt
^lib/trace.txt

|------------ cursor
#inicioarch 0
#cntarch 20
#cant 0

#iniprog 0
#hayarr 0
#hayaba 0

#indice	)( 2048

#sndclick

#juegos "Juegos"
"tetris"
"baremin" "peleador" "zzDestroyIt" "Tunel"
"slimeatlet" "invasion"
"clickotron"
"pong" "memoria" "carucha"
"carrera" "xe" "cartonman"
"sraceituna" "lagranjita"
"clock"   0

#tests "Test"
"test-dbgui" "test-n2gl"
"test-zonas" "test-gui3" "test-bmps"
"test-font" "test-sprite"
"test-yuvrgb" "test-joy"
"test-atan"
"funciones" "personaje" 0

#devs "Devs"
"jetpatrol" "esqueleto" "escena" "planedef" "crayon"  "bitmap"
"3dlatr"  "pizarra"  "adentro"
"rmation" "mys" "render" "rtable"
0

#edits "Editores"
"edit-code" "edit-spr" "edit-font32"
"edit-fnther"
0

#sistemas "Sistema"
"scancode" "fps" "runtime" "runtime2"
 0

#dominios  'juegos 'edits 'devs 'tests 'sistemas  0

:printarbol | adr --
	( @+ 1? )(
		dup blanco print cr gris
		( c@+ 1? )( drop ) drop
		( dup c@ 1? )( drop
			dup allowcr print sp
			( c@+ 1? )( drop ) drop
			)
		2drop cr ) 2drop ;

#actuald 'dominios

#filas 9   | [ 3 4 + ]
#columnas 3
#actual 0

:calbotons
	iniprog 0 >? ( 1 )( 0 ) nip 'hayarr !
	iniprog 3 9 * + cant <? ( 1 )( 0 ) nip 'hayaba ! ;

:nextpag
	iniprog  3 9 * +  cant >=? ( drop ; )
	'iniprog ! calbotons ;
:prevpag
	iniprog 3 9 * - 0 <? ( drop ; )
	'iniprog ! calbotons ;

:haceindice |'lista --
	'indice swap
	0 'cant !
	( c@+ 1? )( drop
		dup 1-  rot !+ swap
		1 'cant +!
		( c@+ 1? )( drop ) drop
		) drop
	swap !  ;

:nextdom | --
	actuald 4+
:hacedom  | adr --
	dup @ 0? ( 2drop 'dominios dup @ )
	( c@+ 1? )( drop ) drop haceindice
	'actuald !
	iniprog (  cant >? )( 4 6 * - ) 'iniprog !
	calbotons ;

:grabadom
	"./" dir
	'actuald 4 "nom/main.nom" save
	;

:testdominio | --
	'dominios ( actuald =? ( drop ; ) @+ 1? )( drop ) 2drop
	'dominios 'actuald ! ;

:restauradom
	".//" dir
	'actuald "nom/main.nom" load drop
	testdominio
	actuald hacedom ;

|--------------------------------------------------------------------------------------------
:runactual
	grabadom
	actual 2 << 'indice + @ "%s.txt" mprint run ;

|---------------------------------------
:editarcodigo
	"" 1 "debug.err" save
	"edit-code.txt" run ;

:editargrafico
	"edit-spr.txt" run ;

:editarprojecto
	"r4proj.txt" run ;

:editabitmap
	"edit-bit.txt" run ;

:runtimeerr
	"runtime2.txt" run ;

:configura
	"config.txt" run ;

|-------
:tocoentrada
	dup 'actual ! ;

#dactual
:tocodom
	dup 4 - 'dactual !
	dup hacedom ;

:conbotones
		'dominios (
			dactual =? ( amarillo )( verde )
			@+ 1? )(
			'tocodom
			swap allow2cr sp btnt
|			over allow2cr btnt cr cr
|			drop
|			( >>0 1? )( 'tocoentrada over allow2cr btnt sp ) drop
			) drop

		cr cr cr
		0 ( cant <? )(
			actual =? ( cyan )( gris )
			'tocoentrada
			over 2 << 'indice + @
			allow2cr
       		btnt sp
			1+ ) drop

		cr cr cr
	;


|-------------
#buffertext )( 1024

#ra
:tocorama |
	dup 'ra ! ;

#ha
:tocohoja
	dup 'ha !
	dup "%s.txt" mprint
	here swap load here =? ( drop ; ) drop
	'buffertext here 256 move
	0 'buffertext 512 + c!
	;

:nodo | adr
	gris
	( c@+ 1? )( drop ) drop
	( dup c@ 1? )( drop
		ha =? ( blanco )( gris )
		'tocohoja
		over allow2cr btnt
		( c@+ 1? )( drop ) drop
		) drop ;

:tree | adr --
	( @+ 1? )(
		ra =? ( cyan )( verde )
		'tocorama over btnt
		ra =? ( cr cr nodo cr cr )
		drop ) 2drop ;

|-------------
:ddlista | adr --
	( @+ 1? )(
		ra =? ( cyan )( verde )
		'tocorama over btnt
		drop ) 2drop ;

:ddsub | adr --
	( @+ 1? )(
		ra =? (
		( c@+ 1? )( drop ) drop
		( dup c@ 1? )( drop
			ha =? ( blanco )( gris )
			'tocohoja
			over allow2cr
			|cr
			btnt
			( c@+ 1? )( drop ) drop
			) drop
		) drop ) 2drop ;

|---------------

#consola )( 256

:main
	inigui
	[ actual cant 1- <? ( 1+ ) 'actual ! ; ] <der>
	[ actual 1? ( 1- ) 'actual ! ; ] <izq>
	[ actual 3 + cant 1- min  'actual ! ; ] <aba>
	[ actual 3 - 0 max 'actual ! ; ] <arr>
	'exit <esc>
	'nextdom <esp>
	'runactual <enter>

	'editarcodigo <f1>
	'editargrafico <f2>
	'editabitmap <f3>
	'runtimeerr  <f4>
	'configura <f5>


	show clrscr cr
		font-vard-12-bold
		| menu principal
		'dominios dup ddlista cr cr ddsub cr cr
		| programa actual
		font-vard-8-bold verde
|        ha dup "%d: %s" print cr
		'buffertext text

		| consola
		0 rows 4 - at
		dup verde ":r%d " print

		amarillo "Ok " print
		violeta "#" print blanco
		'consola 32 inputline cr

		| botoner
		0 rows 2 - at cr2
		rojo sp 'exit "ESC" btnt
		cyan
		sp 'editarcodigo "F1-EditCode" btnt
		sp 'editargrafico "F2-Draw" btnt
		sp 'editabitmap "F3-Pict" btnt
| DATABASE
| FORM
		sp 'runtimeerr "F4-runtime error" btnt
		sp 'configura "F5-Config" btnt
		cmano ;

|****** BOOT ******
: 0 paper mark
4
restauradom main grabadom ;
