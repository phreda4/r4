| main.txt
| menu principal
| PHREDA 2009
|----------------------------------
^lib/reda4.txt
^lib/gui.txt
^lib/fontt.txt

|^lib/fonth.txt
^lib/trace.txt

^r4code.txt

|----------- estado
#ra
#ha

|------------ cursor
#inicioarch 0
#cntarch 20
#cant 0

#filas 9   | [ 3 4 + ]
#columnas 3
#actual 0

#iniprog 0
#hayarr 0
#hayaba 0

#consola )( 256

#buffertext )( 1024

#juegos "Juegos"
"cuatros"
"baremin" "peleador" "zzDestroyIt" "Tunel"
"slimeatlet" "invasion"
"clickotron"
"pong" "memoria" "carucha"
"carrera" "xe" "cartonman"
"sraceituna" "lagranjita"
"clock"   0

#tests "Test"
"test-dbgui" "test-n2gl"
"test-zonas" "test-gui3" "test-bmps"
"test-font" "test-sprite"
"test-yuvrgb" "test-joy"
"test-atan"
"funciones" "personaje" 0

#devs "Devs"
"jetpatrol" "esqueleto" "escena" "planedef" "crayon"  "bitmap"
"3dlatr"  "pizarra"  "adentro"
"rmation" "mys" "render" "rtable"
0

#edits "Editores"
"edit-code" "edit-spr" "edit-font32"
"edit-fnther"
0

#sistemas "Sistema"
"scancode" "fps" "runtime" "runtime2"
 0

#dominios  'juegos 'edits 'devs 'tests 'sistemas  0

:printarbol | adr --
	( @+ 1? )(
		dup blanco print cr gris
		( c@+ 1? )( drop ) drop
		( dup c@ 1? )( drop
			dup allowcr print sp
			( c@+ 1? )( drop ) drop
			)
		2drop cr ) 2drop ;

|-------------
:tocorama |
	dup 'ra ! ;

:tocohoja
	dup 'ha !
	dup "%s.txt" mprint
	here swap load here =? ( drop ; ) drop
	'buffertext here 256 move
	0 'buffertext 1023 + c!
	'buffertext ( c@+ 1? )(
|		( 33 <? )( c@
|		$23 =? ( drop 1- 0 swap c! ; ) | #
|		$3a =? ( drop 1- 0 swap c! ; ) | :
		$5e =? ( drop 1- 0 swap c! ; ) | ^
		drop ) 2drop
	;

:setopcion
	ha tocohoja drop ;

:nodo | adr
	gris
	( c@+ 1? )( drop ) drop
	( dup c@ 1? )( drop
		ha =? ( blanco )( gris )
		'tocohoja
		over allow2cr btnt
		( c@+ 1? )( drop ) drop
		) drop ;

:tree | adr --
	( @+ 1? )(
		ra =? ( cyan )( verde )
		'tocorama over btnt
		ra =? ( cr cr nodo cr cr )
		drop ) 2drop ;

|-------------
:ddlista | adr --
	( @+ 1? )(
		ra =? ( cyan )( verde )
		'tocorama over sp btnt
		drop ) 2drop ;

:ddsub | adr --
	( @+ 1? )(
		ra =? (
		( c@+ 1? )( drop ) drop
		( dup c@ 1? )( drop
			ha =? ( blanco )( gris )
			'tocohoja
			over allow2cr
			sp btnt
			( c@+ 1? )( drop ) drop
			) drop
		) drop ) 2drop ;

|---------------
:grabadom
	"./" dir
	'ra 8 "nom/main.nom" save
	;

:restauradom
	".//" dir
	'ra "nom/main.nom" load drop
	setopcion
	;


|--------------------------------------------------------------------------------------------
:runactual
	ha 0? ( drop ; )
	grabadom
	"%s.txt" mprint run ;

:editactual
	ha 0? ( drop ; )
	grabadom
	"%s." mprint drop
	"edit-code.txt" run ;

:editarcodigo
	"" 1 "debug.err" save
	"edit-code.txt" run ;

:editargrafico
	"edit-spr.txt" run ;

:editarprojecto
	"r4proj.txt" run ;

:editabitmap
	"edit-bit.txt" run ;

:runtimeerr
	"runtime2.txt" run ;

:configura
	"config.txt" run ;

:nuevoactual
:borractual
	;

:main
	inigui
	show
		key
		'exit >esc<

		[ actual cant 1- <? ( 1+ ) 'actual ! ; ] <le>
		[ actual 1? ( 1- ) 'actual ! ; ] <ri>
		[ actual 3 + cant 1- min  'actual ! ; ] <dn>
		[ actual 3 - 0 max 'actual ! ; ] <up>

		'runactual <enter>
	|	'editactual <tab>

		'editarcodigo <f1>
		'editargrafico <f2>
		'editabitmap <f3>
		'runtimeerr  <f4>
		'configura <f5>

		drop

		font-vard-12-bold
		clrscr home cr
		| menu principal
		0.5 dup fdim
		0.5 dup fpos
		home cr
		'dominios dup ddlista cr cr cr ddsub cr cr
| programa actual
		0.5 dup fdim
		-0.5 0.5 fpos
		home cr blanco
|        ha dup "%d: %s" print cr
		verde sp 'runactual  "Run" btnt
		cyan sp 'editactual "Edit" btnt
		violeta sp 'nuevoactual "New" btnt
		rojo sp 'borractual "Del" btnt 		
		cr cr
		'buffertext text


| pilas
		0.5 dup fdim
		0.5 -0.5 fpos
		home
		"Status MV" print cr
		blanco
|		cols 10 -
|		cols 4 - ( 8 <? )( 1-
|			2dup at


		scr home
		| consola
		0 rows 4 - at
		dup verde ":r%d " print

		amarillo "Ok " print
		violeta "#" print blanco
		'consola 32 inputline cr

		| botoner
		0 rows 2 - at cr2
		rojo sp 'exit "ESC" btnt
		cyan
		sp 'editarcodigo "F1-EditCode" btnt
|		sp 'editarcodigo "F1-New Project" btnt

		sp 'editargrafico "F2-Draw" btnt
		sp 'editabitmap "F3-Pict" btnt
| DATABASE
| FORM
		sp 'runtimeerr "F4-runtime error" btnt
		sp 'configura "F5-Config" btnt
		cflecha ;

|****** BOOT ******
: 0 paper mark restauradom 4 main grabadom ;
