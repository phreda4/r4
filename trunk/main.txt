| main.txt
| menu principal
| PHREDA 2009
|----------------------------------
^lib/reda4.txt
^lib/parse.txt
^lib/gui.txt
^lib/fontt.txt
|^lib/fonth.txt
^lib/trace.txt

#ramaact
#hojaact
#menuprg

:cargasrc
	hojaact 0? ( drop ; ) "%a.txt" mprint
	here swap load drop ;

|--- MENU de Arbol
:tocorama |
	dup 'ramaact ! ;

:tocohoja
	dup 'hojaact !
	cargasrc ;

:marcado
	" %a" negro printc cr ;

:normal
	'tocohoja onLineMove
	" %a" cyan printc cr ;

:conhijos
	azul oscuro linefill
	2 'ccy +!
	swap dup "%a" blanco printc cr
	>>f
	font-vard-8-bold
	( dup >>f over <>? )( swap
		azul linefill
		hojaact =? ( marcado )( normal )
		) 2drop cr2
	font-vard-12-bold
	;

:sinhijos
	'tocorama onLineMove
	swap "%a" verde printc cr ;

:menutree | adr --
	font-vard-12-bold 2 'cch +!
	( dup >>fi over <>? )(
		( dup c@ 13 =? )( drop 1+ ) drop | quita 13
		ramaact =? ( conhijos )( sinhijos )
		) 2drop ;

|----------- estado
:grabaestado
	"./" dir
	'ramaact 8 "nom/main.nom" save
	;

:restauraestado
	".//" dir
	'ramaact "nom/main.nom" load drop
	cargasrc
	;


|------------ para cada dominio
|-- sistema
:confsis
	grabaestado
	"config.txt" run ;

:keysis
	grabaestado
	"scancode.txt" run ;

|-- programa
:preprg
	fonti home
	"Codigo" print cr
	blanco here text
	;

:runprg
	hojaact 0? ( drop ; )
	grabaestado
	"%a.txt" mprint run ;

:editprg
	hojaact 0? ( drop ; )
	grabaestado
	"%a.txt" mprint count
	"./nom/editor.nom" save
	"edit-code.txt" run ;

|--
:runtimeerr
	"runtime2.txt" run ;

:nuevoactual
:borractual
	;
:helpmodo
	;
:menumodo
	;
:addfile
	;
:adddome
	;

#name )( 32

:newprg
	inigui
	'exit >esc<
	show clrscr font-vard-12-bold home
		azul 2 linesfill cr2
		verde dup " :R%d" print
		blanco "NEW " print
		font-vard-8-bold

	| datos
		0.18 0.8 fdim -0.8 0.1 fpos home cr2
		menuprg menutree

		0.6 0.9 fdim 0 0 fpos font-vard-12-bold home
		cr cr blanco
		" Nombre:" print cr cr
		sp 'name 31 getline

		bmouse 0? ( cflecha )( cmano ) drop	;
	;

|---------- main
:main
	inigui
	'exit >esc<
	'runprg <f1>
	'editprg <f2>
	'newprg <f3>

	'confsis <f10>
	'keysis <f11>


	show clrscr font-vard-12-bold home
		azul 2 linesfill cr2
		verde dup " :R%d" print
		blanco "MENU " print
		font-vard-8-bold
	| acciones
		sp sp verde
		'runprg "f1-RUN" sp btnt
		'editprg "f2-EDIT" sp btnt
		'newprg "f3-NEW" sp btnt

	| menu datos
		sp sp sp cyan
		'confsis "f10-CONFIG" sp btnt
		'keysis "f11-KEY" sp btnt

|		violeta 'adddome "+Fold" btnt
|		verde sp 'addfile "+File" btnt
|		rojo sp 'borractual "Del" btnt

	| datos
		0.18 0.8 fdim -0.8 0.1 fpos home cr2
		menuprg menutree
	| prevista
		0.6 0.9 fdim 0 0 fpos fonti home cr2
		blanco here text

		bmouse 0? ( cflecha )( cmano ) drop	;

:loadmenu
	here dup 'menuprg !
	"mem/prg.txt" load
	0 swap !+ 'here ! ;

:savemenu
	menuprg count "mem/prg.txt" save
	;

|****** BOOT ******
: 0 paper
	mark
	loadmenu
	restauraestado
	4 main
	grabaestado
	;
