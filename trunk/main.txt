| menu principal
| PHREDA 2010
|------------------

^r4/lib/reda4.txt
^r4/lib/fontt.txt
^r4/lib/fonth.txt
^r4/lib/font3d.txt
^r4/lib/gui.txt
^r4/lib/trace.txt
^r4/lib/trazo.txt


#lastrun )( 1024
|--------- edit-code
#ncar
#nlin
#nombre )( 64

::cargaeditornl
	'ncar "mem/edit-code.mem" load drop ;

::grabaeditornl
	'ncar 72 "mem/edit-code.mem" save ;


|--------- estado

#padin ":r4 " )( 1024

#actf
#actv
#ruta )( 1024
#ruta> 'ruta

#cntf
#cntv

#indexf )( 8192
#indexf> 'indexf
#indexv )( 8192
#indexv> 'indexv

:ruta.push | "" --
	ruta> strcpyl
	"/" swap 1- strcpyl
	1- 'ruta> !
	'ruta dir ;

:ruta<< | adr -- adr'
 	ruta> 2 -
	( 'ruta >? )( dup c@ $2f =? ( drop 1+ ; ) drop 1- ) drop
	'ruta ;

:ruta.pop | --
	ruta<< 0 over c! 'ruta> !
	'ruta dir ;

|----------- estado
:grabaestado
	'actf 1032 "mem/mp.mem" save ;

:restauraestado
	'actf "mem/mp.mem" load drop
	'ruta count + 'ruta> !
	'ruta dir
	;

|------------------------------
|-------- Dibujo
|------------------------------

:3dpos2gc
	0 0 0 project3d 'yc ! 'xc ! ;

:3dsize2gc
	0 0 0 project3d
	1.0 1.0 0 project3d
	rot - abs 'h ! - abs 'w ! ;

:3dbox
 	3dpos2gc
	gc.box
	gc2win
	home ;

:3dbtnd | 'acc 'dib --
	3dpos2gc
	btnd ;



:,string | "" -- ""'
	here swap ,s ,eol ;

|------------------
:filex | nro -- "name"
	2 << 'indexf + @ ;

:filev | nro -- "name"
	2 << 'indexv + @ ;

|------------------
:,cpyv | vol
	,string indexv> !+ 'indexv> ! ;

:,cpyf | file
	,string indexf> !+ 'indexf> ! ;

|---- genera indice y nombre

:refresh
	mark
	'ruta dir
	'indexv 'indexv> !
	0 ( dup vol 1? )( ,cpyv 1+ ) drop 'cntv !

	actv filev ruta.push
	'indexf 'indexf> !
	0 ( dup file 1? )( ,cpyf 1+ ) drop 'cntf !
	ruta.pop

	actf filex actv filev 'ruta
	"%s%s/%s" mprint 'lastrun strcpy
	empty
	;

|---------------

:clickf
	dup 'actf ! refresh ;

:drawf
	cntf >=? ( ; )
	actf =? ( verde )( negro )
	3dpos2gc
	'clickf over filex btnb
	actf =? ( bordefoco )
	;

:drawfi
	cntf >=? ( ; )
	3dpos2gc gc.box
	dup filex printc ;

:clickv
	dup 'actv ! refresh ;

:drawv
	cntv >=? ( ; )
	actv =? ( azul )( negro )
	3dpos2gc
	'clickv over filev btnh ;

:drawvi
	cntv >=? ( ; )
	3dpos2gc
	dup filev print ;


#cntpanel
#actualypanel

:runprg
	grabaestado
	'lastrun run
	;

:editprg
	grabaestado
	cargaeditornl
	'lastrun 'nombre = 0? (
		'lastrun 'nombre strcpy
		0 'nlin ! 0 'ncar !
		) drop
	grabaeditornl

	"r4/System/edit-code.txt" run
	;
|----------------------------------
:actv+! | nro --
	actv +
	cntv >=? ( cntv 1- nip )
	-? ( 0 nip )
	'actv !
	refresh ;

:actf+! | nro --
	actf +
	cntf >=? ( cntf 1- nip )
	-? ( 0 nip )
	'actf !
	refresh ;

|------------------------------
|------------------------------
#yvol 0
:drawvolumes | exec --
	mpush
	2.0 1.0 1.0 mscale
	-6.5 -4.2 yvol + 0 mtrans
	0 3dsize2gc
	-4.0 ( 5.0 <? )( swap
		pick2 exec
		1+ swap
		1.1
		0.0 over 0.0 mtransi
		+ )
	3drop
	mpop ;

#yfilv 0
#yfil -4.5
#yfild 0

:drawfiles | exec --
	mpush
	3.0 1.0 1.0 mscale
|	yfilv 'yfil +!
	-3.5 yfil 0 mtrans
	0 3dsize2gc
	-4.0 ( 4.0 <? )(
		-4.0 ( -1.0 <? )( rot
			pick3 exec
			1+ rot rot
			1.1
			dup 0.0 0.0 mtransi
			+ ) drop
		-3.3 1.2 0.0 mtransi
		1.2 + )
	3drop
	mpop ;

:drawfiles2 | exec --
	mpush
	4.0 1.0 1.0 mscale
	-3.5 yfil 0 mtrans
	3dsize2gc
	0 ( cntf <? )(
			over exec 1+
			5.0 0.0 0.0 mtransi

			over exec 1+
			-5.0 1.2 0.0 mtransi
			) drop
	mpop ;

:consola
	scr 12 font home cr2 sp ;

:mododominio
	[ -1 actv+! ; ] <up>
	[ 1 actv+! ; ] <dn>
	[ -1 actv+! ; ] <le>
	[ 1 actv+! ; ] <ri>
	mpush
	1.2 1.1 1.0 mscale
	'drawv drawvolumes
|	0.8 0.8 1.0 mscale
	'drawf drawfiles
	mpop
	consola cyan
	'padin print

	cflecha ;

:modoitem
	[ -3 actf+! 0.01 'yfilv ! ; ] <up>
	[ 3 actf+! -0.01 'yfilv ! ; ] <dn>
	[ -1 actf+! ; ] <le>
	[ 1 actf+! ; ] <ri>
|	font3d
	mpush
	0.8 0.8 1.0 mscale
	'drawv drawvolumes
	1.8 1.4 1.0 mscale
	'drawf drawfiles
	mpop
	consola verde
	'padin print
	clapiz
	;

|---------------------
:modobuscar
	blanco
	mpush
	'drawvi drawvolumes
|	'drawfi drawfiles
	mpop
	consola verde
	'padin 32 inputline
	blanco trazoIn
	clapiz ;

#estado 'modoitem

:cambiaestado
	estado
	'modoitem =? ( 'mododominio )( 'modoitem ) nip
	'estado ! ;

:main
	inigui
	'exit >esc<
	'cambiaestado <spc>
	'runprg dup <enter> <f1>
	'editprg <f2>
|	'newprg <f3>

	refresh
	show clrscr

		32 gc.top $00c gc.hfill
		font-vard-12-bold home cr2
		rojo
		sp sp 'exit "esc-Exit" btnt
		verde
		sp 'runprg "f1-Run" btnt
		sp 'editprg "f2-Edit" btnt
|		sp 'newprg "f3-NewPrg" btnt
|		violeta sp 'adddome "+Fold" btnt
|		rojo sp 'borractual "Del" btnt
|		sp sp 'runtimeerr "f9-RUNERR" btnt
|		sp 'confsis "f10-CONFIG" btnt
|		sp 'fullset "f11-FULL" btnt
|		26 gc.botton $ccc gc.hfill
|		font-vard-8-bold home cr2
|		font-vard-12-bold
|		verde 'clicknew "* NEW *" sp btnt

		32 gc.botton home
		sp
		verde dup " :R%d" print
		blanco " RUN " print

		'lastrun print

		32 32 gc.vbetween
		1.0 3dmode
		0 0 12.0 mtrans
		font-vard-8
		estado exec
;

:	|$808080 paper
	0 paper
	mark
	"r4" ruta.push
	restauraestado
	4 main
	grabaestado
;
