| menu principal
| PHREDA 2010
|------------------

^r4/lib/math.txt
^r4/lib/fontt.txt
^r4/lib/fonth.txt
^r4/lib/btn.txt
^r4/lib/trazo.txt
^r4/lib/usetex.txt
^r4/lib/usebmr.txt

^r4/system/r4code.txt

^r4/lib/trace.txt

|--------- edit-code
#ncar
#nlin
#nombre )( 64

::cargaeditornl
	'ncar "mem/edit-code.mem" load drop ;

::grabaeditornl
	'ncar 72 "mem/edit-code.mem" save ;


|--------- estado
#ok " ok"
#mensaje 'ok
#lastrun )( 1024
#padin )( 1024

#actf
#actv
#ruta )( 1024
#ruta> 'ruta

#cntf
#cntv

#indexf )( 8192
#indexf> 'indexf
#indexv )( 8192
#indexv> 'indexv

:ruta.push | "" --
	ruta> strcpyl
	"/" swap 1- strcpyl
	1- 'ruta> !
	'ruta dir ;

:ruta<< | adr -- adr'
 	ruta> 2 -
	( 'ruta >? )( dup c@ $2f =? ( drop 1+ ; ) drop 1- ) drop
	'ruta ;

:ruta.pop | --
	ruta<< 0 over c! 'ruta> !
	'ruta dir ;

|----------- estado
:grabaestado
	'actf 1032 "mem/mp.mem" save ;

:restauraestado
	'actf "mem/mp.mem" load drop
	'ruta count + 'ruta> !
	'ruta dir
	;

|------------------------------
|-------- Dibujo
|------------------------------

:3dpos2gc
	0 0 0 project3d 'yc ! 'xc ! ;

:3dsize2gc
	0 0 0 project3d
	1.0 1.0 0 project3d
	rot - abs 'h ! - abs 'w ! ;

:3dbox
 	3dpos2gc
	gc.box
	gc2win
	home ;

:3dbtnd | 'acc 'dib --
	3dpos2gc
|	btnd
 ;

:,string | "" -- ""'
	here swap ,s ,eol ;

|------------------
:filex | nro -- "name"
	2 << 'indexf + @ ;

:filev | nro -- "name"
	2 << 'indexv + @ ;

|------------------
:,cpyv | vol
	,string indexv> !+ 'indexv> ! ;

:,cpyf | file
	,string indexf> !+ 'indexf> ! ;

|---- genera indice y nombre

:refresh
	mark
	'ruta dir
	'indexv 'indexv> !
	0 ( dup vol 1? )( ,cpyv 1+ ) drop 'cntv !

	actv filev ruta.push
	'indexf 'indexf> !
	0 ( dup file 1? )( ,cpyf 1+ ) drop 'cntf !
	ruta.pop

	actf filex actv filev 'ruta
	"%s%s/%s" mprint 'lastrun strcpy
	empty
	;

|---------------

:clickf
	dup 'actf ! refresh ;

:clicknew
	;

:clickv
	dup 'actv ! refresh ;

:runprg
	grabaestado
	'lastrun run
	;

:editprg
	grabaestado
	cargaeditornl
	'lastrun 'nombre = 0? (
		'lastrun 'nombre strcpy
		0 'nlin ! 0 'ncar !
		) drop
	grabaeditornl

	"r4/system/edit-code.txt" run
	;
|----------------------------------
:actv+! | nro --
	actv +
	cntv >=? ( cntv 1- nip )
	-? ( 0 nip )
	'actv !
	refresh
	cntf 1- actf min 'actf ! ;

:actf+! | nro --
	actf +
	cntf >=? ( cntf 1- nip )
	-? ( 0 nip )
	'actf !
	refresh ;

|----------------------------------
:helpmodo
	;

:memmapv
	d.get memmap
	;

#voc-system
"BYE" "HELP" "RESETVM" "MEMMAP"
( 0 )

#exe-system
'exit 'helpmodo 'resetvm 'memmapv
0

:nextw
    ( c@+ 32 >? )( drop )
	0? ( drop 1- )( drop )
	;

:pushnro | val --
	basen
	10 =? ( drop d.pushDec ; )
	2 =? ( drop d.pushBin ; )
	drop
	d.pushHex ;
	;

:palabraconsola | str -- str'
	( dup c@ 33 <? )( 0? ( nip ; ) drop 1+ )		| quitar espacios
|	$5e =? ( drop esinclude ; )			| $5e ^  Include
|	$7c =? ( drop escomentario ; )		| $7c |	 Comentario
|	$3A =? ( drop esdefinicion ; )		| $3a :  Definicion
|	$23 =? ( drop esvariable ; )		| $23 #  Variable

|	$22 =? ( drop escadena ; )			| $22 "	 Cadena

	$27 =? ( drop 1+ dup 				| $27 ' Direccion
		dup ?macro 1? ( 3drop "mACROnOTdIR" 'mensaje ! 0 ; ) drop
		?palabra 1? ( nip stepadr nextw ; ) 2drop
		"NOTdIR" seterror 3 - 0 ; )
	drop
	dup ?numero 1? ( drop pushnro nip ; ) drop
	dup ?fnumero 1? ( drop d.pushFix nip ; ) drop
	dup 'voc-system inupp? 1? ( 1- 2 << 'exe-system + @ exec nextw ; ) drop
	dup ?macro 1? ( stepmacro nextw ; ) drop
	dup ?palabra 1? ( stepword nextw ; ) drop
	2drop 0
	"??" 'mensaje !  ;

:correconsola
	'padin ( palabraconsola 0? ) drop
	0 'padin !
	;

:carpetasyarchivos
	home cr2
	|---- carpetas
	0 ( cntv <? )(
		actv =? ( azul )( negro )
		'clickv
		over filev
		8 sp btntf
		 cr cr
		1+ ) drop cr cr

	chome cr2
	|---- archivos
	0 ( cntf <? )(
		actf =? ( verde )( negro )
		'clickf
		over filex |allow2cr
		16 sp btntf cr cr
		bout? 1? ( chome cr2 ) drop
		1+ ) drop
	;

#txtruntime 0
|r4/system/f5-spr.txt
|IP: -715345168 541487096
|D: 541495296 )
|R: 4227140 541252130 4660219 541251766 )
|code:541224944 cnt:27197
|data:4354032 cnt:331640
|20 175 479 3783 32

:leererrores
	here
	dup "runtime.err" load
	over <=? ( 	drop
			dup "debug.err" load
			over <=? ( 	2drop ; )
			)
	0 swap c!+ | termina con cero
	'here ! 'txtruntime !
	;

:runtimeerror
	txtruntime 0? ( drop ; )
	cr cr
	font-vard-8-bold
	rojo print cr cr
	[ "r4/system/runtime2.txt" run ; ] "VIEW" btnt
	[ 0 dup "runtime.err" save
	  0 dup "debug.err" save
		0 'txtruntime ! ; ] "DEL" sp violeta btnt
	;

|----------------------------------
#cya? 1

:main
	usogui
	refresh
	show clrscr
		scr |32 gc.top $00c gc.hfill
		font-vard-12-bold home cr2
		rojo
		sp sp 'exit "esc-Exit" btnt
		verde
		sp 'runprg dup <f1> "f1-Run" btnt
		sp 'editprg dup <f2> "f2-Edit" btnt
		violeta 'clicknew dup <f3> "f3-NEW" sp btnt
		[ cya? 1 xor 'cya? ! ; ] "f4-Pad" sp btnt

|		sp 'newprg "f3-New" btnt
|		violeta sp 'adddome "+Fold" btnt
|		rojo sp 'borractual "Del" btnt
|		sp sp 'runtimeerr "f9-RUNERR" btnt
|		sp 'confsis "f10-CONFIG" btnt
|		sp 'fullset "f11-FULL" btnt

		32 32 gc.vbetween home
		cya? 1? ( carpetasyarchivos ) drop

		chome cr2 verde dup " :R%d " print
		amarillo mensaje print
		blanco 'lastrun sp print
		cr ">" print 'padin 64 inputline cr
		verde vm.dump

		runtimeerror
|		blanco trazo.in

		cminiflecha

		[ cya? 1 xor 'cya? ! ; ] <f4>
		keypad
		[ -1 actf+! ; ] <up>
		[ 1 actf+! ; ] <dn>
		[ -1 actv+! ; ] <pgup>
		[ 1 actv+! ; ] <pgdn>
		'correconsola <enter>
		'exit >esc<
		;


:	$0
	paper
	mark

	"r4" ruta.push
	restauraestado
	leererrores
	4 main
	grabaestado
;
