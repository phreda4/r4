| menu principal
| PHREDA 2010
|------------------

^lib/reda4.txt
^lib/fontt.txt
^lib/fonth.txt

^lib/gui.txt
^lib/trace.txt

#lastrun )( 64
|--------- edit-code
#ncar
#nlin
#nombre )( 64

:cargaeditornl
	'ncar "mem/edit-code.mem" load drop ;

:grabaeditornl
	'ncar 72 "mem/edit-code.mem" save ;

|--------- estado
#actf
#actv

#ruta )( 1024
#ruta> 'ruta

:ruta.push | "" --
	ruta> strcpyl
	"/" swap 1- strcpyl
	1- 'ruta> !
	'ruta dir ;

:ruta<< | adr -- adr'
 	ruta> 2 -
	( 'ruta >? )( dup c@ $2f =? ( drop 1+ ; ) drop 1- ) drop
	'ruta ;

:ruta.pop | --
	ruta<< 0 over c! 'ruta> !
	'ruta dir ;

|----------- estado
:grabaestado
	'actf 1032 "mem/mp.mem" save ;

:restauraestado
	'actf "mem/mp.mem" load drop
	'ruta count + 'ruta> !
	'ruta dir
	;

|------------------------------
|-------- Dibujo
|------------------------------

:3dpos2gc
	0 0 0 project3d 'yc ! 'xc ! ;

:3dsize2gc
	0 0 0 project3d
	1.0 1.0 0 project3d
	rot - abs 'h ! - abs 'w ! ;

:3dbox
 	3dpos2gc
	gc.box
	gc2win
	home ;

:3dbtnd | 'acc 'dib --
	3dpos2gc
	btnd ;

|------------------------------
|------------------------------
:drawvolumes | exec --
	mpush
	2.0 1.0 1.0 mscale
	5.8 -4.2 0 mtrans
	0 3dsize2gc
	-4.0 ( 5.0 <? )( swap
		pick2 exec
		1+ swap
		1.2
		0.0 over 0.0 mtransi
		+ )
	3drop
	mpop ;

:drawfiles | exec --
	mpush
	2.0 1.0 1.0 mscale
	-6.0 -3.6 0 mtrans
	0 3dsize2gc
	-4.0 ( 4.0 <? )(
		-4.0 ( 5.0 <? )( rot
			pick3 exec
			1+ rot rot
			1.2 0.0 0.0 mtransi
			2.0 + ) drop
		-6.0 1.2 0.0 mtransi
		1.2 + )
	3drop
	mpop ;


|		violeta sp 'adddome "+Fold" btnt
|		rojo sp 'borractual "Del" btnt
|		sp sp 'runtimeerr "f9-RUNERR" btnt
|		sp 'confsis "f10-CONFIG" btnt
|		sp 'fullset "f11-FULL" btnt
|		26 gc.botton $ccc gc.hfill
|		font-vard-8-bold home cr2
|		font-vard-12-bold
|		verde 'clicknew "* NEW *" sp btnt


:runprg
	actf file 0? ( drop ; )
	grabaestado
	'ruta "%s%s" mprint run
	;

:editprg
	actf file 0? ( drop ; )
	grabaestado
	'ruta "%s%s" mprint 'lastrun strcpy
	cargaeditornl
	'lastrun 'nombre = 0? (
		'lastrun 'nombre strcpy
		0 'nlin ! 0 'ncar !
		) drop
	grabaeditornl
	"r4/System/edit-code.txt" run
	;

:clickf
	dup 'actf ! ;

:drawf
	actf =? ( cyan )( verde )
	dup file 0? ( drop ; )
	3dpos2gc
	'clickf swap btnb
	actf =? ( bordefoco )
	;

:clickv
	dup vol ruta.push ;

:drawv
	dup vol 0? ( drop ; )
	3dpos2gc
	'clickv swap btnb ;

:panel
	mpush
	5.0 1.0 1.0 mscale
	3dsize2gc
	0.9 -5.0 0 mtrans
	rojo 3dpos2gc | titulo
	[ ruta.pop ; ] 'ruta btnb
	mpop
	verde
	'drawf drawfiles
	azul
	'drawv drawvolumes
	mpush
	2.0 0.5 1.0 mscale
	3dsize2gc
    -6.4 -5.1 0 mtrans
|	'exit 'iarr 3dbtnd
	0 10.2 0 mtrans
|	'exit 'iaba 3dbtnd
	mpop

	mpush
	0.5 3.0 1.0 mscale
	3dsize2gc

    -5.1 0 0 mtrans
|	'exit 'iizq 3dbtnd

	+12.2 0 0 mtrans
|	'exit 'ider 3dbtnd
	mpop
	;

:actf+! | nro --
	actf +
	dup file 0? ( 2drop actf file 0? ( dup 'actf ! ) drop ; ) drop
	'actf ! ;

:main
	inigui
	[ 'ruta "r4/" = 1? ( drop exit ; ) drop ruta.pop ; ] >esc<
	'runprg dup <enter> <f1>
	'editprg <f2>
|	'newprg <f3>
	[ -5 actf+! ; ] <up>
	[ 5 actf+! ; ] <dn>
	[ -1 actf+! ; ] <le>
	[ 1 actf+! ; ] <ri>
	show clrscr

		32 gc.top $00c gc.hfill
		font-vard-12-bold home cr2
		rojo
		sp sp 'exit "esc-Exit" btnt
		verde
		sp 'runprg "f1-Run" btnt
		sp 'editprg "f2-Edit" btnt
|		sp 'newprg "f3-NewPrg" btnt

		sp
		verde dup " :R%d" print
		blanco " MAIN MENU " print

		actf file 1? ( 'ruta "%s%s" print )( drop )

		32 0 gc.vbetween
		home cr2 cr
		1.0 3dmode
		0 0 12.0 mtrans
		font-vard-8
		panel
		bmouse 0? ( cflecha )( cmano ) drop
		;

:	|$808080 paper
	0 paper
	mark
	"r4" ruta.push
	restauraestado
	4 main
	grabaestado
;
