| main.txt
| menu principal
| PHREDA 2009
|----------------------------------
^reda4.txt
^gui3.txt

|------------ cursor
#inicioarch 0
#cntarch 20
#vuelta 0

#cant 0
#iniprog 0
#hayarr 0
#hayaba 0

#indice	)( 2048

#sndclick

#juegos "Juegos"
"baremin" "peleador" "zzDestroyIt" "Tunel"
"slimeatlet" "invasion"
"clickotron"
"pong" "memoria" "carucha"
"carrera" "xe" "cartonman"
"sraceituna" "lagranjita"
"clock"   0

#tests "Test"
"test-n2gl"
"test-zonas" "test-gui3" "test-bmps"
"test-font" "test-sprite"
"test-yuvrgb" "test-joy"
"test-atan"
"funciones" "personaje" 0

#devs "Devs"
"jetpatrol" "esqueleto" "escena" "planedef" "hershey" "crayon"  "bitmap"
"3dlatr"  "pizarra"  "adentro" |"rmation2"
"rmation" "mys" "render"
0

#edits "Editores"
"edit-code" "edit-spr" "edit-font32"
"edit-fnther"
0

#sistemas "Sistema"
"fps" "runtime" "runtime2"
"scancode" 0

#dominios  'juegos 'edits 'devs 'tests 'sistemas 'juegos 0
#actuald 'dominios

#filas 9   | [ 3 4 + ]
#columnas 3

:calbotons
	iniprog 0 >? ( 1 )( 0 ) nip 'hayarr !
	iniprog 3 9 * + cant <? ( 1 )( 0 ) nip 'hayaba ! ;

:nextpag
	iniprog  3 9 * +  cant >=? ( drop ; )
	'iniprog ! calbotons ;
:prevpag
	iniprog 3 9 * - 0 <? ( drop ; )
	'iniprog ! calbotons ;

:haceindice |'lista --
	'indice swap
	0 'cant !
	( c@+ 1? )( drop
		dup 1-  rot !+ swap
		1 'cant +!
		( c@+ 1? )( drop ) drop
		) drop
	swap !  ;

:nextdom | --
	actuald 4+
:hacedom  | adr --
	dup @ 0? ( 2drop 'dominios dup @ )
	( c@+ 1? )( drop ) drop haceindice
	'actuald !
	iniprog (  cant >? )( 4 6 * - ) 'iniprog !
	calbotons ;

:grabadom
	"./" dir
	'actuald 4 "nom/main.nom" save
	;

:testdominio | --
	'dominios ( actuald =? ( drop ; ) @+ 1? )( drop ) 2drop
	'dominios 'actuald ! ;

:restauradom
	".//" dir
	'actuald "nom/main.nom" load drop
	testdominio
	actuald hacedom ;

|--------------------------------------------------------------------------------------------
#actual 0
#fileact 0
#compnombre )( 128
#nombre )( 32

:compone | "ext" n -- "n.ext"
	2 << 'indice + @ 'compnombre swap
	( c@+ 1? )( rot c!+ swap )  2drop
	swap
	( c@+ 1? )( rot c!+ swap ) rot c!
	drop 'compnombre
	;

:runactual
	grabadom
	".txt" actual compone run ;

|---------------------------------------
:drawbot
	$ff00 $2200 gc.vdeg
	lfill gc.frod sfill ;

:cadaentrada | a -- a
	2dup 3 * + iniprog +
	cant >=? ( drop ; )
	actual =? ( 1.2 fzoom drawbot )
	[ dup 'actual ! ; ] onMove
	'runactual onClick
	2 << 'indice + @
	blanco 1 fonth printc
;

:filelist
	3 9 'cadaentrada table ;


|---------------------------------------
:botoneracc
	[ dup 2 << 'dominios + hacedom ; ] onClick
	actuald 'dominios - 2 >>
	=? ( $ff 0 )( 0 $ff ) vbtn
	dup 2 << 'dominios + @ 0? ( drop ; )
	2 fonth cr2 blanco printc ;

:botonera
	5 'botoneracc hlist ;

|---------------------------------------
:editarcodigo
	"" 1 "debug.err" save
	"edit-code.txt" run ;

:editargrafico
	"edit-spr.txt" run ;

:editarprojecto
	"r4proj.txt" run ;

:editabitmap
	"edit-bit.txt" run ;

:runtimeerr
	"runtime2.txt" run ;

:btfunc
	2 fonth blanco
	spc
	$c000 $f0f0f linkcol
	'editarcodigo "F1-R4Edit" link spc
	'editargrafico "F2-R4Draw" link spc
	'editabitmap "F3-R4Bit" link spc
	'runtimeerr "F4-ERROR" link
	;

:main
	inigui
	[ actual cant 1- <? ( 1+ ) 'actual ! ; ] <der>
	[ actual 1? ( 1- ) 'actual ! ; ] <izq>
	[ actual 3 + cant 1- min  'actual ! ; ] <aba>
	[ actual 3 - 0 max 'actual ! ; ] <arr>
	'exit <esc>
	'nextdom <esp>
	'runactual <enter>

|	'editarprojecto <f1>
	'editarcodigo <f1>
	'editargrafico <f2>
	'editabitmap <f3>
	[ "runtime2.txt" run ; ]  <f4>
	show clrscr
		0.9 0.7 fdim
		0.0 0.0 fpos
		filelist

		1.0 0.1 fdim
		0 0.9 fpos botonera
		0 -0.9 fpos  btfunc

		0.08 dup fdim
		0.9 -0.9 fpos
		rojo 'exit 'ifin btnd

|		8 font dup ":r%d" verde print actuald @ "* %s *" rojo printc
|		scr fonti dumpgui
		cmano
		;

|****** BOOT ******
:
	0 paper
	restauradom
	main
	grabadom
	;
