| main.txt
| menu principal
| PHREDA 2009
|----------------------------------
^lib/reda4.txt
^lib/gui.txt
^lib/fontt.txt
|^lib/fonth.txt
^lib/trace.txt
^r4code.txt

|----------- estado
#ra
#ha

|------------ cursor
#inicioarch 0
#cntarch 20
#cant 0

#filas 9   | [ 3 4 + ]
#columnas 3
#actual 0

#iniprog 0
#hayarr 0
#hayaba 0

#consola )( 1024
#mensaje ":Ok." )( 32
#buffertext )( 1024


:mensaje!
	'mensaje strcpy ;

#juegos "Juegos"
"cuatros"
"baremin" "peleador" "zzDestroyIt" "Tunel"
"slimeatlet" "invasion"
"clickotron"
"pong" "memoria" "carucha"
"carrera" "xe" "cartonman"
"sraceituna" "lagranjita"
"clock"   0

#tests "Test"
"test-dbgui" "test-n2gl"
"test-gui3" "test-bmps"
"test-font" "test-sprite"
"test-yuvrgb" "test-joy"
"test-atan"
"funciones" "personaje" 0

#devs "Devs"
"jetpatrol" "esqueleto" "escena" "planedef" "crayon"  "bitmap"
"3dlatr"  "pizarra"  "adentro"
"rmation" "mys" "render" "rtable"
0

#edits "Editores"
"edit-code" "edit-spr" "edit-font32"
"edit-fnther"
0

#sistemas "Sistema"
"scancode" "fps" "runtime" "runtime2"
 0

#dominios  'juegos 'edits 'devs 'tests 'sistemas  0

|-------------
:tocorama |
	dup 'ra ! ;

:tocohoja
	dup 'ha !
	dup "%s.txt" mprint
	here swap load here =? ( drop ; ) drop
	'buffertext here 256 move
	0 'buffertext 1023 + c!
	'buffertext ( c@+ 1? )(
|		( 33 <? )( c@
|		$23 =? ( drop 1- 0 swap c! ; ) | #
|		$3a =? ( drop 1- 0 swap c! ; ) | :
|		$5e =? ( drop 1- 0 swap c! ; ) | ^
		drop ) 2drop
	;

:setopcion
	ha tocohoja drop ;

:nodo | adr
	font-vard-8-bold
	( c@+ 1? )( drop ) drop
	( dup c@ 1? )( drop
		ha =? ( cyan )( gris )
		'tocohoja
		over sp sp sp sp btnt cr cr2
		( c@+ 1? )( drop ) drop
		) drop
	font-vard-12-bold ;

:tree | adr --
	( @+ 1? )(
		ra swap
		verde 'tocorama over sp btnt cr cr2
		=? ( nodo )
		drop ) 2drop ;

|---------------
:grabadom
	"./" dir
	'ra 8 "nom/main.nom" save
	;

:restauradom
	".//" dir
	'ra "nom/main.nom" load drop
	setopcion
	;

|--------------------------------------------------------------------------------------------
:runactual
	ha 0? ( drop ; )
	grabadom
	"%s.txt" mprint run ;

:editactual
	ha 0? ( drop ; )
	grabadom
	"%s." mprint drop
	"edit-code.txt" run ;

:editarcodigo
	"" 0 "debug.err" save
	"edit-code.txt" run ;

:editargrafico
	"edit-spr.txt" run ;

:editarprojecto
	"r4proj.txt" run ;

:editabitmap
	"edit-bit.txt" run ;

:runtimeerr
	"runtime2.txt" run ;

:configura
	"config.txt" run ;

:nuevoactual
:borractual
	;

#voc-system
'exit "bye"
'editactual "edit"
'editargrafico "draw"
'editabitmap "img"
'runtimeerr  "error?"
'configura "config"
0

|--------- cursor de caja
#bx1 #by1 #bx2 #by2

:boxcursor | --
	[ xymouse 'by2 ! 'bx2 ! ; ] onMove
	[ xymouse 'by1 ! 'bx1 ! ; ] onDn
	[ 0 dup 2dup 'bx1 !+ !+ !+ ! ; ] onClick

	127 alpha
	cyan oscuro
	bx1 by1
	2dup op
	over by2 pline
	bx2 by2 pline
	bx2 over pline
	pline poli

	verde
	bx1 by1
	2dup op
	over by2 line
	bx2 by2 line
	bx2 over line
	line
	255 alpha
	;

#modon 0
|------- modos
:modo-menu

	'runactual <f1>

	scr home
	azul 2 linesfill
	cr2
	gris sp [ 1 'modon ! ; ] " Menu " btnt |print

	blanco ha 1? ( sp printx ".txt" printx )( drop )

	verde sp 'runactual dup <F1> "Run" btnt
	cyan sp 'editactual dup <F2> "Edit" btnt
	violeta sp 'nuevoactual dup <F3> "New" btnt
	rojo sp 'borractual dup <F4> "Del" btnt

	| menu
	0.2 0.8 fdim -0.8 0.1 fpos home
	cr2 'dominios tree

	| programa actual
	0.7 0.8 fdim 0  0.1 fpos home blanco
	cr2 'buffertext text
	;

:modo-vm
	scr home
	azul 2 linesfill
	cr2
	gris sp [ 2 'modon ! ; ] " MV " btnt |print

	| pilas
	0.1 0.8 fdim
	0.7 0.1 fpos home
	verde gc.box
	blanco "Dato" printc cr cr2 amarillo
	dpila> ( 'dpila >? )( 4 -
		dup @ "%d" printc cr
		) drop
	0.9 0.1 fpos home
	verde gc.box
	blanco "Retorno" printc cr cr2 amarillo
	rpila> ( 'rpila >? )( 4 -
		dup @ "%d" printc cr
		) drop
	;

:modo-define
	scr home
	azul 2 linesfill
	cr2
	rojo sp [ 3 'modon ! ; ] " DEFINE " btnt |print
	;

:modo-gui
	scr home
	azul 2 linesfill
	cr2
	gris sp [ 0 'modon ! ; ] " GUI " btnt |print

	boxcursor
	;


#modos 'modo-menu 'modo-vm 'modo-define 'modo-gui

:esnumero?
    dup ?numero 0? (
		drop
		?fnumero 0? ( drop ; )
		) drop
	rot drop
	1 'modon !
	d.push
	rdrop ;

:essis? | sistema
	;

:esmacro? |	dup ?macro 1? ( rot drop esmacro ; ) drop		| macro
	;

:espalabra? |	dup	?palabra  1? ( nip espalabra ; ) drop		| palabra
	;

:palabraconsola | str -- str'
	( dup c@ 33 <? )( 0? ( nip "Ok" swap ; ) drop 1+ )		| quitar espacios
|	$5e =? ( drop esinclude ; )			| $5e ^  Include
|	$7c =? ( drop escomentario ; )		| $7c |	 Comentario
|	$3A =? ( drop esdefinicion ; )		| $3a :  Definicion
|	$23 =? ( drop esvariable ; )		| $23 #  Variable
|	$22 =? ( drop escadena ; )			| $22 "	 Cadena
|	$27 =? ( drop 1+ dup 				| $27 ' Direccion
|		dup ?macro 1? ( 3drop " no tiene direccion" mensaje! 0 ; ) drop
|		?palabra 1? ( nip esdir ; ) 2drop
|		" no existe direccion" seterror 3 - 0 ; )
|	drop
	drop
	esnumero? essis? esmacro? espalabra?
	drop
	"NOTfOUND" 0 ;

:correconsola
	'consola ( palabraconsola 0? ) drop
    mensaje!
	0 'consola !
	;

|---------- main
:main
	inigui
	'exit >esc<
	'correconsola <enter>
	show clrscr font-vard-12-bold
		'modos modon 2 << + @ exec
		| consola
		scr 9 font home 0 rows 1- at
		dup verde ":r%d " print
		rojo 'mensaje printx
		blanco ">" print
		'consola 32 inputline cr
		bmouse 0? ( cflecha )( cmano ) drop
		;

|****** BOOT ******
: 0 paper mark restauradom 4 main grabadom ;
