| eval-code.txt
| PHREDA 2007
|---------------------------------------
^reda4.txt
^gui3.txt
^r4code.txt
^medit.txt
^fontt.txt
^fonts/vardana8.txt

#nombre )( 64
#linea )( 1024	| error y mem aux

#primerword
#:hojapal 0

#pilavars )( 1024
#pilavars> 'pilavars

::+variable | nroword --
	pilavars> !+ 'pilavars> ! ;

::-executa | --
	-4 'pilavars> +! ;

|-------- infopal
:printwordal | n --
	4 << 'indicepal +
	@+ "%w " print @+ " %h  " print @+ " %h  " print @+ " %h  " print cr
	drop ;

:printwordin | n --
	4 << 'indicepal +
	@+ "%w " verde print
	@+ drop |"%h " cyan print
	blanco
	@+ "%h " print

|	dup 24 >> "R:%d " print
|	dup 8 << 24 >> "D:%d " print
|	$ff and "F:%b " print

	@+ "%h " print cr
	drop ;

|----------------- muestra lista palabras
::printwordname | n --
|	dup "%d." blanco print | nro de palabra
	4 << 'indicepal +
	indicepal> >=? ( drop ; )
	dup 8 + @ 1 and? ( drop @ "#%w " violeta print ; ) drop
	@+ ":%w " rojo print
	drop
	;
:ll
	verde
	@+ drop |"%d " print direccion
	@+
	dup 24 >> "%d" print
	dup 8 << 24 >> "%d" print
	$fff and  "%b" print
| 	"%h" print
	@ "%d" print
	;

:drawtoken
	primerword
	rows ( 1? )( 1- swap
|		actword =? ( vidi )
		dup printwordin
|		dup printwordname cr
|		actword =? ( vidi )
		1+ swap  )
	2drop
	;

|------------------ definicion con ayuda
:tokeninfolist | cnt adr --
	( swap 1? )( 1- swap | cnt adr
		|dup "%h :" print
		<<ip =? ( ">" print )
|		dup 4 - movstackprint
		tokenprint
|		<<ip 4+ =? ( vidi )
		" " print
		lout? 1? ( cr ) drop
		) 2drop ;


:esvar | adr --
	@ "#%w " violeta print ;

:exprintword | n --
	4 << 'indicepal +
	indicepal> >=? ( drop ; )
	dup 8 + @ 1 and? ( drop esvar ; ) drop
	dup @ ":%w " rojo print
	dup 12 + @
	swap 4+ @
	tokeninfolist ;

:drawtrace
	dumpvm
	'pilaexe
	( pilaexe> <? )( @+ exprintword cr ) drop
	;

|||||||||||||||||||||||||

|------------------ dibuja la memoria
:printvariable | n --
	4 << 'indicepal +
	indicepal> >=? ( drop ; )
	dup 8 + @ 1 nand? ( 2drop ; ) drop
	@+ "#%w " violeta print amarillo
	@ @ " %d " print
|	drop
	;

:drawmemvar
	'pilavars
	( pilavars> <? )(
		@+ printvariable cr ) drop
	;

|------------------ estado MV
:drawstat
	cntvars cntword "pal:%d vars:%d " print cr amarillo
	str> str - 	cte> cte - 2 >> prog> prog - 2 >> "tok:%d cons:%d str:%d" print cr
	<<ip "IP:%h " verde print rojo
	<<ip @ dup $ff and swap 8 >> swap "%d-%d" print
	;

|-------------------------------------------------------------

||||||||||||||||||||||||||||||||||||||||||||||||
:getcte		8 >> cte + @ ;
:getstr     8 >> str + ;
:nro>dicn   8 >> 4 << 'indicepal + @ ;

:coldefw nro>dicn ":%w" ,print ;
:coldefv nro>dicn "#%w" ,print ;
:colitd	getcte "%d" ,print ;
:colith getcte "$%h" ,print ;
:colitb	getcte "%%%b" ,print ;
:colitf getcte "%f" ,print ;
:colits	getstr """%s""" ,print ;
:colwor	nro>dicn "%w" ,print ;
:colvar	nro>dicn "%w" ,print ;
:coldwo	nro>dicn "'%w" ,print ;
:coldva	nro>dicn "'%w" ,print ;

#codei 0 'coldefw 'coldefv colitd colith colitb colitf colits colwor colvar coldwo coldva

:,token | a -- a.
	@+ dup $ff and
	12 <? ( 2 << 'codei + @ exec ; ) nip
	nro>macro ,s ;

:gencod | adrr --
	dup @ swap 8 + @
	( 1? )( 1- swap ,sp ,token swap
		$f nand? ( ,cr ) )
	2drop ;

||||||||||||||||||||||||||||||||||||||||||||||||
:genvar | adrr --
	@
	( @+ dup $ff and
		1 =? ( 0 nip ) | corta con defc
		2 =? ( 0 nip ) | corta con defv
		1? )( 	 | dirv n c
		12 <? ( 2 << 'codei + @ exec )( 2drop )
		,sp
		$3c nand? ( ,cr )
		)
	3drop ;

:gencodigo | nro llamadas -- nro llamadas
	over 4 << 'indicepal +
	dup 8 + @ 1 and? ( 'genvar "#" )( 'gencod ":" ) ,s nip
	swap @+ "%w " ,print
	swap exec ,cr
	;

:genplain
	mark
	0 ( cntword cntvars + <? )(
		dup getllamadas 1? ( gencodigo ) drop
		1+ ) drop
	"plain.txt" savemem
	empty
	exit
	;

||||||||||||||||||||||||||||||||||||||||||||||||
#pagina 0
#actual 0

:refresca
	actual -? ( 0 nip )
	pagina <? ( dup 'pagina ! )
	pagina rows 11 - + >? ( dup rows 11 - - 'pagina ! )
	'actual !
	;

:printlineword | n --
	4 << 'indicepal +
	indicepal> >=? ( drop ; )
	dup 8 + @ 1 and? ( violeta "#" )( rojo ":") 1 boxprint drop
	@+ "%w" 20 boxprint blanco
	@+ drop |"%d " print direccion
	@+
	dup 24 >> "%d" 5 boxprint		| pilaR
	dup 8 << 24 >> "%d" 5 boxprint	| pilaD
	$fff and "%b" 12 boxprintr		| flags
	@ "%d" 7 boxprintr					| largo en bytes
	;

:vercod
	0 dup 'pagina ! 'actual !
	inigui
	'exit >esc<
	[ 1 'actual +! refresca ; ] <dn>
	[ -1 'actual +! refresca ; ] <up>
	[ rows 10 - 'actual +! refresca ; ] <pgdn>
	[ rows 10 - neg 'actual +! refresca ; ] <pgup>
	show clrscr cr
		'vardana8 tfont
		verde
		"Nro" 6 boxprint
		"Nombre" 21 boxprint
		"RS" 5 boxprint
		"DS" 5 boxprint
		"Flags" 12 boxprintr
		"Tokens" 7 boxprintr
		"Calls" 7 boxprintr
		" '" print cr
		pagina rows 10 - ( 1? )( 1-
			swap
			actual =? ( ">" )( "" ) 1 boxprint
			dup "%d." verde 5 boxprint
			dup printlineword
			dup getllamadas 1? ( "%d" )( "" nip ) 4 boxprintr
			dup getflags 1? ( "%d" )( "" nip ) 4 boxprintr
			cr
			1+ swap ) 2drop
		cr
     	verde
		actual printword
		cmano ;

||||||||||||||||||||||||||||||||||||||||||||||||
:consola
	indicepal< 'indicepal - 4 >> | iniciodicc
	'primerword !

	indicepal> 'indicepal - 4 >> | fin dicc
	1- dup +executa

	( primerword 1- >? )(
		dup	4 << 'indicepal +
		8 + @ 1 and? ( over +variable ) drop
		1- ) drop

|	indicepal> 12 - @ 'actual> !
|	ajusta

	inigui
	'exit >esc<
	'stepvm <f1>
	'stepvmf <f2>
	startvm | cambia las direcciones de variable!!
	show clrscr	fonti | 	25 fonth
		"r4Eval: " print blanco 'nombre print cr
		msg 1? ( rojo print cr )( drop )
		drawstat cr
		|drawtoken		|drawmemvar
		drawtrace
		cr cr
		0 rows 2 - at
		blanco
		$222222 $ff0000 linkcol
		'exit "ESC" sp .link sp
		$222222 $ff00 linkcol
		'stepvm "F1-Step" .link sp
		'playvm "F2-Play" .link sp
		'savecode |[ savestat ; ]
			"F4-Graba" .link sp
		cr
		cmano ;


|----- Sistema
:errorcompila
	mark
	saverror
	"debug.err" savemem
	empty
|	savestat
	"edit-code.txt" run ;

:errorpost
	;

:compila
|	savetokens
	"r4fasm.txt" run ;

|----------- principal
:main
	'nombre "./nom/editor.nom" load drop
	'nombre tokencomp
	msg 1? ( drop errorcompila ; ) drop
	tokenpost
	msg 1? ( drop errorpost ; ) drop

	inigui
	'exit >esc<
	'consola <f1>
	'vercod <f2>
	'genplain <f3>
	'compila <f4>
	show clrscr verde
		dup "(%d)" print
		"Programa compilado" print cr

		blanco
		"F1-Debuger" print cr
		"F2-Ver Diccionario" print cr
		"F3-GenCodigo(plain.txt)" print cr
		"F4-Compilar(plain.txt)" print cr
		;

: 0 paper 4 main "edit-code.txt" run  ;