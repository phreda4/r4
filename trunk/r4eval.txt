| eval-code.txt
| PHREDA 2007
|---------------------------------------
^reda4.txt
^gui3.txt
^r4code.txt
^medit.txt

#nombre )( 64
#linea )( 1024	| error y mem aux

#primerword
#:hojapal 0

#pilavars )( 1024
#pilavars> 'pilavars

::+variable | nroword --
	pilavars> !+ 'pilavars> ! ;

::-executa | --
	-4 'pilavars> +! ;

|-------- infopal

:printwordal | n --
	4 << 'indicepal +
	@+ "%w " print @+ " %h  " print @+ " %h  " print @+ " %h  " print cr
	drop ;

:printwordin | n --
	4 << 'indicepal +
	@+ "%w " verde print
	@+ drop |"%h " cyan print
	blanco
	@+ "%h " print

|	dup 24 >> "R:%d " print
|	dup 8 << 24 >> "D:%d " print
|	$ff and "F:%b " print

	@+ "%h " print cr
	drop ;

|----------------- muestra lista palabras
::printwordname | n --
|	dup "%d." blanco print | nro de palabra
	4 << 'indicepal +
	indicepal> >=? ( drop ; )
	dup 8 + @ 1 and? ( drop @ "#%w " violeta print ; ) drop
	@+ ":%w " rojo print
	drop
	;
:ll
	verde
	@+ drop |"%d " print direccion
	@+
	dup 24 >> "%d" print
	dup 8 << 24 >> "%d" print
	$fff and  "%b" print
| 	"%h" print
	@ "%d" print
	;

:drawtoken
	fonti
	primerword
	fils ( 1? )( 1- swap
|		actword =? ( vidi )
		dup printwordin
|		dup printwordname cr
|		actword =? ( vidi )
		1+ swap  )
	2drop
	;

|------------------ definicion con ayuda
:tokeninfolist | cnt adr --
	( swap 1? )( 1- swap | cnt adr
		|dup "%h :" print
		<<ip =? ( vidi )
|		dup 4 - movstackprint
		tokenprint
		<<ip 4+ =? ( vidi )
		" " print
		lout? 1? ( cr ) drop
		) 2drop ;


:esvar | adr --
	@ "#%w " violeta print cr
 	;

:exprintword | n --
	4 << 'indicepal +
	indicepal> >=? ( drop ; )
	dup 8 + @ 1 and? ( drop esvar ; ) drop
	dup @ ":%w " rojo print
	dup 12 + @
	swap 4+ @
	tokeninfolist ;

:drawtrace
	fonti
	dumpvm

	'pilaexe
	( pilaexe> <? )( @+ exprintword cr ) drop

	;

|------------------ dibuja la memoria
:printvariable | n --
	4 << 'indicepal +
	indicepal> >=? ( drop ; )
	dup 8 + @ 1 nand? ( 2drop ; ) drop
	@+ "#%w " violeta print amarillo
	@ @ " %d " print
|	drop
	;

:drawmemvar
	fonti
	'pilavars
	( pilavars> <? )(
		@+ printvariable cr ) drop
	;

|------------------ estado MV
:drawstat
	fonti

	cntvars cntword "pal:%d vars:%d " print cr amarillo
	str> str - 	cte> cte - 2 >> prog> prog - 2 >> "tok:%d cons:%d str:%d" print cr
	<<ip "IP:%h " verde print rojo
	<<ip @ dup $ff and swap 8 >> swap "%d-%d" print
	;

|-------------------------------------------------------------
:inspectormemoria
	inikey
	show cls

		;

|-------------------------------------------------------------
:realplay
	'nombre run  ;


:botonera
	fonti
		blanco
		$222222 $ff0000 linkcol
		'exit "ESC" spc link spc
		$222222 $ff00 linkcol
		'stepvm "F1-Step" link spc
		'playvm "F2-Play" link spc
	;

:titulo
	$444444 $cccccc vbtn
	1 sfont amarillo "r4Eval: " print
	blanco 'nombre print
    msg 1? ( cr rojo print )( drop )
|	'eligearchivo onUp
	;

:consolag
	2 font blanco m.draw ;

|-------------------------------------------------------------

:consola
	indicepal< 'indicepal - 4 >> | iniciodicc
	'primerword !

	indicepal> 'indicepal - 4 >> | fin dicc
	1- dup +executa

	( primerword 1- >? )(
		dup	4 << 'indicepal +
		8 + @ 1 and? ( over +variable ) drop
		1- ) drop

|	indicepal> 12 - @ 'actual> !
|	ajusta

	inigui
	'exit >esc<
	'stepvm <f1>
	'stepvmf <f2>
	startvm
	show clrscr
		0.96 0.06 fdim 0 -0.9 fpos titulo
		0.5 0.1 fdim -0.5 0.9 fpos botonera
		|0.5 0.45 fdim -0.5 -0.4 fpos drawtoken
		|0.5 0.45 fdim 0.5 -0.4 fpos drawmemvar
		1.0 0.8 fdim 0 0 fpos drawtrace
		0.5 0.1 fdim 0.5 0.9 fpos drawstat
		cmano ;

|----- Sistema
:errorcompila
	mark
	saverror
	"debug.err" savemem
	empty

|	savestat
	"edit-code.txt" run ;

:errorpost
	;

|----------- principal
:main
	'nombre "./nom/editor.nom" load drop
	'nombre tokencomp
	msg 1? ( drop errorcompila ; ) drop
	tokenpost
|	savestat | guarda info
|	savecode | guarda codigo
	msg 1? ( drop errorpost ; ) drop
	consola
	;

: 0 paper 4 main "edit-code.txt" run  ;