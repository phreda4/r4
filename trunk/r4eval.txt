| eval-code.txt
| PHREDA 2007
|---------------------------------------
^reda4.txt
^gui3.txt
^r4code.txt
^medit.txt
^fontt.txt

#nombre )( 64
#linea )( 1024	| error y mem aux

#primerword
#:hojapal 0

#pilavars )( 1024
#pilavars> 'pilavars

::+variable | nroword --
	pilavars> !+ 'pilavars> ! ;

::-executa | --
	-4 'pilavars> +! ;

|-------- infopal

:printwordal | n --
	4 << 'indicepal +
	@+ "%w " print @+ " %h  " print @+ " %h  " print @+ " %h  " print cr
	drop ;

:printwordin | n --
	4 << 'indicepal +
	@+ "%w " verde print
	@+ drop |"%h " cyan print
	blanco
	@+ "%h " print

|	dup 24 >> "R:%d " print
|	dup 8 << 24 >> "D:%d " print
|	$ff and "F:%b " print

	@+ "%h " print cr
	drop ;

|----------------- muestra lista palabras
::printwordname | n --
|	dup "%d." blanco print | nro de palabra
	4 << 'indicepal +
	indicepal> >=? ( drop ; )
	dup 8 + @ 1 and? ( drop @ "#%w " violeta print ; ) drop
	@+ ":%w " rojo print
	drop
	;
:ll
	verde
	@+ drop |"%d " print direccion
	@+
	dup 24 >> "%d" print
	dup 8 << 24 >> "%d" print
	$fff and  "%b" print
| 	"%h" print
	@ "%d" print
	;

:drawtoken
	primerword
	rows ( 1? )( 1- swap
|		actword =? ( vidi )
		dup printwordin
|		dup printwordname cr
|		actword =? ( vidi )
		1+ swap  )
	2drop
	;

|------------------ definicion con ayuda
:tokeninfolist | cnt adr --
	( swap 1? )( 1- swap | cnt adr
		|dup "%h :" print
		<<ip =? ( ">" print )
|		dup 4 - movstackprint
		tokenprint
|		<<ip 4+ =? ( vidi )
		" " print
		lout? 1? ( cr ) drop
		) 2drop ;


:esvar | adr --
	@ "#%w " violeta print ;

:exprintword | n --
	4 << 'indicepal +
	indicepal> >=? ( drop ; )
	dup 8 + @ 1 and? ( drop esvar ; ) drop
	dup @ ":%w " rojo print
	dup 12 + @
	swap 4+ @
	tokeninfolist ;

:drawtrace
	dumpvm

	'pilaexe
	( pilaexe> <? )( @+ exprintword cr ) drop

	;

|||||||||||||||||||||||||
:printlineword | n --
	4 << 'indicepal +
	indicepal> >=? ( drop ; )
	dup 8 + @ 1 and? ( violeta "#" )( rojo ":") print drop
	@+ "%w" 20 boxprint blanco
	@+ drop |"%d " print direccion
	@+
	dup 24 >> "%d" 5 boxprint		| pilaR
	dup 8 << 24 >> "%d" 5 boxprint	| pilaD
	$fff and "%b" 10 boxprintr		| flags
	@ " %d" 7 boxprint					| largo en bytes
	;

|------------------ dibuja la memoria
:printvariable | n --
	4 << 'indicepal +
	indicepal> >=? ( drop ; )
	dup 8 + @ 1 nand? ( 2drop ; ) drop
	@+ "#%w " violeta print amarillo
	@ @ " %d " print
|	drop
	;

:drawmemvar
	'pilavars
	( pilavars> <? )(
		@+ printvariable cr ) drop
	;

|------------------ estado MV
:drawstat
	cntvars cntword "pal:%d vars:%d " print cr amarillo
	str> str - 	cte> cte - 2 >> prog> prog - 2 >> "tok:%d cons:%d str:%d" print cr
	<<ip "IP:%h " verde print rojo
	<<ip @ dup $ff and swap 8 >> swap "%d-%d" print
	;

|-------------------------------------------------------------
:compila
|	savetokens
	"r4fasm.txt" run ;

||||||||||||||||||||||||||||||||||||||||||||||||
#pagina 0
:vercod
	inigui
	'exit >esc<
	[ 30 'pagina +! ; ] <dn>
	[ -30 'pagina +! ; ] <up>

	show clrscr
		verde
		"" 5 boxprint
		"Nombre" 21 boxprint
		"RS" 5 boxprint
		"DS" 5 boxprint
		"Flags" 10 boxprintr
		" Bytes" 7 boxprint
		"llamadas" print cr
		pagina 40 ( 1? )( 1-
			swap
			dup "%d." verde 5 boxprint
			dup printlineword
			dup getllamadas 1? ( "%d" )( "" nip ) 6 boxprint
			dup getflags 1? ( "%d" )( "" nip ) print
			cr
			1+ swap ) 2drop
		cr
     	verde
		"| A-01 0 accion 1 dato" print cr
		"| B-02 0 local 1 exportado" print cr
		"| C-04 0 un ; 1 varios ;" print cr
		"| D-08 0 sin calls 1 con calls" print cr
		"| E-10 0 no usas R(A) 1 usa R(A) .. R(A) es cuando R se usa como auxiliar" print cr
		"| F-20 0 nunca como direccion 1 alguna vez como direccion" print cr

		cmano ;

||||||||||||||||||||||||||||||||||||||||||||||||
:consola
	indicepal< 'indicepal - 4 >> | iniciodicc
	'primerword !

	indicepal> 'indicepal - 4 >> | fin dicc
	1- dup +executa

	( primerword 1- >? )(
		dup	4 << 'indicepal +
		8 + @ 1 and? ( over +variable ) drop
		1- ) drop

|	indicepal> 12 - @ 'actual> !
|	ajusta

	inigui
	'exit >esc<
	'stepvm <f1>
	'stepvmf <f2>
	'compila <f3>

	'vercod <f5>
	startvm
	show clrscr	fonti | 	25 fonth
		"r4Eval: " print blanco 'nombre print cr
		msg 1? ( rojo print cr )( drop )
		drawstat
		|drawtoken
		|drawmemvar
		drawtrace
		cr cr
		0 rows 2 - at
		blanco
		$222222 $ff0000 linkcol
		'exit "ESC" spc link spc
		$222222 $ff00 linkcol
		'stepvm "F1-Step" flink spc
		'playvm "F2-Play" flink spc
		'compila "F3-Compila" flink spc
		[ savestat ; ] "F4-Graba" flink spc
		'vercod "F5-Dicc" flink spc
		cr
		cmano ;


|----- Sistema
:errorcompila
	mark
	saverror
	"debug.err" savemem
	empty
|	savestat
	"edit-code.txt" run ;

:errorpost
	;

|----------- principal
:main
	'nombre "./nom/editor.nom" load drop
	'nombre tokencomp
	msg 1? ( drop errorcompila ; ) drop
	tokenpost
	msg 1? ( drop errorpost ; ) drop
	consola
	;

: 0 paper 4 main "edit-code.txt" run  ;