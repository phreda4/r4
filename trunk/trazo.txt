| PHReda 2010
| estructura para trazos en 32 bits
|-------------

#bx1 #by1 #bx2 #by2

#:trazo )( 8192
#:trazo> 'trazo

|----------------
:,linea | xy --
	trazo> 4 - @ =? ( drop ; )
	trazo> !+ 'trazo> ! ;

::resetlinea
	'trazo 'trazo> ! ;

::addline
	tpen @+ 0? ( 2drop ; )
	( 1? )( 1- swap @+ ,linea swap )
	2drop ;

::drawlinea
	'trazo
	@+ 0? ( 2drop ; ) >uv op
	( trazo> <? )( @+ >uv line ) drop ;

|-------tipo coord
:+xydata | x y --
	by1 <? ( dup 'by1 ! )
	by2 >? ( dup 'by2 ! ) drop
	bx1 <? ( dup 'bx1 ! )
	bx2 >? ( dup 'bx2 ! ) drop ;

:xx drop ;
:dd dup $fff and swap 12 >> $fff and +xydata ;
:ddc dup dd dd ;

#accXY xx xx dd dd ddc dd ddc xx

::trazolimites | str --
	sh sw 'bx1 ! 'by1 !
	0 0 'bx2 ! 'by2 !
	( @+ 1? )(
		dup 24 >> $1c and 'accXY + @ exec
		) 2drop ;

::trazobox | -- x1 y1 x2 y2
	bx1 by1 bx2 by2 ;



|------------
#inip

:p000 drop ;
:p001 $ffffff and ink ;
:p010
	inip 1? ( dup "%d " print ) drop
	1 'inip ! drop ;
:p011 1 'inip +! drop ;

#accp p000 p001 p010 p011 p011 p011 p011 p011

::trazopoli | str --
	0 'inip !
	( @+ 1? )(
		dup 24 >> $1c and 'accp + @ exec
		) 2drop
	inip 1? ( dup "%d " print ) drop ;


|-------tipo dibujo
| ...0 00..

#x #y
:t000 | color	$00 000000
	drop ;
:t001 | color	$04 000000
	$ffffff and ink ;
:t010 | op		$08 000000
	dup $fff and swap 12 >> $fff and 2dup op 'y ! 'x ! ;
:t011 | line	$0c 000000
	dup $fff and swap 12 >> $fff and line ;
:t100 | cv-curve
	dup $fff and swap 12 >> $fff and cp @+ dup $fff and swap 12 >> $ffff and curve ;
:t101 | pline
	dup $fff and swap 12 >> $fff and pline ;
:t110 | cv-pcurve
	dup $fff and swap 12 >> $fff and cp @+ dup $fff and swap 12 >> $ffff and pcurve ;
:t111 | endpoli
	drop poli ;

|:t1000 x y gop gline ;

#acc t000 t001 t010 t011 t100 t101 t110 t111

::trazodraw | str --
	( @+ 1? )(
		dup 24 >> $1c and 'acc + @ exec
		) 2drop ;

