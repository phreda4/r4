| para saber que scancode corresponde
|-----------------------------------
^lib/reda4.txt
^lib/gui.txt

#keysolo )( 128
#keyshif )( 128
#keyalt )( 128
#cursor
#modot 'keysolo

:copyact
	'keysolo 'k2asc 128 cmove
	'keyshif 'k2ascs 128 cmove
	'keyalt 'k2alt 128 cmove
	;

:savefile
	mark
	"#:k2asc (" ,print
	'keysolo 128
	( 1? )( 1- swap
		c@+ " %d" ,print
		swap
		$f nand? ( ,cr )
		) 2drop
	" )" ,print ,cr
	"#:k2ascs (" ,print
	'keyshif 128
	( 1? )( 1- swap
		c@+ " %d" ,print
		swap
		$f nand? ( ,cr )
		) 2drop
	" )" ,print ,cr
	"#:k2alt (" ,print
	'keyalt 128
	( 1? )( 1- swap
		c@+ " %d" ,print
		swap
		$f nand? ( ,cr )
		) 2drop
	" )" ,print ,cr

	"key-n.txt" savemem

	empty
	;

|------------------------------------------
:tek | n -- n
	cursor =? ( blanco )( cyan )
	20 28 ccx 2 - ccy 2 - vfill
	ccx 2 - ccy 2 - over 20 + over 28 + cajau
	dup 'keysolo
	modot =? ( negro )( gris )
	+ c@ emit sp sp
	;

:tek2 | n -- n
	dup 'keyshif
	modot =? ( negro )( gris )
	+ c@ emit
	dup 'keyalt
	modot =? ( negro )( gris )
	+ c@ emit sp
	;

:dibteclado
	verde
	cr cr "   " print
	0 ( 16 <? )(
		dup " %h " print 1+ ) drop
	cr
	0 ( 8 <? )(
		cr dup " %h " print
		0 ( 16 <? )(
			over 4 << over +
			tek drop
			1+ ) drop
		cr "   " print
		0 ( 16 <? )(
			over 4 << over +
			tek2 drop
			1+ ) drop cr
		verde
	   	1+ ) drop
	cr ;

#curcar

:dibcar
	0 ( 4 <? )(
		0 ( 64 <? )(
			over 6 << over +
			curcar =? ( blanco )( azul )
			32 <? ( 32 nip ) emit
			1+ ) drop
		cr
		1+ ) drop
	;

:setcar
	cursor modot + c@ $ff and 'curcar ! ;
:putcar
	$ff and
	dup 'curcar ! cursor modot + c! ;

|------------------------------------------
:modificac
	inigui
	setcar
	show
		key
		'exit >esc<
		[ 'keysolo 'modot ! setcar ; ] <f1>
		[ 'keyshif 'modot ! setcar ; ] <f2>
		[ 'keyalt 'modot ! setcar ; ] <f3>
		[ 0 putcar ; ] <f4>

		[ curcar 64 - putcar ; ] <up>
		[ curcar 64 + putcar ; ] <dn>
		[ curcar 1- putcar ; ] <le>
		[ curcar 1+ putcar ; ] <ri>
		drop

		clrscr
		fonti home cr rojo
		"  Elegir CARACTER" print
		dibteclado
		cr
		dibcar
		cr cr
		cursor dup "scancode: %d $%h" print cr cr
		verde
		"F1-Norm  F2-Shift  F3-Alt  Flechas-Elije  ENTER-Continue" print
		cmano ;


|------------------------------------------
:main
	copyact
	inigui
	show
		key
		'exit >esc<
		'savefile <f1>
		'modificac <enter>
		1? ( dup $7f and 'cursor ! )
		 drop

		clrscr
		fonti home cr verde
		"  Elegir TECLA" print
		dup "(%d)" print

        dibteclado
		cr cr
		cursor dup "scancode: %d $%h" print cr cr
		verde
		"F1-Save""key-n.txt""  ENTER-Edit  ESC-Exit" print
		cmano ;

: 0 paper 33 main ;