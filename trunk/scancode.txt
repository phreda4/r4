| para saber que scancode corresponde
|-----------------------------------
^reda4.txt
^gui3.txt

#keysolo )( 128
#keyshif )( 128
#keyalt )( 128
#cursor
#modot 'keysolo

:copyact
	'keysolo 'k2asc 128 cmove
	'keyshif 'k2ascs 128 cmove
	'keyalt 'k2alt 128 cmove
	;

:savefile
	mark
	"#:k2asc (" ,print
	'keysolo 128
	( 1? )( 1- swap
		c@+ " %d" ,print
		swap
		$f nand? ( ,cr )
		) 2drop
	" )" ,print ,cr
	"#:k2ascs (" ,print
	'keyshif 128
	( 1? )( 1- swap
		c@+ " %d" ,print
		swap
		$f nand? ( ,cr )
		) 2drop
	" )" ,print ,cr
	"#:k2alt (" ,print
	'keyalt 128
	( 1? )( 1- swap
		c@+ " %d" ,print
		swap
		$f nand? ( ,cr )
		) 2drop
	" )" ,print ,cr
	"key-n.txt" savemem
	empty
	;

|------------------------------------------

:ctecla | n --
	cursor =? ( rojo )( blanco )
	dup "%h: " print
	modot + c@ emit
	;

:drawlist
	0 ( 32 <? )(
		2 over 4+ at dup 2 << ctecla
		20 over 4+ at dup 2 << 1+ ctecla
		40 over 4+ at dup 2 << 2 + ctecla
		60 over 4+ at dup 2 << 3 + ctecla
		1+ ) drop
	;


:incc
	1 cursor modot + c+! ;

:decc
	-1 cursor modot + c+! ;

|------------------------------------------
:modi
	inikey
	'exit >esc<
	'incc <f1>
	'decc <f2>
	show cls scr
		fonti cr rojo
		"Modifica tabla" print
		drawlist
		amarillo
		cr
		cursor modot + c@ $ff and 1- 0 ( 30 <? )( 1+ swap 1+ dup emit swap ) 2drop
		cr cr
		cursor dup "scancode: %d $%h" print cr cr
		verde
		"F1->>  F2-<<  ESC-Continue Edit" print

	;


:modificac
	key.push
	modi
	key.pop	;

|------------------------------------------
:modosh
    modot 'keysolo =? ( 'keyshif )(
		'keyshif =? ( 'keyalt )(
		'keysolo ) ) nip
	'modot !
	;

|------------------------------------------
:nir
	key 'cursor ! ;

:main
	copyact
	inigui
	128 ( 1? )( 'nir over >key 1- ) drop
	'exit >esc<
	'modosh <f1>
	'savefile <f2>
	'modificac <enter>
	show clrscr
		fonti cr verde
		"Elegir caracter, Tabla: " print
		modot 'keysolo =? ( "Normal" )(	'keyshif =? ( "Shift" )( "Alt" ) ) nip
		print
		drawlist
		cr cr
		cursor dup "scancode: %d $%h" print cr cr
		verde
		"F1-Norm/Shift/Alt  F2-Save  ENTER-Edit  ESC-Exit" print
		;

: 0 paper main "main.txt" run ;