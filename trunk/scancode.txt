| para saber que scancode corresponde
|-----------------------------------
^lib/reda4.txt
^lib/gui.txt
^lib/fontt.txt

#keysolo )( 128
#keyshif )( 128
#keyalt )( 128
#cursor
#modot 'keysolo

:copyact
	'keysolo 'k2asc 128 cmove
	'keyshif 'k2ascs 128 cmove
	'keyalt 'k2alt 128 cmove
	;

:savefile
	mark
	"#:k2asc (" ,print
	'keysolo 128
	( 1? )( 1- swap
		c@+ " %d" ,print
		swap
		$f nand? ( ,cr )
		) 2drop
	" )" ,print ,cr
	"#:k2ascs (" ,print
	'keyshif 128
	( 1? )( 1- swap
		c@+ " %d" ,print
		swap
		$f nand? ( ,cr )
		) 2drop
	" )" ,print ,cr
	"#:k2alt (" ,print
	'keyalt 128
	( 1? )( 1- swap
		c@+ " %d" ,print
		swap
		$f nand? ( ,cr )
		) 2drop
	" )" ,print ,cr

	"key-n.txt" savemem

	empty
	;

|------------------------------------------
:tek | n -- n
	cursor =? ( blanco )( cyan )
	20 28 ccx 2 - ccy 2 - vfill
	ccx 2 - ccy 2 - over 20 + over 28 + cajau
	dup 'keysolo
	modot =? ( negro )( gris )
	+ c@ emit sp sp
	;

:tek2 | n -- n
	dup 'keyshif
	modot =? ( negro )( gris )
	+ c@ emit
	dup 'keyalt
	modot =? ( negro )( gris )
	+ c@ emit sp
	;

:dibteclado
	verde
	cr cr "   " print
	0 ( 16 <? )(
		dup " %h " print 1+ ) drop
	cr
	0 ( 8 <? )(
		cr dup " %h " print
		0 ( 16 <? )(
			over 4 << over +
			tek drop
			1+ ) drop
		cr "   " print
		0 ( 16 <? )(
			over 4 << over +
			tek2 drop
			1+ ) drop cr
		verde
	   	1+ ) drop
	cr ;

#curcar

:dibcar
	0 ( 4 <? )(
		0 ( 64 <? )(
			over 6 << over +
			curcar =? ( blanco )( azul )
			32 <? ( 32 nip ) emit
			1+ ) drop
		cr
		1+ ) drop
	;

:setcar
	cursor modot + c@ $ff and 'curcar ! ;
:putcar
	$ff and
	dup 'curcar ! cursor modot + c! ;

|------------------------------------------
:knorm
	'keysolo 'modot ! setcar ;
:kshift
	'keyshif 'modot ! setcar ;
:kalt
	'keyalt 'modot ! setcar ;

:modifica
	inigui
	'exit >esc<
	'knorm <f1>
	'kshift <f2>
	'kalt <f3>
	[ 0 putcar ; ] <f4>

	[ curcar 64 - putcar ; ] <up>
	[ curcar 64 + putcar ; ] <dn>
	[ curcar 1- putcar ; ] <le>
	[ curcar 1+ putcar ; ] <ri>
	setcar
	show clrscr
		32 gc.top $f gc.hfill
		font-vard-12-bold home cr2
		verde dup " :R%d" print
		blanco "KEYS " print

		26 gc.botton $00000f gc.hfill
		font-vard-8-bold home cr2
		verde
		'knorm "F1-Norm" sp btnt
		'kshift "F2-Shift" sp btnt
		'kalt "F3-Alt" sp btnt
		rojo
		'exit "ESC-Continue" sp btnt
		blanco
		"   Flechas-Elije " print
		scr home cr cr cr
		blanco "  Elegir CARACTER" print
		fonti
		dibteclado
		cr
		dibcar
		cr cr
		cursor dup "scancode: %d $%h" print cr cr
		cmano ;

:modificac
	key.push
	modifica
	key.pop	;

|------------------------------------------
:nir
	key 'cursor ! ;

:main
	copyact
	inigui
	128 ( 1? )( 'nir over >key 1- ) drop
	'exit >esc<
	'savefile <f1>
	'modificac <enter>
	show clrscr
		32 gc.top $f gc.hfill
		font-vard-12-bold home cr2
		verde dup " :R%d" print
		blanco "KEYS " print

		26 gc.botton $00000f gc.hfill
		font-vard-8-bold home cr2
		verde
		'savefile "F1-Save""key-n.txt""" sp btnt
		'modificac "ENTER-Edit" sp btnt
		rojo
		'exit "ESC-Exit" sp sp sp btnt
		scr home cr cr cr
		blanco "  Elegir TECLA" print
		fonti
        dibteclado
		cr cr
		cursor dup "scancode: %d $%h" print cr cr
		cmano ;

: 0 paper 4 main ;