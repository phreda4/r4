| Libreria para campo memo
| PHREDA
|--------------------------------
^reda4.txt
^font.txt

#padi>	#pad>	#padf>	 #cmax

:lins  | c --
	padf> padi> - cmax >=? ( 2drop ; ) drop
	pad> dup 1- padf> over - 1+ cmove> 1 'padf> +!
:lover | c -- 
	pad> c!+ dup 'pad> !
	padf> >? ( 
			dup padi> - cmax >=? ( swap 1- swap -1 'pad> +! ) drop
			dup 'padf> ! ) drop

:0lin | --
	0 padf> c! ;
:ddel
	pad> padf> >=? ( drop ; ) drop
	1 'pad> +!
:del
	pad> padi> <=? ( drop ; )
	dup 1- swap padf> over - 1+ cmove -1 'padf> +! -1 'pad> +! ;

#modo 'lins

::m.clear
	padi> dup 'pad> ! 'padf> ! 0lin ;

::m.copy | str --
	padi> strcpy
	padi>
	( c@+ 1? )( drop ) drop 1- dup 
	'pad> ! 'padf> !
	;

::m.input | 'var max -- 
	'cmax ! dup 'padi> !
	( c@+ 1? )( drop ) drop 1- dup 
	'pad> ! 'padf> !
	[ key toasc modo exec ; ] <visible>
	'del <back>
	'ddel <del>
	[ modo 'lins =? ( 'lover )( 'lins ) 'modo ! drop ; ] <ins>
	[ pad> padf> <? ( 1 'pad> +! ) drop ; ] <der>
	[ pad> padi> >? ( -1 'pad> +! ) drop ; ] <izq>
	[ padi> 'pad> ! ; ] <home>
	[ padf> 'pad> ! ; ] <end> 
	0lin ;

:dcursor
	blink 1? ( modo 'lins =? ( azul )( rojo ) drop )( negro ) drop
	printcur ;

::m.draw | --
	padi> ( pad> =? ( ink@ >r dcursor r> ink )
		c@+ 1? )( emit ) 2drop ;
	
