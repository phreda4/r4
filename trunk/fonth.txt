| Fuentes hershey
|
|^inc/hershey1.inc
^inc/hersheyf2.inc
#xa #ya

|--- fuente de letra 8 bits
| 000000 00
| valor  control
:tox ccw 5 *>> ccx + ;
:toy cch 5 *>> ccy + ;

|--------- dibuja linea 6 bits
:xy.. | adr -- adr+2
	2 >> tox 'xa ! c@+ 2 >> toy 'ya ! ;

| control
:p00 xy.. xa ya op ;				| 00 - X Y op
:p01 2 >> tox dup 'xa ! ya line ;	| 01 - X hline
:p10 xa swap 2 >> toy dup 'ya ! line ;  | 10 - Y vline
:p11 xy.. xa ya line ;					| 11 - X Y line

#h8acc p00 p01 p10 p11

::h8draw | addr --
	0 dup tox 'xa ! toy 'ya !
	@+ $ff and ( 1? )( 1- swap
		c@+ dup $3 and 2 << 'h8acc + @ exec
		swap ) 2drop ;

::h8char | c --
	$ff and
    2 << 'hfntd1 + @ h8draw ;

:h8size | byte -- size
	$ff and
	2 << 'hfntd1 + @ @ 8 >>
	ccw 5 *>> ;

::fonth | l --
	wtext home
	'h8char 'h8size setfont ;

|...............
| control
:bp00 xy.. xa ya gop ;				| 00 - X Y op
:bp01 2 >> tox dup 'xa ! ya gline ;	| 01 - X hline
:bp10 xa swap 2 >> toy dup 'ya ! gline ;  | 10 - Y vline
:bp11 xy.. xa ya gline ;					| 11 - X Y line

#h8accb bp00 bp01 bp10 bp11

::h8drawb | addr --
|	cch 5 >> 1+ gg
	2 gg
	0 dup tox 'xa ! toy 'ya !
	@+ $ff and ( 1? )( 1- swap
		c@+ dup $3 and 2 << 'h8accb + @ exec
		swap ) 2drop ;

::h8charb | c --
	$ff and
    2 << 'hfntd1 + @ h8drawb ;

::fonthb | l --
	wtext home
	'h8charb 'h8size setfont ;