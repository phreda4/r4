| tabla.txt
| PHREDA
|-------------------
^parse.txt

#tuso
#buff )( 2048
#campos )( 1024
#campos> 'campos
#index )( 65536 | 16000 registros..
#index> 'index 
#cntreg 0

:endSub | -- b
	2drop 0 swap c! 'buff ;

:getSub | "" -- "" n
	'buff 0 over ! over
	( c@+ 1? )(  13 =? ( endSub ; ) $7c =? ( endSub ; ) $3b =? ( endSub ; )  |
		rot c!+ swap ) rot c!+ 2drop 'buff ; 

:nextSub  | "..;.." -- 0 / "..;.." ..".."	; termina con | y con <enter>
	 ( c@+ 1? )( 13 =? ( 2drop 0 ; ) $7c =? ( 2drop 0 ; ) $3b =? ( drop ; ) drop ) nip ; 

:nextCol  | "..|.." -- 0 / "..|.." ..".."
	 ( c@+ 1? )( 13 =? ( 2drop 0 ; ) $7c =? ( drop ; ) drop ) nip ;

:skip1013
	drop c@+ 10 =? ( drop ; ) drop 1- ;

:nextReg | "" -- "" /0
	( c@+ 1? )( 13 =? ( skip1013 ; )  drop ) nip ;


#ctipo

:btipo 
	dup c@ 
	0 'ctipo !
	$41 =? ( 0 'ctipo ! ) | L left
|	$ | R right
|		| C enter

	$4e =? ( 1 'ctipo ! ) | N
	| M numero 
	drop ;

:blargo | "12" - nn
	?numero 0? ( 20 dup ) drop nip ;

::td.uso | 'db --
	dup 'tuso !
	'campos over  (
		dup nextSub btipo nextSub 1? ( getSub blargo )( 20 ) 8 << ctipo or nip 
		rot !+ swap 
		nextCol 	0? ) over !  
	'campos> !
	'index swap
	( nextReg 1? )(
		dup rot !+ swap
		)  over ! 
	dup 'index - 2 >> 'cntreg !
	'index> ! 
	;	

::td.cntReg | -- cnt
	cntreg ;

::td.cntCmp  | -- cntcamp
	campos> 'campos - 2 >> ;

::td.reg  |  reg --  reg
	2 << 'index + @ ;

:everSub | 'exe "" --
	( getSub pick2 exec nextSub 0? ) 2drop ;

::td.csub | 'exe "" --
	'campos swap
	( getSub		| 'exe 'campo  "" buff
		pick2 @	8 >>	| 'exe 'campos "" buff nca 
		pick4 exec	| 'exe 'campos "" 
		swap 4+ swap
		nextSub 0? ) 3drop ;

::td.all | 'exe --
	'index ( @+ 1? )( pick2 exec ) 3drop ;

::td.page | 'exe cnt first --
	2 << 'index +
	swap ( 1? )( 1- | exe ind cnt
		>r @+ pick2 exec 
		r> ) 3drop ;

::td.titles | --
	'campos tuso 
	 ( getSub | campos "" buff
		pick2 @ 8 >>
|		lprint	| campos ""
		2drop
		swap 4+ swap
		nextCol 	0? ) drop ;

|-------------------------------------------------------------------

| loadtable
| filter ( campo cond..)
| sort c1,c2
| insert
| update
