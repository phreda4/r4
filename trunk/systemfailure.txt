| system failure
| experimental gameplay project
| PHReda - 2009 - http://www.reda4.org
| http://code.google.com/p/reda4/
|-------------------------------------
^reda4.txt
^gui3.txt
^inc/systemfailure.inc

#xcam 0
#ycam 0
#zcam 10.0

|||| DRAW 2D
| x  y  s r  vx vy acc tl
|    4  8 12 16 20 24  28
|-28-24-20-16-12 -8 -4

:sxy@ | adrrl -- adrrl x y
	dup 28 - @+ swap @ ;

:svxy! | adrrl +vy +vx -- adrrl
	pick2 12 - !+ ! ;

:ss! | adrrl scale -- adrrl
	over 20 - ! ;

:sr! | adrrl rotacion -- adrrl
	over 16 - ! ;
:+sr! | adrrl +rotacion -- adrrl
	over 16 - +! ;
:sr@ | adrrl  -- adrrl rotacion
	dup 16 - @ ;

||||||||||||||||||||||||||||||||||
:draw2d | adr -- adr tl
	mpush
	>r
	r@+ r@+ 0 mtransi
	r@+ dup 0 mscalei
	r@+ mrotzi
	r@+ r 20 - +!
	r@+ r 20 - +!
	r> @+ exec | adrtl t1
	mpop ;


:simplesprite | adr -- adr'
	@+ draw2d 2drop ;

|
| 2 listfx
| 1 <-lsig
| listfx ... 'borr <-act

:delspr | 'lsp 'lsig 'act -- 'lsig
	dup 32 - over | nueva vieja
	10 | *** cnt
	5 << move | de sr cnt
	32 - ;


:lsprite	| adr -- adr'
	@+
	@+ ( 1? )( 1- swap  | nexl cant lis'
		draw2d
        1? ( swap !+ )( drop 4+
				delspr
				-1 pick3 4 - @ +! 	| resta 1 a cantidad
				)
		swap ) 2drop
	;

:msprite
:noop
	@+ drop
	;

#accdib 'simplesprite 'lsprite 'msprite 'noop 'noop 'noop 'noop

:drawlayer | layer --
	( @+ 1? )(
		1- $3 and 2 << 'accdib + @ exec
		) 2drop ;

:+layer | tl 'acc vy vx r s y x  'lista --
	dup @+ | 'listfx 'dirini cantact
	>r r
	5 << + | 'listfx 'diractual
	swap >r | ... y x 'diractual
	!+ !+ !+ !+ !+ !+ !+ !
	r> r> | 'diract cantact
	1+ swap ! ;

||||||||||||||||||||||||||||||||||
:r0.1 | -- r
	rand 0.1 mod ;

:r01 | -- r
	rand 1.0 mod ;

||||||||||||||||||||||||||||||||||
|||| GOAL
:accgoal | adr -- adr t1
	'dib5 3dnsprite
	0.8 pinpon 0.8 + ss!
	-1 ;

#spr-goal 2.0 2.0 0.5 0 0 0 'accgoal -1

:resetgoal
	'accgoal 0 0 0 0.5 2.0 2.0 'spr-goal !+ !+ !+ !+ !+ !+ ! ;

|||| NAVE
| x y s r vx vy acc tl
#vrnave 0
#arnave 0
#vanave 0
#aanave 0

:accnave | adrtl -- adrtl tl
	'dib1 3dnsprite
	arnave 'vrnave +!
	vrnave +sr!	| rotacion
	aanave 'vanave +!
	sr@ sincos
	vanave neg *. swap vanave neg *. svxy! | velocidad
	-1 ;

#spr-nave 0 0 0.5 0 0 0 'accnave -1

:resetnave
	'accnave 0 0 0 0.5 0 0
	'spr-nave !+ !+ !+ !+ !+ !+ !
	0 0 0 0 'vrnave !+ !+ !+ !
	;

:posnave
	'spr-nave @+ swap @ swap ;

#listafx 0 )( 8192

#layer1
2	'listafx
1	'spr-goal
1	'spr-nave
0


|||| SPRITES

|||| ROCKS
:rocks
	0.01 +sr!
	sxy@
	abs 4.0 >? ( 0 nip nip ; ) drop
	abs 4.0 >? ( 0 nip ; ) drop
    -1
    'dib6 3dnsprite
	;

:+rocks
	-1 'rocks
	r0.1 r0.1
	r01 0.4              | rot y tam
	0 0
	'listafx +layer
	;

|||| FIRE
:fire
	dup @ 1-
	'diba 3dnsprite ;

:+fire
	100 'fire
	'spr-nave 12 + @ dup sincos
	-0.2 *. swap -0.2 *. | velocidad
	rot 0.1              | rot y tam
	posnave
	'listafx +layer
	;

|||| SMOKE
#humo

:mismoke
	0.02 +sr!
|	dup 10 << 0.2 + ss!
	dup @ 1-
	dup 3 << alpha
	'dib2 3dnsprite
	255 alpha
	;

:+smoke
	30 'mismoke
	'spr-nave 12 + @ sincos
	humo *. swap humo *. | velocidad
	r01 0.4              | rot y tam
	posnave
	'listafx +layer
	;

|||| KEYS
:kder
	-0.001 'arnave ! ;
:ukder
	0 'arnave !
	vrnave 2/ 'vrnave ! ;
:kizq
	0.001 'arnave !	;
:ukizq
	0 'arnave !
	vrnave 2/ 'vrnave ! ;
:kup
	0.001 'aanave !
	0.05 'humo ! ;
:ukup
	0 'aanave !
	0 'humo ! ;
:kdn
	-0.001 'aanave !
	-0.05 'humo ! ;
:ukdn
	0 'aanave !
	0 'humo ! ;

:kfire
	+fire
	;

|||| CONSOLA
#line1 )( 64

:consola+keys
	0.05 dup fdim verde

	0.7 -0.8 fpos 'exit 'iarr btnd
	0.6 -0.9 fpos 'exit 'iizq btnd
	0.7 -0.9 fpos 'exit 'iaba btnd
	0.8 -0.9 fpos 'exit 'ider btnd
	0.4 0.05 fdim rojo
	0.0 -0.9 fpos 'exit "space" btnh

	scr 20 fonth verde
	'line1 print cr
	;


|||| GAME LOOP
:debug scr
		20 fonth verde
		0 16 at
		dup "%d " print
		listafx "cnt:%d " print cr
		vrnave arnave "%f %f - " print
		vanave aanave "%f %f " print ;

:ajustacam
	posnave
	neg xcam 3.0 + >? ( 3.0 - 'xcam ! )(
		xcam 3.0 - <? ( 3.0 + 'xcam ! )( drop ) )
	neg ycam 3.0 + >? ( 3.0 - 'ycam ! )(
		ycam 3.0 - <? ( 3.0 + 'ycam ! )( drop ) )
	;

:juego
	inigui
	resetnave
	resetgoal
	'exit >esc<
	'kder <der> 'ukder >der<
	'kizq <izq>	'ukizq >izq<
	'kup <arr> 'ukup >arr<
	'kdn <aba> 'ukdn >aba<
	'kfire <esp>
	show clrscr

		omode
		ajustacam
		xcam ycam zcam  mtrans

		'layer1 drawlayer

		consola+keys

		debug
|		cmano
		100 .mseg .restart
		humo 1? ( +smoke ) drop
		+rocks
		;


#inigame1 "Use the arrows to move the ship."
#inigame2 "Use SPACE to fire and destroy."
#inigame3 "Avoid obstacles and go to the station."
#inigame4 "Good Luck."

#titleini "*** System Failure ****%.ESPACE to continue...%.ESC to exit..."

|||| TITLE
:titulo
	inigui
	[ exit 1 ; ] >esp<
	[ exit 0 ; ] <esc>
	show clrscr
		cmano
		;

|||| BOOT
: 33
	clear
|	titulo
	juego
	;


