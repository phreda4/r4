| juego de memoria
| PHREDA
|-------------------------
^reda4.txt
^gui.txt
^sprites.txt

|^inc/jonael.inc
^inc/kamart.inc

#cntcartas 8
#cntacierta
#cartas )( 1024
#ele1 #ele2
#testime

#dibujos 
'dibu1 'dibu2 'dibu3 'dibu4 'dibu5 'dibu6 'dibu7 'dibu8 'dibu9 'dibua 'dibub 'dibuc 'dibud 'dibue 'dibuf

|------ cartas
:llenar | --
 'cartas cntcartas ( 1? )( 1- >r
  r dup rot c!+ c!+
  r> ) 2drop 
  cntcartas 'cntacierta ! ;
  
:cambia | fd  --
 'cartas + dup c@ rot 'cartas + dup c@ 
 rot rot c! swap c! ;
 
:mexclar | --
 cntcartas 2* dup 2* ( 1? )( 1- >r
  rand over mod abs rand pick2 mod abs cambia
  r> ) 2drop 
  ;
 
:quita | val --
 'cartas cntcartas 2* ( 1? )( 1- >r
  dup c@ pick2 =? ( drop -1 over c! )( drop ) 1+
  r> ) 2drop 
  -1 'cntacierta +! ;
  
:conte | nro -- val  
 'cartas + c@ ;

|------ 
:testea
	ele1 conte ele2 conte =? ( quita ) drop
:iniele
  -1 dup 'ele1 ! 'ele2 ! 0 'testime ! ;
  
|------ interaccion
:dcarta | dir --
  'cartas + c@ 2* 2* 'dibujos + @ sprite ;
  
:cdraw | nro --
  dup 'cartas + c@ -? ( 2drop ; )
  -2 dup +dim gris gc.fbox
  drop ele1 =? ( dcarta ; ) ele2 =? ( dcarta ; ) drop ;
	
:cclick | nro --
  ele2 1+ +? ( 2drop ; ) drop
  dup 'cartas + c@ -? ( 2drop ; ) drop
  ele1 -? ( drop 'ele1 ! ; ) drop
  ele1 =? ( drop ; ) 'ele2 !  
  20 'testime ! ;

|--- pantallas
:gano?
	cntacierta 1? ( drop ; ) drop 
	.page 
	16 16 .table
	4 12 .at 8 2 .to 
	verde oscuro "Otro" 'exit .tbtn 
	.show cls
		6 font rojo 0 1 at "Ganaste" printc ;

:play
	llenar mexclar iniele
	.page
	'exit >esc<
	10 10 .table 
	1 1 .at 8 8 .to 'cdraw 'cclick 
	cntcartas 2/ dup
	|4 4 
	.btabla
	.show cls 16 font
		|   blanco 0 0 at dup ele1 ele2 "%d %d %d" print
		testime 1? ( 1- 0? ( testea ) 'testime ! )( drop )
		cntacierta 0? ( exit ) drop ;
 
:presenta
    rerand
	.page
	12 12 .table
	1 9 .at 4 2 .to rojo "SALIR" 'exit dup >esc< .tbtn
	7 9 .at 4 2 .to verde "JUGAR" [ play gano? ; ] dup >esp< .tbtn
	.show cls scr
		4 font rojo 0 1 at
		"Memoria" printc
		;

: 0 paper presenta "main.txt" run ;
