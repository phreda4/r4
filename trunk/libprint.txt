| Fuentes unificadas
| :r4 libreria de sistema - Fuente de letras
|---------------------------------------------
^reda4.txt
^libmprint.txt
^libgraf.txt
^fonti.txt

#tx1 0 #ty1 0 #tx2 640 #ty2 480 		| ventana de texto

#:rows 40	#:cols 80		| filas/columnas texto
#:ccx 0  	#:ccy 0			| cursor
#:ccw 8		#:cch 12		| tamaño

#charsize 'charsize8i
#charemit 'char8i
#:charrom
#:charsiz
#:charini
#:charlin

| 6 bits de valor (-32..31)
::toxt ccw dup 2/ rot rot 5 *>> ccx + + ;
::toyt cch dup 2/ rot rot 5 *>> ccy + + ;

|--------- palabras de dibujo de fuentes (reemplazables)
::fontset | tipof --
	@+ 'charsize !
	@+ 'charemit !
	@+ 'ccw !
	@+ 'cch !
	@+ 'charrom !
	@+ 'charsiz !
	@+ 'charini !
	@ 'charlin !
	;

::fontbold | --
::fontnorm | --
::fontdoub | --
::fontoutl | --

::setfont | 'char 'size --
	'charsize !+ ! ;

::window | x1 y1 x2 y2 --
	cch * 'ty2 ! ccw * 'tx2 !
	cch * 'ty1 ! ccw * 'tx1 ! ;

::win2gc
	tx2 tx1 2dup - 'w ! + 2/ 'xc !
	ty2 ty1 2dup - 'h ! + 2/ 'yc ! ;

::gc2win
	xc w 2/ - dup 'tx1 ! w + 'tx2 !
	yc h 2/ - dup 'ty1 ! h + 'ty2 ! ;


|------------------------------------------------------
::cr
	cch 'ccy +! tx1 'ccx ! ;

::cr2
	cch 2/ 'ccy +! tx1 'ccx ! ;

:dtab
	ccx 5 >> 1+ 5 << 'ccx ! ;

::emit | c --
	9 =? ( drop dtab ; )
	13 =? ( drop cr ; )
	dup charsize exec  | c sizex
|	dup ccx + tx2 >? ( 3drop ; ) drop | c sizex (*1)
|	dup ccx + tx2 >? ( cr ) drop | c sizex (*2)
	dup ccx + tx2 >? ( 'ccx ! 2drop ; ) drop | c sizex (*3)
	ccx ccy setxy  |*********
	swap charemit exec
	'ccx +! ;

::gemit | c --
	13 =? ( drop cr ; )
:gemiti
	9 =? ( drop dtab ; )
	charsize exec 'ccx +! ;

::emitout? | -- f
	ccy ty2 >? ( 1 )( 0 ) nip ;

::lout? | -- f ; nunca se cumple porque emit lo evita  (*1)
	ccx tx2 >? ( 1 )( 0 ) nip ;

::spc
	32 emit ;

|--------------------------------------------------------------------------

::fontd
	cch 2/ 'cch ! ccw 2/ 'ccw ! ;

::fonta
	cch 2* 'cch ! ccw 2* 'ccw ! ;

|----- cursor
::printcur | adr -- adr
	ccx >r
	ccx ccy  2dup op cch + pline
	c@+ 0? ( ccw 'ccx +! ) gemiti
	ccx ccy cch + 2dup pline cch - pline
	poli
	r> 'ccx !
	;

::printcurw | adr -- adr
	ccx ccy 2dup op cch + pline
	( c@+ 32 >? )( gemit ) gemit
	ccx ccy cch + 2dup pline cch - pline
	poli
	;

|-------------------------------------
:sizeprint | "" -- "" x
	0 >r dup ( c@+ 1? )( charsize exec r+ ) 2drop r> ;

|------------------- ventana de texto
::fixsize | w h --
	gc2win
	'cch ! 'ccw !
:filcol
	h cch / 'rows !
	w ccw / 'cols !
    ;

::wtext | lin --		; texto en ventana de cursor
	gc2win
	h swap / dup 'cch ! 2/ dup 2/ + 'ccw !
	filcol
|	home ;
::home
	0 dup
::at | x y --
	cch * ty1 + 'ccy !
::col | x --
	ccw * tx1 + 'ccx ! ;

|///////////////////////////////////////////////////////////////////////////
::print | .. "" --
	mprint
::printx | "" --
	( c@+ 1? )( emit ) 2drop ;

::printc | .. "" --	; en centro de la ventana
	mprint
	sizeprint xc swap 2/ - 'ccx !
	printx ;

::printr | .. "" --	; en derecha de la ventana
	mprint
	sizeprint tx2 swap - 'ccx !
	printx ;

::cntprint | .. "" cnt --
	>r mprint r>
	( 1? )( 1- swap c@+ 0? ( drop 1- 32 )
		emit swap ) 2drop ;

|-----------------------------------------
::fonti
	8 12 fixsize
	'char8i 'charsize8i setfont ;


|--- dibujo de seleccion
#conex

::sel>>
	tx1 ccy cch + dup 'conex ! op
	ccx ccy cch + pline
	ccx ccy pline
	tx2 ccy pline
	;

::sel<<
	emitout? 1? (
		tx2 ty2 pline
		tx1 ty2 pline
	)(
		tx2 ccy pline
		ccx ccy pline
		ccx ccy cch + pline
		tx1 ccy cch + pline
   		) drop
	tx1 conex pline
	poli ;

|-----------------------------------------
::printbox | .. "" -- ""  ; pone gc en el texto (resuelve print)
	mprint sizeprint 'w !
	ccx w 2/ + 'xc !
	ccy cch dup 'h ! 2/ + 'yc ! ;

::cntprintbox | cnt -- cnt  ; pone gc en el texto
	ccw over * 'w !
	ccx w 2/ + ccw 2/ + 'xc !
	ccy cch dup 'h ! 2/ + 'yc ! ;

||||| boxprint
::boxprint | .. "" cnt --
	ccw * ccx + >r
	print
	r> 'ccx ! ;

::boxprintc | .. "" cnt --
	ccw * ccx + >r
	mprint
	sizeprint r ccx - 2/ swap - 'ccx +!
	printx
	r> 'ccx ! ;

::boxprintr | .. "" cnt --
	ccw * ccx + >r
	mprint
	sizeprint r swap - 'ccx !
	printx
	r> 'ccx ! ;

|||| fills
::linefill
	tx1 ccy 2dup op
	2dup cch + pline
	tx2 ccy 2dup cch + pline pline pline poli
	;

::linebox
	tx1 ccy 2dup op
	2dup cch + line
	tx2 ccy 2dup cch + line line line
	;

::linehfill | col1 col2 --
	fcol
	tx1 tx2 + 2/ ty1 ty2 + 2/ fcen
	1.0 ccw / 0 fmat
	tx1 ccy 2dup op
	2dup cch + pline
	tx2 ccy
	2dup cch + pline pline pline
	lfill poli sfill
	;

::linevfill | col1 col2 --
	fcol
	tx1 tx2 + 2/ ty1 ty2 + 2/ fcen
	0 1.0 cch / fmat
	tx1 ccy 2dup op
	2dup cch + pline
	tx2 ccy
	2dup cch + pline pline pline
	lfill poli sfill
	;
