| Fuentes unificadas
| :r4 libreria de sistema - Fuente de letras
|---------------------------------------------
^reda4.txt
^libmprint.txt
^libgraf.txt
^fonti.txt

#tx1 0 #ty1 0
#tx2 640 #ty2 480 		| ventana de texto
#:fils 40 #:cols 80		| filas/columnas texto

#charsize 'charsize8i
#charo 'char8i
#sizef

|--------- palabras de dibujo de fuentes (reemplazables)
::setfont | 'char 'size --
	64 over exec 2* 'sizef !
	'charsize !+ ! ;

::window | x1 y1 x2 y2 --
	cch * 'ty2 ! ccw * 'tx2 ! cch * 'ty1 ! ccw * 'tx1 ! ;

::win2gc
	tx2 tx1 2dup - 'w ! + 2/ 'xc !
	ty2 ty1 2dup - 'h ! + 2/ 'yc !
	;

|------------------------------------------------------
::cr
	cch 'ccy +! tx1 'ccx ! ;

::cr2
	cch 2/ 'ccy +! tx1 'ccx ! ;

:dtab
	ccx 5 >> 1+ 5 << 'ccx ! ;

::emit | c --
	9 =? ( drop dtab ; )
	13 =? ( drop cr ; )
	dup charsize exec  | c sizex
|	dup 2* ccx + tx2 >? ( 3drop ; ) drop | c sizex (*1)
|	dup 2* ccx + tx2 >? ( cr ) drop | c sizex (*2)
	dup 2* ccx + tx2 >? ( 'ccx ! 2drop ; ) drop | c sizex (*3)
	'ccx 2dup +!
	rot charo exec
	+! ;

::gemit | c --
	13 =? ( drop cr ; )
:gemiti
	9 =? ( drop dtab ; )
	charsize exec 2*
	'ccx +! ;

::emitout? | -- f
	ccy ty2 >? ( 1 )( 0 ) nip ;

::lout? | -- f ; nunca se cumple porque emit lo evita  (*1)
	ccx tx2 >? ( 1 )( 0 ) nip ;

|--------------------------------------------------------------------------
::fontd
	cch 2/ 'cch ! ccw 2/ 'ccw ! ;

::fonta
	cch 2* 'cch ! ccw 2* 'ccw ! ;

|----- cursor
::printcur | adr -- adr
	ccx >r
	ccx ccy cch 2/ - 2dup op cch + pline
	c@+ 0? ( ccw 2/ 'ccx +! ) gemiti
	ccx ccy cch 2/ + 2dup pline cch - pline
	poli
	r> 'ccx !
	;

::printcurw | adr -- adr
	ccx ccy cch 2/ - 2dup op cch + pline
	( c@+ 32 >? )( gemit ) gemit
	ccx ccy cch 2/ + 2dup pline cch - pline
	poli
	;

|-------------------------------------
:sizeprint | "" -- "" x
	0 >r dup ( c@+ 1? )( charsize exec r+ ) 2drop r> ;

::vidi
	inverso 0? ( $ffffffff )( 0 ) nip 'inverso ! ;

|------------------- ventana de texto
::fixsize | w h --
	xc w 2/ - dup 'tx1 ! w + 'tx2 !
	yc h 2/ - dup 'ty1 ! h + 'ty2 !
	'cch ! 'ccw !
:filcol
	h cch / 'fils !
	w ccw 2* / 'cols !
    ;

::wtext | lin --		; texto en ventana de cursor
	xc w 2/ - dup 'tx1 ! w + 'tx2 !
	yc h 2/ - dup 'ty1 ! h + 'ty2 !
	h swap / dup 'cch ! 2/ dup 2/ + 'ccw !
	filcol
|	home ;
::home
	0 0
::at | x y --
	cch * ty1 + cch 2/ + 'ccy !
::col | x --
	ccw * tx1 + 'ccx ! ;

::ttext | fil col --
	xc w 2/ - dup 'tx1 ! w + 'tx2 !
	yc h 2/ - dup 'ty1 ! h + 'ty2 !
	h swap / 'cch !
	w swap / 'ccw !
	home ;

|///////////////////////////////////////////////////////////////////////////
::print | .. "" --
	mprint
::printx | "" --
	( c@+ 1? )( emit ) 2drop ;

::printc | .. "" --	; en centro de la ventana
	mprint
	sizeprint xc swap - 'ccx !
	printx ;

::printr | .. "" --	; en derecha de la ventana
	mprint
	sizeprint tx2 swap 2* - 'ccx !
	printx ;

::cntprint | .. "" cnt --
	>r mprint r>
	( 1? )( 1- swap c@+ 0? ( drop 1- 32 )
		emit swap ) 2drop ;

|-----------------------------------------
::fonti
	4 12 fixsize home
	'char8i 'charsize8i setfont ;

::fontid
	8 24 fixsize home
	'char8id 'charsize8id setfont ;

|--- dibujo de seleccion
#conex

::sel>>
	tx1 ccy cch 2/ + dup 'conex ! op
	ccx ccy cch 2/ + pline
	ccx ccy cch 2/ - pline
	tx2 ccy cch 2/ - pline
	;

::sel<<
	emitout? 1? (
		tx2 ty2 pline
		tx1 ty2 pline
	)(
		tx2 ccy cch 2/ - pline
		ccx ccy cch 2/ - pline
		ccx ccy cch 2/ + pline
		tx1 ccy cch 2/ + pline
   		) drop
	tx1 conex pline
	poli ;

|-----------------------------------------
::printbox | .. "" -- ""  ; pone gc en el texto (resuelve print)
	mprint sizeprint 2* 'w !
	cch 'h !
	ccx w 2/ + 'xc !
	ccy 'yc ! ;

::cntprintbox | cnt -- cnt  ; pone gc en el texto
	sizef over * 'w !
	cch 'h !
	ccx w 2/ + 'xc !
	ccy 'yc ! ;

||||| boxprint
::boxprint | .. "" cnt --
	sizef * ccx + >r
	print
	r> 'ccx ! ;

::boxprintc | .. "" cnt --
	sizef * ccx + >r
	mprint
	sizeprint r ccx - 2/ swap - 'ccx +!
	printx
	r> 'ccx ! ;

::boxprintr | .. "" cnt --
	sizef * ccx + >r
	mprint
	sizeprint 2* r swap - 'ccx !
	printx
	r> 'ccx ! ;