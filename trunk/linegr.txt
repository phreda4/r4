| linea gruesa
| PHREDA 2008
|-----------------------------------
#grueso )( 32768
#grueso> 'grueso

#:gro 18

#x3 #y3 #x4 #y4
#xa #ya
#xb #yb
#xe #ye
#ang1 #ang2

:calg | x y angulo -- x y
	sincos gro 16 *>> rot + swap gro 16 *>> rot + swap ;

:,gro | x y --
	xy>d grueso> !+ 'grueso> ! ;

:caso1 | se cortan las lineas
	xb yb ang1 calg ,gro
	xb yb ang2 calg ,gro
	;

:parte2
	xb 'xa ! yb 'ya ! ang2 'ang1 !
	xe 'xb ! ye 'yb !
	;

:res
	xa ya ang1 calg 'y1 ! 'x1 !
	xb yb ang1 calg  'y2 ! 'x2 !  | x2 y2
	xb yb ang2 calg  'y3 ! 'x3 !  | x3 y3
	x2 x1 - dup * y2 y1 - dup * +
	x3 x1 - dup * y3 y1 - dup * + <=? ( drop caso1 ; ) drop
	xe ye ang2 calg  'y4 ! 'x4 !  | x4 y4
	y2 y1 - x4 x3 - * y4 y3 - x2 x1 - * - | d
	0? ( drop caso1 ; )
	x4 x3 - y3 y1 - * y4 y3 - x3 x1 - * - | d la
	y2 y1 - over pick3 */ y1 + >r
	x2 x1 - swap rot */ x1 + r>
	,gro ;

#desde
#hasta
::hacegrueso | desde hasta --
	over 8 + <? ( 2drop ; )  'hasta !  'desde !
	'grueso 'grueso> !
	desde
	@+ d>xy 'ya ! 'xa !
	@+ d>xy 'yb ! 'xb !
	xa ya ang1 calg op
	xa xb - ya yb - atan2 $200 + 'ang1 !
	xa ya ang1 calg ,gro | punto 0
	( hasta <? )(
		@+ d>xy 'ye ! 'xe !
		xb xe - yb ye - atan2 $200 + 'ang2 !
		res parte2 )
	xb yb ang1 calg ,gro
	-$400 'ang1 +!
	xa xb 'xa ! 'xb !
	ya yb 'ya ! 'yb !
	xa ya ang1 calg ,gro
	8 -
	( desde >? )( 4 - dup
		@ d>xy 'ye ! 'xe !
		xb xe - yb ye - atan2 $200 + 'ang2 !
		res parte2 ) drop
	xb yb ang1 calg ,gro
|	$400 'ang1 +!
|	xa ya ang1 calg ,gro
	;

::dibujagrueso
	'grueso @+ dup d>xy op swap
	( grueso> <? )( @+ d>xy pline ) drop
	d>xy pline poli
	;
