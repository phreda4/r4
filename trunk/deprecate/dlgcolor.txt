| Navegador de Colores
| PHREDA 2006
|---------------------------------------
^reda4.txt
^libgui.txt
^libcolor.txt

#huea
#hueb

#colorb
#colora
#tipo

#colora.r #colora.g #colora.b
#colorb.r #colorb.g #colorb.b

:recalcula
	colora dup $ff and $80 - 8 << 'colora.b ! dup 8 >> $ff and $80 - 8 << 'colora.g ! 16 >> $ff and $80 - 8 << 'colora.r !
	colorb dup $ff and $80 - 8 << 'colorb.b ! dup 8 >> $ff and $80 - 8 << 'colorb.g ! 16 >> $ff and $80 - 8 << 'colorb.r !
	;

:fondoa
	colora ink gc.fbox ;

:fondob
	colorb ink gc.fbox ;

:sr1 rojo 'colora.r .hslide ;
:sg1 verde 'colora.g .hslide ;
:sb1 azul 'colora.b .hslide ;

:sr2 rojo 'colorb.r .hslide ;
:sg2 verde 'colorb.g .hslide ;
:sb2 azul 'colorb.b .hslide ;

:colsolido	0 'tipo ! ;
:coldegl	1 'tipo ! ;
:coldegr	2 'tipo ! ;

:cambiacol colora colorb 'colora ! 'colorb ! recalcula ;

#botonesgui2 1
$ff0000 'ifin 'exit
$00ff00 'istop 'colsolido
$00ff00 'iplay 'coldegl
$00ff00 'irec 'coldegr
$00ff00 'irecicla 'cambiacol

:btnsis
	'botonesgui2 .hbtns ;

#btnf 4
$ff0000 'colsolido "sol"
$ff0000 'coldegl "lin"
$ff0000 'coldegr "rad"
$ff00 'cambiacol "<>"

:btnfunc
	'btnf .hbtnt ;

 :2.h | n --
	$10 <? ( "0" print ) "%h" print ;

:label1
	1 sfont
	rojo colora.r 8 >> $80 + $ff and
	dup 2.h
	16 <<
	verde colora.g 8 >> $80 + $ff and
	dup 2.h
	8 << or
	azul colora.b 8 >> $80 + $ff and
	dup 2.h
	or 'colora ! ;

:label2
	1 sfont
	rojo colorb.r 8 >> $80 + $ff and
	dup 2.h
	16 <<
	verde colorb.g 8 >> $80 + $ff and
	dup 2.h
	8 << or
	azul colorb.b 8 >> $80 + $ff and
	dup 2.h
	or 'colorb ! ;

:colorcompleto
	tipo
	0? ( colora ink sfill )
	1- 0? ( colorb colora gc.hdeg lfill )
	1- 0? ( colorb colora gc.hdeg rfill )
	drop
	gc.fbox
	sfill
	;

#colorgui2
0.4 0.4 0.0 -0.15 'colorcompleto
0.2 0.3 -0.35 -0.15 'fondoa
0.4 0.1 -0.25 0.05 'sr1
0.4 0.1 -0.25 0.15 'sg1
0.4 0.1 -0.25 0.25 'sb1
0.2 0.3 0.35  -0.15 'fondob
0.4 0.1 0.25 0.05 'sr2
0.4 0.1 0.25 0.15 'sg2
0.4 0.1 0.25 0.25 'sb2
0.3 0.1 -0.2 -0.05 'label1
0.3 0.1 0.2 -0.05 'label2
0.1 0.1 -0.45 0.45 'btnsis
0.4 0.1 -0.2 0.45 'btnfunc
0

|---------------------------------------------------------
:colorshow
	inikey
	'exit >esc<
	show cls
		scr 'colorgui2 gui
		cmano ;

::dlgcolor	| tipo col1 col2  -- col2 col1 tipo
	'colorb !+ !+ !
	recalcula
	key.push
	colorshow
	key.pop
	colorb colora tipo ;

