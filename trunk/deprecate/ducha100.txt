| juego de pelea 100% ducha
| PHREDA
|-------------------------
^reda4.txt
^gui.txt
^r4inc/ducha100.inc

#dimc 0.2
:inicio
	16 font
	.page
	12 12 .table
	1 9 .at 4 2 .to
	rojo oscuro "SALIR" [ "main.txt" run ; ] dup >esc< .tbtn
	.> .> .> .> .> .>
	verde oscuro "JUGAR" 'exit dup >esp< .tbtn
	200 'dimc !
	.show cls
		0 0 fpos
		dimc dup fdim
		'dibu2 sprite
		dimc 0.01 +
		1.0 >? ( drop 0.2 ) 'dimc !
		;

#x1 #x2 #s1 #s2 #v1 #v2 #pnt1 #pnt2

:golpe1
  x2 x1 - 0.08 >? ( drop ;  ) drop
  s2 2 =? ( -0.04 'x2 +! drop ; ) drop
  1 'pnt1 +! ;

:golpe2
  x2 x1 - 0.08 >? ( drop ; ) drop
  s1 2 =? ( 0.04 'x1 +! drop ; ) drop
  1 'pnt2 +! ;

#ijug1 'dibu3 'dibu4 'dibu5
#ijug2 'dibu6 'dibu7 'dibu8

:juego
-0.7 'x1 ! 0.7 'x2 !
'exit >esc<
0 >esp<
[ 1 's2 ! golpe2 ; ] <arr>
[ 2 's2 ! ; ] <aba>
[ 0 's2 ! ; ] dup >aba< >arr<
[ 0.01  'v2 ! ; ] <der>
[ -0.01  'v2 ! ; ] <izq>
[ 0 'v2 ! ; ] dup >der< >izq<
[ 1 's1 ! golpe1 ; ] $10 >key
[ 2 's1 ! ; ] $1e >key
[ 0 's1 ! ; ] dup $10 >ukey $1e >ukey
[ -0.01 'v1 ! ; ] $2c >key
[ 0.01 'v1 ! ; ] $2d >key
[ 0 'v1 ! ; ] dup $2c >ukey $2d >ukey
0 paper
show cls
  0 0 fpos
  1.0 1.0 fdim
  'dibu9 sprite
  1.0 1.0 fdim 'dibu1 sprite
  0.2 0.4 fdim
  x1 -0.4 fpos s1 2 << 'ijug1 + @ sprite
  x2 -0.4 fpos s2 2 << 'ijug2 + @ sprite
  8 font blanco 1 0 at
  pnt2 pnt1 "%d : %d" print
  |0 15 at s1 s2 v1 v2 "%d %d %d %d " print
  x1 v1 + -0.8 <? ( drop -0.8 ) 0.8 >? ( drop 0.8 ) 'x1 !
  x2 v2 + -0.8 <? ( drop -0.8 ) 0.8 >? ( drop 0.8 ) 'x2 !
  x2 x1 - 0.04 <? ( -0.01 'x1 +! 0.01 'x2 +! ) drop
|  100 <? ( 1 's1 ! 0 'v1 ! )( 1 'v1 ! 0 's1 ! ) drop
  ;


: 0 paper ( inicio juego ) ;