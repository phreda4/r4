^reda4.txt
^gui.txt
^sprites.txt
^r4inc/camila2.inc

#xini #yini #vx1 #vy1
#s1 'dibu1
#accion
#tacc
#dir1 1
#tileaba

#nivel
#puntos
#vidas

#tiles 0 'dibua 'dibuc 'dibu14 'dibu15 'dibu12 'dibu24 'dibu13  0 'dibu11

#pantalla (
7 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 7
7 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 7
7 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 7
7 0 0 7 7 7 7 7 7 7 7 0 0 0 0 0 0 0 0 7
7 0 0 3 0 0 0 0 0 0 3 0 0 0 0 0 0 0 0 7
7 0 0 3 0 0 0 0 0 0 3 0 0 0 0 0 0 0 0 7
7 0 0 3 0 0 0 0 0 0 3 0 0 0 0 0 0 0 0 7
7 0 0 3 0 0 0 0 0 0 3 0 0 0 0 0 0 0 0 7
7 0 0 3 0 0 0 0 0 0 3 0 0 0 0 0 0 0 2 7
7 0 0 3 0 0 0 0 0 0 7 7 9 9 9 9 9 7 7 7
7 0 0 3 0 0 0 0 0 0 7 7 7 7 7 7 7 7 7 7
7 1 0 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 7
7 7 7 7 7 7 0 0 0 0 0 0 0 0 0 0 0 0 0 7
7 0 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 7
7 0 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 7
7 0 3 0 0 0 0 0 0 7 7 7 0 0 0 0 0 0 0 7
7 0 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 7
7 0 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 7
7 0 3 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 2
7 7 7 7 7 7 9 9 9 9 9 9 9 9 7 7 7 7 7 7
)

#pantalla1 (
7 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 7
7 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 7
7 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 7
7 0 5 5 4 4 4 4 4 4 4 4 4 4 5 5 5 0 0 7
7 0 3 0 0 0 0 0 0 0 1 0 0 0 0 0 3 0 0 7
7 0 3 0 0 0 0 0 0 0 0 0 0 0 0 0 3 0 0 7
7 0 3 0 0 0 0 0 0 0 0 0 0 0 0 0 3 0 0 7
7 0 3 0 0 0 0 0 0 0 0 0 0 0 0 5 5 0 0 7
7 0 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 7
7 0 5 5 5 5 5 5 0 0 0 0 0 1 0 0 0 0 0 7
7 0 0 0 0 0 0 3 0 0 0 0 0 5 5 0 0 0 0 7
7 0 0 0 0 0 0 3 0 0 0 0 0 0 0 0 0 0 0 7
7 0 0 0 0 0 0 3 0 0 0 0 0 0 0 0 0 0 0 7
7 0 0 0 0 0 0 3 0 0 0 0 0 0 0 5 5 0 0 7
7 0 0 0 0 0 0 3 0 0 0 0 0 0 0 0 0 0 0 7
7 0 0 0 0 1 0 3 0 0 0 0 0 0 0 0 0 0 0 7
7 0 0 5 5 5 5 5 0 0 0 0 0 0 0 0 0 0 0 7
7 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 7
7 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 2 7
7 5 5 5 5 5 9 9 9 9 9 9 9 9 5 5 5 5 5 7
)

#digitos 'dibu17 'dibu18 'dibu19 'dibu1a 'dibu1b 'dibu1c 'dibu1d 'dibu1e 'dibu1f 'dibu20

:d3num | nro --
	200 %dim
	dup 100 / 10 mod 2 << 'digitos + @ sprite
	100 0 +%pos
	dup 10 / 10 mod 2 << 'digitos + @ sprite
	100 0 +%pos
	10 mod 2 << 'digitos + @ sprite ;

:d1num | nro --
	200 %dim
	10 mod 2 << 'digitos + @ sprite ;

|---------------------------------------------------------

:gettile | x y -- t
	-? ( 2drop 0 ; ) 19 >? ( 2drop 6 ; )
	swap 
	-? ( 2drop 6 ; ) 19 >? ( 2drop 6 ; )
	swap
	20 * + 'pantalla + c@ ;

:settile | c x y -- 
	-? ( 3drop ; ) 19 >? ( 3drop ; )
	swap 
	-? ( 3drop ; ) 19 >? ( 3drop  ; )
	swap
	20 * + 'pantalla + c! ;

:tiledraw | x y --
	gettile
	2 << 'tiles  + @ 1? ( 
		'dibu11 =? ( blink 1? ( nip 'dibu23 swap ) drop )
		'dibua =? ( blink 1? ( nip 'dibub swap ) drop )
		sprite )( drop ) ;

:fondo
	104 104 %o dim
	-4 ( 24 <? )(
		0 ( 23 <? )( | n n
		xini pick2 100 * +
		yini pick2 100 * + %pos
		2dup tiledraw
		1+ ) drop
	1+ ) drop ;

:lugartile | -- x y
	xini neg 550 + 100 /
	yini neg 550 + 100 / ;

| sprites:  2 qword(32 bits) uv, 'accion
|------------------------------------------------------------------------
#sprts )( 8192
#sprts> 'sprts

:spr.clear
	'sprts 'sprts> ! ;

:+spr | 'acc x y --
	uv> sprts> !+ !+ 'sprts> !  ;

:spr.del | dir hast -- dir
	over 8 + ( over <? )( 
		dup @ over 8 - ! 4+ 
		dup @ over 8 - ! 4+ 
		) 2drop ;

:distspr | x y 'uv -- d
	>uv rot - 3 >> dup * >r - 3 >> dup * r> + ;

:spr.draw
	'sprts ( sprts> <? )( 
 		dup @ >uv  | adr x y
		pick2 4+ @ exec 
		0? ( drop uv> swap !+ 4+ )( 3drop sprts> spr.del -8 'sprts> +! )
		) drop ;

:spracc! | x y 'adrconhit  'newacc -- x y
	swap 4+ ! ;

#menor
#menor>

:gethit | x y tipo tam -- x y 'adrconhit 
	'menor !
	0 'menor> !
	'sprts >r
	sprts>	| x y tipo sprts>
	( r >? )(
		over | x y tipo sprts> tipo
		r 4+ @ 
		<>? ( drop )( drop | x y tipo sprts>
			pick3 pick3 r @ distspr | x y tipo sprts dist
			menor <? ( 'menor !  r 'menor> ! )( drop )
			)
		8 r+ ) rdrop 2drop
	menor> ;


|------------------------------------------------------------------------
:ballfire | x y -- x y e
	100 %dim
	over xini + over yini + %pos
	blink 1? ( 'dibud )( 'dibue ) nip sprite 

	10 rand 3 mod + + 
	2000 >? ( 1 ; )
	swap
	rand 10 mod + swap

|	swap 16 - -? ( 1 ; )
|	swap 
|	over xc - 3 >> dup * 
|	over yc - 3 >> dup * + 
|	140 <? ( drop -1 'vida +! 1 exit ; ) drop
	0 ;

:dispara
	'ballfire 1000 rand 1000 mod +  0 +spr ;

#vele -10

:enemigo | x y -- x y e
	200 %dim
	over xini + over yini + %pos
	blink 1? ( 'dibuf )( 'dibu10 ) nip sprite 
	swap vele +
	300 <? ( vele -? ( neg ) 'vele ! )
	1700 >? ( vele +? ( neg ) 'vele ! )
	swap
	rand $f nand? ( 'ballfire  pick3 100 +spr drop 0 ; ) drop
|	swap 16 - -? ( 1 ; )
|	swap 
|	over xc - 3 >> dup * 
|	over yc - 3 >> dup * + 
|	140 <? ( drop -1 'vida +! 1 exit ; ) drop
	0 ;

|	blink 1? ( 'dibuf )( 'dibu10 ) nip sprite 
|------------------------------------------------------------------------

:+accion | acc --
	'accion !	0 'tacc ! 	;

#caminar 'dibu1 'dibu2 'dibu1 'dibu3

:ajusta
	yini 50 mod neg 'yini +! ;

:pasos
	vx1 neg 'xini +!	
	lugartile gettile
	vx1 'xini +!
	4 >? ( 0 'vx1 ! )  drop
	lugartile 1+ gettile | abajo
	4 >? ( 0 'vy1 !  ajusta )( vy1 20 >? ( 20 nip ) 2 + 'vy1 ! ) 'tileaba !
	tacc 
	vx1 0? ( nip 7 swap ) drop
	1+ 7 >? ( 0 nip ) dup 'tacc !
	1 >> 2 << 'caminar + @ 's1 !
	;

:salto | 
	tacc
	0? ( -36 'vy1 ! 'dibu4 )( 2 'vy1 +! 'dibu5 ) 's1 ! 
	vy1 0? ( 'pasos +accion ) drop 
	1+ 'tacc ! ;

:escalera
	lugartile gettile 3 <>? ( 'pasos +accion drop ; ) drop
	20 'yini +!
	tacc
	4 <? ( 'dibu6 )(  'dibu7 ) 's1 ! 
	1+ 7 >? ( 0 nip ) 'tacc ! ;

:soga
	tacc
	4 <? ( 'dibu8 )( 'dibu9  ) 's1 !
	1+ 7 >? ( 0 nip ) 'tacc ! ;

:direccion | n
	dup 'dir1 ! 'vx1 ! ;

:teclarriba
	lugartile gettile 3 =? ( 'escalera +accion drop ; ) drop
	tileaba 5 <? ( drop ; ) drop
	accion 'salto =? ( drop ; ) drop
	'salto +accion ;

:essoga
	accion 'soga =? ( drop ; ) drop
	'soga +accion 0 'vy1 ! ajusta ;

:juego
	.page
	'exit >esc<
	[ 10 direccion ; ] <der>
	[ -10 direccion ; ] <izq>
	[   0 'vx1 ! ; ] dup >der< >izq<
	'teclarriba <arr>
	'enemigo  1000 0 +spr 
	.show cls
		fondo
		accion exec 
		vx1 neg 'xini +!
		vy1 neg 'yini +! 

		lugartile gettile
		1 =? ( 1 'puntos +! 0 lugartile settile )
		2 =? ( 5 'puntos +! 0 lugartile settile )
		4 =? ( essoga )( accion 'soga =? ( -14 'vy1 ! 'salto +accion ) drop )
		drop
		200 dir1 +? ( drop %dim )( drop -%dim )
		500 500 %pos s1 sprite

		spr.draw

		700 150 %pos
		puntos d3num
		100 150 %pos
		vidas d1num
|		16 dup screen blanco 
|		dup " (%d)" print
|		1 0 at puntos "Puntos:%d " print 12 0 at vidas "Vida:%d" print
		;


:posicioninicio
	0 dup 'vx1 ! 'vy1 ! 
	400 'xini ! 
	-1250 'yini !
	'pasos +accion
	spr.clear
	;

:inicionivel
	3 'vidas !
	0 'nivel !
	0 'puntos !
	posicioninicio
	juego
	;

:perdiste
	;

:pasonivel
	;

#ang
:presentacion
	.page
	12 12 .table
	1 9 .at 4 2 .to 
	rojo "SALIR" 'exit dup >esc< .tbtn 
	.> .> .> .> .> .> 
	verde "JUGAR" 'inicionivel dup >esp< .tbtn 
	.show cls
		todo 
		'dibu22 sprite
		'dibu21 ang rsprite 
		3 'ang +! ;

: 0 paper 4 presentacion "main.txt" run ;