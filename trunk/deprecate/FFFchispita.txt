| Chispita Invaders
| La Casita 
| dibujos: Jonael  edad: 7
|-------------------------------
^reda4.txt
^sprites.txt
^gui.txt
^r4inc/jonael.inc

#puntos
#vida

#xc 200 
#yc 500
#xvc 0
#yvc 0

#rch 0

:chismuere
	.page
	'exit <esc>
	0 'rch !
	.show cls
		250 %dim
		xc yc %pos
		blink 1? ( 'dibu23 )( 'dibu24 ) nip rch rsprite
		40 'rch +! 
		5 'yc +!
		yc 1200 >? ( exit ) 5 + 'yc ! ;

|---------------
| acciones
|	Dibujo
|	movimiento y limites
|	colisiones
|
:heli1 
	200 %dim
	2dup %pos
	'dibu15 sprite

	swap 16 - -? ( 1 ; )
	swap 
	over xc - 3 >> dup * 
	over yc - 3 >> dup * + 
	140 <? ( drop -1 'vida +! chismuere 1 exit ; ) drop
	0 ;

:heli1die
	200 %dim
	2dup %pos
	'dibu15 msec 2/ neg pick3 + rsprite

	20 + 1000 >? ( 1 ; )
	swap 4 + swap 0 
	;

	
:disp |  x y -- nx ny f (1=del)
	60 %dim
	2dup %pos
	'dibu22 sprite

	swap 20 + 1060 >? ( 1 ; )
	swap 
	'heli1 70 sprite.gethit 1? ( 'heli1die sprite.acc! 1 1 'puntos +! ; ) 
	drop 0 ;

:misil
	;

:misilcrash
	;

:explotchispita
	200 %dim
	2dup %pos
	'dibu13 sprite
	1000 >? ( 1 ; )
	10 + 0 ;

:+helicoptero
	'heli1 1100 rand 200 mod yc + +sprite ;

:drawscore
	24 16 screen  blanco 
	1 0 at puntos "Puntos:%d " print 
	18 0 at vida "Vida:%d" print ;
	
:play 
	.page
	[ 0 'vida ! exit ; ] >esc<
	[ -10 'yvc ! ; ] <arr>
	[ 10 'yvc ! ; ] <aba>
	[ 0 'yvc ! ; ] dup >aba< >arr<
	[ -10 'xvc ! ; ] <izq>
	[ 10 'xvc ! ; ] <der>
	[ 0 'xvc ! ; ] dup >der< >izq<
	[ 'disp xc 40 + yc 28 - +sprite  ; ] <esp>
	.show cls |		todo 'dibud sprite
		xvc 'xc +!
		yvc 'yc +!
		xc 100 <? ( 100 'xc ! 0 'xvc ! ) 800 >? ( 800 'xc ! 0 'xvc ! ) drop
		yc 100 <? ( 100 'yc ! 0 'yvc ! ) 900 >? ( 900 'yc ! 0 'yvc ! ) drop
		sprite.draw
		250 %dim
		xc yc %pos
		blink 1? ( 'dibu23 )( 'dibu24 ) nip sprite
		rand $f nand? ( +helicoptero ) drop
|		32 dup screen  verde 0 2 at debugspr
		drawscore ;

:caida
	.page
	'exit >esc<
	.show cls
		250 %dim
		xc yc %pos
		blink 1? ( 'dibu23 )( 'dibu24 ) nip sprite
		8 dup screen
		0 1 at verde
		blink 1? ( "Preparate" pcenter print ) drop
		drawscore
		3 .segs exit ;

:resetnivel
	500 'yc !
	200 'xc !
	0 dup 'xvc ! 'yvc ! 
	sprite.clear ;

:inicionivel
	|swarning splay 
	3 'vida !
	( vida 1? )( drop
		resetnivel  
		play
		resetnivel  
		vida 1? ( caida ) drop | ( fin )
		) drop	;

#fade 0
#swarning

:presenta
	.page
	12 12 .table
	1 9 .at 4 2 .to 
	rojo "SALIR" 'exit dup >esc< .tbtn 
	.> .> .> .> .> .> 
	verde "JUGAR" 'inicionivel dup >esp< .tbtn 
	.show cls
		fade 64 >? ( 0 nip )( 1+ ) dup ink 'fade ! 
		3 2 screen ":R4%.Games" print
		6 6 screen 
		rojo 0 1 at "Chispita" pcenter print
		verde 0 2 at "Invaders" pcenter print ;
	
: 0 paper
|	"sound\\boink3.wav" sload 'sgolpe !
|	"sound\\drip.wav" sload 'sdisparo !
|	"sound\\siren_loop.wav" sload 'swarning !
	33 presenta "main.txt" run  	;
