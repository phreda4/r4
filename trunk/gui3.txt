| gui3
| idea en esta version:
|  cada palabra se dibuja y actua sola (no esta "compilada")
|  el teclado esta incluido en la interaccion
|
^libprint.txt
^libsprite.txt
^libgc.txt
^memscr.txt
^font.txt
^fonth.txt

#bmouse..
#click..
#focus.. 1
#idf
#idl
#sucio 1

||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
::clrscr
	idf 'idl !	| ultimo
	0 'idf !	| primero
	cls scr home
	bmouse bmouse.. =? ( drop ; )
	0? ( 1 )( 0 ) 'click.. !
	'bmouse.. !
	;

::onIn | 'vec --
	xymouse whin 1? ( drop exec ; ) 2drop ;

::onInOut | 'vecin 'vecout --
	xymouse whin 1? ( 2drop )( drop nip ) exec ;

::onClick | 'vec --
	click.. 1? ( drop xymouse whin 1? ( 0 'click.. ! drop exec ; ) 2drop ; )
	2drop ;

||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
| botones sin foco
::btns | acc "str" --
	gc.push
	ink@ dup >r $222222
	[ swap ; ] [ 0.95 fzoom ; ] onInOut
	vbtn
	1 font
	blanco printc r> ink
	onClick
	gc.pop ;

::btnh | acc "str" --
	gc.push
	ink@ dup >r $222222
	[ swap ; ] [ 0.95 fzoom ; ] onInOut
	vbtn
	1 fonthb
	blanco printc r> ink
	onClick
	gc.pop ;

::btnd | acc 'v8d --
	gc.push
	ink@ dup >r $222222
	[ swap ; ] [ 0.95 fzoom ; ] onInOut
	vbtn
	blanco v8draw r> ink
	onClick
	gc.pop ;

||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
::ccruz
	xymouse bmouse 2 << 8 + cruz ;

#flecha  $9B8FEFC1 $FA3BEC91 $19C1B2B3 $2F4D1B43 $5839C8D3 $74B160B3 $4560BA93 $6DDC7533 $FA3BEC9A
 $D $7102111 $51DC7753 $35F0AD43 $65995A93 $56A18D03 $2A84DAF3 $1CBD42F3 $710211A
 $FFFFFFD $0

::cflecha
	xymouse 'yc ! 'xc !
	sw 3 >>	bmouse 2 << + dup 'w ! 'h !
	'flecha nsprite ;

||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
|------------------- edita linea
#cmax
#padi>	| inicio
#pad>	| cursor
#padf>	| fin

:lins  | c --
	padf> padi> - cmax >=? ( 2drop ; ) drop
	pad> dup 1- padf> over - 1+ cmove> 1 'padf> +!
:lover | c --
	pad> c!+ dup 'pad> !
	padf> >? (
		dup padi> - cmax >=? ( swap 1- swap -1 'pad> +! ) drop
		dup 'padf> ! ) drop
:0lin | --
	0 padf> c! ;
:kdel
	pad> padf> >=? ( drop ; ) drop
	1 'pad> +!
:kback
	pad> padi> <=? ( drop ; )
	dup 1- swap padf> over - 1+ cmove -1 'padf> +! -1 'pad> +! ;
:kder
	pad> padf> <? ( 1+ )  'pad> ! ;
:kizq
	 pad> padi> >? ( 1- ) 'pad> !  ;

#modo 'lins

|------- manejo de foco
:nextfoco
	focus.. 1+ idl >? ( 1 nip ) 'focus.. !
	1 'sucio ! ;

:ktab
	mshift 0? ( drop nextfoco ; ) drop
	focus.. 1- 0 <=? ( idl nip ) 'focus.. !
	1 'sucio ! ;

|------- teclado para linea de texto
:setinput | 'var max --
	'cmax ! dup 'padi> !
	( c@+ 1? )( drop ) drop 1- dup
	'pad> ! 'padf> !
	'lins 'modo !
	[ key toasc modo exec ; ] <visible>
	[ modo 'lins =? ( 'lover )( 'lins ) 'modo ! drop  ; ] <ins>
	'kback <back>	'kdel <del>
	'kder <der>		'kizq <izq>
	[ padi> 'pad> ! ; ] <home>
	[ padf> 'pad> ! ; ] <end>
	'ktab dup <tab> <enter>
	;

|-------- teclado para boton
:setinputb | 'var --  ; boton con foco
	<enter>
	0 <visible>
	0 <ins> 0 <back> 0 <del> 0 <der> 0 <izq>
	0 <home> 0 <end>
	'ktab <tab>
	;

:dcursor
	blink 1? ( drop ; ) drop
	ink@ >r
	modo 'lins =? ( azul )( rojo ) drop
	printcur 1-
	r> ink ;

:drawinput | --
	cmax cntprintbox ink@ blanco gc.fbox ink
	padi> ( pad> =? ( dcursor )
		c@+ 1? )( emit ) 2drop ;

||||||||||||||||||||||||||||
| con foco

::getline | 'dir cnt --
	1 'idf +!
	focus..
	idf =? (
		sucio 1? ( 0 'sucio ! pick3 pick3 setinput ) drop
		3drop drawinput ; )
	drop
	cntprintbox |gc.box
	xc w 2/ - yc h 2/ + 2dup op
	swap w + swap line
	cntprint
	;

::fbtns | 'acc "tex" --
	1 'idf +!
	gc.push
	ink@ dup >r $222222
	[ swap ; ] [ 0.95 fzoom ; ] onInOut
	vbtn
	1 font
	blanco printc r> ink
	focus..
	idf =? (
		sucio 1? ( 0 'sucio ! pick3 setinputb ) drop
		0.95 fzoom ink@ negro gc.rod ink )
	drop
	onClick
	gc.pop ;

::fbtnh | 'acc "tex" --
	1 'idf +!
	gc.push
	ink@ dup >r $222222
	[ swap ; ] [ 0.95 fzoom ; ] onInOut
	vbtn
	1 fonthb
	blanco printc r> ink
	focus..
	idf =? (
		sucio 1? ( 0 'sucio ! pick3 setinputb ) drop
		0.95 fzoom ink@ negro gc.rod ink )
	drop
	onClick
	gc.pop ;

::fbtnd | acc 'v8d --
	1 'idf +!
	gc.push
	ink@ dup >r $222222
	[ swap ; ] [ 0.95 fzoom ; ] onInOut
	vbtn
	1 fonthb
	blanco v8draw r> ink
	focus..
	idf =? (
		sucio 1? ( 0 'sucio ! pick3 setinputb ) drop
		0.95 fzoom ink@ negro gc.rod ink )
	drop
	onClick
	gc.pop ;

::dumpgui
	scr fonti
	focus.. "foco..:%d " print
	idf "idf:%d " print
	;

::inigui
	inikey
	1 'focus.. !
	1 'sucio !

	;

||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
||||| Dialogos

#tt

:dlgsinor | -- 0/1
	inikey
	[ 0 exit ; ] >esc<
	[ 1 exit ; ] >esp<
	show
		clrscr
		screencopy
		0 0 fpos
		0.6 0.5 fdim azul gc.frod
		0.15 dup fdim
		-0.2 -0.3 fpos
		rojo oscuro
		[ 0 exit ; ] 'ifin btnd
		0.2 -0.3 fpos
		verde oscuro
		[ 1 exit ; ] 'iok btnd
		blanco
		0.6 dup fdim 0 0 fpos
		7 fonthb cr cr
		tt printc
		cflecha ;

::dlgsino | "" -- 0/1
	'tt !
	oscurecetodo
	screenmark
	key.push
	dlgsinor
	key.pop
	screenempty
	;
