| gui3
| idea en esta version:
|  cada palabra se dibuja y actua sola (no esta "compilada")
|  el teclado esta incluido en la interaccion
|
^libprint.txt
^libsprite.txt
^libgc.txt
^memscr.txt
^font.txt
^fonth.txt

		| fondo fuente
#colores $408080 $ffffff | elegido
#color.titulo $C0C0C0 $0 | titulo
#color.entrada $ffffff $0 | Entrada

#bmouse..
#click..
#focus.. 1
#sucio 1
#idf
#idl

||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
::clrscr
	idf 'idl !	| ultimo
	0 'idf !	| primero
	cls scr home
	bmouse bmouse.. =? ( drop 0 'click.. ! ; )
	0? ( 1 )( -1 ) 'click.. !
	'bmouse.. !
	;

::inigui
	key.push
	inikey
	1 dup 'focus.. ! 'sucio !
	;

::exit
	1 '.exit !
	key.pop
	1 dup 'focus.. ! 'sucio !
	;

::onIn | 'vec --
	xymouse whin 0? ( 2drop ; ) drop
	exec ;

::onMove | 'vec --
	xymouse whin 0? ( 2drop ; ) drop
	bmouse 0? ( 2drop ; ) drop
	exec ;

::onInOut | 'vecin 'vecout --
	xymouse whin 1? ( 2drop )( drop nip ) exec ;

::onClick | 'vec --
	click.. 0? ( 2drop ; ) -? ( 2drop ; ) drop
	xymouse whin 0? ( 2drop ; ) drop
	0 'click.. ! exec ;

::onDn | 'vec --
	click.. 0? ( 2drop ; ) +? ( 2drop ; ) drop
	xymouse whin 0? ( 2drop ; ) drop
	0 'click.. ! exec ;


||||||||||||||||||||||
||  Edita linea
#cmax
#padi>	| inicio
#pad>	| cursor
#padf>	| fin

:lins  | c --
	padf> padi> - cmax >=? ( 2drop ; ) drop
	pad> dup 1- padf> over - 1+ cmove> 1 'padf> +!
:lover | c --
	pad> c!+ dup 'pad> !
	padf> >? (
		dup padi> - cmax >=? ( swap 1- swap -1 'pad> +! ) drop
		dup 'padf> ! ) drop
:0lin | --
	0 padf> c! ;
:kdel
	pad> padf> >=? ( drop ; ) drop
	1 'pad> +!
:kback
	pad> padi> <=? ( drop ; )
	dup 1- swap padf> over - 1+ cmove -1 'padf> +! -1 'pad> +! ;
:kder
	pad> padf> <? ( 1+ )  'pad> ! ;
:kizq
	 pad> padi> >? ( 1- ) 'pad> !  ;

#modo 'lins

|------- manejo de foco
:nextfoco
	focus.. 1+ idl >? ( 1 nip ) 'focus.. !
::guifresh
	1 'sucio ! ;

:prevfoco
	focus.. 1- 0 <=? ( idl nip ) 'focus.. !
	1 'sucio ! ;

:ktab
	mshift 0? ( nextfoco )( prevfoco ) drop ;



|-------- teclado para boton
:setinputb | 'var --  ; boton con foco
	<enter>
	0 <visible>
	0 <ins> 0 <back> 0 <del>
	0 <home> 0 <end>
	'ktab <tab>
	'nextfoco <der> 'prevfoco <izq>
	'nextfoco <aba> 'prevfoco <arr>
	;

|-------- teclado para slide
:setinputs | 'var --  ; boton con foco
	0 <enter>
	0 <visible>
	0 <ins> 0 <back> 0 <del>
	[ 0.01 over +! ; ] <der> [ -0.01 over +! ; ] <izq>
	[ -0.5 over ! ; ] <home> [ 0.5 over ! ; ] <end>
	'ktab <tab>
	'nextfoco <aba> 'prevfoco <arr>
	drop
	;

||||||||||||||||||||||||||||
|| con foco
:clickfoco
	idf focus.. =? ( drop ; )
	'focus.. ! 1 'sucio ! ;

:tienefoco | adr -- |
	1 'idf +!
	focus..
	idf =? (
		sucio 0? ( 2drop exec dup 'sucio ! )( nip nip )
		exec ; )
	drop

|------- teclado para linea de texto
:setinput | 'var max --
	'cmax ! dup 'padi> !
	( c@+ 1? )( drop ) drop 1- dup
	'pad> ! 'padf> !
	'lins 'modo !
	[ key toasc modo exec ; ] <visible>
	[ modo 'lins =? ( 'lover )( 'lins ) 'modo ! drop  ; ] <ins>
	'kback <back>	'kdel <del>
	'kder <der>		'kizq <izq>
	[ padi> 'pad> ! ; ] <home>
	[ padf> 'pad> ! ; ] <end>
	'ktab dup <tab> <enter>
	'nextfoco <aba> 'prevfoco <arr>
	;
|---- ENTRADA

:dcursor
	blink 1? ( drop ; ) drop
	ink@ >r
	modo 'lins =? ( azul )( rojo ) drop
	printcur 1-
	r> ink ;

:drawinput | --
	padi> ( pad> =? ( dcursor ) c@+ 1? )( emit ) 2drop
	xc w 2/ + 'ccx !
	;

::getline | 'dir cnt --
	| 'focogetline  tienefoco
 	cntprintbox
	'color.entrada @+ ink gc.fbox @ ink
	1 'idf +!
	focus..
	idf =? ( sucio 1? ( 0 'sucio ! pick3 pick3 setinput ) drop
		drawinput 3drop )( drop boxprint )
	'clickfoco onClick
	;

|--- focos
:bordefoco
	ink@ blink 1? ( blanco )( negro ) drop
	gc.rod -2 dup 'w +! 'h +! gc.rod
	ink ;

:bordefocos
	ink@ blink 1? ( blanco )( gris ) drop
	gc.box
	ink ;

|---- BOTONES sin foco
::btns | acc "str" --
	gc.push
	ink@ dup >r $222222
	[ swap ; ] [ 0.95 fzoom ; ] onInOut
	vbtn
	1 wtext home |	1 font

	blanco printc r> ink
	onClick
	gc.pop ;

::btnd | acc 'v8d --
	gc.push
	ink@ dup >r $222222
	[ swap ; ] [ 0.95 fzoom ; ] onInOut
	vbtn
	blanco v8draw r> ink
	onClick
	gc.pop ;

|---- BOTONES con foco
::fbtns | 'acc "tex" --
	gc.push
	ink@ dup >r $222222
	[ swap ; ] [ 0.95 fzoom ; ] onInOut
	vbtn
	1 wtext home |	1 font
	blanco printc r> ink
|	'focobtn1 tienefoco
	1 'idf +!
	focus..
	idf =? (
		sucio 1? ( 0 'sucio ! pick2 setinputb ) drop
		bordefoco )
	drop

	onClick
	gc.pop ;

::fbtnd | acc 'v8d --
	gc.push
	ink@ dup >r $222222
	[ swap ; ] [ 0.95 fzoom ; ] onInOut
	vbtn
	blanco v8draw r> ink

|	'focobtn1 tienefoco
	1 'idf +!
	focus..
	idf =? (
		sucio 1? ( 0 'sucio ! pick2 setinputb ) drop
		bordefoco )
	drop

	onClick
	gc.pop ;

::fbtne | acc --
	gc.push

|	'focobtn2 tienefoco

	1 'idf +!
	focus..
	idf =? (
		sucio 1? ( 0 'sucio ! pick2 setinputb ) drop
		blink 1? ( linebox ) drop
		)
	drop

	onClick
	gc.pop ;

||||||||||||||||||||
|| botones de consola
#col1 $ff0000 0

::linkcol | c1 c2
	'col1 !+ ! ;

::link | 'vec "label" --
	printbox
	ink@ >r
	'col1 @+ swap @ [ swap 1.1 fzoom ; ] onIn vbtn
	r> ink
	print
	onClick	;

::clink | 'vec "label" cnt --
	cntprintbox drop
	ink@ >r
	'col1 @+ swap @ [ swap 1.1 fzoom ; ] onIn vbtn
	r> ink
	printc
	onClick ;

::flink | 'vec "label" --
	printbox
	ink@ >r
	'col1 @+ swap @
	[ swap 1.1 fzoom ; ] onIn vbtn
	r> ink
	print
|	'focobtn2 tienefoco

	1 'idf +!
	focus..
	idf =? (
		sucio 1? ( 0 'sucio ! pick2 setinputb ) drop
		bordefoco )
	drop
	onClick
	;

|---- SLIDE
::hslide | 'var --
	[ xymouse drop xc - 16 << w / over ! ; ] onMove
	gc.push 0.98 0.2 fscala
	gc.frod gc.popush
	@ w 16 *>> 'xc +!
 	0.1 0.98 fscala ink@
	$dddddd $222222 vbtn
	gc.pop ink
	;

::vslide | 'var --
	[ xymouse nip yc - 16 << h / over ! ; ] onMove
	gc.push 0.2 0.98 fscala
	gc.frod gc.popush
	@ h 16 *>> 'yc +!
 	0.98 0.1 fscala ink@
	$dddddd $222222 hbtn
	gc.pop ink
	;

|---- corredores
::>>s | adr -- adr'  _
	( c@+ 1? )(
		$5f =? ( drop ; )
		$7c =? ( drop 1- ; )
		$7e =? ( drop 1- ; ) | ~ registro
		drop ) drop 1- ;

::>>f | adr -- adr' |
	( c@+ 1? )(
		$7c =? ( drop ; )
		$7e =? ( drop 1- ; ) | ~ registro
		drop ) drop 1- ;

::>>fi | adr -- adr'  _
	( c@+ 1? )(
		$7e =? ( drop ; ) | ~ registro
		drop ) drop 1- ;

|---- LISTA
::guilista | 'var 'opciones --

	[ xymouse nip yc h 2/ - - rows h */ pick2 ! ; ] onClick
	gc.push
 	0 ( rows  <? )(
 		pick2 @ =? ( |cch 2/ dup 'cch +! 2/ 'ccy +!
				'colores  )( 1 and? ( 'color.titulo )( 'color.entrada ) )
		@+ ink linefill @ ink
		swap dup "%a" printc cr
		>>f
		dup c@ 0? ( 4drop gc.pop ; ) drop
		swap
|		pick2 @ =? ( cch 2/ neg dup 'cch +! 2/ 'ccy +! )
		1+ ) drop
	2drop
	gc.pop ;

|---- botonera
::guibtns | 'var 'opciones --
	gc.push
 	0 ( rows  <? )(
 		pick2 @ =? ( amarillo )( verde oscuro )
		[ dup pick3 ! ; ] pick2 "%a" mprint flink cr
		swap >>f dup c@ 0? ( 4drop gc.pop ; ) drop
		swap
		1+ ) drop
	2drop
	gc.pop ;

|---- GRILLA
#maxanchos )( 64

:genmax | grilla --
	'maxanchos swap  | 'anchos "titi" --
	( dup >>f over - 1? )(	| 'anchos "ll" cnt
			rot c!+ swap
			>>f
			)
	3drop ;

::guigrilla | 'var 'grilla 3 -- ; "N_S_P|1_2_3|44_22_33"
	[ xymouse yc h 2/ - - rows h */ swap
		xc w 2/ - - cols w */ 16 << or pick3 ! ; ] onMove
	gc.push
	over genmax
	0 ( rows <? )(
 			0? ( 'colores )( 1 and? ( 'color.titulo )( 'color.entrada ) )
			@+ ink linefill @ ink
|			dup "%d|" 2 boxprintr
|			pick3 @ $ff and =? ( ">" )( " " ) print
			>r
			0 ( over <? )( >r >r
				dup "%a" r 'maxanchos + c@ 
|				boxprint >>f
"%d" print print >>f
				r> r> 1+ ) drop
			swap >>fi swap cr
			r>
			1+ ) 4drop
	gc.pop ;


|||||| Agrupadores (tratar de no usarlos)
::vlist | cnt 'acc --
	>r
	1.0 over / | cnt sw
	0 ( pick2 <? )( | cnt sw a
		gc.push
		1.0 pick2 2over * over 2/ + 0.5 - 0 swap
		gc.conv | w h xc yc --
		r exec
		gc.pop 1+ )
	3drop rdrop ;

::hlist | cnt 'acc --
	>r
	1.0 over / | cnt sw
	0 ( pick2 <? )( | cnt sw a
		gc.push
		over 1.0 2over * pick2 2/ + 0.5 - 0
		gc.conv | w h xc yc --
		r exec
		gc.pop 1+ )
	3drop rdrop ;
	;

::table | row col 'acc --
	>r
	1.0 pick2 / | row col sw
	1.0 pick2 / | row col sw sh
	0 ( pick4 <? )(
		 0 ( pick4 <? )( | row col sw sh x y
			gc.push
			2over		| row col sw sh x y sw sh
			pick3 pick2 * pick2 2/ + 0.5 -
			pick3 pick2 * pick2 2/ + 0.5 -
			gc.conv | w h xc yc --
			r exec
			gc.pop
			1+ ) drop
		1+ ) drop
	4drop rdrop ;


|||||||||||||||||||
||  Cursores
#flecha  $9B8FEFC1 $FA3BEC91 $19C1B2B3 $2F4D1B43 $5839C8D3 $74B160B3 $4560BA93 $6DDC7533 $FA3BEC9A
 $D $7102111 $51DC7753 $35F0AD43 $65995A93 $56A18D03 $2A84DAF3 $1CBD42F3 $710211A
 $FFFFFFD $0

#mano  $F4440001 $5BB762 $BBC0164 $EB47043 $23707EB2 $3AC8A514 $41810E02 $36D570D4 $36D59753
 $1D35A9E2 $FA519474 $F9416B23 $D98CADC2 $F3E874C4 $F444000A $D $F8ECB3A1 $F8840163
 $FFFFD112 $8880004 $AB09DB3 $132C6762 $1A10B074 $22987662 $2970BB24 $32088A62 $3778C5D4
 $3ED520C2 $35096574 $1B258432 $FB5D5FC4 $E160A6A2 $F6688674 $F8ECB3AA $1CB4DBE7 $2B29A128
 $FFFFFFC $E $FCCD70E1 $1A658E72 $328D7524 $323191A3 $1975A152 $FD818A84 $FCCD70EA
 $150D4BE7 $2511BBB8 $FFFFFFC $E 0

#lapiz  $191 $6B05833 $BD03A43 $16D428F3 $19A $D $6B05371 $9D83263 $167028F3
 $300C4EC3 $1EB86E43 $11B8BE93 $6B0537A $FFFF00D $1154BB71 $18686663 $2EDC4D33 $69293B13
 $4D31B2C3 $1154BB7A $FF7F00D $47AD73C1 $4B3934C2 $5AF929B4 $6D192372 $6B8568C4 $6B21AC82
 $54ADB914 $4551B5F2 $47AD73C9 $FFFF00D $0
#goma  $F17017A1 $90FE543 $2AFBFF63 $3FB06B83 $2734A873 $111C36C3 $F17017AA $7D7E7ED $F1A017F1
 $121C3593 $2708A513 $1B64F063 $F1A017FA $9E9D9FD $26FCA3D1 $3F0067F3 $61AD29B3 $4B396A43
 $26FCA3DA $434344D $1AD8F031 $2708A303 $4BAD6B13 $29D13BD3 $1AD8F03A $787877D $0

::ccruz
	xymouse bmouse 2 << 8 + cruz ;

::cflecha
	xymouse 'yc ! 'xc !
	sw 3 >>	bmouse 2 << + dup 'w ! 'h !
	'flecha nsprite ;

::cmano
	xymouse 'yc ! 'xc !
	sw 3 >>	bmouse 2 << + dup 'w ! 'h !
	'mano nsprite ;

::clapiz
	xymouse 'yc ! 'xc !
	sw 3 >>	bmouse 2 << + dup 'w ! 'h !
	'lapiz nsprite ;

::cgoma
	xymouse 'yc ! 'xc !
	sw 3 >>	bmouse 2 << + dup 'w ! 'h !
	'goma nsprite ;


||||||||||||||||||||
||  Dialogos (separar a otra lib)
#tt
:dlgsinor | "" -- 0/1
	inigui
	[ 0 exit ; ] >esc<
	[ 1 exit ; ] >esp<
	show
		clrscr
		screencopy
		0 0 fpos
		0.6 0.5 fdim
		$ff $11 vbtn
		0.15 dup fdim
		-0.2 -0.3 fpos
		rojo oscuro
		[ 0 exit ; ] 'ifin fbtnd
		0.2 -0.3 fpos
		verde oscuro
		[ 1 exit ; ] 'iok fbtnd
		0.6 dup fdim 0 0 fpos
		7 fonthb cr cr
		negro tt printc
		-2 dup 'xc +! 'ccy +! | porque printc lo centra
		blanco tt printc
		cmano ;

::dlgsino | "" -- 0/1
	'tt !
	oscurecetodo
	screenmark
	dlgsinor
	screenempty ;
