| gui3
| idea en esta version:
|  cada palabra se dibuja y actua sola (no esta "compilada")
|  el teclado esta incluido en la interaccion
|
^libprint.txt
^libsprite.txt
^libgc.txt
^font.txt

#bmouse..
#click..

||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
::clrscr
	cls scr home
	bmouse bmouse.. =? ( drop ; )
	0? ( 1 )( 0 ) 'click.. !
	'bmouse.. ! ;

::onIn | 'vec --
	xymouse whin 1? ( drop exec ; ) 2drop ;

::onInOut | 'vecin 'vecout --
	xymouse whin 1? ( 2drop )( drop nip ) exec ;

::onClick | 'vec --
	click.. 1? ( drop xymouse whin 1? ( 0 'click.. ! drop exec ; ) 2drop ; )
	2drop ;

||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
::sbtn | acc "str" --
	gc.push
	ink@ $222222
	[ swap ; ] [ 0.95 fzoom ; ] onInOut
	vbtn
	1 font blanco printc
	onClick
	gc.pop ;


::dbtn | acc 'v8d --
	gc.push
	ink@ $222222
	[ swap ; ] [ 0.95 fzoom ; ] onInOut
	vbtn
	blanco v8draw
	onClick
	gc.pop ;

||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
::ccruz
	xymouse bmouse 2 << 8 + cruz ;

#flecha  $9B8FEFC1 $FA3BEC91 $19C1B2B3 $2F4D1B43 $5839C8D3 $74B160B3 $4560BA93 $6DDC7533 $FA3BEC9A
 $D $7102111 $51DC7753 $35F0AD43 $65995A93 $56A18D03 $2A84DAF3 $1CBD42F3 $710211A
 $FFFFFFD $0

::cflecha
	xymouse 'yc ! 'xc !
	sw 3 >>	bmouse 2 << + dup 'w ! 'h !
	'flecha nsprite ;

||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
|------------------- edita linea
#cmax
#padi>	| inicio
#pad>	| cursor
#padf>	| fin

:lins  | c --
	padf> padi> - cmax >=? ( 2drop ; ) drop
	pad> dup 1- padf> over - 1+ cmove> 1 'padf> +!
:lover | c --
	$7f and
	pad> c!+ dup 'pad> !
	padf> >? (
		dup padi> - cmax >=? ( swap 1- swap -1 'pad> +! ) drop
		dup 'padf> ! ) drop
:0lin | --
	0 padf> c! ;
:kdel
	pad> padf> >=? ( drop ; ) drop
	1 'pad> +!
:kback
	pad> padi> <=? ( drop ; )
	dup 1- swap padf> over - 1+ cmove -1 'padf> +! -1 'pad> +! ;
:kder
	pad> padf> <? ( 1+ )  'pad> ! ;
:kizq
	 pad> padi> >? ( 1- ) 'pad> !  ;

#modo 'lins

:setinput | 'var max --
	'cmax ! dup 'padi> !
	( c@+ 1? )( drop ) drop 1- dup
	'pad> ! 'padf> !
	'lins 'modo !
	[ key toasc modo exec ; ] <visible>
	[ modo 'lins =? ( 'lover )( 'lins ) 'modo ! drop  ; ] <ins>
	'kback <back>	'kdel <del>
	'kder <der>		'kizq <izq>
	[ padi> 'pad> ! ; ] <home>
	[ padf> 'pad> ! ; ] <end>
	;

:dcursor
	blink 1? ( drop ; ) drop
	ink@ >r
	modo 'lins =? ( azul )( rojo ) drop
	printcur 1-
	r> ink ;

:drawinput | --
	padi> ( pad> =? ( dcursor )
		c@+ 1? )( emit ) 2drop ;

|-------------------- formulario
#sform )( 256
#sform> 'sform
#forms
#foco

:nextfoco
	forms @ 1-
	foco forms - 4 >>
	<=? ( forms 4+ )( foco 16 + ) nip
|	setfoco ;
:setfoco | adr --
	dup 'foco !
	8 + @+ swap @ setinput
	;

:ktab
	mshift 0? ( drop nextfoco ; ) drop
	foco
	forms 4+ =? ( drop forms @+ 1- 4 << + )( 16 - )
	setfoco ;

::iniform | 'adr --
	dup 'forms !
	4+ setfoco	| foco en el primero
	'ktab <tab>
	;

::guiform | --
	forms >r
	foco r@+ | cnt
    ( 1? )( 1- swap
		r <>? (
			r@+ r@+ at
			r@+ r@+ cntprint
			)( 16 r+ )
		swap ) 2drop
	rdrop
	foco @+ swap @ at drawinput
	;