| bare minimun
| experimental gameplay project
| PHReda - 2009 - http://www.reda4.org
| http://code.google.com/p/reda4/
|-------------------------------------

^reda4.txt
^libgui.txt

|--------   DIBUJOS
#fondo |spr
 $8D0FFD61 $6EF402A3 $71A1DBE3 $8A61DE93 $8D0FFD6A $FF59CBD7 $FC03ED58 $1A339C $75E
 0

#avion |spr
 $BBD78A61 $C2845033 $35785033 $45800D53 $14BB8A63 $D7EBD543 $C7338513 $BBD78A6A $F8A85AE7
 $EF4F64F8 $9F0000C $FFFFFFE $E5440801 $C5309063 $D1E4A5C3 $16B822C3 $E544080A $1B641567
 $B7D09308 $910000C $FFFFFFE $1DE3A2F1 $55BDFF3 $12B7FAB3 $38D3EAA3 $1DE3A2FA $236C1007
 $EB7BA88 $52DAE9C $FFFFFFE 0

#salvavidas |spr
 $1578D11 $B92B8D12 $B92BFD64 $B9286D92 $1546D94 $49846D92 $4987FD64 $49878D12 $1578D19
 $1540AB7 $FC035248 $FFFFFFC $E $EF4FE2A1 $DBEBED53 $EBF406A2 $1540804 $1EBC0802
 $24C3E7F4 $A07DFF3 $EF4FE2AA $6AC3D77 $A07D7F8 $FFFFFFC $E $F5FC1AB1 $F4A45033
 $CB04D83 $CB02013 $F5FC1ABA $D5CA067 $2000D58 $FFFFFFC $FF0000E 0

#nauf1 |spr
 $E2989B11 $E69C2813 $C7DC1D63 $B3276FA3 $C12F5CF3 $CF3BE2A3 $EB4BF003 $C0BED53 $2217E2A3
 $2F775CF3 $3ED77253 $2C1C1563 $9582AC3 $CB09B13 $E2989B1A $EEA0F5F7 $DF431F78 $FFFFFFC
 $E $F6AB6FA1 $E7F36FA2 $E7F3AD24 $E7F3EAA2 $F6ABEAA4 $55BEAA2 $55BAD24 $55B6FA2
 $F6AB6FA9 $407F007 $EF4F2778 $FFFFFFC $E 0

#nauf2 |spr
 $E3F0A311 $E69C2813 $C1D80553 $C73364F3 $D8976D03 $D53FE2A3 $EB4BF003 $C0BED53 $1C13DAA3
 $160F77B3 $256F6D03 $30CBFAB3 $9582AC3 $E08A063 $E3F0A31A $EEA0F5F7 $DF431F78 $FFFFFFC
 $E $F6AB6FA1 $E7F36FA2 $E7F3AD24 $E7F3EAA2 $F6ABEAA4 $55BEAA2 $55BAD24 $55B6FA2
 $F6AB6FA9 $407F007 $EF4F2778 $FFFFFFC $E 0

#explopar |spr
 $CE8C9301 $A3185582 $B47FA274 $BE8334D2 $DF4334D4 $2AEBF32 $1EBF3A34 $31774F92 $2CCBAD24
 $4A33F2B2 $39788304 $22C11E12 $FB54FB54 $D53CF8A2 $CE8C9309 $156075A7 $C6DF63A8 $FFFF03C
 $FF0000E 0

#ayuda |spr
 $A473BD31 $B724A063 $C12C85A3 $BA802D73 $C5301AB3 $CF385D93 $DB404AD3 $C2877A63 $B72B9273
 $BE7FDA93 $B67FED53 $ADCFA523 $A473BD3A $E89876F7 $B27B5798 $FFFFFFC $E $CDE35791
 $E9444023 $A82563 $FE03ED53 $ED4C0553 $E69FCFE3 $EF4FBD33 $EB4B9A73 $E1EFAA83 $DD437503
 $EF4F5793 $E89F24D3 $CDE3579A $1D42AC7 $D9EB2228 $FFFFFFC $E $FC032CD1 $8B00AB3
 $1F680D53 $1F6BDAA3 $100BDAA3 $AB32223 $FC032CDA $15640C17 $FF5AFA08 $FFFFFFC $E
 $24C00D51 $34232223 $583B6A52 $5037B284 $4787ED52 $377BDAA4 $397BAD23 $4583C292 $48339D24
 $4CDF7252 $3B7F7254 $31742AC3 $24C00D5A $276C52E7 $48DAFCB8 $FFFFFFC $E 0


|------	 x		y	size r 'sprite
#sfondo	 0.0   0.0  20.0 0 'fondo
#savion	-9.0  -3.0  1.5  0 'avion
#ssalva -9.0  -6.0  0.7  0 'salvavidas
#snaufr	 2.0   2.6  1.0  0 'nauf1

#escena 'sfondo 'savion 'snaufr 'ssalva 0

#puntaje 0
#tiempo 0

|----------  ESCENA
:ini2d
	omode
	0 0 10.0  mtrans ;

:draw2d | adr --
	( @+ 1? )( >r
		mpush
		r@+ r@+ 0 mtransi
		r@+ dup 0 mscalei
		r@+ 1? ( mrotzi )( drop )
		r> @ 3dnsprite
		mpop
		) 2drop
	255 alpha ;

|----------- FX
#fx 0
#fx> 0

:inifx
	clear
	0 here dup dup 'fx> ! 'fx ! !
	;

:+fx | 'acc x y vx vy timelife --
	fx> >r r!+ r!+ r!+ r!+ r!+ r!+ r> 'fx> !
	;

:delfx | parn -- parn
	dup 24 - over
	fx> over - 2 >>
	move | de sr cnt
	-24 'fx> +!
	;

:drawfx
	fx ( fx> <? )(
		mpush @+ | dirx acc
		exec | adr
		1? ( swap 4 - !+ )( drop delfx )
		mpop
		) drop
	255 alpha ;

:fxvel | dirx -- dirtl
	>r
	r@+ r@+ 0 mtransi
	r@+ r 12 - +!
	r @ 0.008 - r ! | sube
	r@+ r 12 - +!
	r> ;

:fxvel2 | dirx -- dirtl
	>r
	r@+ r@+ 0 mtransi
	r@+ r 12 - +!
	r@+ r 12 - +!
	r> ;

|-------------- Naufrago
#velnaufr 0.01

:nuevonaufrago
	rand 7.0 mod 'snaufr !
	rand 0.02 mod 'velnaufr !
	;

:naufrago
	velnaufr 'snaufr +!
	blink 1? ( 'nauf1 )( 'nauf2 ) nip
	'snaufr 16 + !
	;
|-------------  explosion
:getxysalva | -- x y
	'ssalva @+ swap @ swap ;

:rand0.1 | -- r
	rand 0.1 mod ;

:velrand
	rand0.1 rand0.1 ;

:explosion | dirx -- dirtl tl
	fxvel | dirtl
	@+ |dirnext tl
	0? ( ; ) 1-
	dup 2* alpha
	dup 9 << mrotzi
	'explopar 3dnsprite
	;

:+explode
	30 ( 1? )( 1-
		50 velrand getxysalva 'explosion +fx
		)
	drop
	1 'puntaje +!
	nuevonaufrago
	;

|------------- flotador
:flotador
	fxvel2
	@+ 0? ( ; ) 1-
    0.7 dup 0 mscalei
	dup 15 - 6 << mrotzi
	'salvavidas 3dnsprite
	;

:+flota
	30 0.03 0.0 getxysalva 'flotador +fx ;

|-------------  ayuda
:acchelp
	fxvel2
	@+ 0? ( ; ) 1-
	dup 2* alpha
	dup 0.01 * dup 0 mscalei
	'ayuda 3dnsprite
	;

:getxynaufr | -- x y
	'snaufr @+ swap @ swap ;

:+ayuda
	120 -0.03 0 getxynaufr swap 1.0 - swap 'acchelp +fx ;

|-------------- avion
#velavion 0.05

:test
	ssalva snaufr - abs
	0.4 <? ( +explode )( +flota )
	drop
	-6.0 'ssalva 4+ !
	;

:avionYsalva
	velavion 'savion +!
	savion
	9.0 >? ( -9.0 'savion ! rand 0.08 mod abs 0.06 + 'velavion ! )
	drop

	'ssalva 4+ dup
	@ -6.0 =? ( 2drop ; )
	0.04 + 3.0 >? ( 2drop test ; )
	swap !
	$40 'ssalva 12 + +!
	;

|-----------
:arroja
	'ssalva 4+ @ -6.0 <>? ( drop ; ) drop
	'savion
	'ssalva >r
	@+ r!+
	@ r> !
	;

|-----------  JUEGO
:resetgame
	0 'puntaje !
	60 'tiempo !
	;

:main
	inikey
	inifx
	ini2d
	'exit <esc>
	'arroja <esp>
	resetgame
	show cls scr
		'escena draw2d
		drawfx
		avionYsalva
		naufrago

		14 sfont
		blanco cr
		puntaje " %d" print
		tiempo "%d " printr

		fonti
		dup fx> fx - 6/ 2 >>  "%d %d" blanco print

		1 .segs .restart
		-1 'tiempo +!
		tiempo $3 and? ( velnaufr neg 'velnaufr ! +ayuda ) drop

		;

:titulo
	inikey
	'exit <esc>
	show cls scr
		5 font lfill
		$ff00 $00 gc.vdeg
		"Bare Minimun" printc
		8 font
		$ff0000 $00 gc.vdeg
		cr cr cr cr "Experimental Gameplay" printc
		cr "Proyect" printc

		sfill
		;

: 33 titulo main ;


