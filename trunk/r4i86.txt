| R4i86.txt - PHREDA 2009
| Generacion de codigo para i86-FASM
|
| .. [esp] - pila R
| .. [esi] eax - pila D
|
| ebx edi - Libres
| ecx - libre | sar sal
| edx - libre | * /
|
| ebp - VFRAME pointer
| MMX - auxiliares de almacenamiento
|
| nivel 0 -- solo macros,variable y direcciones
| nivel 1 -- llamada a palabras de nivel 0
| nivel 2 -- llamada a palabras de nivel 1 y 0
|--------------------------------------------
^lib/gui.txt
^lib/parse.txt
^lib/trace.txt

|----- nro de str y label
#nstr | nro de string
#nlabel | nro de etiqueta

::inistr	0 'nstr ! ;
::inilab    0 'nlabel ! ;

|----- pila de compilador
#pilac )( 256	#pilac> 'pilac

:>pilac pilac> ! 4 'pilac> +! ;
:<pilac -4 'pilac> +! pilac> @ ;
:pilac! 'pilac 'pilac> ! ;

|------ optimizador
:label	"_" ,s ,h 	;
:jmp,	"jmp " ,s label ,cr ;

#ra "eax"
#rb "ebx"
#rc "ecx"
#rd "edx"
#re "edi"
#nos "[esi]"
#cl "cl"

#vTOS ra
#vNOS nos

|---- nivel 0 - sin desordenar la pila
:,DUP
	"lea esi,[esi-4]" ,ln
	"mov [esi],eax" ,ln ;
:,DROP
	"lodsd" ,ln ;
:,NIP
	"lea esi,[esi+4]" ,ln ;
:,OVER
	,DUP "mov eax,[esi+4]" ,ln ;
:,PICK2
	,DUP "mov eax,[esi+4*2]" ,ln ;
:,PICK3
	,DUP "mov eax,[esi+4*3]" ,ln ;
:,PICK4
	,DUP "mov eax,[esi+4*4]" ,ln ;
:,SWAP
|	"mov edx,eax" ,ln |"xchg eax,[esi]" ,ln ;|ggrr
|	"mov eax,[esi]" ,ln
|	"mov [esi],edx" ,ln ;
	"push eax" ,ln	| otra version (sin dx)
	"mov eax,[esi]" ,ln
	"popd [esi]" ,ln ;
:,ROT
|	"mov ecx,[esi+4]" ,ln
|	"mov edx,[esi]" ,ln
|	"mov [esi],eax" ,ln
|	"mov [esi+4],edx" ,ln
|	"mov eax,ecx" ,ln ;
	"pushd [esi+4]" ,ln	| otra version (sin dx,cx)
	"pushd [esi]" ,ln
	"popd [esi+4]" ,ln
	"mov [esi],eax" ,ln
	"pop eax" ,ln ;
:,2DUP
	,OVER ,OVER ;
:,2OVER
	"mov [esi-4],eax" ,ln
	"lea esi,[esi-8]" ,ln
|	"mov edx,[esi+16]" ,ln
|	"mov [esi],edx" ,ln
	"pushd [esi+16]" ,ln
	"popd [esi]" ,ln
	"mov eax,[esi+12]" ,ln ;
:,2SWAP
|	"mov ecx,[esi+4]" ,ln
	"pushd [esi+4]" ,ln
	"mov [esi+4],eax" ,ln
|	"mov edx,[esi+8]" ,ln
	"pushd [esi+8]" ,ln
	"mov eax,[esi]" ,ln
	"mov [esi+8],eax" ,ln
|	"mov [esi],edx" ,ln
|	"mov eax,ecx" ,ln
	"popd [esi]" ,ln
	"pop eax" ,ln ;
:,2DROP
	,NIP ,DROP ;
:,3DROP
	"lea esi,[esi+8]" ,ln
	,DROP ;
:,4DROP
	"lea esi,[esi+12]" ,ln
	,DROP ;

:,EXEC
	"mov ecx,eax" ,ln
	,DROP
	"or ecx,ecx" ,ln
	"jz @f" ,ln
	"call ecx" ,ln
	"@@:" ,ln ;

:,>R
	"push eax" ,ln
	,DROP ;
:,R>
	,DUP "pop eax" ,ln ;
:,R
	,DUP "mov eax,[esp]" ,ln ;

:,RDROP
	"lea esp,[esp+4]" ,ln ;

:,R+
	"add [esp],eax" ,ln
	,DROP ;
:,R!+
	"pop edx" ,ln
	"mov [edx],eax" ,ln
	"add edx,4" ,ln
	"push edx" ,ln
	,DROP ;
:,R@+
	,DUP
	"pop edx" ,ln
	"mov eax,[edx]" ,ln
	"add edx,4" ,ln
	"push edx" ,ln ;

:1,AND
	vTOS vNOS "and %s,%s" ,print ,cr ;
:,AND
	"and eax,[esi]" ,ln
	,NIP ;

:1,OR
	vTOS vNOS "or %s,%s" ,print ,cr ;
:,OR
	"or eax,[esi]" ,ln
	,NIP ;

:1,XOR
	vTOS vNOS "xor %s,%s" ,print ,cr ;
:,XOR
	"xor eax,[esi]" ,ln
	,NIP ;

:1,NOT
	vTOS "not %s" ,print ,cr ;
:,NOT
	"not eax" ,ln ;

:1,+
	vTOS vNOS "add %s,%s" ,print ,cr ;
:,+
	"add eax,[esi]" ,ln
	,NIP ;

:1,-
	vTOS vNOS "sub %s,%s" ,print ,cr ;
:,-
	"neg eax" ,ln
	"add eax,[esi]" ,ln
	,NIP ;

:1,*
	"cdq" ,ln
	vTOS "imul dword %s" ,ln
	;
:,*
	"cdq" ,ln
	"imul dword [esi]" ,ln
	,NIP ;

:1,/
:,/
	"mov ecx,eax" ,ln
	,DROP
	"cdq" ,ln
	"idiv ecx" ,ln ;

:1,*/
:,*/
	"mov ecx,eax" ,ln
	,DROP
	"cdq" ,ln
	"imul dword [esi]" ,ln
	"idiv ecx" ,ln
	,NIP ;
:,*>>
	"mov ecx,eax" ,ln
	,DROP
	"cdq" ,ln
	"imul dword [esi]" ,ln
	"shrd eax,edx,cl" ,ln
	,NIP ;
:,/MOD
	"mov ecx,eax" ,ln
	"mov eax,[esi]" ,ln
	"cdq" ,ln
	"idiv ecx" ,ln
	"mov [esi],eax" ,ln
	"mov eax,edx" ,ln ;
:,MOD
	"mov ecx,eax" ,ln
	,DROP
	"cdq" ,ln
	"idiv ecx" ,ln
	"mov eax,edx" ,ln ;

:1,<<
	vTOS vNOS "sal %s,%s" ,print ,cr ; | cl o cte
:,<<
	"mov ecx,eax" ,ln
	,DROP
	"sal eax,cl" ,ln ;

:1,>>
	vTOS vNOS "sar %s,%s" ,print cr ; | cl o cte
:,>>
	"mov ecx,eax" ,ln
	,DROP
	"sar eax,cl" ,ln ;

:,ABS
	"cdq" ,ln
	"add eax,edx" ,ln
	"xor eax,edx" ,ln ;

:1,NEG
	vTOS "neg %s" ,print ,cr ;
:,NEG
	"neg eax" ,ln ;

:1,1+
	vTOS "inc %s" ,print ,cr ;
:,1+
	"inc eax" ,ln ;

:1,4+
	vTOS "add %s,4" ,print ,cr ;
:,4+
	"add eax,4" ,ln ;

:1,1-
	vTOS "dec %s" ,print ,cr ;
:,1-
	"dec eax" ,ln ;

:1,2/
	vTOS "sar %s,1" ,print ,cr ;
:,2/
	"sar eax,1" ,ln ;

:1,2*
	vTOS "sal %s,1" ,print ,cr ;
:,2*
	"sal eax,1" ,ln ; | "add eax,eax" ,ln

:,@
	"mov eax,dword [eax]" ,ln ;
:,C@
	"movsx eax,byte [eax]" ,ln ;
:,W@
	"movsx eax,word [eax]" ,ln ;
:,!
	"mov ecx,[esi]" ,ln
	"mov [eax],ecx" ,ln
	,2DROP ;
:,C!
	"mov ecx,[esi]" ,ln
	"mov byte [eax],cl" ,ln
	,2DROP ;
:,W!
	"mov ecx,[esi]" ,ln
	"mov word [eax],cx" ,ln
	,2DROP ;
:,+!
	"mov ecx,[esi]" ,ln
	"add [eax],ecx" ,ln
	,2DROP ;
:,W+!
	"mov ecx,[esi]" ,ln
	"add word [eax],cx" ,ln
	,2DROP ;
:,C+!
	"mov ecx,[esi]" ,ln
	"add byte [eax],cl" ,ln
	,2DROP ;
:,!+
	"mov ecx,[esi]" ,ln
	"mov [eax],ecx" ,ln
	"add eax,4" ,ln
	,NIP ;
:,W!+
	"movsx ecx,word [esi]" ,ln
	"mov [eax],cx" ,ln
	"add eax,2" ,ln
	,NIP ;
:,C!+
	"movsx ecx,byte [esi]" ,ln
	"mov [eax],cl" ,ln
	"inc eax" ,ln
	,NIP ;
:,@+
	"mov ecx,[eax]" ,ln
	"add eax,4" ,ln
	"mov [esi-4],eax" ,ln
	"mov eax,ecx" ,ln
	"lea esi,[esi-4]" ,ln ;
:,W@+
	"movsx ecx,word [eax]" ,ln
	"add eax,2" ,ln
	"mov [esi-4],eax" ,ln
	"mov eax,ecx" ,ln
	"lea esi,[esi-4]" ,ln ;
:,C@+
	"movsx ecx,byte [eax]" ,ln
	"inc eax" ,ln
	"mov [esi-4],eax" ,ln
	"mov eax,ecx" ,ln
	"lea esi,[esi-4]" ,ln ;

:,;
:,(
:,)(
:,)
:,[
:,]	;

:,0?
	"or eax,eax" ,ln
	"jnz " ,s ;
:,+?
	"or eax,eax" ,ln
	"js " ,s ;
:,-?
	"or eax,eax" ,ln
	"jns " ,s ;
:,1?
	"or eax,eax" ,ln
	"jz " ,s ;
:,=?
	"mov ecx,eax" ,ln
	,DROP
	"cmp eax,ecx" ,ln
	"jnz " ,s ;
:,<?
	"mov ecx,eax" ,ln
	,DROP
	"cmp eax,ecx" ,ln
	"jge " ,s ;
:,>?
	"mov ecx,eax" ,ln
	,DROP
	"cmp eax,ecx" ,ln
	"jle " ,s ;
:,<=?
	"mov ecx,eax" ,ln
	,DROP
	"cmp eax,ecx" ,ln
	"jg " ,s ;
:,>=?
	"mov ecx,eax" ,ln
	,DROP
	"cmp eax,ecx" ,ln
	"jl " ,s ;
:,<>?
	"mov ecx,eax" ,ln
	,DROP
	"cmp eax,ecx" ,ln
	"jz " ,s ;
:,and?
	"mov ecx,eax" ,ln
	,DROP
	"test eax,ecx" ,ln
	"jz " ,s ;
:,nand?
	"mov ecx,eax" ,ln
	,DROP
	"test eax,ecx" ,ln
	"jnz " ,s ;

:,DIR		
	"call SYSDIR" ,ln ;
:,FILE      
	"call SYSFILE" ,ln ;
:,FSIZE     
	"call SYSFSIZE" ,ln ;
:,VOL       
	"call SYSVOL" ,ln ;
:,LOAD      
	"call SYSLOAD" ,ln ;
:,SAVE      
	"call SYSSAVE" ,ln ;
:,UPDATE    
	"call SYSUPDATE" ,ln ;
:,MSEC      
	"call SYSMSEC" ,ln ;
:,TIME      
	"call SYSTIME" ,ln ;
:,DATE      
	"call SYSDATE" ,ln ;
:,END       
	"jmp SYSEND" ,ln ;
:,RUN       
	"call SYSRUN" ,ln ;
:,SW
	,DUP  "mov eax,[SYSW]" ,ln ;
:,SH
	,DUP  "mov eax,[SYSH]" ,ln ;
:,MEM
	,DUP  "mov eax,FREE_MEM" ,ln ;

:,PAPER
	"mov [SYSPAPER],eax" ,ln
	,DROP ;

:,CLS
	"call SYSCLS" ,ln ;

:,REDRAW
	"call SYSREDRAW" ,ln ;
:,FRAMEV
	,DUP  "mov eax,SYSFRAME" ,ln ;

:,XFB
	,DUP  "mov eax,XFB" ,ln ;
:,>XFB
	"call SYSTOXFB" ,ln ;
:,XFB>
	"call SYSXFBTO" ,ln ;

:,SETXY | x y --
	"cdq" ,ln
	"imul dword [SYSW]" ,ln
	"add eax,[esi]" ,ln
	"lea ebp,[SYSFRAME+eax*4]" ,ln
	,2DROP ;

:,PX+!	| s --
	"lea ebp,[ebp+eax*4]" ,ln
	,DROP ;
:,PX!+	| rgb --
	"mov [ebp],eax" ,ln
	"add ebp,4" ,ln
	,DROP ;
:,PX@	| -- rgb
	,DUP "mov eax,[ebp]" ,ln ;

:,iPEN! | v --
	"mov [SYSiPEN],eax" ,ln
	,DROP ;

:,TPEN | --
	,DUP
	"mov eax,[MCNT]" ,ln
	"dec eax" ,ln
	"mov [MBUFF],eax" ,ln
	"xor eax,eax" ,ln
	"inc eax" ,ln
	"mov [MCNT],eax" ,ln
	"mov eax,MBUFF" ,ln ;

:,XYMOUSE | -- x y
	,DUP
	"lea esi,[esi-4]" ,ln
	"mov eax,[SYSXYM]" ,ln
	"mov ecx,eax" ,ln
	"and ecx,$ffff" ,ln
	"shr eax,16" ,ln
	"mov [esi],ecx" ,ln ;

:,BMOUSE | -- c
	,DUP "mov eax,[SYSBM]" ,ln ;

:,iKEY! | v --
	"mov [SYSiKEY],eax" ,ln
	,DROP ;

:,KEY	| -- c
	,DUP "mov eax,[SYSKEY]" ,ln ;

|-----------
:,MOVE
:,MOVE>
:,CMOVE
:,CMOVE>


:,INK
:,INK@
:,ALPHA
:,OP
:,CP
:,LINE
:,CURVE
:,PLINE
:,PCURVE
:,POLI
:,FCOL
:,FCEN
:,FMAT
:,SFILL
:,LFILL
:,RFILL
:,TFILL

:,CNTJOY
:,GETJOY

:,SLOAD
:,SPLAY
:,MLOAD
:,MPLAY

:,SERVER
:,CLIENT
:,SEND
:,RECV
:,CLOSE

:,DOCINI
:,DOCEND
:,DOCAT
:,DOCLINE
:,DOCTEXT
:,DOCFONT
:,DOCBIT
:,DOCRES
:,DOCSIZE
:,SYSTEM
	;

#nivel0
,; ,( ,)( ,) ,[ ,] ,EXEC
,0? ,+? ,-? ,1? ,=? ,<? ,>? ,<=? ,>=? ,<>? ,AND? ,NAND?
,DUP ,DROP ,OVER ,PICK2 ,PICK3 ,PICK4 ,SWAP ,NIP	|--- pila
,ROT ,2DUP ,2DROP ,3DROP ,4DROP ,2OVER ,2SWAP
,>R ,R> ,R ,R+ ,R@+ ,R!+ ,RDROP					|--- pila direcciones
,AND ,OR ,XOR ,NOT  								|--- logicas
,+ ,- ,* ,/ ,*/ ,*>> ,/MOD ,MOD ,ABS  			|--- aritmeticas
,NEG ,1+ ,4+ ,1- ,2/ ,2* ,<< ,>>
,@ ,C@ ,W@ ,! ,C! ,W! ,+! ,C+! ,W+!  			|--- memoria
,@+ ,!+ ,C@+ ,C!+ ,W@+ ,W!+
,MOVE ,MOVE> ,CMOVE ,CMOVE>
,MEM ,DIR ,FILE ,FSIZE ,VOL ,LOAD ,SAVE			|--- memoria bloques
,UPDATE ,MSEC ,TIME ,DATE ,END ,RUN 				|--- sistema
,SW ,SH ,CLS ,REDRAW ,FRAMEV  						|--- pantalla
,SETXY ,PX+! ,PX!+ ,PX@
,XFB ,>XFB ,XFB>
,PAPER ,INK ,INK@ ,ALPHA							|--- color
,OP ,CP ,LINE ,CURVE ,PLINE ,PCURVE ,POLI		|--- dibujo
,FCOL ,FCEN ,FMAT ,SFILL ,LFILL ,RFILL ,TFILL
,TPEN ,XYMOUSE ,BMOUSE
,IKEY! ,KEY
,CNTJOY ,GETJOY
,SLOAD ,SPLAY	,MLOAD ,MPLAY					|--- Sonido
,SERVER ,CLIENT ,SEND ,RECV ,CLOSE
,DOCINI ,DOCEND ,DOCAT ,DOCLINE ,DOCTEXT ,DOCFONT ,DOCBIT ,DOCRES ,DOCSIZE  |-- impresora
,SYSTEM

| compila nivel 0 de palabras
:gen0 | val byte -- val
	2 << 'nivel0 + @ exec ;


|------------ compila CODIGO
|:nro>dicn2   8 >> 5 << 'indicepal + @ ; | nombre
:nro>dicn   8 >> "w%h" mprint ;			| numero de palabra

:+etiqueta	| -- nueva   // y agrega a pila
	nlabel 1+ dup 'nlabel ! dup >pilac ;

:cpycad | adr --
	( c@+ 1? )( 34 =? ( drop c@+ 34 <>? ( 2drop ; ) ) ,c ) 2drop ;

:salto? | adr++ xx
	over 8 - @ $ff and
	18 >? ( 31 <? ( 1 nip ; ) ) 0 nip ;

:,cdefw
:,cdefv drop ;
:,clitd
:,clith
:,clitb
:,clitf	tok>cte ,DUP "mov eax," ,s ,d ,cr ;
:,clits	drop ,DUP "mov eax,s" ,s nstr ,h ,cr 1 'nstr +! ;
:,cwor nro>dicn "call " ,s ,s ,cr ;
:,cvar nro>dicn ,DUP "mov eax,[" ,s ,s "]" ,s ,cr ;
:,cdwo nro>dicn ,DUP "mov eax," ,s ,s ,cr ;
:,cdva nro>dicn ,DUP "mov eax," ,s ,s ,cr ;
:,c; drop "ret" ,ln ;
:,c( drop +etiqueta salto? 1? ( swap label ,cr 1 )( swap label ":" ,ln 2 ) >pilac drop ;
:,c)( drop
	<pilac salto? 0? ( drop
			1 =? ( <pilac +etiqueta jmp, label ":" ,ln 1 >pilac drop ; )
			)( drop
			2 =? ( <pilac +etiqueta label ,cr >pilac 3 >pilac drop ; ) )
	drop ;
:,c) drop
	<pilac salto? 0? ( drop
			1 =? ( <pilac label ":" ,ln drop ; )
			2 =? ( <pilac jmp, drop ; )
			3 =? ( <pilac jmp, <pilac label ":" ,ln drop ; )
		)( drop
			2 =? ( <pilac label ,cr drop ; ) )
	drop ;
:,c[ drop
	,DUP +etiqueta +etiqueta "mov eax," ,s label ,cr jmp, <pilac label ":" ,ln ;
:,c] drop
	<pilac label ":" ,ln ;

#codec 0 ,cdefw ,cdefv ,clitd ,clith ,clitb ,clitf ,clits ,cwor ,cvar ,cdwo ,cdva
,c; ,c( ,c)( ,c) ,c[ ,c]

::,token0 | a -- a.
	@+ dup $ff and
	18 <? ( 2 << 'codec + @ exec ; )
	12 - gen0
	drop ;


|------------ compila DATO
#d1 "dd "
#d2 "dw "
#d3	"db "
#d4 "rb "
#dtipo 'd1
#dini 0

:dfin	dini 1? ( dtipo )( "," ) ,s drop 0 'dini ! ;
:dfini	dini 1? ( dtipo ,s 0 ,d ) drop 0 'dini ! ;
:dfins	dini 1? ( "db " )( "," ) ,s drop 0 'dini ! ;
:dfind	dini 1? ( "dd " )( "," ) ,s drop 0 'dini ! ;

|----------------------------------------------
:,ddefw nro>dicn ":%w" ,print ;
:,ddefv nro>dicn "#%w" ,print ;
:,dlitd
:,dlith
:,dlitb
:,dlitf	dfin tok>cte ,d ;
:,dlits	dfins tok>str 34 ,c cpycad 34 ,c ",0" ,s ;
:,dwor
:,dvar
:,ddwo
:,ddva dfind nro>dicn ,s ;
:,d; drop ;
:,d( drop dtipo >pilac 'd3 'dtipo ! ;
:,d)( drop 'd4 'dtipo ! ;
:,d) drop <pilac 'dtipo ! ;
:,d[ drop dtipo >pilac 'd2 'dtipo ! ;
:,d] drop <pilac 'dtipo ! ;

#coded 0
,ddefw ,ddefv ,dlitd ,dlith ,dlitb ,dlitf ,dlits ,dwor ,dvar ,ddwo ,ddva
,d; ,d( ,d)( ,d) ,d[ ,d]

|---- datos
::compvar | nropal cntlla -- nropal
	drop dup "w%h " ,print
	'd1 'dtipo ! 1 'dini ! pilac!
	dup nro>tok
	( @+ dup $ff and
		1 =? ( 0 nip ) | corta con defc
		2 =? ( 0 nip ) | corta con defv
		1? )( 	 | dirv n c
		18 <? ( 2 << 'coded + @ exec )( 2drop )
		,sp
		)
	3drop
	dfini
	,cr ;

:otrostr
	over
	1 'dini !
	nstr "s%h " ,print
	,dlits
	1 'nstr +!
	,cr ;

::compwor | nropal cntlla -- nropal
	drop |dup "w%h  " ,print
	dup
	nro>toklen
	( 1? )( 1- swap
		@+ dup $ff and
		7 =? ( otrostr ) 2drop
		swap )
	2drop ;
