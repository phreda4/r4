| Navegador de Colores
| PHREDA 2006
|---------------------------------------
^reda4.txt
^gui3.txt
^libcolor.txt

#huea
#hueb

#paleta1 )( $3ffff

#colorb
#colora
#tipo

#colora.r #colora.g #colora.b
#colorb.r #colorb.g #colorb.b

:recalcula
	colora dup $ff and $80 - 8 << 'colora.b ! dup 8 >> $ff and $80 - 8 << 'colora.g ! 16 >> $ff and $80 - 8 << 'colora.r !
	colorb dup $ff and $80 - 8 << 'colorb.b ! dup 8 >> $ff and $80 - 8 << 'colorb.g ! 16 >> $ff and $80 - 8 << 'colorb.r !
	;

:fondoa
	colora ink gc.fbox ;

:fondob
	colorb ink gc.fbox ;

:colsolido	0 'tipo ! ;
:coldegl	1 'tipo ! ;
:coldegr	2 'tipo ! ;

:cambiacol colora colorb 'colora ! 'colorb ! recalcula ;

 :2.h | n --
	$10 <? ( "0" print ) "%h" print ;

:label1
	1 sfont
	rojo colora.r 8 >> $80 + $ff and
	dup 2.h
	16 <<
	verde colora.g 8 >> $80 + $ff and
	dup 2.h
	8 << or
	azul colora.b 8 >> $80 + $ff and
	dup 2.h
	or 'colora ! ;

:label2
	1 sfont
	rojo colorb.r 8 >> $80 + $ff and
	dup 2.h
	16 <<
	verde colorb.g 8 >> $80 + $ff and
	dup 2.h
	8 << or
	azul colorb.b 8 >> $80 + $ff and
	dup 2.h
	or 'colorb ! ;

:colorcompleto
	tipo
	0? ( colora ink sfill )
	1- 0? ( colorb colora gc.hdeg lfill )
	1- 0? ( colorb colora gc.hdeg rfill )
	drop
	gc.fbox
	sfill
	'exit onClick
	;

#claridad 1.0
#clarisli
#xcol -1 #ycol

:llenapaleta
	'paleta1 >r
	0 ( 1.0 <=? )(
		0 ( 1.0 <=? )(
			over over swap claridad hsv2rgb r!+
			$101 + ) drop
		$101 + ) drop
	rdrop
	;

:reccolor
	xcol ycol setxy px@ 'colora !
	recalcula ;

:textura
	clarisli 0.5 +
	claridad <>? ( 'claridad ! llenapaleta )( drop )
	'paleta1 tfill
	gc.all
	gc.fbox
	sfill
	[ xymouse 'ycol ! 'xcol ! reccolor ; ] onMove
	xcol -1 =? ( drop ; ) ycol
	3 negro box
	;

:slideluz
	'clarisli
	[ xymouse nip yc - 16 << h / over ! reccolor ; ] onMove
	$0 $ffffff vfil
	@ h 16 *>> 'yc +!
	negro
	xc w 2/ - yc 2dup op
	over w + over h 5 >> - line
	over w + over h 5 >> + line
	line
| 	0.98 0.1 fscala
|	$dddddd $222222 hbtn
	;

#coloresg
$000000 $404040 $808080
$B0B0B0 $FFFFFF $F8F8B8
$FF80FF $FF0000 $800000
$FF8000 $804000 $FFFF00
$808000 $80FF00 $00FF00
$008000 $00FFFF $008080
$0000FF $000080 $8080FF
$8000FF $400080 $FF00FF
$800080 $FFFF80 $E0E080
$8000FF $400080 $004080

:minipaleta
	0.05 0.05 fdim
	-0.9 0.5 fpos
	'coloresg
	10 ( 1? )( 1-
		3 ( 1? )( 1-
			rot @+ ink gc.fbox
			[ dup 4 - @ 'colora ! recalcula ; ] onClick
			rot rot gc>>
			) drop
		gcdn gc<< gc<< gc<<
		) 2drop ;

|---------------------------------------------------------
:colorshow
	inigui
	'exit >esc<
	show clrscr
		0.8 dup fdim -0.2 0.2 fpos
		$ff $11 vbtn

		0.1 dup fdim -0.9 0.9 fpos
		colorcompleto gc>>
		tipo 0? ( amarillo )( verde )
		'colsolido 'istop btnd gc>>
		1 =? ( amarillo )( verde )
		'coldegl 'iplay btnd gc>>
		2 =? ( amarillo )( verde )
		'coldegr 'irec btnd gc>>
		cyan
		'cambiacol 'irecicla btnd gc>>

		0.5 0.5 fdim -0.1 0 fpos
		textura
		0.05 0.5 fdim 0.5 0 fpos
		slideluz

		minipaleta

		0.4 0.1 fdim 0 0.7 fpos
		label1

		cflecha ;

::dlgcolor	| tipo col1 col2  -- col2 col1 tipo
	'colorb !+ !+ !
	recalcula
	llenapaleta
	colorshow
	colorb colora tipo ;

|: 0 $fffff $fff dlgcolor 3drop "main.txt" run ;
