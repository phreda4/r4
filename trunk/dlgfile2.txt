| Navegador de Archivos
| PHREDA 2006
|---------------------------------------
^reda4.txt
^gui3.txt

#cntlineas 0
#inifile 0
#fileact 0
#pnombre 0
#entrada )( 64

:copye | str --
	'entrada swap
	( c@+ 1? )( rot c!+ swap ) rot c! drop ;
:cleare
	0 'entrada ! ;

|---------------------------------------------------------
:sumafile | nro --
	fileact + dup file 0? ( 2drop ; ) copye  'fileact !
:ubicar
	inifile cntlineas +  ( fileact <=? )( 1+ ) cntlineas - 'inifile !
	inifile ( fileact >? )(  1- ) 'inifile !
	;

:buscafile
	'entrada c@ 0? ( drop ; ) drop
	0 ( dup file 1? )(  | nro file
		'entrada =pre 1? ( drop 'fileact ! ubicar ; ) drop 1+ )
	2drop ;

|---------------------------------------------------------
:nuevoreal
	exit
	'entrada c@ 0? ( drop ; ) drop
	'entrada pnombre strcpy
	;

:cadafile | nro -- nro
	dup inifile +
	fileact =? ( verde ">" )( blanco " " ) print
	file 0? ( drop ; )
	printbox
	[ dup copye ; ] onClick
	print
	;

|---------------------------------------------------------
::dlgfile | nn --
	inigui
	'pnombre !
	0 'fileact !
	'exit  >esc<
	'nuevoreal >enter< |[ fileact file 1? ( pnombre strcpy exit ; ) drop ; ]  >enter<
	[ 1 sumafile ; ] <pgdn>
	[ -1 sumafile ; ] <pgup>
	pnombre copye

	scr fonti fils 4 - 'cntlineas !
	show clrscr
		fonti
		verde linefill
		blanco "Elegir Archivo" printc cr

		0 ( cntlineas <? )(
			2 col cadafile cr
			1+ ) drop

		verde spc 'entrada 32 getline cr

		0.1 0.1 fdim
		0.9 0.9 fpos
		rojo [ "" pnombre strcpy exit ; ] 'ifin btnd
		0.7 0.9 fpos
		verde 'nuevoreal 'iok btnd

		buscafile
		cmano
		;
