| Crayon - dibujador simple
| PHREDA
|---------------------
^lib/reda4.txt
^lib/gui.txt
^lib/graf.txt

#tinta $ff00

#tecla
#fondo 0
#skip

|----------------------------------------
#sizebm
:copybm	framev mem sizebm move ; | de sr cnt --	; de mem a video
:copymm	mem framev sizebm move ; | de sr cnt --	; de video a mem

|------------ Grabar simple RLE
#memc
#memc>

:,comp | v --
	memc> !+ 'memc> ! ;

#cnt
#colact

:scanlast | --
	colact cnt ( 255 >? )( over $ff000000 or ,comp 255 - )
	24 << or ,comp ;

:scanconv | adr -- adr+
	@+ $ffffff and | adr+ color
	colact =? ( 1 'cnt +! drop ; )
	scanlast
:scanini | color --
	$ffffff and 'colact ! 1 'cnt ! ;

:grabar
	mem @+ scanini
	sizebm 2dup 2 << + dup 'memc ! 'memc> !
	( 1- 1? )( >r scanconv r> ) 2drop
	scanlast
	memc memc> over - "bmr/test.bmr" save
	;

|-------------- Cargar simple RLE
:cargar
	mem sizebm 2 << + 'memc !
	memc "bmr/test.bmr" load 'memc> !
	mem >r
	memc ( memc> <? )( @+
		dup $ffffff and swap 24 >> $ff and | color cant
		( 1? )( over r!+ 1- ) 2drop
		) drop
	rdrop
	copybm ;


|-------------- vectorizar 0.1
#vect
#vect>
#vectl>
#coloract

:,,  | nro --
	vect> !+ 'vect> ! ;

:clearvec | --
	mem sizebm 2 << + dup dup 'vect ! 'vect> ! 'vectl> !
	$ff000000 'coloract !
	;

:addvec | color --
	$ffffff and
	coloract =? ( 1 'cnt +! drop ; )
	cnt 1 >? ( coloract ,, ) drop
	'coloract ! 1 'cnt !
	;
:closevec | --
	$ff000000 'coloract !
	;

:vectorizar
	mem >r
	clearvec
	sh ( 1? )( 1-
		sw ( 1? )( 1-
			r@+ addvec
			) drop
		closevec
		) drop
	rdrop
	inigui
	'exit >esc<
	show clrscr
		32 font azul
		vect vect> "%d %d" print
	;

|-----------------------------------------
#testimg
	$ff0000
	$ff0000
	$ff0000
	$ff0000
	0

:dibujovect | addr --
	mem >r
	( @+ 1? )(

		r!+
		) 2drop rdrop
	copybm ;

|----------------------------------------
#x1 #y1
#linegro 8

#trazo )( 2048
#trazo> 'trazo

:++trazo |
	trazo> 4 - @ =? ( drop ; )
:+trazo | u --
	trazo> !+ 'trazo> ! ;
:-trazo
	'trazo 'trazo> ! ;

:trazo.draw
	linegro dup gg gg
	tinta ink
	'trazo @+ 0? ( 2drop ; ) >xy gop
	( trazo> <? )( @+ >xy gline ) drop ;

:refresca
	copybm
	scr trazo.draw
	copymm
	;

:trazodraw
	[ refresca -trazo ; ] onClick
	[ xymouse xy> +trazo ; ] onDn
	[ xymouse xy> ++trazo ; ] onMove
    trazo.draw ;

:barra
	-trazo
	inigui
	[ rand 'tinta ! ; ] <esp>
	[ cls ; ] <enter>
	'exit >esc<

	tinta ink
|	'clcolor 'xcolor dup <f1> .xbtn .>
|	"F2" "Grabar" 'grabar dup <f2> .fbtn .>
|	"F3" "Cargar" 'cargar dup <f3> .fbtn .>
|	"F4" "Vector" 'vectorizar dup <f4> .fbtn .>
|	"F5" "test" [ 'testimg dibujovect ; ] dup <f5> .fbtn .>
|	5 1 .to 80 'linegro .hsli
	33
	show clrscr
		copybm
		blink 1? ( blanco )( tinta ink ) drop
		xymouse linegro gcursor
		trazodraw
		scr 16 font home
		azul
		dup "%d" print cr
		trazo> 'trazo - "%d" print cr
		rojo
		0.1 dup fdim -0.9 dup fpos
		'exit 'ifin btnd
	 	cflecha
		;

:inicio
	sw sh *	'sizebm !
	cls copymm
	barra ;

: $ffffff paper 33 inicio ;