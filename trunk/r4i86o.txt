| R4i86.txt - PHREDA 2009
| Generacion de codigo para i86-FASM
|
| .. [esp] - pila R
| .. [esi] eax - pila D
|
| ebx edi - Libres
| ecx - libre | sar sal
| edx - libre | * /
|
| ebp - VFRAME pointer
| MMX - auxiliares de almacenamiento
|
| nivel 0 -- solo macros,variable y direcciones
| nivel 1 -- llamada a palabras de nivel 0
| nivel 2 -- llamada a palabras de nivel 1 y 0
|--------------------------------------------
^lib/gui.txt
^lib/parse.txt
^lib/trace.txt

|----- nro de str y label
#nstr	| nro de string
#nlabel | nro de etiqueta
#nanon	| nro de def anonima

::ini.compila
	0 'nstr ! 0 'nlabel ! 0 'nanon ! ;

|----- pila de compilador
#pilac )( 256	#pilac> 'pilac

:>pilac pilac> !+ 'pilac> ! ;
:<pilac -4 'pilac> +! pilac> @ ;
:pilac! 'pilac 'pilac> ! ;

:label	"_" ,s ,h 	;
:jmp,	"jmp " ,s label ,cr ;

|------ pila virtual
| 		valor 		tipo
| TOS = dpila>-4  dpila>-8

#dpila )( 2048
#dpila> 'dpila

:d.ini
	'dpila 'dpila> ! ;
:d.cnt | -- cnt
	dpila dpila> - 3 >> ;
:d.pop | -- nro
	dpila> 8 -
	dup 'dpila> ! 4+ @ ;
:d.pop2
	-16 'dpila> +!
	dpila> 4+ @+ swap 4+ @ ;
:d.pop3
	-24 'dpila> +!
	dpila> 4+ @+ swap 4+ @+ swap 4+ @ ;

:d.get dpila> 4 - @ ;
:d.get2 dpila> 12 - @ ;

:d.push | nro tipo --
	dpila> !+ !+ 'dpila> ! ;

|----------- tipo de pila
:d.pushReg	0 d.push ; | eax ebx ecx edx edi
:d.pushEsi  1 d.push ; | dword [esi+4*] ..
:d.pushEsp	2 d.push ; | dword	[esp+4*] ..
:d.pushVar	3 d.push ; | var	[w1]
:d.pushIcod	4 d.push ; | dcod	'w2
:d.pushDcod	5 d.push ; | dcod	'w2
:d.pushDvar	6 d.push ; | dvar	w1
:d.pushStr	7 d.push ; | str    "hola"
:d.pushCte	8 d.push ; | cte	23

#ra "eax" #rb "ebx" #rc "ecx" #rd "edx" #re "edi"

#reg 'ra 'rb 'rc 'rd 're

:,Reg	@ 2 << 'regp + @ ,s ; | eax ebx ecx edx edi
:,Esi	@	0? ( "dword [esi]" ,s drop ; )
			-? ( neg "dword [esi-4*%d]" )( "dword [esi+4*%d]" )
			mprint ,s ;
:,Esp	@	0? ( "dword [esp]" ,s drop ; )
			-? ( neg "dword [esp-4*%d]" )( "dword [esp+4*%d]" )
			mprint ,s ;
:,Var	@ "[w%h]" mprint ,s ;
:,Icod  @ "i%h" mprint ,s ;
:,Dcod
:,Dvar	@ "w%h" mprint ,s ;
:,Str	@ "s%h" mprint ,s ;
:,Cte	@ "%d" mprint ,s ;

#tipopila
,Reg ,Esi ,Esp ,Var	,Icod ,Dcod ,Dvar ,Str ,Cte

:,getval | nro -- ""
	3 << dpila> swap - 4 -
	dup 4 - @ 2 << 'tipopila + @ exec | tipo
	;

:,,
	"," ,s ;

|----------- uso de registro

#regused 0  0  0  0  0
#regname ra rb rc rd re

:reg.reset
	0 0 0 0 1 'regused !+ !+ !+ !+ !
	d.ini ;

:reg.free | -- nro/-1
	'regused
	5 ( 1? )( swap @+
		0? ( drop 'regused - 2 >> 1- nip ; )
		drop swap ) 2drop
	-1 ;

:reg.set | nro --
	2 << 'regused + 1 swap ! ;


|---------------------------
:,lit
	dup tok>cte d.pushCte ; |	dup tok>cte ,DUP "mov eax," ,s ,d ,cr ;
:,lits
	nstr d.pushStr 1 'nstr +! ; |	,DUP "mov eax,s" ,s nstr ,h ,cr 1 'nstr +! ;
:,wor
	| elegir modo
	| comprobar ultimo para jmp
	|dup nro>dicn "call " ,s ,s ,cr
	;
:,var
	dup 8 >> d.pushVar ; |	dup nro>dicn ,DUP "mov eax,[" ,s ,s "]" ,s ,cr ;
:,dwo
	dup 8 >> d.pushDcod ; |	dup nro>dicn ,DUP "mov eax," ,s ,s ,cr ;
:,dva
	dup 8 >> d.pushDvar ; |	dup nro>dicn ,DUP "mov eax," ,s ,s ,cr ;


:,[
	1 'niva +!
	nanon d.pushIcod 1 'nanon +! ; |,DUP +etiqueta +etiqueta "mov eax," ,s label ,cr jmp, <pilac label ":" ,ln ;
:,]
	-1 'niva +! ; |<pilac label ":" ,ln ;

|----------------------------
|-----
:*DROP  drop -8 'dpila> +! ;
:*2DROP drop -16 'dpila> +! ;
:*3DROP drop -24 'dpila> +! ;
:*4DROP drop -32 'dpila> +! ;
:*DUP   drop
	dpila> 8 - >r
	r@+ r@+ swap
	r!+ r!+ r> 'dpila> ! ;
:*2DUP  drop
	dpila> 16 - >r
	r@+ r@+ swap
	r@+ r@+ swap 2swap
	r!+ r!+ r!+ r!+
	r> 'dpila> ! ;
:*OVER  drop
	dpila> 16 - >r
	r@+ r@+ swap r> 8 + >r
	r!+ r!+ r> 'dpila> ! ;
:*PICK2 drop
	dpila> 24 - >r
	r@+ r@+ swap r> 16 + >r
	r!+ r!+ r> 'dpila> ! ;
:*PICK3 drop
	dpila> 32 - >r
	r@+ r@+ swap r> 24 + >r
	r!+ r!+ r> 'dpila> ! ;
:*PICK4 drop
	dpila> 40 - >r
	r@+ r@+ swap r> 32 + >r
	r!+ r!+ r> 'dpila> ! ;
:*SWAP drop
	dpila> 16 - >r
	r@+ r@+ swap
	r@+ r> @ swap
	dpila> 16 -
	!+ !+ !+ ! ;
:*NIP drop
	dpila> 8 - >r
	r@+ r@+ swap r> 16 -
	!+ !+ 'dpila> ! ;
:*ROT drop
	dpila> 24 - >r
	r@+ r@+ swap
	r@+ r@+ swap
	r@+ r> @ swap 2swap
	dpila> 24 -
	!+ !+ !+ !+ !+ ! ;
:*2OVER drop
	dpila> 32 - >r
	r@+ r@+ swap
	r@+ r@+ swap 2swap
	r> 16 +
	!+ !+ !+ !+ 'dpila> ! ;
:*2SWAP drop
	dpila> 32 - >r
	r@+ r@+ swap
	r@+ r@+ swap 2swap
	r@+ r@+ swap
	r@+ r@+ swap 2swap r> 32 -
	!+ !+ !+ !+ !+ !+ !+ ! ;

:,DUP
	"lea esi,[esi-4]" ,ln
	"mov [esi],eax" ,ln ;
:,DROP
	"lodsd" ,ln ;
:,NIP
	"lea esi,[esi+4]" ,ln ;
:,2DROP
	,NIP ,DROP ;
:,3DROP
	"lea esi,[esi+8]" ,ln
	,DROP ;
:,4DROP
	"lea esi,[esi+12]" ,ln
	,DROP ;
:,OVER
	,DUP "mov eax,[esi+4]" ,ln ;
:,PICK2
	,DUP "mov eax,[esi+4*2]" ,ln ;
:,PICK3
	,DUP "mov eax,[esi+4*3]" ,ln ;
:,PICK4
	,DUP "mov eax,[esi+4*4]" ,ln ;
:,SWAP
|	"mov edx,eax" ,ln |"xchg eax,[esi]" ,ln ;|ggrr
|	"mov eax,[esi]" ,ln
|	"mov [esi],edx" ,ln ;
	"push eax" ,ln	| otra version (sin dx)
	"mov eax,[esi]" ,ln
	"popd [esi]" ,ln ;
:,ROT
|	"mov ecx,[esi+4]" ,ln
|	"mov edx,[esi]" ,ln
|	"mov [esi],eax" ,ln
|	"mov [esi+4],edx" ,ln
|	"mov eax,ecx" ,ln ;
	"pushd [esi+4]" ,ln	| otra version (sin dx,cx)
	"pushd [esi]" ,ln
	"popd [esi+4]" ,ln
	"mov [esi],eax" ,ln
	"pop eax" ,ln ;
:,2DUP
	,OVER ,OVER ;
:,2OVER
	"mov [esi-4],eax" ,ln
	"lea esi,[esi-8]" ,ln
|	"mov edx,[esi+16]" ,ln
|	"mov [esi],edx" ,ln
	"pushd [esi+16]" ,ln
	"popd [esi]" ,ln
	"mov eax,[esi+12]" ,ln ;
:,2SWAP
|	"mov ecx,[esi+4]" ,ln
	"pushd [esi+4]" ,ln
	"mov [esi+4],eax" ,ln
|	"mov edx,[esi+8]" ,ln
	"pushd [esi+8]" ,ln
	"mov eax,[esi]" ,ln
	"mov [esi+8],eax" ,ln
|	"mov [esi],edx" ,ln
|	"mov eax,ecx" ,ln
	"popd [esi]" ,ln
	"pop eax" ,ln ;

|--------------------------------------------
:,AND
	d.cnt 0? ( drop "and eax,[esi]" ,ln ,NIP ; ) drop
	2cte? 1? ( drop d.pop2 and d.pushCte ; ) drop
	NOSset
	"and " ,s ,NOS ,, ,TOS ,cr *DROP ;

:,OR
	d.cnt 0? ( drop "or eax,[esi]" ,ln ,NIP ; ) drop
	2cte? 1? ( drop d.pop2 or d.pushCte ; ) drop
	NOSset
	"or " ,s ,NOS ,, ,TOS ,cr *DROP ;

:,XOR
	d.cnt 0? ( drop "xor eax,[esi]" ,ln ,NIP ; ) drop
	2cte? 1? ( drop d.pop2 xor d.pushCte ; ) drop
	NOSset
	"xor " ,s ,NOS ,, ,TOS ,cr *DROP ;

:,NOT
	d.cnt 0? ( drop "not eax" ,ln ; ) drop
	1cte? 1? ( drop d.pop not d.pushCte ; ) drop
	TOSset
	"not" ,s ,TOS ,cr ;

:,+
	d.cnt 0? ( drop "add eax,[esi]" ,ln ,NIP ; ) drop
	2cte? 1? ( drop d.pop2 + d.pushCte ; ) drop
	NOSset
	"add " ,s ,NOS ,, ,TOS ,cr *DROP ;

:,-
	d.cnt 0? ( drop "neg eax" ,ln "add eax,[esi]" ,ln ,NIP ; ) drop
	2cte? 1? ( drop d.pop2 - d.pushCte ; ) drop
	NOSset
	"sub " ,s ,NOS ,, ,TOS ,cr *DROP ;

:,*
	d.cnt 0? ( drop "imul eax,dword [esi]" ,ln ,NIP ; ) drop
	2cte? 1? ( drop d.pop2 * d.pushCte ; ) drop
	NOSset
	"imul " ,s ,NOS ,, ,TOS ,cr *DROP ;

:,/
	d.cnt 0? ( drop "idiv eax,dword [esi]" ,ln ,NIP ; ) drop
	2cte? 1? ( drop d.pop2 / d.pushCte ; ) drop
	NOSset
	"idiv " ,s ,NOS ,, ,TOS ,cr *DROP ;

:,*/
	d.cnt 0? ( drop
			"mov ecx,eax" ,ln
			,DROP
			"cdq" ,ln
			"imul dword [esi]" ,ln
			"idiv ecx" ,ln
			,NIP ; ) drop
	3cte? 1? ( drop d.pop3 */ d.pushCte ; ) drop
	NOS2eax
	FREEedx
	"cdq" ,ln
	"imul " ,s ,NOS ,cr
	"idiv " ,s ,TOS ,cr
	*2DROP ;

:,*>>
	d.cnt 0? ( drop
		"mov ecx,eax" ,ln
		,DROP
		"cdq" ,ln
		"imul dword [esi]" ,ln
		"shrd eax,edx,cl" ,ln
		,NIP ; ) drop
	3cte? 1? ( drop d.pop3 *>> d.pushCte ; ) drop
	-2cte? 1? ( drop
		NOS2eax
		FREEedx
		"cdq" ,ln
		"imul dword [esi]" ,ln
		"shrd eax,edx," ,s ,TOSbyte ,cr
		*2DROP ; ) drop
	TOSecx
	NOS2eax
	FREEedx
	"cdq" ,ln
	"imul dword [esi]" ,ln
	"shrd eax,edx,cl" ,ln
	*2DROP ;

:,<</
	d.cnt 0? ( drop
		"mov ecx,eax" ,ln
		"pushd [esi+4]"
		,2DROP
		"cdq" ,ln
	    "shld eax,edx,cl" ,ln
		"pop ecx" ,ln
		"idiv ecx" ,ln ; ) drop
|	3cte? 1? ( drop d.pop3 <</ d.pushCte ; ) drop
	TOS>cl
	NOS2>eax
	"cdq" ,ln
    TOS "shld eax,edx,%s" ,print ,cr
	NOS "idiv %s" ,print ,cr
	*2DROP ;


:,/MOD
	d.cnt 0? ( drop
		"mov ecx,eax" ,ln
		"mov eax,[esi]" ,ln
		"cdq" ,ln
		"idiv ecx" ,ln
		"mov [esi],eax" ,ln
		"mov eax,edx" ,ln ; ) drop
	2cte? 1? ( drop d.get2 /mod d.pushCte d.pushCte ; )
	NOS>eax
	"cdq" ,ln
	"idiv " ,s ,TOS ,cr
	NOS>eax
	TOS>edx ;

:*,MOD
	NOS>eax
	"cdq" ,ln
 	TOS "idiv %s" ,ln
	*DROP ;
:,MOD
	d.cnt 0? ( drop
		"mov ecx,eax" ,ln
		,DROP
		"cdq" ,ln
		"idiv ecx" ,ln
		"mov eax,edx" ,ln ; ) drop
	2cte? 1? ( drop d.get2 mod d.pushCte ; ) drop
	NOS>eax
	FREEedx
	"idiv " ,s ,TOS ,cr
	"mov eax,edx" ,ln
	*DROP ;

:,<<
	d.cnt 0? ( drop
		"mov ecx,eax" ,ln ,DROP
		"sal eax,cl" ,ln ; ) drop
	2cte? 1? ( drop d.get2 << d.pushCte ; ) drop
	NOSset
	"sal ",s ,NOS ,, ,TOS ,cr
	*DROP ;

:,>>
	d.cnt 0? ( drop
		"mov ecx,eax" ,ln ,DROP
		"sar eax,cl" ,ln ; ) drop
	2cte? 1? ( drop d.get2 << d.pushCte ; ) drop
	NOSset
	"sar ",s ,NOS ,, ,TOS ,cr
	*DROP ;

:,ABS
	d.cnt 0? ( drop
		"cdq" ,ln
		"add eax,edx" ,ln
		"xor eax,edx" ,ln ; ) drop
	1cte? 1? ( drop d.pop abs d.pushCte ; ) drop
	TOSeax
	FREEedx
	"cdq" ,ln
	"add eax,edx" ,ln
	"xor eax,edx" ,ln ;

:,NEG
	d.cnt 0? ( drop "neg eax" ,ln ; ) drop
	1cte? 1? ( drop d.pop neg d.pushCte ; ) drop
	TOSset
	"neg" ,s ,TOS ,cr ;

:,1+
	d.cnt 0? ( drop "add eax,1" ,ln ; ) drop
	1cte? 1? ( drop d.pop 1+ d.pushCte ; ) drop
	TOSset
	"add " ,s ,TOS ",1" ,s ,cr ;

:,1-
	d.cnt 0? ( drop "add eax,-1" ,ln ; ) drop
	1cte? 1? ( drop d.pop 1- d.pushCte ; ) drop
	TOSset
	"add " ,s ,TOS ",-1" ,s ,cr ;

:,4+
	d.cnt 0? ( drop "add eax,4" ,ln ; ) drop
	1cte? 1? ( drop d.pop 4+ d.pushCte ; ) drop
	TOSset
	"add " ,s ,TOS ",4" ,s ,cr ;


:,2/
	d.cnt 0? ( drop "sar eax,1" ,ln ; ) drop
	1cte? 1? ( drop d.pop 2/ d.pushCte ; ) drop
	TOSset
	"sar " ,s ,TOS ",1" ,s ,cr ;

:,2*
	d.cnt 0? ( drop "sal eax,1" ,ln ; ) drop
	1cte? 1? ( drop d.pop 2* d.pushCte ; ) drop
	TOSset
	"sal " ,s ,TOS ",1" ,s ,cr ;

|---------------------

:,EXEC
	"mov ecx,eax" ,ln
	,DROP
	"or ecx,ecx" ,ln
	"jz @f" ,ln
	"call ecx" ,ln
	"@@:" ,ln ;

:*,>R
	| ( ; )
	TOS REGR "mov %s,%s" ,print ,cr
	*,DROP ;
:,>R
	"push eax" ,ln
	,DROP ;

:*,R>
	;
:,R>
	,DUP "pop eax" ,ln ;

:*,R
	| ( ; )
	*,DUP
	TOS REGR "mov %s,%s" ,print ,cr ;
:,R
	,DUP "mov eax,[esp]" ,ln ;

:*,RDROP
| ( ; )
:,RDROP
	"lea esp,[esp+4]" ,ln ;

:*,R+
	TOS REGR "add [%s],%s" ,print ,cr ;
:,R+
	"add [esp],eax" ,ln
	,DROP ;

:*,R!+
	TOS REGR "mov [%s],%s" ,print ,cr
	REGR "add %s,4" ,print ,cr
	;
:,R!+
	"pop edx" ,ln
	"mov [edx],eax" ,ln
	"add edx,4" ,ln
	"push edx" ,ln
	,DROP ;

:*,R@+
	REGR TOS "move %s,[%s]" ,print ,cr
	REGR "add %s,4" ,print ,cr ;
:,R@+
	,DUP
	"mov eax,[esp]" ,ln
	"mov eax,[eax]" ,ln
	"add [esp],4" ,ln ;
|	"pop edx" ,ln
|	"mov eax,[edx]" ,ln
|	"add edx,4" ,ln
|	"push edx" ,ln ;


:,@
	d.cnt 0? ( drop "mov eax,dword [eax]" ,ln ; ) drop
	"mov " ,s ,TOS "dword [" ,s ,TOS "]" ,s ,cr ;

:,C@
	d.cnt 0? ( drop "movsx eax,byte [eax]" ,ln ; ) drop
	"movsx " ,s ,TOS "byte [" ,s ,TOS "]" ,s ,cr ;

:,W@
	d.cnt 0? ( drop "movsx eax,word [eax]" ,ln ; ) drop
	"movsx " ,s ,TOS "word [" ,s ,TOS "]" ,s ,cr ;

:,!
	d.cnt 0? ( drop
		"mov ecx,[esi]" ,ln
		"mov [eax],ecx" ,ln
		,2DROP ; ) drop
	"mov [" ,s ,TOS "]," ,s ,NOS ,cr
	*2DROP ;

:,C!
	d.cnt 0? ( drop
		"mov ecx,[esi]" ,ln
		"mov byte [eax],cl" ,ln
		,2DROP ; ) drop
	"mov byte [" ,s ,TOS "]," ,s ,NOSbyte ,cr
	*2DROP ;

:,W!
	d.cnt 0? ( drop
		"mov ecx,[esi]" ,ln
		"mov word [eax],cx" ,ln
		,2DROP ; ) drop
	"mov word [" ,s ,TOS "]," ,s ,NOSword ,cr
	*2DROP ;

:,+!
	d.cnt 0? ( drop
		"mov ecx,[esi]" ,ln
		"add [eax],ecx" ,ln
		,2DROP ; ) drop
	"add [" ,s ,TOS "]," ,s ,NOS ,cr
	*2DROP ;

:,W+!
	d.cnt 0? ( drop
		"mov ecx,[esi]" ,ln
		"add word [eax],cx" ,ln
		,2DROP ; ) drop
	"add word [" ,s ,TOS "]," ,s ,NOSword ,cr
	*2DROP ;

:,C+!
	d.cnt 0? ( drop
		"mov ecx,[esi]" ,ln
		"add byte [eax],cl" ,ln
		,2DROP ; ) drop
	"add byte [" ,s ,TOS "]," ,s ,NOSbyte ,cr
	*2DROP ;

:,!+
	d.cnt 0? ( drop
		"mov ecx,[esi]" ,ln
		"mov [eax],ecx" ,ln
		"add eax,4" ,ln
		,NIP ; ) drop
	"mov [" ,s ,TOS "]," ,s ,NOS ,cr
	"add " ,s ,TOS ",4" ,s ,cr
	*NIP ;

:,W!+
	d.cnt 0? ( drop
		"movsx ecx,word [esi]" ,ln
		"mov [eax],cx" ,ln
		"add eax,2" ,ln
		,NIP ; ) drop
	"mov word [" ,s ,TOS "]," ,s ,NOSword ,cr
	"add " ,s ,TOS ",2" ,s ,cr
	*NIP ;

:,C!+
	d.cnt 0? ( drop
		"movsx ecx,byte [esi]" ,ln
		"mov [eax],cl" ,ln
		"inc eax" ,ln
		,NIP ; ) drop
	"mov byte [" ,s ,TOS "]," ,s ,NOSbyte ,cr
	"add " ,s ,TOS ",1" ,s ,cr
	*NIP ;

:,@+
	d.cnt 0? ( drop
		"mov ecx,[eax]" ,ln
		"add eax,4" ,ln
		"mov [esi-4],eax" ,ln
		"mov eax,ecx" ,ln
		"lea esi,[esi-4]" ,ln ; ) drop
	*DUP
	"mov " ,s ,TOS ",[" ,NOS "]" ,ln
	"add " ,s ,NOS ",4" ,ln ;

:,W@+
	d.cnt 0? ( drop
		"movsx ecx,word [eax]" ,ln
		"add eax,2" ,ln
		"mov [esi-4],eax" ,ln
		"mov eax,ecx" ,ln
		"lea esi,[esi-4]" ,ln ; ) drop
	*DUP
	"movsx " ,s ,TOS ",word [" ,NOS "]" ,ln
	"add " ,s ,NOS ",2" ,ln ;


:,C@+
	d.cnt 0? ( drop
		"movsx ecx,byte [eax]" ,ln
		"inc eax" ,ln
		"mov [esi-4],eax" ,ln
		"mov eax,ecx" ,ln
		"lea esi,[esi-4]" ,ln ; ) drop
	*DUP
	"movsx " ,s ,TOS ",byte [" ,NOS "]" ,ln
	"add " ,s ,NOS ",1" ,ln ;

|------------------------------------
:,0?
	d.cnt 0? ( drop
		"or eax,eax" ,ln
		"jnz " ,s ; ) drop
	"or " ,TOS ,, ,,TOS ,cr
	"jnz " ,s ;

:,+?
	d.cnt 0? ( drop
		"or eax,eax" ,ln
		"js " ,s ; ) drop
	"or " ,TOS ,, ,,TOS ,cr
	"js " ,s ;

:,-?
	d.cnt 0? ( drop
		"or eax,eax" ,ln
		"jns " ,s ; ) drop
	"or " ,TOS ,, ,,TOS ,cr
	"jns " ,s ;

:,1?
	d.cnt 0? ( drop
		"or eax,eax" ,ln
		"jz " ,s ; ) drop
	"or " ,TOS ,, ,,TOS ,cr
	"jz " ,s ;

:,=?
	d.cnt 0? ( drop
		"mov ecx,eax" ,ln ,DROP
		"cmp eax,ecx" ,ln
		"jnz " ,s ; ) drop
	"cmp " ,s ,NOS ,, ,TOS ,cr *DROP
	"jnz " ,s ;

:,<?
	d.cnt 0? ( drop
		"mov ecx,eax" ,ln ,DROP
		"cmp eax,ecx" ,ln
		"jge " ,s ; ) drop
	"cmp " ,s ,NOS ,, ,TOS ,cr *DROP
	"jge " ,s ;

:,>?
	d.cnt 0? ( drop
		"mov ecx,eax" ,ln ,DROP
		"cmp eax,ecx" ,ln
		"jle " ,s ; ) drop
	"cmp " ,s ,NOS ,, ,TOS ,cr *DROP
	"jle " ,s ;

:,<=?
	d.cnt 0? ( drop
		"mov ecx,eax" ,ln ,DROP
		"cmp eax,ecx" ,ln
		"jg " ,s ; ) drop
	"cmp " ,s ,NOS ,, ,TOS ,cr *DROP
	"jg " ,s ;

:,>=?
	d.cnt 0? ( drop
		"mov ecx,eax" ,ln ,DROP
		"cmp eax,ecx" ,ln
		"jl " ,s ; ) drop
	"cmp " ,s ,NOS ,, ,TOS ,cr *DROP
	"jl " ,s ;

:,<>?
	d.cnt 0? ( drop
		"mov ecx,eax" ,ln ,DROP
		"cmp eax,ecx" ,ln
		"jz " ,s ; ) drop
	"cmp " ,s ,NOS ,, ,TOS ,cr *DROP
	"jz " ,s ;

:,and?
	d.cnt 0? ( drop
		"mov ecx,eax" ,ln ,DROP
		"test eax,ecx" ,ln
		"jz " ,s ; ) drop
	"test " ,s ,NOS ,, ,TOS ,cr *DROP
	"jz " ,s ;

:,nand?
	d.cnt 0? ( drop
		"mov ecx,eax" ,ln ,DROP
		"test eax,ecx" ,ln
		"jnz " ,s ; ) drop
	"test " ,s ,NOS ,, ,TOS ,cr *DROP
	"jnz " ,s ;

|--------- Sistema
:,DIR
	"call SYSDIR" ,ln ;
:,FILE
	"call SYSFILE" ,ln ;
:,FSIZE
	"call SYSFSIZE" ,ln ;
:,VOL
	"call SYSVOL" ,ln ;
:,LOAD
	"call SYSLOAD" ,ln ;
:,SAVE
	"call SYSSAVE" ,ln ;
:,UPDATE
	"call SYSUPDATE" ,ln ;
:,MSEC
	"call SYSMSEC" ,ln ;
:,TIME
	"call SYSTIME" ,ln ;
:,DATE
	"call SYSDATE" ,ln ;
:,END
	"jmp SYSEND" ,ln ;
:,RUN
	"call SYSRUN" ,ln ;
:,SW
	,DUP  "mov eax,[SYSW]" ,ln ;
:,SH
	,DUP  "mov eax,[SYSH]" ,ln ;
:,MEM
	,DUP  "mov eax,FREE_MEM" ,ln ;

:,PAPER
	"mov [SYSPAPER],eax" ,ln ,DROP ;

:,CLS
	"call SYSCLS" ,ln ;

:,REDRAW
	"call SYSREDRAW" ,ln ;
:,FRAMEV
	,DUP  "mov eax,SYSFRAME" ,ln ;

:,XFB
	,DUP  "mov eax,XFB" ,ln ;
:,>XFB
	"call SYSTOXFB" ,ln ;
:,XFB>
	"call SYSXFBTO" ,ln ;

:,SETXY | x y --
	"cdq" ,ln
	"imul dword [SYSW]" ,ln
	"add eax,[esi]" ,ln
	"lea ebp,[SYSFRAME+eax*4]" ,ln
	,2DROP ;

:,PX+!	| s --
	"lea ebp,[ebp+eax*4]" ,ln
	,DROP ;
:,PX!+	| rgb --
	"mov [ebp],eax" ,ln
	"add ebp,4" ,ln
	,DROP ;
:,PX@	| -- rgb
	,DUP "mov eax,[ebp]" ,ln ;

:,iPEN! | v --
	"mov [SYSiPEN],eax" ,ln
	,DROP ;

:,TPEN | --
	,DUP
	"mov eax,[MCNT]" ,ln
	"dec eax" ,ln
	"mov [MBUFF],eax" ,ln
	"xor eax,eax" ,ln
	"inc eax" ,ln
	"mov [MCNT],eax" ,ln
	"mov eax,MBUFF" ,ln ;

:,XYMOUSE | -- x y
	,DUP
	"lea esi,[esi-4]" ,ln
	"mov eax,[SYSXYM]" ,ln
	"mov ecx,eax" ,ln
	"and ecx,$ffff" ,ln
	"shr eax,16" ,ln
	"mov [esi],ecx" ,ln ;

:,BMOUSE | -- c
	,DUP "mov eax,[SYSBM]" ,ln ;

:,iKEY! | v --
	"mov [SYSiKEY],eax" ,ln
	,DROP ;

:,KEY	| -- c
	,DUP "mov eax,[SYSKEY]" ,ln ;

|-----------
:,MOVE
:,MOVE>
:,CMOVE
:,CMOVE>
:,INK
:,INK@
:,ALPHA
:,OP
:,CP
:,LINE
:,CURVE
:,PLINE
:,PCURVE
:,POLI
:,FCOL
:,FCEN
:,FMAT
:,SFILL
:,LFILL
:,RFILL
:,TFILL

:,CNTJOY
:,GETJOY
:,SLOAD
:,SPLAY
:,MLOAD
:,MPLAY
:,SERVER
:,CLIENT
:,SEND
:,RECV
:,CLOSE
:,DOCINI
:,DOCEND
:,DOCAT
:,DOCLINE
:,DOCTEXT
:,DOCFONT
:,DOCBIT
:,DOCRES
:,DOCSIZE
:,SYSTEM
	;

|------------ compila CODIGO
|:nro>dicn2   8 >> 5 << 'indicepal + @ ; | nombre
:nro>dicn   8 >> "w%h" mprint ;			| numero de palabra

:+etiqueta	| -- nueva   // y agrega a pila
	nlabel 1+ dup 'nlabel ! dup >pilac ;

:salto? | adr++ xx xx -- adr++ xx xx 0/1
	pick2 8 - @ $ff and
	18 >? ( 31 <? ( 1 nip ; ) ) 0 nip ;

:,defw :,defv ;

:ini.wordcomp
	;

:,;
	| reacomoda pila
	"ret" ,ln ;
:,(
	+etiqueta salto? 1? ( swap label ,cr 1 )( swap label ":" ,ln 2 ) >pilac drop ;
:,)(
	<pilac salto? 0? ( drop
			1 =? ( <pilac +etiqueta jmp, label ":" ,ln 1 >pilac drop ; )
			)( drop
			2 =? ( <pilac +etiqueta label ,cr >pilac 3 >pilac drop ; ) )
	drop ;
:,)
	<pilac salto? 0? ( drop
			1 =? ( <pilac label ":" ,ln drop ; )
			2 =? ( <pilac jmp, drop ; )
			3 =? ( <pilac jmp, <pilac label ":" ,ln drop ; )
		)( drop
			2 =? ( <pilac label ,cr drop ; ) )
	drop ;

|---- nivel 0 - sin desordenar la pila
#nivel0 0
,defw ,defv ,lit ,lit ,lit ,lit ,lits ,wor ,var ,dwo ,dva
,; ,( ,)( ,) ,[ ,] ,EXEC
,0? ,+? ,-? ,1? ,=? ,<? ,>? ,<=? ,>=? ,<>? ,AND? ,NAND?
,DUP ,DROP ,OVER ,PICK2 ,PICK3 ,PICK4 ,SWAP ,NIP	|--- pila
,ROT ,2DUP ,2DROP ,3DROP ,4DROP ,2OVER ,2SWAP
,>R ,R> ,R ,R+ ,R@+ ,R!+ ,RDROP					|--- pila direcciones
,AND ,OR ,XOR ,NOT  								|--- logicas
,+ ,- ,* ,/ ,*/ ,*>> ,/MOD ,MOD ,ABS  			|--- aritmeticas
,NEG ,1+ ,4+ ,1- ,2/ ,2* ,<< ,>>
,@ ,C@ ,W@ ,! ,C! ,W! ,+! ,C+! ,W+!  			|--- memoria
,@+ ,!+ ,C@+ ,C!+ ,W@+ ,W!+
,MOVE ,MOVE> ,CMOVE ,CMOVE>
,MEM ,DIR ,FILE ,FSIZE ,VOL ,LOAD ,SAVE			|--- memoria bloques
,UPDATE ,MSEC ,TIME ,DATE ,END ,RUN 				|--- sistema
,SW ,SH ,CLS ,REDRAW ,FRAMEV  						|--- pantalla
,SETXY ,PX+! ,PX!+ ,PX@
,XFB ,>XFB ,XFB>
,PAPER ,INK ,INK@ ,ALPHA							|--- color
,OP ,CP ,LINE ,CURVE ,PLINE ,PCURVE ,POLI		|--- dibujo
,FCOL ,FCEN ,FMAT ,SFILL ,LFILL ,RFILL ,TFILL
,TPEN ,XYMOUSE ,BMOUSE
,IKEY! ,KEY
,CNTJOY ,GETJOY
,SLOAD ,SPLAY	,MLOAD ,MPLAY					|--- Sonido
,SERVER ,CLIENT ,SEND ,RECV ,CLOSE
,DOCINI ,DOCEND ,DOCAT ,DOCLINE ,DOCTEXT ,DOCFONT ,DOCBIT ,DOCRES ,DOCSIZE  |-- impresora
,SYSTEM

| compila nivel 0 de palabras
:,token0 | a -- a.
	@+ dup
	$ff and 2 << 'nivel0 + @ exec
	drop ;

::compword | nro --
	ini.wordcomp
	nro>toklen
	( 1? )( 1- swap	,token0 swap )
	2drop ;


|------------ compila DATO
#d1 "dd "
#d2 "dw "
#d3	"db "
#d4 "rb "
#dtipo 'd1
#dini 0

:dfin	dini 1? ( dtipo )( "," ) ,s drop 0 'dini ! ;
:dfini	dini 1? ( dtipo ,s 0 ,d ) drop 0 'dini ! ;
:dfins	dini 1? ( "db " )( "," ) ,s drop 0 'dini ! ;
:dfind	dini 1? ( "dd " )( "," ) ,s drop 0 'dini ! ;

|----------------------------------------------
:cpycad | adr --
	( c@+ 1? )( 34 =? ( drop c@+ 34 <>? ( 2drop ; ) ) ,c ) 2drop ;

:,ddefw
:,ddefv drop ;
:,dlit	dfin tok>cte ,d ;
:,dlits	dfins tok>str 34 ,c cpycad 34 ,c ",0" ,s ;
:,dwor	dfind nro>dicn ,s ;
:,d;	drop ;
:,d(	drop dtipo >pilac 'd3 'dtipo ! ;
:,d)(	drop 'd4 'dtipo ! ;
:,d)	drop <pilac 'dtipo ! ;
:,d[	drop dtipo >pilac 'd2 'dtipo ! ;
:,d]	drop <pilac 'dtipo ! ;

#coded 0
,ddefw ,ddefv ,dlit ,dlit ,dlit ,dlit ,dlits ,dwor ,dwor ,dwor ,dwor
,d; ,d( ,d)( ,d) ,d[ ,d]

|---- datos
::compvar | nropal cntlla -- nropal
	drop dup "w%h " ,print
	'd1 'dtipo ! 1 'dini ! pilac!
	dup nro>tok
	( @+ dup $ff and 3 <? )( 	 | dirv n c
		18 <? ( 2 << 'coded + @ exec )( 2drop )
		,sp )
	3drop
	dfini
	,cr ;

:otrostr
	over
	1 'dini !
	nstr "s%h " ,print
	,dlits
	1 'nstr +!
	,cr ;

::compvarw | nropal cntlla -- nropal
	drop |dup "w%h  " ,print
	dup
	nro>toklen
	( 1? )( 1- swap
		@+ dup $ff and
		7 =? ( otrostr ) 2drop
		swap )
	2drop ;
