|--------------------------------------------------------------------
| click-o-tron
| Manuel Cornes 2008
| Rounds of 10sec
| Click as many blue disks as you can
| Get 1 point per correct click, -2 points per incorrect click
|--------------------------------------------------------------------
^lib/reda4.txt
^lib/gui3.txt

#score 0 #hiscore 0 #x #y #r_ #mousedown #clock

:bounds over -

:randbtw | ( min max -- v )
       over - rand abs swap mod + ;

:rand_circ 50 sw bounds 'x ! 50 sh bounds 'y ! 10 50 randbtw 'r_ ! ;

:bonus mousedown 1 =? ( 1 'score +! ) ;

:malus mousedown 1 =? ( score 2 - 'score ! ) ;

:elapsed msec clock - ;

:tosec 1000 / ;

:countdown 10 swap - ;

:roundstart msec 'clock ! ;

:updatehiscore score hiscore >? ( score 'hiscore ! ) ;

:timeleft elapsed tosec countdown ;

:stats timeleft score hiscore
       "Clickotron%.Hiscore:%d%.Score:%d%.Time Left:%d" ;

:mousepixel xymouse getpixel ;

:go
   rand_circ
   inigui
   'exit <esc>
   roundstart
   show clrscr 20 font
       bmouse 0? ( 0 'mousedown ! ) drop
                               amarillo 0 0 at stats print
       azul x y r_ fcircle
       mousepixel $0000FF =? ( blanco x y r_ 1 + circle ) drop
       bmouse 1? ( 1 'mousedown +! mousepixel $0000FF =?
           ( bonus rand_circ )( malus ) drop ) drop
       timeleft 0 <=? ( updatehiscore 0 'score ! roundstart ) 
		cmano ;

: go "main.txt" run ;