| Navegador de Colores
| PHREDA 2006
|---------------------------------------
^lib/reda4.txt
^lib/gui.txt
^lib/color.txt

#paleta1 )( $3ffff

#colorb
#colora
#tipo

:colsolido	0 'tipo ! ;
:coldegl	1 'tipo ! ;
:coldegr	2 'tipo ! ;

:cambiacol colora colorb 'colora ! 'colorb ! ;

:colorcompleto
	tipo
	0? ( colora ink sfill )
	1- 0? ( colorb colora gc.hdeg lfill )
	1- 0? ( colorb colora gc.hdeg rfill )
	drop
	gc.fbox
	sfill
	'exit onClick
	;

#claridad 1.0
#clarisli
#xcol -1 #ycol

:llenapaleta
	'paleta1 >r
	0 ( 1.0 <=? )(
		0 ( 1.0 <=? )(
			over over swap claridad hsv2rgb r!+
			$101 + ) drop
		$101 + ) drop
	rdrop
	;

:reccolor
	xcol ycol setxy px@ 'colora ! ;

:textura
	clarisli 0.5 +
	claridad <>? ( 'claridad ! llenapaleta )( drop )
	'paleta1 tfill
	gc.all
	gc.fbox
	sfill
	[ xymouse 'ycol ! 'xcol ! reccolor ; ] onMove
	xcol -1 =? ( drop ; ) ycol
	3 negro box
	;

:slideluz
	'clarisli
	[ xymouse nip yc - 16 << h / over ! ; ] onMove
	$0 $ffffff vfil
	@ h 16 *>> 'yc +!
	negro
	xc w 2/ - yc 2dup op
	over w + over h 5 >> - line
	over w + over h 5 >> + line
	line
| 	0.98 0.1 fscala
|	$dddddd $222222 hbtn
	;

#colorseg )( 256

:minipaleta
	0.04 0.04 fdim
	-0.9 0.7 fpos
	'colorseg
	16 ( 1? )( 1-
		4 ( 1? )( 1-
			rot @+ ink gc.fbox
			[ dup 4 - @ 'colora ! ; ] onClick
			rot rot gc>>
			) drop
		gcdn gc<< gc<< gc<< gc<<
		) 2drop ;

:makepaleta
	'colorseg >r
	0 r!+ $555555 r!+ $aaaaaa r!+ $FFFFFF r!+
	0.0 ( 1.0 <? )(
		dup 1.0 0.1 hsv2rgb r!+
		dup 1.0 0.35 hsv2rgb r!+
		dup 1.0 0.65 hsv2rgb r!+
		dup 1.0 1.0 hsv2rgb r!+
		0.067 + ) drop
	rdrop ;

|---------------------------------------------------------
:colorshow
	makepaleta
	inigui
	'exit >esc<
	show clrscr
		0.8 dup fdim -0.2 0.2 fpos
		azul gc.fbox

		0.1 dup fdim -0.9 0.9 fpos
		colorcompleto gc>>
		tipo 0? ( amarillo )( verde )
		'colsolido 'istop btnd gc>>
		1 =? ( amarillo )( verde )
		'coldegl 'iplay btnd gc>>
		2 =? ( amarillo )( verde )
		'coldegr 'irec btnd gc>>
		drop
		cyan
		'cambiacol 'irecicla btnd gc>>

		fonti
		colora ink gc.fbox
		home colora "%h" blanco printc
		gc>>
		colorb ink gc.fbox
		home colorb "%h" blanco printc
		scr
		0.5 0.5 fdim -0.1 0 fpos
		textura
		0.05 0.5 fdim 0.5 0 fpos
		slideluz

		minipaleta

		cflecha ;

::dlgcolor	| tipo col1 col2  -- col2 col1 tipo
	'colorb !+ !+ !
	llenapaleta
	colorshow
	colorb colora tipo ;

|: 0 $fffff $fff dlgcolor 3drop ;


:inipal | x y --
	;

