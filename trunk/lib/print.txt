| Fuentes unificadas
| :r4 libreria de sistema - Fuente de letras
|---------------------------------------------
^lib/reda4.txt
^lib/mprint.txt
^lib/graf.txt
^lib/fonti.txt

#:tx1 0 #:ty1 0 #:tx2 640 #:ty2 480 		| ventana de texto

#:rows 40	#:cols 80		| filas/columnas texto

#charsize 'charsize8i
#charemit 'char8i
#:charrom
#:charsiz
#:charini
#:charlin

|--------- palabras de dibujo de fuentes (reemplazables)

::window | x1 y1 x2 y2 --
	cch * 'ty2 ! ccw * 'tx2 !
	cch * 'ty1 ! ccw * 'tx1 ! ;

::win2gc
	tx2 tx1 2dup - 'w ! + 2/ 'xc !
	ty2 ty1 2dup - 'h ! + 2/ 'yc ! ;

::gc2win
	xc w 2/ - dup 'tx1 ! w + 'tx2 !
	yc h 2/ - dup 'ty1 ! h + 'ty2 ! ;


|------------------------------------------------------
::cr
	cch 'ccy +! tx1 'ccx ! ;

::cr2
	cch 2/ 'ccy +! tx1 'ccx ! ;

:dtab
	ccx 5 >> 1+ 5 << 'ccx ! ;

::emit | c --
	9 =? ( drop dtab ; )
	13 =? ( drop cr ; )
	ccy ty2 >? ( 2drop ; ) drop | ---
	dup charsize exec  | c sizex
|	ccx + tx2 >? ( 2drop ; )	| c sizex (*1)
|	ccx + tx2 >? ( cr )			| c sizex (*2)
	ccx + tx2 >? ( 'ccx ! drop ; )  | c sizex (*3)
	swap charemit exec
	'ccx ! ;

::gemit | c --
	13 =? ( drop cr ; )
:gemiti
	9 =? ( drop dtab ; )
	charsize exec 'ccx +! ;

::noemit? | -- f
	ccx tx1 <? ( 0 nip ) tx2 >? ( 0 nip ) drop
	ccy ty1 <? ( 0 nip ) tx2 >? ( 0 nip ) drop
	1 ;

::bout? | -- f
	ccy cch + ty2 >? ( 1 )( 0 ) nip ;

::lout? | -- f ;
	ccx tx2 >? ( 1 )( 0 ) nip ;

::sp
	32 charsize exec 'ccx +! ;

|----- cursor
::printcur | adr -- adr
	ccx >r
	ccx ccy  2dup op cch + pline
	c@+ gemiti
	ccx ccy cch + 2dup pline cch - pline
	poli
	r> 'ccx !
	;

::printcurw | adr -- adr
	ccx ccy 2dup op cch + pline
	( c@+ 32 >? )( gemit ) gemit
	ccx ccy cch + 2dup pline cch - pline
	poli
	;

|-------------------------------------
::sizeprint | "" -- "" cnt
	0 >r dup ( c@+ 1? )( charsize exec r+ ) 2drop r> ;

:sizeword | "" -- "" cnt
	0 >r
	dup  ( c@+ 1? )( 32 =? ( 2drop r> ; ) charsize exec r+ ) 2drop r> ;

|--bajar si no entra en la linea
::allowcr | "" -- ""
	sizeprint ccx + tx2 >? ( cr ) drop ;

::allow2cr | "" -- ""
	sizeprint ccx + tx2 >? ( cr cr cr2 ) drop ;

|------------------- ventana de texto
::fixsize | w h --
	gc2win
	'cch ! 'ccw !
:filcol
	ty2 ty1 - cch / 'rows !
	tx2 tx1 - ccw / 'cols !
    ;

::wtext | lin --		; texto en ventana de cursor
	gc2win
	ty2 ty1 - swap / dup 'cch !
	2/ dup 2/ + 'ccw !
	filcol ;

::home
	gc2win filcol
	0 dup
::at | x y --
	cch * ty1 + 'ccy !
::col | x --
	ccw * tx1 + 'ccx ! ;

|///////////////////////////////////////////////////////////////////////////
::print | .. "" -- ""
	mprint
::printx | "" --
	( c@+ 1? )( emit ) 2drop ;

::printc | .. "" --	; en centro de la ventana
	mprint
	sizeprint xc swap 2/ - 'ccx !
	printx ;

::printr | .. "" --	; en derecha de la ventana
	mprint
	sizeprint tx2 swap - 'ccx !
	printx ;

::cntprint | .. "" cnt --
	>r mprint r>
	( 1? )( 1- swap c@+ 0? ( drop 1- 32 )
		emit swap ) 2drop ;


::text | "" --
	( c@+ 1? )( emit ) 2drop ;

|-----------------------------------------
::fontset | tipof --
	@+ 'charsize !
	@+ 'charemit !
	@+ 'ccw !
	@+ 'cch !
	@+ 'charrom !
	@+ 'charsiz !
	@+ 'charini !
	@ 'charlin ! ;

::setfont | 'char 'size --
	'charsize !+ ! ;

::fonti
	8 12 fixsize
	'char8i 'charsize8i setfont ;

|--- dibujo de seleccion
#conex

::sel>>
	tx1 ccy cch + dup 'conex ! op
	ccx ccy cch + pline
	ccx ccy pline
	tx2 ccy pline
	;

::sel<<
	bout? 1? (
		tx2 ty2 pline
		tx1 ty2 pline
	)(
		tx2 ccy pline
		ccx ccy pline
		ccx ccy cch + pline
		tx1 ccy cch + pline
   		) drop
	tx1 conex pline
	poli ;

|-----------------------------------------
::print2gc | .. "" -- ""  ; pone gc en el texto (resuelve print)
	mprint sizeprint 2 + 'w !
	ccx w 2/ + 1- 'xc !
	ccy cch dup 'h ! 2/ + 'yc ! ;

::cntprintbox | cnt -- cnt  ; pone gc en el texto
	ccw over * dup 2/ + 'w !
	ccx w 2/ + 'xc !
	ccy cch dup 'h ! 2/ + 'yc ! ;

||||| boxprint
::boxprint | .. "" cnt --
	ccw * dup 2/ + ccx + >r
	print
	r> 'ccx ! ;

::boxprintc | .. "" cnt --
	ccw * ccx + >r
	mprint sizeprint
	r ccx - swap - 2/ 'ccx +!
	printx
	r> 'ccx ! ;

::boxprintr | .. "" cnt --
	ccw * ccx + >r
	mprint
	sizeprint r swap - 'ccx !
	printx
	r> 'ccx ! ;


|||| fills
::linefill
	tx1 ccy 2dup op
	2dup cch + 1+ pline
	tx2 ccy 2dup cch + 1+ pline pline pline poli
	;

::linesfill | cnt --
	>r
	tx1 ccy 2dup op
	2dup cch r * + pline
	tx2 ccy 2dup cch r> * + pline pline pline poli
	;

::linebox
	tx1 ccy 2dup op
	2dup cch + line
	tx2 ccy 2dup cch + line line line
	;

::linehfill | col1 col2 --
	fcol
	tx1 tx2 + 2/ ty1 ty2 + 2/ fcen
	1.0 ccw / 0 fmat
	tx1 ccy 2dup op
	2dup cch + pline
	tx2 ccy
	2dup cch + pline pline pline
	lfill poli sfill
	;

::linevfill | col1 col2 --
	fcol
	tx1 tx2 + 2/ ty1 ty2 + 2/ fcen
	0 1.0 cch / fmat
	tx1 ccy 2dup op
	2dup cch + pline
	tx2 ccy
	2dup cch + pline pline pline
	lfill poli sfill
	;
