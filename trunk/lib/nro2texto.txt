| Nro 2 texto
| PHREDA 2009
|---------------------

#u1 "uno"			#u2 "dos"			#u3 "tres"
#u4 "cuatro"		#u5 "cinco"			#u6 "seis"
#u7 "siete"			#u8 "ocho"			#u9 "nueve"

#d1 "diez"			#d2 "veinte"		#d3 "treinta"
#d4 "cuarenta"		#d5 "cincuenta"		#d6 "sesenta"
#d7 "setenta"		#d8 "ochenta"		#d9 "noventa"

#c1 "ciento"		#c2 "doscientos"	#c3 "trescientos"
#c4 "cuatrocientos"	#c5 "quinientos"	#c6 "seiscientos"
#c7 "setecientos"	#c8 "ochocientos"	#c9 "novecientos"

					#e2 "veinti"		#e3 "treinta y "
#e4 "cuarenta y "	#e5 "cincuenta y "	#e6 "sesenta y "
#e7 "setenta y "	#e8 "ochenta y "	#e9 "noventa y "

#f1 "once"			#f2 "doce"			#f3 "trece"
#f4 "catorce"		#f5 "quince"		#f6 "dieciseis"
#f7 "diecisiete"	#f8 "dieciocho"		#f9 "diecinueve"

#cUnidad  u1 u2 u3 u4 u5 u6 u7 u8 u9
#cDecena  d1 d2 d3 d4 d5 d6 d7 d8 d9
#cCentena c1 c2 c3 c4 c5 c6 c7 c8 c9
#cDecimal 0  e2 e3 e4 e5 e6 e7 e8 e9
#c1Decena f1 f2 f3 f4 f5 f6 f7 f8 f9

#buff )( 128
#buff> 'buff

:,c | c
	buff> c!+ 'buff> ! ;
:,s | adr --
	( c@+ 1? )( ,c ) 2drop ;

:decena | nro decena -- nro
	0? ( drop ; )
	over 10 mod 0? ( drop 1- 2 << 'cDecena + @ ,s ; )
	swap 1 =? ( drop 1- 2 << 'c1Decena + @ ,s 0 nip ; )
	nip 1- 2 << 'cDecimal + @ ,s ;

:cien2texto | nro 0 --
	swap 0? ( 2drop ; )
	100 =? ( "cien" ,s 2drop ; )
	dup 100 / 10 mod
	1? ( 1- 2 << 'cCentena + @ ,s 32 ,c )( drop )
	dup 10 / 10 mod
	decena
	10 mod
	0? ( 2drop ; )
	1 =? ( swap 0? ( "un" ,s 2drop ; ) drop )( nip )
	1- 2 << 'cUnidad + @ ,s ;

::nro2texto | nro -- ""
	'buff 'buff> !
	0? ( "cero" ,s ,c 'buff ; )
	dup 1000000 / 1000 mod
	1 >? ( 0 cien2texto " millones " ,s )( 1 =? ( "un millon " ,s ) drop )
	dup 1000 / 1000 mod
	1 >? ( 0 cien2texto " mil " ,s )( 1 =? ( "mil " ,s ) drop )
	1000 mod
	1 cien2texto
	0 ,c 'buff ;

