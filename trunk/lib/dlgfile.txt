| Navegador de Archivos
| PHREDA 2006
|---------------------------------------
^lib/reda4.txt
^lib/gui3.txt
^lib/fontt.txt

#cntlineas 0
#inifile 0
#fileact 0
#pnombre 0
#entrada )( 64

|---------------------------------------------------------
:sumafile | nro --
	fileact + dup file 0? ( 2drop ; ) 'entrada strcpy  'fileact !
:ubicar
	rows 1- 'cntlineas !
	inifile cntlineas +  ( fileact <? )( 1+ ) cntlineas - 'inifile !
	inifile ( fileact >? )(  1- ) 'inifile !
	;

:buscafile
	'entrada c@ 0? ( drop ; ) drop
	0 ( dup file 1? )(  | nro file
		'entrada =pre 1? ( drop 'fileact ! ubicar ; ) drop 1+ )
	2drop ;

|---------------------------------------------------------
:cadafile | n -- n
	blanco
	dup inifile +
	[ dup 'fileact ! dup file 1? ( 'entrada strcpy )( drop ) ; ] onClick
	fileact =? ( $0 $ffffff )( $fffff 0 )
	ink linefill ink
	file 1? ( sp print )( drop ) cr ;

:eligeactual
	fileact
	file 1? ( pnombre strcpy exit ; )
	drop ;

:nuevonombre
	'entrada c@ 0? ( drop ; ) drop
	'entrada pnombre strcpy exit
	;

|---------------------------------------------------------
::dlgfile | nn --
	'pnombre !
	0 'fileact !

	inigui
	'exit  >esc<
	'eligeactual >enter<
	[ 1 sumafile ; ] <dn>
	[ -1 sumafile ; ] <up>
	[ cntlineas sumafile ; ] <pgdn>
	[ cntlineas neg sumafile ; ] <pgup>

	pnombre 'entrada strcpy

	show clrscr
		font-vard-12-bold cr
		blanco pnombre " : %s : " print
|		dup "(%d)" print
		sp verde 'nuevonombre "Nuevo" btnt
		sp amarillo 'eligeactual "Actual" btnt
		sp rojo [ "" pnombre strcpy exit ; ] "Cancelar" btnt cr cr
		blanco " : " print 'entrada 64 inputline cr cr

		0.4 0.8 fdim -0.5 -0.1 fpos home
		'cadafile rows guimapv
|		0.4 0.8 fdim 0.5 -0.1 fpos home
|		'cadafile rows guimapv

		buscafile
		cmano ;
