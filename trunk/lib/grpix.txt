| new frame buffer
| PHREDA 2010
|-------------

#rc #gc #bc
#sr #sg #sb

#copiadeg )( 24

::degrade! |  cant $000FFF --
    dup  $f00000 and dup 4 >> or
	over $000f00 and 8 << dup 4 << or
	dup 'rc ! - pick2 / 'sr !
	dup  $0f0000 and dup 4 << or
	over $0000f0 and 12 << dup 4 << or
	dup 'gc ! - pick2 / 'sg !
	dup  $00f000 and 4 << dup 4 << or
	swap $00000f and 16 << dup 4 << or
	dup 'bc ! - swap / 'sb ! ;

::deg@+ | -- color
	rc dup sr + 'rc ! $ff0000 and
	gc dup sg + 'gc ! 8 >> $ff00 and or
	bc dup sb + 'bc ! 16 >> $ff and or ;

::*deg | guarda
	'copiadeg 'rc 6 move ;

::deg* | restaura
	'rc 'copiadeg 6 move ;

#xa #ya

::.at | x y --
	'ya ! 'xa ! ;

:.fline | w --
	xa ya setxy dup 'xa +!
	( 1? )( 1- ink@ px!+ ) drop ;

:.fdeg | w --
	xa ya setxy dup 'xa +!
	( 1? )( 1- deg@+ px!+ ) drop ;

::.fbox | w h --
	( 1? )( 1-
		xa ya setxy
		over ( 1? )( 1-
			ink@ px!+
			) drop
		1 'ya +!
		) 2drop ;

::.fboxdh | w h --
	( 1? )( 1-
		xa ya setxy
		deg@+ pick2
		( 1? )( 1-
			over px!+
			) 2drop
		1 'ya +!
		) 2drop ;

::.fboxdv | w h --
	*deg
	( 1? )( 1-
		deg*
		xa ya setxy
		over
		( 1? )( 1-
			deg@+ px!+
			) drop
		1 'ya +!
		) 2drop ;

::getpixel | x y -- c
	setxy px@ ;	|	vmem @ ;

::setpixel | c x y --
	setxy px!+ ;    |	vmem ! ;

::pix! | x y --
	setxy ink@ px!+ ;

|---- bitmap
::oscurecetodo
	0 0 setxy
	0 ( sh <? )( 1+
		0 ( sw <? )( 1+
			px@ 2/ $7f7f7f and | oscurece
			px!+
			) drop
		) drop
|	framev sw sh *  ( 1? )( 1- swap dup @ 2/ $7f7f7f and swap !+ swap )	2drop
	;

::bmnoise |ancho alto x y
	( 2swap 1? )( 1-
		2over setxy
		over ( 1? )( 1- rand px!+ ) drop
		2swap 1+ )
	4drop ;


