^lib/reda4.txt
^lib/gui3.txt

#modod "( %d %d %d %d %d"
#modoh "( %h %h %h %h %h"
#modob "( %b %b %b %b %b"
#modof "( %f %f %f %f %f"

#modo 0
#modos 'modod 'modoh 'modob 'modof

::trace | --
	inigui
	[ "edit-code.txt" run ] >esc<
	'exit  <esp>
	[ 0 'modo ! ; ] <f1>
	[ 1 'modo ! ; ] <f2>
	[ 2 'modo ! ; ] <f3>
	[ 3 'modo ! ; ] <f4>
	show
		sw 24 dim
		sw 2/ 12 pos
		negro gc.fbox
		fonti verde home
		":r4trace " print
		amarillo "F1-dec F2-hex F3-bin F4-fix ESP-cont ESC-edit " print cr blanco
		pick4 pick4 pick4 pick4 pick4
		modo 2 << 'modos + @
		print
		;

#memini
::memmap | inimem --
	'memini !
	inigui
	[ "edit-code.txt" run ] >esc<
	'exit <esp>
	[ 16 'memini +! ; ] <dn>
	[ -16 'memini +! ; ] <up>
	[ 16 rows 5 - * 'memini +! ; ] <pgdn>
	[ -16 rows 5 - * 'memini +! ; ] <pgup>
	[ 0 'modo ! ; ] <f1>
	[ 1 'modo ! ; ] <f2>
	[ 2 'modo ! ; ] <f3>
	[ 3 'modo ! ; ] <f4>
    show clrscr verde fonti
		":r4trace " print
		amarillo "F1-dec F2-hex F3-bin F4-fix ESP-cont ESC-edit " print cr blanco
		pick4 pick4 pick4 pick4 pick4
		modo 2 << 'modos + @
		print cr cr

		memini 0
		( rows 5 - <? )(
			over "%h" 8 cntprint ": " print
			swap
			dup 16 ( 1? )( 1- swap c@+ $ff and "%h " 3 cntprint swap ) 2drop
			": " print
			16 ( 1? )( 1- swap c@+ 32 <? ( 33 nip ) emit swap ) drop
			cr
			swap 1+ )
 		2drop
 		;