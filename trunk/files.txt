| Navegador de Archivos
| PHREDA 2006
|---------------------------------------
^reda4.txt
^medit.txt
^gui.txt

#inicioarch 0
#cntarch 20
#vuelta 0
#cant 0
#inifile 0
#iniprog 0

#iarr ( -94 -95 4 -27 100 37 4 101 1 -92 0 )
#iaba ( 98 -95 4 -27 -92 37 4 101 -7 100 0 )
#ishift ( -94 -95 4 -27 100 37 4 101 1 -92 0 )

|---------------------------------------------------------------------------
#actual 0

#fileact 0
#compnombre )( 128
#nnombre )( 64
#pnombre 0

#entrada )( 64
#hayarr 0
#hayaba 0

:compone | ".ext" "n"  -- "n.ext"
	'compnombre swap
	( c@+ 1? )( rot c!+ swap )  2drop
	swap
	( c@+ 1? )( rot c!+ swap ) rot c! 
	drop 'compnombre ;

|--------------------------------------------------------------------------
:nuevo-archivo
	0 'nnombre !
	.page
	'nnombre 16 m.input
	12 12 .table 
	rojo oscuro 0 11 .at 'ifin [ exit ; ] dup >esc< .sbtn .>
	verde oscuro 'iok [ 'nnombre pnombre strcpy exit ; ] dup dup <enter> <f1> .sbtn .>
	.show cls scr
		12 font
		2 3 at verde "NOMBRE:" print
		2 5 at |10 1 2dup verde oscuro ptofill ptowin 
		blanco m.draw
		;

|--------------------------------------------------------------------------
:filedraw | nro --
	inifile +
	dup file 0? ( 2drop ; ) drop
	fileact =? ( verde oscuro gc.fbox blanco )
	10 1 ttext blanco
	dup file 0? ( 2drop ; )
	" %s" print drop ;
	
:fileclick | v nro --
	inifile + dup file 1? ( m.copy )( drop )
	'fileact !  
	 0? ( drop fileact file 1? ( pnombre strcpy exit ; ) ) 
	 drop ;

:calbotons
	inifile 0 >? ( 1 )( 0 ) nip 'hayarr !
	inifile 4 20 * + file 1? ( 1 )( 0 ) nip 'hayaba !
	;

:sumafile | nro --
	fileact + dup file 0? ( 2drop ; ) drop 'fileact !
:ubicar
	inifile 4 20 * +  ( fileact <=? )( 4+ ) 4 20 * - 'inifile !
	inifile ( fileact >? )(  4 - ) 'inifile !
	;

:buscafile
	'entrada c@ 0? ( drop ; ) drop
	0 ( dup file 1? )(  | nro file
		'entrada =pre 1? ( drop 'fileact ! ubicar ; ) drop 1+ ) 
	2drop ;

::explore | nn --
	'pnombre !
	0 'fileact !
	.page
	11 dup .table
	0 1 .at 'filedraw 'fileclick 11 9 .to 4 20 .ttabla
	azul oscuro
	'hayarr 1 1 .cond
		5 0 .at 'iarr [ inifile 1? ( 4 - ) 'inifile ! calbotons ; ] .sbtn | next
	'hayaba 1 1 .cond
		5 10 .at 'iaba [ 4 'inifile +! calbotons ; ] .sbtn | prev
	6 12 .table
	verde oscuro
	4 0 .at  rojo oscuro
	"ESC" "Salir" [ "" pnombre strcpy exit ; ] dup >esc< .fbtn .>
	verde oscuro
	"F1" "Nuevo" [ nuevo-archivo exit ; ] dup <f1> .fbtn .>
	 [ fileact file 1? ( pnombre strcpy exit ; ) drop ; ]  <enter>
	calbotons
	'entrada 64 m.input | 'var max --
	[ m.clear 1 sumafile ; ] <der>
	[ m.clear -1 sumafile ; ] <izq>
	[ m.clear 4 sumafile ; ] <aba>
	[ m.clear -4 sumafile ; ] <arr>
	.show cls scr
		12 sfont
		blanco
		0 0 at ":" print m.draw
		buscafile
		;

