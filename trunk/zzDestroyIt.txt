| zzDestroyIt.txt
| Gustavo Leguizamon 2009
|--------------------------------
^lib/gui.txt

|| -- GRAFICOS

#logoD
 $FB572221 $D1E67F03 $93100003 $BD2D4B83 $B5D0E03 $3540553 $D49002A3 $D5E475A3 $E89C7AF3
 $E7F0C083 $CDE0CDD3 $B07BFD63 $F95724D3 $FB57222A $FF0000D $1C128711 $56E6A1D3 $48309B13
 $6F9C5583 $77A538D3 $160D5633 $1ABE89C3 $1C12871A $FEF9D $0

#logoJuego 
 $C330F0A1 $C3314633 $30C94B83 $3020F0A3 $C330F0AA $400FCD $14BBA521 $55FA522 $55FDFF4
 $55C1AC2 $14B81AC4 $24181AC2 $241BDFF4 $241BA522 $14BBA524 $14BBA52A $1367D547 $55C5598
 $FFFFFFC $280000E $0

#barrita |spr
 $81AF04B1 $81ACFB53 $7EFCFB53 $7EFF04B3 $81AF04BA $540007 $81AC0008 $10959C $1FCE
 0

#bloqueRojo |spr
 $8F0F5FA1 $8F0C9DB3 $70F49DB3 $70F75FA3 $8F0F5FAA $BDD5C687 $C0862E8 $FFFFFFC $F40000E
 0

#bloqueCeleste |spr
 $8F0F5FA1 $8F0C9DB3 $70F49DB3 $70F75FA3 $8F0F5FAA $4004D87 $F5F2778 $1F4FAC $300FCE
 0

#bloqueVerde |spr
 $8F0F5FA1 $8F0C9DB3 $70F49DB3 $70F75FA3 $8F0F5FAA $BDD5C687 $2EC812B8 $CDF9FFC $12FF00E
 0

#bloqueAmarillo |spr
 $8F0F5FA1 $8F0C9DB3 $70F49DB3 $70F75FA3 $8F0F5FAA $BDD5C687 $C0862E8 $F7FEFFC $FCFC00E
 0

#bloqueNaranja |spr
 $8F0F5FA1 $8F0C9DB3 $70F49DB3 $70F75FA3 $8F0F5FAA $F2A52617 $C0BBD38 $FAC200C $FF510BE
 0

#pelota |spr
 $AF9D01 $E5EF9D02 $E5EC0554 $E5EC6D82 $AC6D84 $1B646D82 $1B640554 $1B679D02 $AF9D04
 $AF9D0A $FBFCFAD 0


|| -- JUEGO
 
:bonus ;

| tipo contador_golpes bonus
#nivel1 | 9 x 5 - 3 words 
45
	1 1 'bonus 1 1 'bonus 1 1 'bonus 
	1 1 'bonus 1 1 'bonus 1 1 'bonus 
	1 1 'bonus 1 1 'bonus 1 1 'bonus 

	1 1 'bonus 1 1 'bonus 1 1 'bonus
	1 1 'bonus 1 1 'bonus 1 1 'bonus 
	1 1 'bonus 1 1 'bonus 1 1 'bonus 

	1 1 'bonus 1 1 'bonus 1 1 'bonus 
	1 1 'bonus 1 1 'bonus 1 1 'bonus 
	1 1 'bonus 1 1 'bonus 1 1 'bonus 

	2 1 'bonus 2 1 'bonus 2 1 'bonus 
	2 1 'bonus 2 1 'bonus 2 1 'bonus 
	2 1 'bonus 2 1 'bonus 2 1 'bonus 

	3 2 'bonus 3 2 'bonus 3 2 'bonus 
	3 2 'bonus 3 2 'bonus 3 2 'bonus 
	3 2 'bonus 3 2 'bonus 3 2 'bonus 

#nivel2 | 9 x 5 - 3 words 
38
	1 1 'bonus 1 1 'bonus 1 1 'bonus 
	1 1 'bonus 1 1 'bonus 1 1 'bonus
	1 1 'bonus 1 1 'bonus 1 1 'bonus 

	3 2 'bonus 0 2 'bonus 3 2 'bonus 
	3 2 'bonus 0 2 'bonus 3 2 'bonus 
	3 2 'bonus 0 2 'bonus 3 2 'bonus

	1 1 'bonus 0 1 'bonus 1 1 'bonus 
	1 1 'bonus 0 1 'bonus 1 1 'bonus
	1 1 'bonus 0 1 'bonus 1 1 'bonus 

	2 1 'bonus 2 1 'bonus 2 1 'bonus 
	2 1 'bonus 2 1 'bonus 2 1 'bonus 
	2 1 'bonus 2 1 'bonus 2 1 'bonus 

	3 2 'bonus 3 2 'bonus 3 2 'bonus 
	3 2 'bonus 0 2 'bonus 3 2 'bonus 
	3 2 'bonus 3 2 'bonus 3 2 'bonus


#niveles 'nivel1 'nivel2 0
#nivel

#cant 0
#mapa )( 550 |540

#mapa> 'mapa

| sprite_bloque golpes_necesarios
#bloques 
	0	
	'bloqueRojo 	
	'bloqueCeleste 	 
	'bloqueVerde 	
	'bloqueAmarillo 
	'bloqueNaranja

:dibbloque | nrobloque --
	0? ( drop ; )		
	4 * 'bloques + @ nsprite ;


#xcoor #ycoor

:coordamem | x y -- x' y'
	0.291 + 0.18 /. 16 >> 3 +  	| x y'
	swap			  			| y' x
	0.15 + 0.12 /. 16 >> 2 -   	| y' x'
	swap						| x' y'
;

:dibmapa | mapa> -- 
	0.1 0.1 fdim
 	0 ( 5 <? )(
  		0 ( 9 <? )( | mapa> y x
			over 2 + 0.12 * 0.1 - | transformar y 	
			over 3 - 0.18 *	0.2 - | transformar x	
			swap fpos 		| mapa> y x
			rot dup 12 + 	| y x mapa> mapa>+ 
			swap @			| y x mapa>+ mapa
			dibbloque 		| y x mapa>+
			rot rot 1+		| mapa> y x+
		) drop 1+
	) 2drop	;

#barraX 
#barraVXDer #barraVXIzq
#pelotaX #pelotaVX 
#pelotaY #pelotaVY
#score
#vidas
#modo

:reset
	|posiciones y velocidades
	0.0 dup	'barraX ! 'barraVXDer !
	0.0 dup	'barraVXIzq ! 'pelotaX !
	0.0 dup 'pelotaVX ! 'pelotaVY !
	-0.83 'pelotaY ! ;

:mapa? | -- 0 | dirMapa
	'niveles nivel 4 * + @ ;

:cargarmapa
	mapa? 1? ( 'cant swap 137 move ; ) ;
	
:resetAll | -- 0 | -
	reset
	|variables del juego
	0 'score !
	3 'vidas !
	0 'nivel !
	|mapa
	cargarmapa
	|'cant 'nivel1 137 move ;

:pasarNivel
	reset
	cargarmapa ;

:toc
	pelotaVY neg 'pelotaVY ! ;

:tac
	pelotaVX neg 'pelotaVX ! ;


:buscaChoque | x y --
	| Controla Y
	0 <? ( 2drop 0 ; )
	8  >? ( 2drop 0 ; )
	swap
	| Controla X
	0 <? ( 2drop 0 ; )
	4 >? ( 2drop 0 ; )
	| Obtener Direccion --> (X * 9 + y)* 12 + mapa>
	9 * + 12 * mapa> +
	| Verificar que ese bloque vive 
	dup | dir dir 
	@ | dir valor
	0? ( 2drop 0 ; ) 
	drop |dir
	1 ;

:Choque | dirBloque -- 
	|Si vive verificar Golpes
	dup | dir dir
	4+	| dir dirGolpe
	dup	| dir dirGolpe dirGolpe
	@ 	| dir dirGolpe Golpes
	1 -	| dir dirGolpes Golpes-
	dup >r swap ! r> | dir Golpes-
	score 10 + 'score !
	0? ( swap ! cant 1 - 'cant ! )( 2drop )
	|Gane?
	cant 0? ( 2 'modo ! exit ) drop ;

:movimientoPelota
	pelotaX
	1.0 >? ( tac )
	-1.0 <? ( tac )
	pelotaVX + 'pelotaX !
	
	pelotaY
	1.0 >? ( toc )
	|-1.0 <? ( toc )
	pelotaVY + 'pelotaY !

	| -- perder

	pelotaY -0.95 <? ( 	drop 
						vidas 1- 'vidas !
						vidas 0? ( 3 )( 1 ) 'modo !
						drop
						exit
						;
					 )
	 
	| -- choque con barra
	
	-0.85 <? (
				-0.88 >? (   
					drop
					barraX pelotaX - abs
					0.1 <? ( drop toc ; )
					drop
					;
				) 
			)
	 
	| -- choque con ladrillos
	 		
	| X+VX Y --> Buscar Choque --> TOC
	PelotaX PelotaVX +
	coordamem
	buscaChoque
	1? ( drop Choque tac ; ) drop
	
	| X Y+VY --> Buscar Choque --> TAC
	PelotaY PelotaVY + PelotaX
	coordamem
	buscaChoque
	1? ( drop Choque toc ; ) drop
	;  

:dibpelota
	0.1 0.1 fdim
	pelotaX pelotaY fpos
	'pelota nsprite

	pelotaVY 0? ( barraX 'pelotaX ! )( movimientoPelota ) drop
	;

	
:dibbarra
	barraVXDer barraVXIzq + barraX + 
	0.9 >? ( 0 'barraVXDer ! drop 0.9 )
	-0.9 <? ( 0 'barraVXIzq ! drop -0.9 )
	dup -0.9 fpos 'barraX ! 0.1 0.1 fdim
	'barrita nsprite ;

|| -- LAYERS


:fondo
	$444444 paper ;

:_accionar
	mapa> dibmapa
	dibbarra
	dibpelota
;

:_puntaje
	1.5 0.85 fpos
	0.8 0.1 fdim
	rojo 3 font home
	score "Puntaje: %d" print cr
;

:_vidas
	-0.9 0.90 fpos
	0.05 0.05 fdim
	azul 3 font home
	-0.9 vidas ( 1? )( 'barrita nsprite 1- swap 0.12 + dup 0.90 fpos swap ) 2drop
;

:_nivel
	0.7 0.85 fpos 
	0.8 0.1 fdim
	blanco 2 font
	nivel 1+ "Nivel %d" print
;

|| -- ENGINE

:juego
	inigui
	[ 4 'modo ! exit ; ] >esc<
	[ 0.03 	'barraVXDer ! ; ] <ri>
	[ 0 	'barraVXDer ! ; ] >ri<
	[ -0.03 'barraVXIzq ! ; ] <le>
	[ 0 	'barraVXIzq ! ; ] >le<
	[ 0.02 dup 'pelotaVY ! 'pelotaVX ! ; ] >esp<

	fondo
	show
		cls
	 	_accionar
		_puntaje
		_vidas
		_nivel ;

:nogano
	show
		cls
		0.0 0.2 fpos
		0.8 0.1 fdim
		| Cartel Verde
		$00ff00 ink 1 font home
		nivel 1+ "Nivel %d" printc cr
	 	3 .segs exit ;

:gano
	show
		cls
		0.0 0.2 fpos
		0.8 0.1 fdim
		| Cartel Verde
		$00ff00 ink 1 font home
		"Ganaste!!!" printc cr cr cr
		| Puntaje Azul
		$0000ff ink
		score "Puntaje: %d" printc
	 	3 .segs exit ;

:perdio
	show
		cls
		0.0 0.2 fpos
		0.8 0.1 fdim
		| Cartel Rojo
		$ff0000 ink 1 font home
		"Perdiste!" printc cr cr cr
		| Puntaje Verde
		$00ff00 ink
		score "Puntaje: %d" printc
     	3 .segs exit ;

:prejuego
         inigui
	show
		modo

		| -- juego nuevo
		0? (
		 	drop
			resetAll
			juego ;
		)

		| -- perdio vida
		1 =? (
			drop
			reset
			juego ;
		)

		| -- gano nivel
		2 =? (
				drop
				| Proximo nivel
				nivel 1+ 'nivel !
				| Existe este nivel ?
				mapa?
				| No existe GANO el juego
				0? ( drop gano exit ; ) drop
				| Existe Al prox. Nivel
				nogano pasarNivel juego ;
			)

		| -- perdio
		3 =? ( drop perdio exit ; )

		| -- salio del juego
		4 =? ( drop exit ; )

		drop
	;


|| -- GUI

:iniciarGui
	'exit >esc<
	[ 0 'modo ! prejuego ; ] <enter>
	$222222 paper ;

:mostrarLogo
	0.0 0.4 fpos 0.5 0.5 fdim nsprite ;

:mostrarLeyenda
	0.0 -0.3 fpos 0.8 0.1 fdim ink 1 font printc ;

:debug
	scr blanco 25 font dup "%d" print ;

:menu
	inigui
	iniciarGui
	show
		clrscr home
		'logoJuego mostrarLogo
		"Destroy It" $0000ff mostrarLeyenda
		cr cr sp
        verde 'prejuego "Jugar" btnt
		sp
        rojo 'exit "Salir" btnt
|	debug
		cmano ;

|| -- BOOT
: 33 menu ;

