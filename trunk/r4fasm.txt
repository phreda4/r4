| Compilador
| PHREDA 2009
|---------------
^lib/gui.txt
^r4comp.txt
^r4post.txt
^r4i86.txt

#nombreimg )( 64

#msgOK "OK."
#mensaje 'msgOk

||||||||||||||||||||||||||||||||||||||||||||||||
:getcte		8 >> cte + @ ;
:getstr     8 >> str + ;

:nro>dicn2   8 >> 4 << 'indicepal + @ ; | nombre
:nro>dicn   8 >> "w%h" mprint ;			| numero de palabra

||||||||||||||||||||||||||||||||||||||||||||||||
:coldefw nro>dicn ":%w" ,print ;
:coldefv nro>dicn "#%w" ,print ;
:colitd
:colith
:colitb
:colitf
	getcte "%d" ,print ;
:colits
	getstr """%s""" ,print ",0" ,s ;
:colwor
:colvar
:coldwo
:coldva
	nro>dicn ,s ;

#codei 0 'coldefw 'coldefv colitd colith colitb colitf colits colwor colvar coldwo coldva

||||||||||||||||||||||||||||||||||||||||||||||||
:genvar | nro llamadas -- nro llamadas
	over "v%h  " ,print
	over 4 << 'indicepal + 4+ @
	( @+ dup $ff and
		1 =? ( 0 nip ) | corta con defc
		2 =? ( 0 nip ) | corta con defv
		1? )( 	 | dirv n c
		12 <? ( 2 << 'codei + @ exec )( 2drop )
		,sp
		)
	3drop
	,cr ;

:compiladato
	mark
	";---:r4 compiler dat.asm" ,s ,cr
	0 ( cntword cntvars + <? )(
		pal@inf | n -- n i
		1 nand? ( drop )( drop
			dup getllamadas 1? ( genvar ) drop
			)
		1+ ) drop
	0 ,c
	"r4asm/dat.asm" savemem
	empty ;

||||||||||||||||||||||||||||||||||||||||||||||||
:,token | a -- a.
	@+ dup $ff and
	12 <? ( 2 << 'codei + @ exec ; ) nip
	nro>macro ,s ;

:gencodigo | nro llamadas -- nro llamadas
	over "w%h: " ,print
	over 4 << 'indicepal +
	dup @ "; %w" ,print ,cr
	dup 4+ @ swap 12 + @
	( 1? )( 1- swap
		,sp ,token swap
		$f nand? ( ,cr ) )
	2drop
	,cr
	;

:compilacodigo
	mark
	";---:r4 compiler cod.asm" ,s ,cr
	0 ( cntword cntvars + <? )(
		pal@inf | n -- n i
		1 and? ( drop )( drop
			dup getllamadas 1? ( gencodigo ) drop
			)
		1+ ) drop
	0 ,c
	"r4asm/cod.asm" savemem
	empty ;

|-----------------------
:cargaimagen
	'nombreimg "./nom/editor.nom" load drop
	'nombreimg tokencomp
|	msg 1? ( dup 'mensaje ! ) drop
	tokenpost
|	msg 1? ( dup 'mensaje ! ) drop
	;

|-----------------------
:main
	mark
	cargaimagen | genera en realidad

	compiladato
    compilacodigo

	inigui
	'exit >esc<
	show clrscr
		fonti home verde
		mensaje print cr
		'nombreimg "programa: %s" print cr
		prog> prog - 2 >> "Programa: %d tokens" print cr
		cte> cte - 2 >> "Constantes: %d" print cr
		str> str - "Strings: %d bytes" print cr
		cntvars cntword "Palabras: %d  Variables: %d" print cr

|		"cmd /c dir" system
|		"command /c .." system
		;

: 33 main ;